<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.These.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- An either-or-both data type, basic type and operations</a>
<a id="164" class="Comment">------------------------------------------------------------------------</a>

<a id="238" class="Symbol">{-#</a> <a id="242" class="Keyword">OPTIONS</a> <a id="250" class="Pragma">--without-K</a> <a id="262" class="Pragma">--safe</a> <a id="269" class="Symbol">#-}</a>

<a id="274" class="Keyword">module</a> <a id="281" href="Data.These.Base.html" class="Module">Data.These.Base</a> <a id="297" class="Keyword">where</a>

<a id="304" class="Keyword">open</a> <a id="309" class="Keyword">import</a> <a id="316" href="Level.html" class="Module">Level</a>
<a id="322" class="Keyword">open</a> <a id="327" class="Keyword">import</a> <a id="334" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="348" class="Keyword">using</a> <a id="354" class="Symbol">(</a><a id="355" href="Data.Sum.Base.html#612" class="Datatype Operator">_⊎_</a><a id="358" class="Symbol">;</a> <a id="360" href="Data.Sum.Base.html#967" class="Function Operator">[_,_]′</a><a id="366" class="Symbol">)</a>
<a id="368" class="Keyword">open</a> <a id="373" class="Keyword">import</a> <a id="380" href="Function.html" class="Module">Function</a>

<a id="390" class="Keyword">private</a>
  <a id="400" class="Keyword">variable</a>
    <a id="413" href="Data.These.Base.html#413" class="Generalizable">a</a> <a id="415" href="Data.These.Base.html#415" class="Generalizable">b</a> <a id="417" href="Data.These.Base.html#417" class="Generalizable">c</a> <a id="419" href="Data.These.Base.html#419" class="Generalizable">d</a> <a id="421" href="Data.These.Base.html#421" class="Generalizable">e</a> <a id="423" href="Data.These.Base.html#423" class="Generalizable">f</a> <a id="425" class="Symbol">:</a> <a id="427" href="Agda.Primitive.html#408" class="Postulate">Level</a>
    <a id="437" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="439" class="Symbol">:</a> <a id="441" class="PrimitiveType">Set</a> <a id="445" href="Data.These.Base.html#413" class="Generalizable">a</a>
    <a id="451" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="453" class="Symbol">:</a> <a id="455" class="PrimitiveType">Set</a> <a id="459" href="Data.These.Base.html#415" class="Generalizable">b</a>
    <a id="465" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="467" class="Symbol">:</a> <a id="469" class="PrimitiveType">Set</a> <a id="473" href="Data.These.Base.html#417" class="Generalizable">c</a>
    <a id="479" href="Data.These.Base.html#479" class="Generalizable">D</a> <a id="481" class="Symbol">:</a> <a id="483" class="PrimitiveType">Set</a> <a id="487" href="Data.These.Base.html#419" class="Generalizable">d</a>
    <a id="493" href="Data.These.Base.html#493" class="Generalizable">E</a> <a id="495" class="Symbol">:</a> <a id="497" class="PrimitiveType">Set</a> <a id="501" href="Data.These.Base.html#421" class="Generalizable">e</a>
    <a id="507" href="Data.These.Base.html#507" class="Generalizable">F</a> <a id="509" class="Symbol">:</a> <a id="511" class="PrimitiveType">Set</a> <a id="515" href="Data.These.Base.html#423" class="Generalizable">f</a>

<a id="518" class="Keyword">data</a> <a id="These"></a><a id="523" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="529" class="Symbol">{</a><a id="530" href="Data.These.Base.html#530" class="Bound">a</a> <a id="532" href="Data.These.Base.html#532" class="Bound">b</a><a id="533" class="Symbol">}</a> <a id="535" class="Symbol">(</a><a id="536" href="Data.These.Base.html#536" class="Bound">A</a> <a id="538" class="Symbol">:</a> <a id="540" class="PrimitiveType">Set</a> <a id="544" href="Data.These.Base.html#530" class="Bound">a</a><a id="545" class="Symbol">)</a> <a id="547" class="Symbol">(</a><a id="548" href="Data.These.Base.html#548" class="Bound">B</a> <a id="550" class="Symbol">:</a> <a id="552" class="PrimitiveType">Set</a> <a id="556" href="Data.These.Base.html#532" class="Bound">b</a><a id="557" class="Symbol">)</a> <a id="559" class="Symbol">:</a> <a id="561" class="PrimitiveType">Set</a> <a id="565" class="Symbol">(</a><a id="566" href="Data.These.Base.html#530" class="Bound">a</a> <a id="568" href="Agda.Primitive.html#621" class="Primitive Operator">⊔</a> <a id="570" href="Data.These.Base.html#532" class="Bound">b</a><a id="571" class="Symbol">)</a> <a id="573" class="Keyword">where</a>
  <a id="These.this"></a><a id="581" href="Data.These.Base.html#581" class="InductiveConstructor">this</a>  <a id="587" class="Symbol">:</a> <a id="589" href="Data.These.Base.html#536" class="Bound">A</a>     <a id="595" class="Symbol">→</a> <a id="597" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="603" href="Data.These.Base.html#536" class="Bound">A</a> <a id="605" href="Data.These.Base.html#548" class="Bound">B</a>
  <a id="These.that"></a><a id="609" href="Data.These.Base.html#609" class="InductiveConstructor">that</a>  <a id="615" class="Symbol">:</a>     <a id="621" href="Data.These.Base.html#548" class="Bound">B</a> <a id="623" class="Symbol">→</a> <a id="625" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="631" href="Data.These.Base.html#536" class="Bound">A</a> <a id="633" href="Data.These.Base.html#548" class="Bound">B</a>
  <a id="These.these"></a><a id="637" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="643" class="Symbol">:</a> <a id="645" href="Data.These.Base.html#536" class="Bound">A</a> <a id="647" class="Symbol">→</a> <a id="649" href="Data.These.Base.html#548" class="Bound">B</a> <a id="651" class="Symbol">→</a> <a id="653" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="659" href="Data.These.Base.html#536" class="Bound">A</a> <a id="661" href="Data.These.Base.html#548" class="Bound">B</a>

<a id="664" class="Comment">------------------------------------------------------------------------</a>
<a id="737" class="Comment">-- Operations</a>

<a id="752" class="Comment">-- injection</a>

<a id="fromSum"></a><a id="766" href="Data.These.Base.html#766" class="Function">fromSum</a> <a id="774" class="Symbol">:</a> <a id="776" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="778" href="Data.Sum.Base.html#612" class="Datatype Operator">⊎</a> <a id="780" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="782" class="Symbol">→</a> <a id="784" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="790" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="792" href="Data.These.Base.html#451" class="Generalizable">B</a>
<a id="794" href="Data.These.Base.html#766" class="Function">fromSum</a> <a id="802" class="Symbol">=</a> <a id="804" href="Data.Sum.Base.html#967" class="Function Operator">[</a> <a id="806" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="811" href="Data.Sum.Base.html#967" class="Function Operator">,</a> <a id="813" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="818" href="Data.Sum.Base.html#967" class="Function Operator">]′</a>

<a id="822" class="Comment">-- map</a>

<a id="map"></a><a id="830" href="Data.These.Base.html#830" class="Function">map</a> <a id="834" class="Symbol">:</a> <a id="836" class="Symbol">(</a><a id="837" href="Data.These.Base.html#837" class="Bound">f</a> <a id="839" class="Symbol">:</a> <a id="841" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="843" class="Symbol">→</a> <a id="845" href="Data.These.Base.html#451" class="Generalizable">B</a><a id="846" class="Symbol">)</a> <a id="848" class="Symbol">(</a><a id="849" href="Data.These.Base.html#849" class="Bound">g</a> <a id="851" class="Symbol">:</a> <a id="853" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="855" class="Symbol">→</a> <a id="857" href="Data.These.Base.html#479" class="Generalizable">D</a><a id="858" class="Symbol">)</a> <a id="860" class="Symbol">→</a> <a id="862" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="868" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="870" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="872" class="Symbol">→</a> <a id="874" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="880" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="882" href="Data.These.Base.html#479" class="Generalizable">D</a>
<a id="884" href="Data.These.Base.html#830" class="Function">map</a> <a id="888" href="Data.These.Base.html#888" class="Bound">f</a> <a id="890" href="Data.These.Base.html#890" class="Bound">g</a> <a id="892" class="Symbol">(</a><a id="893" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="898" href="Data.These.Base.html#898" class="Bound">a</a><a id="899" class="Symbol">)</a>    <a id="904" class="Symbol">=</a> <a id="906" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="911" class="Symbol">(</a><a id="912" href="Data.These.Base.html#888" class="Bound">f</a> <a id="914" href="Data.These.Base.html#898" class="Bound">a</a><a id="915" class="Symbol">)</a>
<a id="917" href="Data.These.Base.html#830" class="Function">map</a> <a id="921" href="Data.These.Base.html#921" class="Bound">f</a> <a id="923" href="Data.These.Base.html#923" class="Bound">g</a> <a id="925" class="Symbol">(</a><a id="926" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="931" href="Data.These.Base.html#931" class="Bound">b</a><a id="932" class="Symbol">)</a>    <a id="937" class="Symbol">=</a> <a id="939" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="944" class="Symbol">(</a><a id="945" href="Data.These.Base.html#923" class="Bound">g</a> <a id="947" href="Data.These.Base.html#931" class="Bound">b</a><a id="948" class="Symbol">)</a>
<a id="950" href="Data.These.Base.html#830" class="Function">map</a> <a id="954" href="Data.These.Base.html#954" class="Bound">f</a> <a id="956" href="Data.These.Base.html#956" class="Bound">g</a> <a id="958" class="Symbol">(</a><a id="959" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="965" href="Data.These.Base.html#965" class="Bound">a</a> <a id="967" href="Data.These.Base.html#967" class="Bound">b</a><a id="968" class="Symbol">)</a> <a id="970" class="Symbol">=</a> <a id="972" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="978" class="Symbol">(</a><a id="979" href="Data.These.Base.html#954" class="Bound">f</a> <a id="981" href="Data.These.Base.html#965" class="Bound">a</a><a id="982" class="Symbol">)</a> <a id="984" class="Symbol">(</a><a id="985" href="Data.These.Base.html#956" class="Bound">g</a> <a id="987" href="Data.These.Base.html#967" class="Bound">b</a><a id="988" class="Symbol">)</a>

<a id="map₁"></a><a id="991" href="Data.These.Base.html#991" class="Function">map₁</a> <a id="996" class="Symbol">:</a> <a id="998" class="Symbol">(</a><a id="999" href="Data.These.Base.html#999" class="Bound">f</a> <a id="1001" class="Symbol">:</a> <a id="1003" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1005" class="Symbol">→</a> <a id="1007" href="Data.These.Base.html#451" class="Generalizable">B</a><a id="1008" class="Symbol">)</a> <a id="1010" class="Symbol">→</a> <a id="1012" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1018" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1020" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="1022" class="Symbol">→</a> <a id="1024" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1030" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1032" href="Data.These.Base.html#465" class="Generalizable">C</a>
<a id="1034" href="Data.These.Base.html#991" class="Function">map₁</a> <a id="1039" href="Data.These.Base.html#1039" class="Bound">f</a> <a id="1041" class="Symbol">=</a> <a id="1043" href="Data.These.Base.html#830" class="Function">map</a> <a id="1047" href="Data.These.Base.html#1039" class="Bound">f</a> <a id="1049" href="Function.html#708" class="Function">id</a>

<a id="map₂"></a><a id="1053" href="Data.These.Base.html#1053" class="Function">map₂</a> <a id="1058" class="Symbol">:</a> <a id="1060" class="Symbol">(</a><a id="1061" href="Data.These.Base.html#1061" class="Bound">g</a> <a id="1063" class="Symbol">:</a> <a id="1065" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1067" class="Symbol">→</a> <a id="1069" href="Data.These.Base.html#465" class="Generalizable">C</a><a id="1070" class="Symbol">)</a> <a id="1072" class="Symbol">→</a> <a id="1074" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1080" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1082" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1084" class="Symbol">→</a> <a id="1086" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1092" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1094" href="Data.These.Base.html#465" class="Generalizable">C</a>
<a id="1096" href="Data.These.Base.html#1053" class="Function">map₂</a> <a id="1101" class="Symbol">=</a> <a id="1103" href="Data.These.Base.html#830" class="Function">map</a> <a id="1107" href="Function.html#708" class="Function">id</a>

<a id="1111" class="Comment">-- fold</a>

<a id="fold"></a><a id="1120" href="Data.These.Base.html#1120" class="Function">fold</a> <a id="1125" class="Symbol">:</a> <a id="1127" class="Symbol">(</a><a id="1128" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1130" class="Symbol">→</a> <a id="1132" href="Data.These.Base.html#465" class="Generalizable">C</a><a id="1133" class="Symbol">)</a> <a id="1135" class="Symbol">→</a> <a id="1137" class="Symbol">(</a><a id="1138" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1140" class="Symbol">→</a> <a id="1142" href="Data.These.Base.html#465" class="Generalizable">C</a><a id="1143" class="Symbol">)</a> <a id="1145" class="Symbol">→</a> <a id="1147" class="Symbol">(</a><a id="1148" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1150" class="Symbol">→</a> <a id="1152" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1154" class="Symbol">→</a> <a id="1156" href="Data.These.Base.html#465" class="Generalizable">C</a><a id="1157" class="Symbol">)</a> <a id="1159" class="Symbol">→</a> <a id="1161" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1167" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1169" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1171" class="Symbol">→</a> <a id="1173" href="Data.These.Base.html#465" class="Generalizable">C</a>
<a id="1175" href="Data.These.Base.html#1120" class="Function">fold</a> <a id="1180" href="Data.These.Base.html#1180" class="Bound">l</a> <a id="1182" href="Data.These.Base.html#1182" class="Bound">r</a> <a id="1184" href="Data.These.Base.html#1184" class="Bound">lr</a> <a id="1187" class="Symbol">(</a><a id="1188" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1193" href="Data.These.Base.html#1193" class="Bound">a</a><a id="1194" class="Symbol">)</a>    <a id="1199" class="Symbol">=</a> <a id="1201" href="Data.These.Base.html#1180" class="Bound">l</a> <a id="1203" href="Data.These.Base.html#1193" class="Bound">a</a>
<a id="1205" href="Data.These.Base.html#1120" class="Function">fold</a> <a id="1210" href="Data.These.Base.html#1210" class="Bound">l</a> <a id="1212" href="Data.These.Base.html#1212" class="Bound">r</a> <a id="1214" href="Data.These.Base.html#1214" class="Bound">lr</a> <a id="1217" class="Symbol">(</a><a id="1218" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1223" href="Data.These.Base.html#1223" class="Bound">b</a><a id="1224" class="Symbol">)</a>    <a id="1229" class="Symbol">=</a> <a id="1231" href="Data.These.Base.html#1212" class="Bound">r</a> <a id="1233" href="Data.These.Base.html#1223" class="Bound">b</a>
<a id="1235" href="Data.These.Base.html#1120" class="Function">fold</a> <a id="1240" href="Data.These.Base.html#1240" class="Bound">l</a> <a id="1242" href="Data.These.Base.html#1242" class="Bound">r</a> <a id="1244" href="Data.These.Base.html#1244" class="Bound">lr</a> <a id="1247" class="Symbol">(</a><a id="1248" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1254" href="Data.These.Base.html#1254" class="Bound">a</a> <a id="1256" href="Data.These.Base.html#1256" class="Bound">b</a><a id="1257" class="Symbol">)</a> <a id="1259" class="Symbol">=</a> <a id="1261" href="Data.These.Base.html#1244" class="Bound">lr</a> <a id="1264" href="Data.These.Base.html#1254" class="Bound">a</a> <a id="1266" href="Data.These.Base.html#1256" class="Bound">b</a>

<a id="foldWithDefaults"></a><a id="1269" href="Data.These.Base.html#1269" class="Function">foldWithDefaults</a> <a id="1286" class="Symbol">:</a> <a id="1288" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1290" class="Symbol">→</a> <a id="1292" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1294" class="Symbol">→</a> <a id="1296" class="Symbol">(</a><a id="1297" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1299" class="Symbol">→</a> <a id="1301" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1303" class="Symbol">→</a> <a id="1305" href="Data.These.Base.html#465" class="Generalizable">C</a><a id="1306" class="Symbol">)</a> <a id="1308" class="Symbol">→</a> <a id="1310" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1316" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1318" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1320" class="Symbol">→</a> <a id="1322" href="Data.These.Base.html#465" class="Generalizable">C</a>
<a id="1324" href="Data.These.Base.html#1269" class="Function">foldWithDefaults</a> <a id="1341" href="Data.These.Base.html#1341" class="Bound">a</a> <a id="1343" href="Data.These.Base.html#1343" class="Bound">b</a> <a id="1345" href="Data.These.Base.html#1345" class="Bound">lr</a> <a id="1348" class="Symbol">=</a> <a id="1350" href="Data.These.Base.html#1120" class="Function">fold</a> <a id="1355" class="Symbol">(</a><a id="1356" href="Function.html#1300" class="Function">flip</a> <a id="1361" href="Data.These.Base.html#1345" class="Bound">lr</a> <a id="1364" href="Data.These.Base.html#1343" class="Bound">b</a><a id="1365" class="Symbol">)</a> <a id="1367" class="Symbol">(</a><a id="1368" href="Data.These.Base.html#1345" class="Bound">lr</a> <a id="1371" href="Data.These.Base.html#1341" class="Bound">a</a><a id="1372" class="Symbol">)</a> <a id="1374" href="Data.These.Base.html#1345" class="Bound">lr</a>

<a id="1378" class="Comment">-- swap</a>

<a id="swap"></a><a id="1387" href="Data.These.Base.html#1387" class="Function">swap</a> <a id="1392" class="Symbol">:</a> <a id="1394" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1400" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1402" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1404" class="Symbol">→</a> <a id="1406" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1412" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1414" href="Data.These.Base.html#437" class="Generalizable">A</a>
<a id="1416" href="Data.These.Base.html#1387" class="Function">swap</a> <a id="1421" class="Symbol">=</a> <a id="1423" href="Data.These.Base.html#1120" class="Function">fold</a> <a id="1428" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1433" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1438" class="Symbol">(</a><a id="1439" href="Function.html#1300" class="Function">flip</a> <a id="1444" href="Data.These.Base.html#637" class="InductiveConstructor">these</a><a id="1449" class="Symbol">)</a>

<a id="1452" class="Comment">-- align</a>

<a id="alignWith"></a><a id="1462" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1472" class="Symbol">:</a> <a id="1474" class="Symbol">(</a><a id="1475" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1481" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1483" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="1485" class="Symbol">→</a> <a id="1487" href="Data.These.Base.html#493" class="Generalizable">E</a><a id="1488" class="Symbol">)</a> <a id="1490" class="Symbol">→</a> <a id="1492" class="Symbol">(</a><a id="1493" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1499" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1501" href="Data.These.Base.html#479" class="Generalizable">D</a> <a id="1503" class="Symbol">→</a> <a id="1505" href="Data.These.Base.html#507" class="Generalizable">F</a><a id="1506" class="Symbol">)</a> <a id="1508" class="Symbol">→</a> <a id="1510" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1516" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="1518" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="1520" class="Symbol">→</a> <a id="1522" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1528" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="1530" href="Data.These.Base.html#479" class="Generalizable">D</a> <a id="1532" class="Symbol">→</a> <a id="1534" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="1540" href="Data.These.Base.html#493" class="Generalizable">E</a> <a id="1542" href="Data.These.Base.html#507" class="Generalizable">F</a>
<a id="1544" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1554" href="Data.These.Base.html#1554" class="Bound">f</a> <a id="1556" href="Data.These.Base.html#1556" class="Bound">g</a> <a id="1558" class="Symbol">(</a><a id="1559" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1564" href="Data.These.Base.html#1564" class="Bound">a</a><a id="1565" class="Symbol">)</a>    <a id="1570" class="Symbol">(</a><a id="1571" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1576" href="Data.These.Base.html#1576" class="Bound">c</a><a id="1577" class="Symbol">)</a>    <a id="1582" class="Symbol">=</a> <a id="1584" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1589" class="Symbol">(</a><a id="1590" href="Data.These.Base.html#1554" class="Bound">f</a> <a id="1592" class="Symbol">(</a><a id="1593" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1599" href="Data.These.Base.html#1564" class="Bound">a</a> <a id="1601" href="Data.These.Base.html#1576" class="Bound">c</a><a id="1602" class="Symbol">))</a>
<a id="1605" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1615" href="Data.These.Base.html#1615" class="Bound">f</a> <a id="1617" href="Data.These.Base.html#1617" class="Bound">g</a> <a id="1619" class="Symbol">(</a><a id="1620" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1625" href="Data.These.Base.html#1625" class="Bound">a</a><a id="1626" class="Symbol">)</a>    <a id="1631" class="Symbol">(</a><a id="1632" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1637" href="Data.These.Base.html#1637" class="Bound">d</a><a id="1638" class="Symbol">)</a>    <a id="1643" class="Symbol">=</a> <a id="1645" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1651" class="Symbol">(</a><a id="1652" href="Data.These.Base.html#1615" class="Bound">f</a> <a id="1654" class="Symbol">(</a><a id="1655" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1660" href="Data.These.Base.html#1625" class="Bound">a</a><a id="1661" class="Symbol">))</a> <a id="1664" class="Symbol">(</a><a id="1665" href="Data.These.Base.html#1617" class="Bound">g</a> <a id="1667" class="Symbol">(</a><a id="1668" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1673" href="Data.These.Base.html#1637" class="Bound">d</a><a id="1674" class="Symbol">))</a>
<a id="1677" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1687" href="Data.These.Base.html#1687" class="Bound">f</a> <a id="1689" href="Data.These.Base.html#1689" class="Bound">g</a> <a id="1691" class="Symbol">(</a><a id="1692" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1697" href="Data.These.Base.html#1697" class="Bound">a</a><a id="1698" class="Symbol">)</a>    <a id="1703" class="Symbol">(</a><a id="1704" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1710" href="Data.These.Base.html#1710" class="Bound">c</a> <a id="1712" href="Data.These.Base.html#1712" class="Bound">d</a><a id="1713" class="Symbol">)</a> <a id="1715" class="Symbol">=</a> <a id="1717" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1723" class="Symbol">(</a><a id="1724" href="Data.These.Base.html#1687" class="Bound">f</a> <a id="1726" class="Symbol">(</a><a id="1727" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1733" href="Data.These.Base.html#1697" class="Bound">a</a> <a id="1735" href="Data.These.Base.html#1710" class="Bound">c</a><a id="1736" class="Symbol">))</a> <a id="1739" class="Symbol">(</a><a id="1740" href="Data.These.Base.html#1689" class="Bound">g</a> <a id="1742" class="Symbol">(</a><a id="1743" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1748" href="Data.These.Base.html#1712" class="Bound">d</a><a id="1749" class="Symbol">))</a>
<a id="1752" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1762" href="Data.These.Base.html#1762" class="Bound">f</a> <a id="1764" href="Data.These.Base.html#1764" class="Bound">g</a> <a id="1766" class="Symbol">(</a><a id="1767" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1772" href="Data.These.Base.html#1772" class="Bound">b</a><a id="1773" class="Symbol">)</a>    <a id="1778" class="Symbol">(</a><a id="1779" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1784" href="Data.These.Base.html#1784" class="Bound">c</a><a id="1785" class="Symbol">)</a>    <a id="1790" class="Symbol">=</a> <a id="1792" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1798" class="Symbol">(</a><a id="1799" href="Data.These.Base.html#1762" class="Bound">f</a> <a id="1801" class="Symbol">(</a><a id="1802" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1807" href="Data.These.Base.html#1784" class="Bound">c</a><a id="1808" class="Symbol">))</a> <a id="1811" class="Symbol">(</a><a id="1812" href="Data.These.Base.html#1764" class="Bound">g</a> <a id="1814" class="Symbol">(</a><a id="1815" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1820" href="Data.These.Base.html#1772" class="Bound">b</a><a id="1821" class="Symbol">))</a>
<a id="1824" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1834" href="Data.These.Base.html#1834" class="Bound">f</a> <a id="1836" href="Data.These.Base.html#1836" class="Bound">g</a> <a id="1838" class="Symbol">(</a><a id="1839" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1844" href="Data.These.Base.html#1844" class="Bound">b</a><a id="1845" class="Symbol">)</a>    <a id="1850" class="Symbol">(</a><a id="1851" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1856" href="Data.These.Base.html#1856" class="Bound">d</a><a id="1857" class="Symbol">)</a>    <a id="1862" class="Symbol">=</a> <a id="1864" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1869" class="Symbol">(</a><a id="1870" href="Data.These.Base.html#1836" class="Bound">g</a> <a id="1872" class="Symbol">(</a><a id="1873" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1879" href="Data.These.Base.html#1844" class="Bound">b</a> <a id="1881" href="Data.These.Base.html#1856" class="Bound">d</a><a id="1882" class="Symbol">))</a>
<a id="1885" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1895" href="Data.These.Base.html#1895" class="Bound">f</a> <a id="1897" href="Data.These.Base.html#1897" class="Bound">g</a> <a id="1899" class="Symbol">(</a><a id="1900" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1905" href="Data.These.Base.html#1905" class="Bound">b</a><a id="1906" class="Symbol">)</a>    <a id="1911" class="Symbol">(</a><a id="1912" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1918" href="Data.These.Base.html#1918" class="Bound">c</a> <a id="1920" href="Data.These.Base.html#1920" class="Bound">d</a><a id="1921" class="Symbol">)</a> <a id="1923" class="Symbol">=</a> <a id="1925" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1931" class="Symbol">(</a><a id="1932" href="Data.These.Base.html#1895" class="Bound">f</a> <a id="1934" class="Symbol">(</a><a id="1935" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="1940" href="Data.These.Base.html#1918" class="Bound">c</a><a id="1941" class="Symbol">))</a> <a id="1944" class="Symbol">(</a><a id="1945" href="Data.These.Base.html#1897" class="Bound">g</a> <a id="1947" class="Symbol">(</a><a id="1948" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1954" href="Data.These.Base.html#1905" class="Bound">b</a> <a id="1956" href="Data.These.Base.html#1920" class="Bound">d</a><a id="1957" class="Symbol">))</a>
<a id="1960" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="1970" href="Data.These.Base.html#1970" class="Bound">f</a> <a id="1972" href="Data.These.Base.html#1972" class="Bound">g</a> <a id="1974" class="Symbol">(</a><a id="1975" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="1981" href="Data.These.Base.html#1981" class="Bound">a</a> <a id="1983" href="Data.These.Base.html#1983" class="Bound">b</a><a id="1984" class="Symbol">)</a> <a id="1986" class="Symbol">(</a><a id="1987" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="1992" href="Data.These.Base.html#1992" class="Bound">c</a><a id="1993" class="Symbol">)</a>    <a id="1998" class="Symbol">=</a> <a id="2000" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2006" class="Symbol">(</a><a id="2007" href="Data.These.Base.html#1970" class="Bound">f</a> <a id="2009" class="Symbol">(</a><a id="2010" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2016" href="Data.These.Base.html#1981" class="Bound">a</a> <a id="2018" href="Data.These.Base.html#1992" class="Bound">c</a><a id="2019" class="Symbol">))</a> <a id="2022" class="Symbol">(</a><a id="2023" href="Data.These.Base.html#1972" class="Bound">g</a> <a id="2025" class="Symbol">(</a><a id="2026" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="2031" href="Data.These.Base.html#1983" class="Bound">b</a><a id="2032" class="Symbol">))</a>
<a id="2035" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="2045" href="Data.These.Base.html#2045" class="Bound">f</a> <a id="2047" href="Data.These.Base.html#2047" class="Bound">g</a> <a id="2049" class="Symbol">(</a><a id="2050" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2056" href="Data.These.Base.html#2056" class="Bound">a</a> <a id="2058" href="Data.These.Base.html#2058" class="Bound">b</a><a id="2059" class="Symbol">)</a> <a id="2061" class="Symbol">(</a><a id="2062" href="Data.These.Base.html#609" class="InductiveConstructor">that</a> <a id="2067" href="Data.These.Base.html#2067" class="Bound">d</a><a id="2068" class="Symbol">)</a>    <a id="2073" class="Symbol">=</a> <a id="2075" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2081" class="Symbol">(</a><a id="2082" href="Data.These.Base.html#2045" class="Bound">f</a> <a id="2084" class="Symbol">(</a><a id="2085" href="Data.These.Base.html#581" class="InductiveConstructor">this</a> <a id="2090" href="Data.These.Base.html#2056" class="Bound">a</a><a id="2091" class="Symbol">))</a> <a id="2094" class="Symbol">(</a><a id="2095" href="Data.These.Base.html#2047" class="Bound">g</a> <a id="2097" class="Symbol">(</a><a id="2098" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2104" href="Data.These.Base.html#2058" class="Bound">b</a> <a id="2106" href="Data.These.Base.html#2067" class="Bound">d</a><a id="2107" class="Symbol">))</a>
<a id="2110" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="2120" href="Data.These.Base.html#2120" class="Bound">f</a> <a id="2122" href="Data.These.Base.html#2122" class="Bound">g</a> <a id="2124" class="Symbol">(</a><a id="2125" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2131" href="Data.These.Base.html#2131" class="Bound">a</a> <a id="2133" href="Data.These.Base.html#2133" class="Bound">b</a><a id="2134" class="Symbol">)</a> <a id="2136" class="Symbol">(</a><a id="2137" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2143" href="Data.These.Base.html#2143" class="Bound">c</a> <a id="2145" href="Data.These.Base.html#2145" class="Bound">d</a><a id="2146" class="Symbol">)</a> <a id="2148" class="Symbol">=</a> <a id="2150" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2156" class="Symbol">(</a><a id="2157" href="Data.These.Base.html#2120" class="Bound">f</a> <a id="2159" class="Symbol">(</a><a id="2160" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2166" href="Data.These.Base.html#2131" class="Bound">a</a> <a id="2168" href="Data.These.Base.html#2143" class="Bound">c</a><a id="2169" class="Symbol">))</a> <a id="2172" class="Symbol">(</a><a id="2173" href="Data.These.Base.html#2122" class="Bound">g</a> <a id="2175" class="Symbol">(</a><a id="2176" href="Data.These.Base.html#637" class="InductiveConstructor">these</a> <a id="2182" href="Data.These.Base.html#2133" class="Bound">b</a> <a id="2184" href="Data.These.Base.html#2145" class="Bound">d</a><a id="2185" class="Symbol">))</a>

<a id="align"></a><a id="2189" href="Data.These.Base.html#2189" class="Function">align</a> <a id="2195" class="Symbol">:</a> <a id="2197" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="2203" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="2205" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="2207" class="Symbol">→</a> <a id="2209" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="2215" href="Data.These.Base.html#465" class="Generalizable">C</a> <a id="2217" href="Data.These.Base.html#479" class="Generalizable">D</a> <a id="2219" class="Symbol">→</a> <a id="2221" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="2227" class="Symbol">(</a><a id="2228" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="2234" href="Data.These.Base.html#437" class="Generalizable">A</a> <a id="2236" href="Data.These.Base.html#465" class="Generalizable">C</a><a id="2237" class="Symbol">)</a> <a id="2239" class="Symbol">(</a><a id="2240" href="Data.These.Base.html#523" class="Datatype">These</a> <a id="2246" href="Data.These.Base.html#451" class="Generalizable">B</a> <a id="2248" href="Data.These.Base.html#479" class="Generalizable">D</a><a id="2249" class="Symbol">)</a>
<a id="2251" href="Data.These.Base.html#2189" class="Function">align</a> <a id="2257" class="Symbol">=</a> <a id="2259" href="Data.These.Base.html#1462" class="Function">alignWith</a> <a id="2269" href="Function.html#708" class="Function">id</a> <a id="2272" href="Function.html#708" class="Function">id</a>
</pre></body></html>