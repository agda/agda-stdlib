<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Properties.Monoid.Mult</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Multiplication over a monoid (i.e. repeated addition)</a>
<a id="163" class="Comment">------------------------------------------------------------------------</a>

<a id="237" class="Symbol">{-#</a> <a id="241" class="Keyword">OPTIONS</a> <a id="249" class="Pragma">--without-K</a> <a id="261" class="Pragma">--safe</a> <a id="268" class="Symbol">#-}</a>

<a id="273" class="Keyword">open</a> <a id="278" class="Keyword">import</a> <a id="285" href="Algebra.Bundles.html" class="Module">Algebra.Bundles</a> <a id="301" class="Keyword">using</a> <a id="307" class="Symbol">(</a><a id="308" href="Algebra.Bundles.html#4421" class="Record">Monoid</a><a id="314" class="Symbol">)</a>
<a id="316" class="Keyword">open</a> <a id="321" class="Keyword">import</a> <a id="328" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="342" class="Symbol">as</a> <a id="345" class="Module">ℕ</a> <a id="347" class="Keyword">using</a> <a id="353" class="Symbol">(</a><a id="354" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a><a id="355" class="Symbol">;</a> <a id="357" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a><a id="361" class="Symbol">;</a> <a id="363" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a><a id="366" class="Symbol">;</a> <a id="368" href="Data.Nat.Base.html#2475" class="Function">NonZero</a><a id="375" class="Symbol">)</a>
<a id="377" class="Keyword">open</a> <a id="382" class="Keyword">import</a> <a id="389" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="405" class="Keyword">open</a> <a id="410" class="Keyword">import</a> <a id="417" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="455" class="Symbol">as</a> <a id="458" class="Module">P</a> <a id="460" class="Keyword">using</a> <a id="466" class="Symbol">(</a><a id="467" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">_≡_</a><a id="470" class="Symbol">)</a>

<a id="473" class="Keyword">module</a> <a id="480" href="Algebra.Properties.Monoid.Mult.html" class="Module">Algebra.Properties.Monoid.Mult</a> <a id="511" class="Symbol">{</a><a id="512" href="Algebra.Properties.Monoid.Mult.html#512" class="Bound">a</a> <a id="514" href="Algebra.Properties.Monoid.Mult.html#514" class="Bound">ℓ</a><a id="515" class="Symbol">}</a> <a id="517" class="Symbol">(</a><a id="518" href="Algebra.Properties.Monoid.Mult.html#518" class="Bound">M</a> <a id="520" class="Symbol">:</a> <a id="522" href="Algebra.Bundles.html#4421" class="Record">Monoid</a> <a id="529" href="Algebra.Properties.Monoid.Mult.html#512" class="Bound">a</a> <a id="531" href="Algebra.Properties.Monoid.Mult.html#514" class="Bound">ℓ</a><a id="532" class="Symbol">)</a> <a id="534" class="Keyword">where</a>

<a id="541" class="Comment">-- View of the monoid operator as addition</a>
<a id="584" class="Keyword">open</a> <a id="589" href="Algebra.Bundles.html#4421" class="Module">Monoid</a> <a id="596" href="Algebra.Properties.Monoid.Mult.html#518" class="Bound">M</a>
  <a id="600" class="Keyword">renaming</a>
  <a id="611" class="Symbol">(</a> <a id="613" href="Algebra.Bundles.html#4550" class="Field Operator">_∙_</a>       <a id="623" class="Symbol">to</a> <a id="626" class="Field Operator">_+_</a>
  <a id="632" class="Symbol">;</a> <a id="634" href="Algebra.Structures.html#1210" class="Function">∙-cong</a>    <a id="644" class="Symbol">to</a> <a id="647" class="Function">+-cong</a>
  <a id="656" class="Symbol">;</a> <a id="658" href="Algebra.Structures.html#1421" class="Function">∙-congʳ</a>   <a id="668" class="Symbol">to</a> <a id="671" class="Function">+-congʳ</a>
  <a id="681" class="Symbol">;</a> <a id="683" href="Algebra.Structures.html#1360" class="Function">∙-congˡ</a>   <a id="693" class="Symbol">to</a> <a id="696" class="Function">+-congˡ</a>
  <a id="706" class="Symbol">;</a> <a id="708" href="Algebra.Structures.html#2963" class="Function">identityˡ</a> <a id="718" class="Symbol">to</a> <a id="721" class="Function">+-identityˡ</a>
  <a id="735" class="Symbol">;</a> <a id="737" href="Algebra.Structures.html#3024" class="Function">identityʳ</a> <a id="747" class="Symbol">to</a> <a id="750" class="Function">+-identityʳ</a>
  <a id="764" class="Symbol">;</a> <a id="766" href="Algebra.Structures.html#1867" class="Function">assoc</a>     <a id="776" class="Symbol">to</a> <a id="779" class="Function">+-assoc</a>
  <a id="789" class="Symbol">;</a> <a id="791" href="Algebra.Bundles.html#4577" class="Field">ε</a>         <a id="801" class="Symbol">to</a> <a id="804" class="Field">0#</a>
  <a id="809" class="Symbol">)</a>

<a id="812" class="Keyword">open</a> <a id="817" class="Keyword">import</a> <a id="824" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="857" href="Algebra.Structures.html#1285" class="Function">setoid</a>

<a id="865" class="Keyword">open</a> <a id="870" class="Keyword">import</a> <a id="877" href="Algebra.Definitions.html" class="Module">Algebra.Definitions</a> <a id="897" href="Algebra.Bundles.html#4521" class="Field Operator">_≈_</a>

<a id="902" class="Comment">------------------------------------------------------------------------</a>
<a id="975" class="Comment">-- Definition</a>

<a id="990" class="Keyword">open</a> <a id="995" class="Keyword">import</a> <a id="1002" href="Algebra.Definitions.RawMonoid.html" class="Module">Algebra.Definitions.RawMonoid</a> <a id="1032" href="Algebra.Bundles.html#4814" class="Function">rawMonoid</a> <a id="1042" class="Keyword">public</a>
  <a id="1051" class="Keyword">using</a> <a id="1057" class="Symbol">(</a><a id="1058" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">_×_</a><a id="1061" class="Symbol">)</a>

<a id="1064" class="Comment">------------------------------------------------------------------------</a>
<a id="1137" class="Comment">-- Properties of _×_</a>

<a id="×-congʳ"></a><a id="1159" href="Algebra.Properties.Monoid.Mult.html#1159" class="Function">×-congʳ</a> <a id="1167" class="Symbol">:</a> <a id="1169" class="Symbol">∀</a> <a id="1171" href="Algebra.Properties.Monoid.Mult.html#1171" class="Bound">n</a> <a id="1173" class="Symbol">→</a> <a id="1175" class="Symbol">(</a><a id="1176" href="Algebra.Properties.Monoid.Mult.html#1171" class="Bound">n</a> <a id="1178" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×_</a><a id="1180" class="Symbol">)</a> <a id="1182" href="Relation.Binary.Core.html#1563" class="Function Operator">Preserves</a> <a id="1192" href="Algebra.Bundles.html#4521" class="Field Operator">_≈_</a> <a id="1196" href="Relation.Binary.Core.html#1563" class="Function Operator">⟶</a> <a id="1198" href="Algebra.Bundles.html#4521" class="Field Operator">_≈_</a>
<a id="1202" href="Algebra.Properties.Monoid.Mult.html#1159" class="Function">×-congʳ</a> <a id="1210" class="Number">0</a>       <a id="1218" href="Algebra.Properties.Monoid.Mult.html#1218" class="Bound">x≈x′</a> <a id="1223" class="Symbol">=</a> <a id="1225" href="Relation.Binary.Structures.html#1568" class="Function">refl</a>
<a id="1230" href="Algebra.Properties.Monoid.Mult.html#1159" class="Function">×-congʳ</a> <a id="1238" class="Symbol">(</a><a id="1239" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1243" href="Algebra.Properties.Monoid.Mult.html#1243" class="Bound">n</a><a id="1244" class="Symbol">)</a> <a id="1246" href="Algebra.Properties.Monoid.Mult.html#1246" class="Bound">x≈x′</a> <a id="1251" class="Symbol">=</a> <a id="1253" href="Algebra.Properties.Monoid.Mult.html#647" class="Function">+-cong</a> <a id="1260" href="Algebra.Properties.Monoid.Mult.html#1246" class="Bound">x≈x′</a> <a id="1265" class="Symbol">(</a><a id="1266" href="Algebra.Properties.Monoid.Mult.html#1159" class="Function">×-congʳ</a> <a id="1274" href="Algebra.Properties.Monoid.Mult.html#1243" class="Bound">n</a> <a id="1276" href="Algebra.Properties.Monoid.Mult.html#1246" class="Bound">x≈x′</a><a id="1280" class="Symbol">)</a>

<a id="×-cong"></a><a id="1283" href="Algebra.Properties.Monoid.Mult.html#1283" class="Function">×-cong</a> <a id="1290" class="Symbol">:</a> <a id="1292" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">_×_</a> <a id="1296" href="Relation.Binary.Core.html#1689" class="Function Operator">Preserves₂</a> <a id="1307" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">_≡_</a> <a id="1311" href="Relation.Binary.Core.html#1689" class="Function Operator">⟶</a> <a id="1313" href="Algebra.Bundles.html#4521" class="Field Operator">_≈_</a> <a id="1317" href="Relation.Binary.Core.html#1689" class="Function Operator">⟶</a> <a id="1319" href="Algebra.Bundles.html#4521" class="Field Operator">_≈_</a>
<a id="1323" href="Algebra.Properties.Monoid.Mult.html#1283" class="Function">×-cong</a> <a id="1330" class="Symbol">{</a><a id="1331" href="Algebra.Properties.Monoid.Mult.html#1331" class="Bound">n</a><a id="1332" class="Symbol">}</a> <a id="1334" href="Agda.Builtin.Equality.html#208" class="InductiveConstructor">P.refl</a> <a id="1341" href="Algebra.Properties.Monoid.Mult.html#1341" class="Bound">x≈x′</a> <a id="1346" class="Symbol">=</a> <a id="1348" href="Algebra.Properties.Monoid.Mult.html#1159" class="Function">×-congʳ</a> <a id="1356" href="Algebra.Properties.Monoid.Mult.html#1331" class="Bound">n</a> <a id="1358" href="Algebra.Properties.Monoid.Mult.html#1341" class="Bound">x≈x′</a>

<a id="1364" class="Comment">-- _×_ is homomorphic with respect to _ℕ+_/_+_.</a>

<a id="×-homo-+"></a><a id="1413" href="Algebra.Properties.Monoid.Mult.html#1413" class="Function">×-homo-+</a> <a id="1422" class="Symbol">:</a> <a id="1424" class="Symbol">∀</a> <a id="1426" href="Algebra.Properties.Monoid.Mult.html#1426" class="Bound">x</a> <a id="1428" href="Algebra.Properties.Monoid.Mult.html#1428" class="Bound">m</a> <a id="1430" href="Algebra.Properties.Monoid.Mult.html#1430" class="Bound">n</a> <a id="1432" class="Symbol">→</a> <a id="1434" class="Symbol">(</a><a id="1435" href="Algebra.Properties.Monoid.Mult.html#1428" class="Bound">m</a> <a id="1437" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">ℕ.+</a> <a id="1441" href="Algebra.Properties.Monoid.Mult.html#1430" class="Bound">n</a><a id="1442" class="Symbol">)</a> <a id="1444" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1446" href="Algebra.Properties.Monoid.Mult.html#1426" class="Bound">x</a> <a id="1448" href="Algebra.Bundles.html#4521" class="Field Operator">≈</a> <a id="1450" href="Algebra.Properties.Monoid.Mult.html#1428" class="Bound">m</a> <a id="1452" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1454" href="Algebra.Properties.Monoid.Mult.html#1426" class="Bound">x</a> <a id="1456" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1458" href="Algebra.Properties.Monoid.Mult.html#1430" class="Bound">n</a> <a id="1460" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1462" href="Algebra.Properties.Monoid.Mult.html#1426" class="Bound">x</a>
<a id="1464" href="Algebra.Properties.Monoid.Mult.html#1413" class="Function">×-homo-+</a> <a id="1473" href="Algebra.Properties.Monoid.Mult.html#1473" class="Bound">x</a> <a id="1475" class="Number">0</a>       <a id="1483" href="Algebra.Properties.Monoid.Mult.html#1483" class="Bound">n</a> <a id="1485" class="Symbol">=</a> <a id="1487" href="Relation.Binary.Structures.html#1594" class="Function">sym</a> <a id="1491" class="Symbol">(</a><a id="1492" href="Algebra.Properties.Monoid.Mult.html#721" class="Function">+-identityˡ</a> <a id="1504" class="Symbol">(</a><a id="1505" href="Algebra.Properties.Monoid.Mult.html#1483" class="Bound">n</a> <a id="1507" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1509" href="Algebra.Properties.Monoid.Mult.html#1473" class="Bound">x</a><a id="1510" class="Symbol">))</a>
<a id="1513" href="Algebra.Properties.Monoid.Mult.html#1413" class="Function">×-homo-+</a> <a id="1522" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1524" class="Symbol">(</a><a id="1525" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1529" href="Algebra.Properties.Monoid.Mult.html#1529" class="Bound">m</a><a id="1530" class="Symbol">)</a> <a id="1532" href="Algebra.Properties.Monoid.Mult.html#1532" class="Bound">n</a> <a id="1534" class="Symbol">=</a> <a id="1536" href="Relation.Binary.Reasoning.Base.Single.html#1916" class="Function Operator">begin</a>
  <a id="1544" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1546" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1548" class="Symbol">(</a><a id="1549" href="Algebra.Properties.Monoid.Mult.html#1529" class="Bound">m</a> <a id="1551" href="Agda.Builtin.Nat.html#325" class="Primitive Operator">ℕ.+</a> <a id="1555" href="Algebra.Properties.Monoid.Mult.html#1532" class="Bound">n</a><a id="1556" class="Symbol">)</a> <a id="1558" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1560" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a>    <a id="1565" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="1568" href="Algebra.Properties.Monoid.Mult.html#647" class="Function">+-cong</a> <a id="1575" href="Relation.Binary.Structures.html#1568" class="Function">refl</a> <a id="1580" class="Symbol">(</a><a id="1581" href="Algebra.Properties.Monoid.Mult.html#1413" class="Function">×-homo-+</a> <a id="1590" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1592" href="Algebra.Properties.Monoid.Mult.html#1529" class="Bound">m</a> <a id="1594" href="Algebra.Properties.Monoid.Mult.html#1532" class="Bound">n</a><a id="1595" class="Symbol">)</a> <a id="1597" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
  <a id="1601" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1603" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1605" class="Symbol">(</a><a id="1606" href="Algebra.Properties.Monoid.Mult.html#1529" class="Bound">m</a> <a id="1608" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1610" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1612" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1614" href="Algebra.Properties.Monoid.Mult.html#1532" class="Bound">n</a> <a id="1616" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1618" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a><a id="1619" class="Symbol">)</a>  <a id="1622" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="1625" href="Relation.Binary.Structures.html#1594" class="Function">sym</a> <a id="1629" class="Symbol">(</a><a id="1630" href="Algebra.Properties.Monoid.Mult.html#779" class="Function">+-assoc</a> <a id="1638" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1640" class="Symbol">(</a><a id="1641" href="Algebra.Properties.Monoid.Mult.html#1529" class="Bound">m</a> <a id="1643" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1645" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a><a id="1646" class="Symbol">)</a> <a id="1648" class="Symbol">(</a><a id="1649" href="Algebra.Properties.Monoid.Mult.html#1532" class="Bound">n</a> <a id="1651" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1653" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a><a id="1654" class="Symbol">))</a> <a id="1657" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
  <a id="1661" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1663" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1665" href="Algebra.Properties.Monoid.Mult.html#1529" class="Bound">m</a> <a id="1667" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1669" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a> <a id="1671" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1673" href="Algebra.Properties.Monoid.Mult.html#1532" class="Bound">n</a> <a id="1675" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1677" href="Algebra.Properties.Monoid.Mult.html#1522" class="Bound">x</a>    <a id="1682" href="Relation.Binary.Reasoning.Base.Single.html#2555" class="Function Operator">∎</a>

<a id="×-idem"></a><a id="1685" href="Algebra.Properties.Monoid.Mult.html#1685" class="Function">×-idem</a> <a id="1692" class="Symbol">:</a> <a id="1694" class="Symbol">∀</a> <a id="1696" class="Symbol">{</a><a id="1697" href="Algebra.Properties.Monoid.Mult.html#1697" class="Bound">c</a><a id="1698" class="Symbol">}</a> <a id="1700" class="Symbol">→</a> <a id="1702" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">_+_</a> <a id="1706" href="Algebra.Definitions.html#2578" class="Function Operator">IdempotentOn</a> <a id="1719" href="Algebra.Properties.Monoid.Mult.html#1697" class="Bound">c</a> <a id="1721" class="Symbol">→</a>
         <a id="1732" class="Symbol">∀</a> <a id="1734" href="Algebra.Properties.Monoid.Mult.html#1734" class="Bound">n</a> <a id="1736" class="Symbol">→</a> <a id="1738" class="Symbol">.{{</a><a id="1741" href="Algebra.Properties.Monoid.Mult.html#1741" class="Bound">_</a> <a id="1743" class="Symbol">:</a> <a id="1745" href="Data.Nat.Base.html#2475" class="Function">NonZero</a> <a id="1753" href="Algebra.Properties.Monoid.Mult.html#1734" class="Bound">n</a><a id="1754" class="Symbol">}}</a> <a id="1757" class="Symbol">→</a> <a id="1759" href="Algebra.Properties.Monoid.Mult.html#1734" class="Bound">n</a> <a id="1761" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1763" href="Algebra.Properties.Monoid.Mult.html#1697" class="Bound">c</a> <a id="1765" href="Algebra.Bundles.html#4521" class="Field Operator">≈</a> <a id="1767" href="Algebra.Properties.Monoid.Mult.html#1697" class="Bound">c</a>
<a id="1769" href="Algebra.Properties.Monoid.Mult.html#1685" class="Function">×-idem</a> <a id="1776" class="Symbol">{</a><a id="1777" href="Algebra.Properties.Monoid.Mult.html#1777" class="Bound">c</a><a id="1778" class="Symbol">}</a> <a id="1780" href="Algebra.Properties.Monoid.Mult.html#1780" class="Bound">idem</a> <a id="1785" class="Symbol">(</a><a id="1786" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1790" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a><a id="1794" class="Symbol">)</a>    <a id="1799" class="Symbol">=</a> <a id="1801" href="Algebra.Properties.Monoid.Mult.html#750" class="Function">+-identityʳ</a> <a id="1813" href="Algebra.Properties.Monoid.Mult.html#1777" class="Bound">c</a>
<a id="1815" href="Algebra.Properties.Monoid.Mult.html#1685" class="Function">×-idem</a> <a id="1822" class="Symbol">{</a><a id="1823" href="Algebra.Properties.Monoid.Mult.html#1823" class="Bound">c</a><a id="1824" class="Symbol">}</a> <a id="1826" href="Algebra.Properties.Monoid.Mult.html#1826" class="Bound">idem</a> <a id="1831" class="Symbol">(</a><a id="1832" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1836" class="Symbol">(</a><a id="1837" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1841" href="Algebra.Properties.Monoid.Mult.html#1841" class="Bound">n</a><a id="1842" class="Symbol">))</a> <a id="1845" class="Symbol">=</a> <a id="1847" href="Relation.Binary.Reasoning.Base.Single.html#1916" class="Function Operator">begin</a>
  <a id="1855" href="Algebra.Properties.Monoid.Mult.html#1823" class="Bound">c</a> <a id="1857" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1859" class="Symbol">(</a><a id="1860" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1864" href="Algebra.Properties.Monoid.Mult.html#1841" class="Bound">n</a> <a id="1866" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1868" href="Algebra.Properties.Monoid.Mult.html#1823" class="Bound">c</a><a id="1869" class="Symbol">)</a> <a id="1871" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="1874" href="Algebra.Properties.Monoid.Mult.html#696" class="Function">+-congˡ</a> <a id="1882" class="Symbol">(</a><a id="1883" href="Algebra.Properties.Monoid.Mult.html#1685" class="Function">×-idem</a> <a id="1890" href="Algebra.Properties.Monoid.Mult.html#1826" class="Bound">idem</a> <a id="1895" class="Symbol">(</a><a id="1896" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="1900" href="Algebra.Properties.Monoid.Mult.html#1841" class="Bound">n</a><a id="1901" class="Symbol">)</a> <a id="1903" class="Symbol">)</a> <a id="1905" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
  <a id="1909" href="Algebra.Properties.Monoid.Mult.html#1823" class="Bound">c</a> <a id="1911" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="1913" href="Algebra.Properties.Monoid.Mult.html#1823" class="Bound">c</a>           <a id="1925" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="1928" href="Algebra.Properties.Monoid.Mult.html#1826" class="Bound">idem</a> <a id="1933" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
  <a id="1937" href="Algebra.Properties.Monoid.Mult.html#1823" class="Bound">c</a>               <a id="1953" href="Relation.Binary.Reasoning.Base.Single.html#2555" class="Function Operator">∎</a>

<a id="×-assocˡ"></a><a id="1956" href="Algebra.Properties.Monoid.Mult.html#1956" class="Function">×-assocˡ</a> <a id="1965" class="Symbol">:</a> <a id="1967" class="Symbol">∀</a> <a id="1969" href="Algebra.Properties.Monoid.Mult.html#1969" class="Bound">x</a> <a id="1971" href="Algebra.Properties.Monoid.Mult.html#1971" class="Bound">m</a> <a id="1973" href="Algebra.Properties.Monoid.Mult.html#1973" class="Bound">n</a> <a id="1975" class="Symbol">→</a> <a id="1977" href="Algebra.Properties.Monoid.Mult.html#1971" class="Bound">m</a> <a id="1979" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1981" class="Symbol">(</a><a id="1982" href="Algebra.Properties.Monoid.Mult.html#1973" class="Bound">n</a> <a id="1984" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="1986" href="Algebra.Properties.Monoid.Mult.html#1969" class="Bound">x</a><a id="1987" class="Symbol">)</a> <a id="1989" href="Algebra.Bundles.html#4521" class="Field Operator">≈</a> <a id="1991" class="Symbol">(</a><a id="1992" href="Algebra.Properties.Monoid.Mult.html#1971" class="Bound">m</a> <a id="1994" href="Agda.Builtin.Nat.html#528" class="Primitive Operator">ℕ.*</a> <a id="1998" href="Algebra.Properties.Monoid.Mult.html#1973" class="Bound">n</a><a id="1999" class="Symbol">)</a> <a id="2001" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2003" href="Algebra.Properties.Monoid.Mult.html#1969" class="Bound">x</a>
<a id="2005" href="Algebra.Properties.Monoid.Mult.html#1956" class="Function">×-assocˡ</a> <a id="2014" href="Algebra.Properties.Monoid.Mult.html#2014" class="Bound">x</a> <a id="2016" href="Agda.Builtin.Nat.html#210" class="InductiveConstructor">zero</a>    <a id="2024" href="Algebra.Properties.Monoid.Mult.html#2024" class="Bound">n</a> <a id="2026" class="Symbol">=</a> <a id="2028" href="Relation.Binary.Structures.html#1568" class="Function">refl</a>
<a id="2033" href="Algebra.Properties.Monoid.Mult.html#1956" class="Function">×-assocˡ</a> <a id="2042" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a> <a id="2044" class="Symbol">(</a><a id="2045" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2049" href="Algebra.Properties.Monoid.Mult.html#2049" class="Bound">m</a><a id="2050" class="Symbol">)</a> <a id="2052" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a> <a id="2054" class="Symbol">=</a> <a id="2056" href="Relation.Binary.Reasoning.Base.Single.html#1916" class="Function Operator">begin</a>
  <a id="2064" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a> <a id="2066" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2068" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a> <a id="2070" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="2072" href="Algebra.Properties.Monoid.Mult.html#2049" class="Bound">m</a> <a id="2074" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2076" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a> <a id="2078" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2080" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a>     <a id="2086" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">≈⟨</a> <a id="2089" href="Algebra.Properties.Monoid.Mult.html#696" class="Function">+-congˡ</a> <a id="2097" class="Symbol">(</a><a id="2098" href="Algebra.Properties.Monoid.Mult.html#1956" class="Function">×-assocˡ</a> <a id="2107" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a> <a id="2109" href="Algebra.Properties.Monoid.Mult.html#2049" class="Bound">m</a> <a id="2111" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a><a id="2112" class="Symbol">)</a> <a id="2114" href="Relation.Binary.Reasoning.Setoid.html#1052" class="Function">⟩</a>
  <a id="2118" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a> <a id="2120" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2122" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a> <a id="2124" href="Algebra.Properties.Monoid.Mult.html#626" class="Field Operator">+</a> <a id="2126" class="Symbol">(</a><a id="2127" href="Algebra.Properties.Monoid.Mult.html#2049" class="Bound">m</a> <a id="2129" href="Agda.Builtin.Nat.html#528" class="Primitive Operator">ℕ.*</a> <a id="2133" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a><a id="2134" class="Symbol">)</a> <a id="2136" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2138" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a> <a id="2140" href="Relation.Binary.Reasoning.Setoid.html#1153" class="Function">≈˘⟨</a> <a id="2144" href="Algebra.Properties.Monoid.Mult.html#1413" class="Function">×-homo-+</a> <a id="2153" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a> <a id="2155" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a> <a id="2157" class="Symbol">(</a><a id="2158" href="Algebra.Properties.Monoid.Mult.html#2049" class="Bound">m</a> <a id="2160" href="Agda.Builtin.Nat.html#528" class="Primitive Operator">ℕ.*</a> <a id="2164" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a><a id="2165" class="Symbol">)</a> <a id="2167" href="Relation.Binary.Reasoning.Setoid.html#1153" class="Function">⟩</a>
  <a id="2171" class="Symbol">(</a><a id="2172" href="Agda.Builtin.Nat.html#223" class="InductiveConstructor">suc</a> <a id="2176" href="Algebra.Properties.Monoid.Mult.html#2049" class="Bound">m</a> <a id="2178" href="Agda.Builtin.Nat.html#528" class="Primitive Operator">ℕ.*</a> <a id="2182" href="Algebra.Properties.Monoid.Mult.html#2052" class="Bound">n</a><a id="2183" class="Symbol">)</a> <a id="2185" href="Algebra.Definitions.RawMonoid.html#1080" class="Function Operator">×</a> <a id="2187" href="Algebra.Properties.Monoid.Mult.html#2042" class="Bound">x</a>     <a id="2193" href="Relation.Binary.Reasoning.Base.Single.html#2555" class="Function Operator">∎</a>
</pre></body></html>