<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.DifferenceList</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Lists with fast append</a>
<a id="132" class="Comment">------------------------------------------------------------------------</a>

<a id="206" class="Symbol">{-#</a> <a id="210" class="Keyword">OPTIONS</a> <a id="218" class="Pragma">--cubical-compatible</a> <a id="239" class="Pragma">--safe</a> <a id="246" class="Symbol">#-}</a>

<a id="251" class="Keyword">module</a> <a id="258" href="Data.DifferenceList.html" class="Module">Data.DifferenceList</a> <a id="278" class="Keyword">where</a>

<a id="285" class="Keyword">open</a> <a id="290" class="Keyword">import</a> <a id="297" href="Level.html" class="Module">Level</a> <a id="303" class="Keyword">using</a> <a id="309" class="Symbol">(</a><a id="310" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="315" class="Symbol">)</a>
<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="344" class="Symbol">as</a> <a id="347" class="Module">List</a> <a id="352" class="Keyword">using</a> <a id="358" class="Symbol">(</a><a id="359" href="Agda.Builtin.List.html#147" class="Datatype">List</a><a id="363" class="Symbol">)</a>
<a id="365" class="Keyword">open</a> <a id="370" class="Keyword">import</a> <a id="377" href="Function.Base.html" class="Module">Function.Base</a> <a id="391" class="Keyword">using</a> <a id="397" class="Symbol">(</a><a id="398" href="Function.Base.html#4322" class="Function Operator">_⟨_⟩_</a><a id="403" class="Symbol">)</a>
<a id="405" class="Keyword">open</a> <a id="410" class="Keyword">import</a> <a id="417" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="431" class="Keyword">using</a> <a id="437" class="Symbol">(</a><a id="438" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="439" class="Symbol">)</a>

<a id="442" class="Keyword">private</a>
  <a id="452" class="Keyword">variable</a>
    <a id="465" href="Data.DifferenceList.html#465" class="Generalizable">a</a> <a id="467" href="Data.DifferenceList.html#467" class="Generalizable">b</a> <a id="469" class="Symbol">:</a> <a id="471" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="481" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="483" class="Symbol">:</a> <a id="485" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="489" href="Data.DifferenceList.html#465" class="Generalizable">a</a>
    <a id="495" href="Data.DifferenceList.html#495" class="Generalizable">B</a> <a id="497" class="Symbol">:</a> <a id="499" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="503" href="Data.DifferenceList.html#467" class="Generalizable">b</a>

<a id="506" class="Comment">------------------------------------------------------------------------</a>
<a id="579" class="Comment">-- Type definition and list function lifting</a>

<a id="DiffList"></a><a id="625" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="634" class="Symbol">:</a> <a id="636" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="640" href="Data.DifferenceList.html#465" class="Generalizable">a</a> <a id="642" class="Symbol">→</a> <a id="644" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="648" href="Data.DifferenceList.html#465" class="Generalizable">a</a>
<a id="650" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="659" href="Data.DifferenceList.html#659" class="Bound">A</a> <a id="661" class="Symbol">=</a> <a id="663" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="668" href="Data.DifferenceList.html#659" class="Bound">A</a> <a id="670" class="Symbol">→</a> <a id="672" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="677" href="Data.DifferenceList.html#659" class="Bound">A</a>

<a id="lift"></a><a id="680" href="Data.DifferenceList.html#680" class="Function">lift</a> <a id="685" class="Symbol">:</a> <a id="687" class="Symbol">(</a><a id="688" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="693" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="695" class="Symbol">→</a> <a id="697" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="702" href="Data.DifferenceList.html#481" class="Generalizable">A</a><a id="703" class="Symbol">)</a> <a id="705" class="Symbol">→</a> <a id="707" class="Symbol">(</a><a id="708" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="717" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="719" class="Symbol">→</a> <a id="721" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="730" href="Data.DifferenceList.html#481" class="Generalizable">A</a><a id="731" class="Symbol">)</a>
<a id="733" href="Data.DifferenceList.html#680" class="Function">lift</a> <a id="738" href="Data.DifferenceList.html#738" class="Bound">f</a> <a id="740" href="Data.DifferenceList.html#740" class="Bound">xs</a> <a id="743" class="Symbol">=</a> <a id="745" class="Symbol">λ</a> <a id="747" href="Data.DifferenceList.html#747" class="Bound">k</a> <a id="749" class="Symbol">→</a> <a id="751" href="Data.DifferenceList.html#738" class="Bound">f</a> <a id="753" class="Symbol">(</a><a id="754" href="Data.DifferenceList.html#740" class="Bound">xs</a> <a id="757" href="Data.DifferenceList.html#747" class="Bound">k</a><a id="758" class="Symbol">)</a>

<a id="761" class="Comment">------------------------------------------------------------------------</a>
<a id="834" class="Comment">-- Building difference lists</a>

<a id="864" class="Keyword">infixr</a> <a id="871" class="Number">5</a> <a id="873" href="Data.DifferenceList.html#913" class="Function Operator">_∷_</a> <a id="877" href="Data.DifferenceList.html#1010" class="Function Operator">_++_</a>

<a id="[]"></a><a id="883" href="Data.DifferenceList.html#883" class="Function">[]</a> <a id="886" class="Symbol">:</a> <a id="888" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="897" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="899" href="Data.DifferenceList.html#883" class="Function">[]</a> <a id="902" class="Symbol">=</a> <a id="904" class="Symbol">λ</a> <a id="906" href="Data.DifferenceList.html#906" class="Bound">k</a> <a id="908" class="Symbol">→</a> <a id="910" href="Data.DifferenceList.html#906" class="Bound">k</a>

<a id="_∷_"></a><a id="913" href="Data.DifferenceList.html#913" class="Function Operator">_∷_</a> <a id="917" class="Symbol">:</a> <a id="919" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="921" class="Symbol">→</a> <a id="923" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="932" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="934" class="Symbol">→</a> <a id="936" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="945" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="947" href="Data.DifferenceList.html#913" class="Function Operator">_∷_</a> <a id="951" href="Data.DifferenceList.html#951" class="Bound">x</a> <a id="953" class="Symbol">=</a> <a id="955" href="Data.DifferenceList.html#680" class="Function">lift</a> <a id="960" class="Symbol">(</a><a id="961" href="Data.DifferenceList.html#951" class="Bound">x</a> <a id="963" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">List.∷_</a><a id="970" class="Symbol">)</a>

<a id="[_]"></a><a id="973" href="Data.DifferenceList.html#973" class="Function Operator">[_]</a> <a id="977" class="Symbol">:</a> <a id="979" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="981" class="Symbol">→</a> <a id="983" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="992" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="994" href="Data.DifferenceList.html#973" class="Function Operator">[</a> <a id="996" href="Data.DifferenceList.html#996" class="Bound">x</a> <a id="998" href="Data.DifferenceList.html#973" class="Function Operator">]</a> <a id="1000" class="Symbol">=</a> <a id="1002" href="Data.DifferenceList.html#996" class="Bound">x</a> <a id="1004" href="Data.DifferenceList.html#913" class="Function Operator">∷</a> <a id="1006" href="Data.DifferenceList.html#883" class="Function">[]</a>

<a id="_++_"></a><a id="1010" href="Data.DifferenceList.html#1010" class="Function Operator">_++_</a> <a id="1015" class="Symbol">:</a> <a id="1017" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1026" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1028" class="Symbol">→</a> <a id="1030" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1039" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1041" class="Symbol">→</a> <a id="1043" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1052" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="1054" href="Data.DifferenceList.html#1054" class="Bound">xs</a> <a id="1057" href="Data.DifferenceList.html#1010" class="Function Operator">++</a> <a id="1060" href="Data.DifferenceList.html#1060" class="Bound">ys</a> <a id="1063" class="Symbol">=</a> <a id="1065" class="Symbol">λ</a> <a id="1067" href="Data.DifferenceList.html#1067" class="Bound">k</a> <a id="1069" class="Symbol">→</a> <a id="1071" href="Data.DifferenceList.html#1054" class="Bound">xs</a> <a id="1074" class="Symbol">(</a><a id="1075" href="Data.DifferenceList.html#1060" class="Bound">ys</a> <a id="1078" href="Data.DifferenceList.html#1067" class="Bound">k</a><a id="1079" class="Symbol">)</a>

<a id="1082" class="Keyword">infixl</a> <a id="1089" class="Number">6</a> <a id="1091" href="Data.DifferenceList.html#1096" class="Function Operator">_∷ʳ_</a>
<a id="_∷ʳ_"></a><a id="1096" href="Data.DifferenceList.html#1096" class="Function Operator">_∷ʳ_</a> <a id="1101" class="Symbol">:</a> <a id="1103" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1112" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1114" class="Symbol">→</a> <a id="1116" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1118" class="Symbol">→</a> <a id="1120" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1129" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="1131" href="Data.DifferenceList.html#1131" class="Bound">xs</a> <a id="1134" href="Data.DifferenceList.html#1096" class="Function Operator">∷ʳ</a> <a id="1137" href="Data.DifferenceList.html#1137" class="Bound">x</a> <a id="1139" class="Symbol">=</a> <a id="1141" class="Symbol">λ</a> <a id="1143" href="Data.DifferenceList.html#1143" class="Bound">k</a> <a id="1145" class="Symbol">→</a> <a id="1147" href="Data.DifferenceList.html#1131" class="Bound">xs</a> <a id="1150" class="Symbol">(</a><a id="1151" href="Data.DifferenceList.html#1137" class="Bound">x</a> <a id="1153" href="Agda.Builtin.List.html#199" class="InductiveConstructor Operator">List.∷</a> <a id="1160" href="Data.DifferenceList.html#1143" class="Bound">k</a><a id="1161" class="Symbol">)</a>

<a id="1164" class="Comment">------------------------------------------------------------------------</a>
<a id="1237" class="Comment">-- Conversion back and forth with List</a>

<a id="toList"></a><a id="1277" href="Data.DifferenceList.html#1277" class="Function">toList</a> <a id="1284" class="Symbol">:</a> <a id="1286" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1295" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1297" class="Symbol">→</a> <a id="1299" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1304" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="1306" href="Data.DifferenceList.html#1277" class="Function">toList</a> <a id="1313" href="Data.DifferenceList.html#1313" class="Bound">xs</a> <a id="1316" class="Symbol">=</a> <a id="1318" href="Data.DifferenceList.html#1313" class="Bound">xs</a> <a id="1321" href="Agda.Builtin.List.html#184" class="InductiveConstructor">List.[]</a>

<a id="1330" class="Comment">-- fromList xs is linear in the length of xs.</a>

<a id="fromList"></a><a id="1377" href="Data.DifferenceList.html#1377" class="Function">fromList</a> <a id="1386" class="Symbol">:</a> <a id="1388" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1393" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1395" class="Symbol">→</a> <a id="1397" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1406" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="1408" href="Data.DifferenceList.html#1377" class="Function">fromList</a> <a id="1417" href="Data.DifferenceList.html#1417" class="Bound">xs</a> <a id="1420" class="Symbol">=</a> <a id="1422" class="Symbol">λ</a> <a id="1424" href="Data.DifferenceList.html#1424" class="Bound">k</a> <a id="1426" class="Symbol">→</a> <a id="1428" href="Data.DifferenceList.html#1417" class="Bound">xs</a> <a id="1431" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="1433" href="Data.List.Base.html#1712" class="Function Operator">List._++_</a> <a id="1443" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="1445" href="Data.DifferenceList.html#1424" class="Bound">k</a>

<a id="1448" class="Comment">------------------------------------------------------------------------</a>
<a id="1521" class="Comment">-- Transforming difference lists</a>

<a id="1555" class="Comment">-- It is OK to use List._++_ here, since map is linear in the length of</a>
<a id="1627" class="Comment">-- the list anyway.</a>

<a id="map"></a><a id="1648" href="Data.DifferenceList.html#1648" class="Function">map</a> <a id="1652" class="Symbol">:</a> <a id="1654" class="Symbol">(</a><a id="1655" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1657" class="Symbol">→</a> <a id="1659" href="Data.DifferenceList.html#495" class="Generalizable">B</a><a id="1660" class="Symbol">)</a> <a id="1662" class="Symbol">→</a> <a id="1664" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1673" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1675" class="Symbol">→</a> <a id="1677" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1686" href="Data.DifferenceList.html#495" class="Generalizable">B</a>
<a id="1688" href="Data.DifferenceList.html#1648" class="Function">map</a> <a id="1692" href="Data.DifferenceList.html#1692" class="Bound">f</a> <a id="1694" href="Data.DifferenceList.html#1694" class="Bound">xs</a> <a id="1697" class="Symbol">=</a> <a id="1699" class="Symbol">λ</a> <a id="1701" href="Data.DifferenceList.html#1701" class="Bound">k</a> <a id="1703" class="Symbol">→</a> <a id="1705" href="Data.List.Base.html#1612" class="Function">List.map</a> <a id="1714" href="Data.DifferenceList.html#1692" class="Bound">f</a> <a id="1716" class="Symbol">(</a><a id="1717" href="Data.DifferenceList.html#1277" class="Function">toList</a> <a id="1724" href="Data.DifferenceList.html#1694" class="Bound">xs</a><a id="1726" class="Symbol">)</a> <a id="1728" href="Function.Base.html#4322" class="Function Operator">⟨</a> <a id="1730" href="Data.List.Base.html#1712" class="Function Operator">List._++_</a> <a id="1740" href="Function.Base.html#4322" class="Function Operator">⟩</a> <a id="1742" href="Data.DifferenceList.html#1701" class="Bound">k</a>

<a id="1745" class="Comment">-- concat is linear in the length of the outer list.</a>

<a id="concat"></a><a id="1799" href="Data.DifferenceList.html#1799" class="Function">concat</a> <a id="1806" class="Symbol">:</a> <a id="1808" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1817" class="Symbol">(</a><a id="1818" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1827" href="Data.DifferenceList.html#481" class="Generalizable">A</a><a id="1828" class="Symbol">)</a> <a id="1830" class="Symbol">→</a> <a id="1832" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1841" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="1843" href="Data.DifferenceList.html#1799" class="Function">concat</a> <a id="1850" href="Data.DifferenceList.html#1850" class="Bound">xs</a> <a id="1853" class="Symbol">=</a> <a id="1855" href="Data.DifferenceList.html#1885" class="Function">concat′</a> <a id="1863" class="Symbol">(</a><a id="1864" href="Data.DifferenceList.html#1277" class="Function">toList</a> <a id="1871" href="Data.DifferenceList.html#1850" class="Bound">xs</a><a id="1873" class="Symbol">)</a>
  <a id="1877" class="Keyword">where</a>
  <a id="1885" href="Data.DifferenceList.html#1885" class="Function">concat′</a> <a id="1893" class="Symbol">:</a> <a id="1895" href="Agda.Builtin.List.html#147" class="Datatype">List</a> <a id="1900" class="Symbol">(</a><a id="1901" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1910" href="Data.DifferenceList.html#481" class="Generalizable">A</a><a id="1911" class="Symbol">)</a> <a id="1913" class="Symbol">→</a> <a id="1915" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1924" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
  <a id="1928" href="Data.DifferenceList.html#1885" class="Function">concat′</a> <a id="1936" class="Symbol">=</a> <a id="1938" href="Data.List.Base.html#4126" class="Function">List.foldr</a> <a id="1949" href="Data.DifferenceList.html#1010" class="Function Operator">_++_</a> <a id="1954" href="Data.DifferenceList.html#883" class="Function">[]</a>

<a id="take"></a><a id="1958" href="Data.DifferenceList.html#1958" class="Function">take</a> <a id="1963" class="Symbol">:</a> <a id="1965" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1967" class="Symbol">→</a> <a id="1969" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1978" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="1980" class="Symbol">→</a> <a id="1982" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="1991" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="1993" href="Data.DifferenceList.html#1958" class="Function">take</a> <a id="1998" href="Data.DifferenceList.html#1998" class="Bound">n</a> <a id="2000" class="Symbol">=</a> <a id="2002" href="Data.DifferenceList.html#680" class="Function">lift</a> <a id="2007" class="Symbol">(</a><a id="2008" href="Data.List.Base.html#8389" class="Function">List.take</a> <a id="2018" href="Data.DifferenceList.html#1998" class="Bound">n</a><a id="2019" class="Symbol">)</a>

<a id="drop"></a><a id="2022" href="Data.DifferenceList.html#2022" class="Function">drop</a> <a id="2027" class="Symbol">:</a> <a id="2029" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2031" class="Symbol">→</a> <a id="2033" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="2042" href="Data.DifferenceList.html#481" class="Generalizable">A</a> <a id="2044" class="Symbol">→</a> <a id="2046" href="Data.DifferenceList.html#625" class="Function">DiffList</a> <a id="2055" href="Data.DifferenceList.html#481" class="Generalizable">A</a>
<a id="2057" href="Data.DifferenceList.html#2022" class="Function">drop</a> <a id="2062" href="Data.DifferenceList.html#2062" class="Bound">n</a> <a id="2064" class="Symbol">=</a> <a id="2066" href="Data.DifferenceList.html#680" class="Function">lift</a> <a id="2071" class="Symbol">(</a><a id="2072" href="Data.List.Base.html#8509" class="Function">List.drop</a> <a id="2082" href="Data.DifferenceList.html#2062" class="Bound">n</a><a id="2083" class="Symbol">)</a>
</pre></body></html>