<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Properties.Semiring.Mult</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Multiplication by a natural number over a semiring</a>
<a id="160" class="Comment">------------------------------------------------------------------------</a>

<a id="234" class="Symbol">{-#</a> <a id="238" class="Keyword">OPTIONS</a> <a id="246" class="Pragma">--cubical-compatible</a> <a id="267" class="Pragma">--safe</a> <a id="274" class="Symbol">#-}</a>

<a id="279" class="Keyword">open</a> <a id="284" class="Keyword">import</a> <a id="291" href="Algebra.Bundles.html" class="Module">Algebra.Bundles</a> <a id="307" class="Keyword">using</a> <a id="313" class="Symbol">(</a><a id="314" href="Algebra.Bundles.html#18455" class="Record">Semiring</a><a id="322" class="Symbol">)</a>

<a id="325" class="Keyword">module</a> <a id="332" href="Algebra.Properties.Semiring.Mult.html" class="Module">Algebra.Properties.Semiring.Mult</a>
  <a id="367" class="Symbol">{</a><a id="368" href="Algebra.Properties.Semiring.Mult.html#368" class="Bound">a</a> <a id="370" href="Algebra.Properties.Semiring.Mult.html#370" class="Bound">ℓ</a><a id="371" class="Symbol">}</a> <a id="373" class="Symbol">(</a><a id="374" href="Algebra.Properties.Semiring.Mult.html#374" class="Bound">S</a> <a id="376" class="Symbol">:</a> <a id="378" href="Algebra.Bundles.html#18455" class="Record">Semiring</a> <a id="387" href="Algebra.Properties.Semiring.Mult.html#368" class="Bound">a</a> <a id="389" href="Algebra.Properties.Semiring.Mult.html#370" class="Bound">ℓ</a><a id="390" class="Symbol">)</a> <a id="392" class="Keyword">where</a>

<a id="399" class="Keyword">open</a> <a id="404" class="Keyword">import</a> <a id="411" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="425" class="Symbol">as</a> <a id="428" class="Module">ℕ</a> <a id="430" class="Keyword">using</a> <a id="436" class="Symbol">(</a><a id="437" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a><a id="441" class="Symbol">;</a> <a id="443" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a><a id="446" class="Symbol">)</a>

<a id="449" class="Keyword">open</a> <a id="454" href="Algebra.Bundles.html#18455" class="Module">Semiring</a> <a id="463" href="Algebra.Properties.Semiring.Mult.html#374" class="Bound">S</a> <a id="465" class="Keyword">renaming</a> <a id="474" class="Symbol">(</a><a id="475" href="Algebra.Structures.html#14902" class="Function">zero</a> <a id="480" class="Symbol">to</a> <a id="483" class="Function">*-zero</a><a id="489" class="Symbol">)</a>

<a id="492" class="Keyword">open</a> <a id="497" class="Keyword">import</a> <a id="504" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="537" href="Algebra.Structures.html#1873" class="Function">setoid</a>
<a id="544" class="Keyword">open</a> <a id="549" class="Keyword">import</a> <a id="556" href="Algebra.Definitions.html" class="Module">Algebra.Definitions</a> <a id="576" href="Algebra.Bundles.html#18574" class="Field Operator">_≈_</a> <a id="580" class="Keyword">using</a> <a id="586" class="Symbol">(</a><a id="587" href="Algebra.Definitions.html#3757" class="Function Operator">_IdempotentOn_</a><a id="601" class="Symbol">)</a>

<a id="604" class="Comment">------------------------------------------------------------------------</a>
<a id="677" class="Comment">-- Re-export definition from the monoid</a>

<a id="718" class="Keyword">open</a> <a id="723" class="Keyword">import</a> <a id="730" href="Algebra.Properties.Monoid.Mult.html" class="Module">Algebra.Properties.Monoid.Mult</a> <a id="761" href="Algebra.Bundles.html#18182" class="Function">+-monoid</a> <a id="770" class="Keyword">public</a>

<a id="778" class="Comment">------------------------------------------------------------------------</a>
<a id="851" class="Comment">-- Properties of _×_</a>

<a id="873" class="Comment">-- (0 ×_) is (0# *_)</a>

<a id="×-homo-0#"></a><a id="895" href="Algebra.Properties.Semiring.Mult.html#895" class="Function">×-homo-0#</a> <a id="905" class="Symbol">:</a> <a id="907" class="Symbol">∀</a> <a id="909" href="Algebra.Properties.Semiring.Mult.html#909" class="Bound">x</a> <a id="911" class="Symbol">→</a> <a id="913" class="Number">0</a> <a id="915" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="917" href="Algebra.Properties.Semiring.Mult.html#909" class="Bound">x</a> <a id="919" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="921" href="Algebra.Bundles.html#18663" class="Field">0#</a> <a id="924" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="926" href="Algebra.Properties.Semiring.Mult.html#909" class="Bound">x</a>
<a id="928" href="Algebra.Properties.Semiring.Mult.html#895" class="Function">×-homo-0#</a> <a id="938" href="Algebra.Properties.Semiring.Mult.html#938" class="Bound">x</a> <a id="940" class="Symbol">=</a> <a id="942" href="Relation.Binary.Structures.html#1667" class="Function">sym</a> <a id="946" class="Symbol">(</a><a id="947" href="Algebra.Structures.html#11819" class="Function">zeroˡ</a> <a id="953" href="Algebra.Properties.Semiring.Mult.html#938" class="Bound">x</a><a id="954" class="Symbol">)</a>

<a id="957" class="Comment">-- (1 ×_) is (1# *_)</a>

<a id="×-homo-1#"></a><a id="979" href="Algebra.Properties.Semiring.Mult.html#979" class="Function">×-homo-1#</a> <a id="989" class="Symbol">:</a> <a id="991" class="Symbol">∀</a> <a id="993" href="Algebra.Properties.Semiring.Mult.html#993" class="Bound">x</a> <a id="995" class="Symbol">→</a> <a id="997" class="Number">1</a> <a id="999" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1001" href="Algebra.Properties.Semiring.Mult.html#993" class="Bound">x</a> <a id="1003" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1005" href="Algebra.Bundles.html#18688" class="Field">1#</a> <a id="1008" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1010" href="Algebra.Properties.Semiring.Mult.html#993" class="Bound">x</a>
<a id="1012" href="Algebra.Properties.Semiring.Mult.html#979" class="Function">×-homo-1#</a> <a id="1022" href="Algebra.Properties.Semiring.Mult.html#1022" class="Bound">x</a> <a id="1024" class="Symbol">=</a> <a id="1026" href="Relation.Binary.Structures.html#1693" class="Function">trans</a> <a id="1032" class="Symbol">(</a><a id="1033" href="Algebra.Properties.Monoid.Mult.html#1595" class="Function">×-homo-1</a> <a id="1042" href="Algebra.Properties.Semiring.Mult.html#1022" class="Bound">x</a><a id="1043" class="Symbol">)</a> <a id="1045" class="Symbol">(</a><a id="1046" href="Relation.Binary.Structures.html#1667" class="Function">sym</a> <a id="1050" class="Symbol">(</a><a id="1051" href="Algebra.Structures.html#14683" class="Function">*-identityˡ</a> <a id="1063" href="Algebra.Properties.Semiring.Mult.html#1022" class="Bound">x</a><a id="1064" class="Symbol">))</a>

<a id="1068" class="Comment">-- (n ×_) commutes with _*_</a>

<a id="×-comm-*"></a><a id="1097" href="Algebra.Properties.Semiring.Mult.html#1097" class="Function">×-comm-*</a> <a id="1106" class="Symbol">:</a> <a id="1108" class="Symbol">∀</a> <a id="1110" href="Algebra.Properties.Semiring.Mult.html#1110" class="Bound">n</a> <a id="1112" href="Algebra.Properties.Semiring.Mult.html#1112" class="Bound">x</a> <a id="1114" href="Algebra.Properties.Semiring.Mult.html#1114" class="Bound">y</a> <a id="1116" class="Symbol">→</a> <a id="1118" href="Algebra.Properties.Semiring.Mult.html#1112" class="Bound">x</a> <a id="1120" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1122" class="Symbol">(</a><a id="1123" href="Algebra.Properties.Semiring.Mult.html#1110" class="Bound">n</a> <a id="1125" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1127" href="Algebra.Properties.Semiring.Mult.html#1114" class="Bound">y</a><a id="1128" class="Symbol">)</a> <a id="1130" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1132" href="Algebra.Properties.Semiring.Mult.html#1110" class="Bound">n</a> <a id="1134" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1136" class="Symbol">(</a><a id="1137" href="Algebra.Properties.Semiring.Mult.html#1112" class="Bound">x</a> <a id="1139" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1141" href="Algebra.Properties.Semiring.Mult.html#1114" class="Bound">y</a><a id="1142" class="Symbol">)</a>
<a id="1144" href="Algebra.Properties.Semiring.Mult.html#1097" class="Function">×-comm-*</a> <a id="1153" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="1161" href="Algebra.Properties.Semiring.Mult.html#1161" class="Bound">x</a> <a id="1163" href="Algebra.Properties.Semiring.Mult.html#1163" class="Bound">y</a> <a id="1165" class="Symbol">=</a> <a id="1167" href="Algebra.Structures.html#11865" class="Function">zeroʳ</a> <a id="1173" href="Algebra.Properties.Semiring.Mult.html#1161" class="Bound">x</a>
<a id="1175" href="Algebra.Properties.Semiring.Mult.html#1097" class="Function">×-comm-*</a> <a id="1184" class="Symbol">(</a><a id="1185" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1189" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a><a id="1190" class="Symbol">)</a> <a id="1192" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1194" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a> <a id="1196" class="Symbol">=</a> <a id="1198" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
  <a id="1206" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1208" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1210" class="Symbol">(</a><a id="1211" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1215" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a> <a id="1217" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1219" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a><a id="1220" class="Symbol">)</a>       <a id="1228" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
  <a id="1234" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1236" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1238" class="Symbol">(</a><a id="1239" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a> <a id="1241" href="Algebra.Bundles.html#18605" class="Field Operator">+</a> <a id="1243" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a> <a id="1245" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1247" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a><a id="1248" class="Symbol">)</a>       <a id="1256" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1259" href="Algebra.Structures.html#13374" class="Function">distribˡ</a> <a id="1268" class="Symbol">_</a> <a id="1270" class="Symbol">_</a> <a id="1272" class="Symbol">_</a> <a id="1274" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="1278" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1280" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1282" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a> <a id="1284" href="Algebra.Bundles.html#18605" class="Field Operator">+</a> <a id="1286" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1288" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1290" class="Symbol">(</a><a id="1291" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a> <a id="1293" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1295" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a><a id="1296" class="Symbol">)</a>   <a id="1300" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1303" href="Algebra.Structures.html#13676" class="Function">+-congˡ</a> <a id="1311" class="Symbol">(</a><a id="1312" href="Algebra.Properties.Semiring.Mult.html#1097" class="Function">×-comm-*</a> <a id="1321" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a> <a id="1323" class="Symbol">_</a> <a id="1325" class="Symbol">_)</a> <a id="1328" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="1332" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1334" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1336" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a> <a id="1338" href="Algebra.Bundles.html#18605" class="Field Operator">+</a> <a id="1340" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a> <a id="1342" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1344" class="Symbol">(</a><a id="1345" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1347" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1349" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a><a id="1350" class="Symbol">)</a>   <a id="1354" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
  <a id="1360" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1364" href="Algebra.Properties.Semiring.Mult.html#1189" class="Bound">n</a> <a id="1366" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1368" class="Symbol">(</a><a id="1369" href="Algebra.Properties.Semiring.Mult.html#1192" class="Bound">x</a> <a id="1371" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1373" href="Algebra.Properties.Semiring.Mult.html#1194" class="Bound">y</a><a id="1374" class="Symbol">)</a>       <a id="1382" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>

<a id="1385" class="Comment">-- (n ×_) associates with _*_</a>

<a id="×-assoc-*"></a><a id="1416" href="Algebra.Properties.Semiring.Mult.html#1416" class="Function">×-assoc-*</a> <a id="1426" class="Symbol">:</a> <a id="1428" class="Symbol">∀</a> <a id="1430" href="Algebra.Properties.Semiring.Mult.html#1430" class="Bound">n</a> <a id="1432" href="Algebra.Properties.Semiring.Mult.html#1432" class="Bound">x</a> <a id="1434" href="Algebra.Properties.Semiring.Mult.html#1434" class="Bound">y</a> <a id="1436" class="Symbol">→</a> <a id="1438" class="Symbol">(</a><a id="1439" href="Algebra.Properties.Semiring.Mult.html#1430" class="Bound">n</a> <a id="1441" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1443" href="Algebra.Properties.Semiring.Mult.html#1432" class="Bound">x</a><a id="1444" class="Symbol">)</a> <a id="1446" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1448" href="Algebra.Properties.Semiring.Mult.html#1434" class="Bound">y</a> <a id="1450" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1452" href="Algebra.Properties.Semiring.Mult.html#1430" class="Bound">n</a> <a id="1454" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1456" class="Symbol">(</a><a id="1457" href="Algebra.Properties.Semiring.Mult.html#1432" class="Bound">x</a> <a id="1459" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1461" href="Algebra.Properties.Semiring.Mult.html#1434" class="Bound">y</a><a id="1462" class="Symbol">)</a>
<a id="1464" href="Algebra.Properties.Semiring.Mult.html#1416" class="Function">×-assoc-*</a> <a id="1474" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a> <a id="1479" href="Algebra.Properties.Semiring.Mult.html#1479" class="Bound">x</a> <a id="1481" href="Algebra.Properties.Semiring.Mult.html#1481" class="Bound">y</a> <a id="1483" class="Symbol">=</a> <a id="1485" href="Algebra.Structures.html#11819" class="Function">zeroˡ</a> <a id="1491" href="Algebra.Properties.Semiring.Mult.html#1481" class="Bound">y</a>
<a id="1493" href="Algebra.Properties.Semiring.Mult.html#1416" class="Function">×-assoc-*</a> <a id="1503" class="Symbol">(</a><a id="1504" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1508" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a><a id="1509" class="Symbol">)</a> <a id="1511" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a> <a id="1513" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a> <a id="1515" class="Symbol">=</a> <a id="1517" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
  <a id="1525" class="Symbol">(</a><a id="1526" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1530" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a> <a id="1532" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1534" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a><a id="1535" class="Symbol">)</a> <a id="1537" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1539" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a>       <a id="1547" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
  <a id="1553" class="Symbol">(</a><a id="1554" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a> <a id="1556" href="Algebra.Bundles.html#18605" class="Field Operator">+</a> <a id="1558" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a> <a id="1560" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1562" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a><a id="1563" class="Symbol">)</a> <a id="1565" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1567" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a>       <a id="1575" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1578" href="Algebra.Structures.html#13436" class="Function">distribʳ</a> <a id="1587" class="Symbol">_</a> <a id="1589" class="Symbol">_</a> <a id="1591" class="Symbol">_</a> <a id="1593" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="1597" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a> <a id="1599" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1601" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a> <a id="1603" href="Algebra.Bundles.html#18605" class="Field Operator">+</a> <a id="1605" class="Symbol">(</a><a id="1606" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a> <a id="1608" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1610" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a><a id="1611" class="Symbol">)</a> <a id="1613" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1615" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a>   <a id="1619" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1622" href="Algebra.Structures.html#13676" class="Function">+-congˡ</a> <a id="1630" class="Symbol">(</a><a id="1631" href="Algebra.Properties.Semiring.Mult.html#1416" class="Function">×-assoc-*</a> <a id="1641" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a> <a id="1643" class="Symbol">_</a> <a id="1645" class="Symbol">_)</a> <a id="1648" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="1652" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a> <a id="1654" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1656" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a> <a id="1658" href="Algebra.Bundles.html#18605" class="Field Operator">+</a> <a id="1660" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a> <a id="1662" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1664" class="Symbol">(</a><a id="1665" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a> <a id="1667" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1669" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a><a id="1670" class="Symbol">)</a>   <a id="1674" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
  <a id="1680" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1684" href="Algebra.Properties.Semiring.Mult.html#1508" class="Bound">n</a> <a id="1686" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1688" class="Symbol">(</a><a id="1689" href="Algebra.Properties.Semiring.Mult.html#1511" class="Bound">x</a> <a id="1691" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1693" href="Algebra.Properties.Semiring.Mult.html#1513" class="Bound">y</a><a id="1694" class="Symbol">)</a>       <a id="1702" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>

<a id="1705" class="Comment">-- (_× x) is homomorphic with respect to _ℕ.*_/_*_ for idempotent x.</a>

<a id="idem-×-homo-*"></a><a id="1775" href="Algebra.Properties.Semiring.Mult.html#1775" class="Function">idem-×-homo-*</a> <a id="1789" class="Symbol">:</a> <a id="1791" class="Symbol">∀</a> <a id="1793" href="Algebra.Properties.Semiring.Mult.html#1793" class="Bound">m</a> <a id="1795" href="Algebra.Properties.Semiring.Mult.html#1795" class="Bound">n</a> <a id="1797" class="Symbol">{</a><a id="1798" href="Algebra.Properties.Semiring.Mult.html#1798" class="Bound">x</a><a id="1799" class="Symbol">}</a> <a id="1801" class="Symbol">→</a> <a id="1803" class="Symbol">(</a><a id="1804" href="Algebra.Bundles.html#18634" class="Field Operator">_*_</a> <a id="1808" href="Algebra.Definitions.html#3757" class="Function Operator">IdempotentOn</a> <a id="1821" href="Algebra.Properties.Semiring.Mult.html#1798" class="Bound">x</a><a id="1822" class="Symbol">)</a> <a id="1824" class="Symbol">→</a> <a id="1826" class="Symbol">(</a><a id="1827" href="Algebra.Properties.Semiring.Mult.html#1793" class="Bound">m</a> <a id="1829" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1831" href="Algebra.Properties.Semiring.Mult.html#1798" class="Bound">x</a><a id="1832" class="Symbol">)</a> <a id="1834" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1836" class="Symbol">(</a><a id="1837" href="Algebra.Properties.Semiring.Mult.html#1795" class="Bound">n</a> <a id="1839" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1841" href="Algebra.Properties.Semiring.Mult.html#1798" class="Bound">x</a><a id="1842" class="Symbol">)</a> <a id="1844" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1846" class="Symbol">(</a><a id="1847" href="Algebra.Properties.Semiring.Mult.html#1793" class="Bound">m</a> <a id="1849" href="Agda.Builtin.Nat.html#539" class="Primitive Operator">ℕ.*</a> <a id="1853" href="Algebra.Properties.Semiring.Mult.html#1795" class="Bound">n</a><a id="1854" class="Symbol">)</a> <a id="1856" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1858" href="Algebra.Properties.Semiring.Mult.html#1798" class="Bound">x</a>
<a id="1860" href="Algebra.Properties.Semiring.Mult.html#1775" class="Function">idem-×-homo-*</a> <a id="1874" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="1876" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="1878" class="Symbol">{</a><a id="1879" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="1880" class="Symbol">}</a> <a id="1882" href="Algebra.Properties.Semiring.Mult.html#1882" class="Bound">idem</a> <a id="1887" class="Symbol">=</a> <a id="1889" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
  <a id="1897" class="Symbol">(</a><a id="1898" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="1900" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1902" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="1903" class="Symbol">)</a> <a id="1905" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1907" class="Symbol">(</a><a id="1908" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="1910" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1912" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="1913" class="Symbol">)</a>   <a id="1917" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1920" href="Algebra.Properties.Semiring.Mult.html#1416" class="Function">×-assoc-*</a> <a id="1930" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="1932" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a> <a id="1934" class="Symbol">(</a><a id="1935" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="1937" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1939" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="1940" class="Symbol">)</a> <a id="1942" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="1946" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="1948" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1950" class="Symbol">(</a><a id="1951" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a> <a id="1953" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1955" class="Symbol">(</a><a id="1956" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="1958" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1960" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="1961" class="Symbol">))</a>   <a id="1966" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="1969" href="Algebra.Properties.Monoid.Mult.html#1218" class="Function">×-congʳ</a> <a id="1977" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="1979" class="Symbol">(</a><a id="1980" href="Algebra.Properties.Semiring.Mult.html#1097" class="Function">×-comm-*</a> <a id="1989" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="1991" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a> <a id="1993" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="1994" class="Symbol">)</a> <a id="1996" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="2000" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="2002" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2004" class="Symbol">(</a><a id="2005" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="2007" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2009" class="Symbol">(</a><a id="2010" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a> <a id="2012" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2014" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="2015" class="Symbol">))</a>   <a id="2020" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2023" href="Algebra.Properties.Monoid.Mult.html#2173" class="Function">×-assocˡ</a> <a id="2032" class="Symbol">_</a> <a id="2034" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="2036" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a> <a id="2038" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="2042" class="Symbol">(</a><a id="2043" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="2045" href="Agda.Builtin.Nat.html#539" class="Primitive Operator">ℕ.*</a> <a id="2049" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a><a id="2050" class="Symbol">)</a> <a id="2052" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2054" class="Symbol">(</a><a id="2055" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a> <a id="2057" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2059" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a><a id="2060" class="Symbol">)</a> <a id="2062" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2065" href="Algebra.Properties.Monoid.Mult.html#1218" class="Function">×-congʳ</a> <a id="2073" class="Symbol">(</a><a id="2074" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="2076" href="Agda.Builtin.Nat.html#539" class="Primitive Operator">ℕ.*</a> <a id="2080" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a><a id="2081" class="Symbol">)</a> <a id="2083" href="Algebra.Properties.Semiring.Mult.html#1882" class="Bound">idem</a> <a id="2088" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
  <a id="2092" class="Symbol">(</a><a id="2093" href="Algebra.Properties.Semiring.Mult.html#1874" class="Bound">m</a> <a id="2095" href="Agda.Builtin.Nat.html#539" class="Primitive Operator">ℕ.*</a> <a id="2099" href="Algebra.Properties.Semiring.Mult.html#1876" class="Bound">n</a><a id="2100" class="Symbol">)</a> <a id="2102" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2104" href="Algebra.Properties.Semiring.Mult.html#1879" class="Bound">x</a>       <a id="2112" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>

<a id="2115" class="Comment">-- (_× 1#) is homomorphic with respect to _ℕ.*_/_*_.</a>

<a id="×1-homo-*"></a><a id="2169" href="Algebra.Properties.Semiring.Mult.html#2169" class="Function">×1-homo-*</a> <a id="2179" class="Symbol">:</a> <a id="2181" class="Symbol">∀</a> <a id="2183" href="Algebra.Properties.Semiring.Mult.html#2183" class="Bound">m</a> <a id="2185" href="Algebra.Properties.Semiring.Mult.html#2185" class="Bound">n</a> <a id="2187" class="Symbol">→</a> <a id="2189" class="Symbol">(</a><a id="2190" href="Algebra.Properties.Semiring.Mult.html#2183" class="Bound">m</a> <a id="2192" href="Agda.Builtin.Nat.html#539" class="Primitive Operator">ℕ.*</a> <a id="2196" href="Algebra.Properties.Semiring.Mult.html#2185" class="Bound">n</a><a id="2197" class="Symbol">)</a> <a id="2199" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2201" href="Algebra.Bundles.html#18688" class="Field">1#</a> <a id="2204" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="2206" class="Symbol">(</a><a id="2207" href="Algebra.Properties.Semiring.Mult.html#2183" class="Bound">m</a> <a id="2209" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2211" href="Algebra.Bundles.html#18688" class="Field">1#</a><a id="2213" class="Symbol">)</a> <a id="2215" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2217" class="Symbol">(</a><a id="2218" href="Algebra.Properties.Semiring.Mult.html#2185" class="Bound">n</a> <a id="2220" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2222" href="Algebra.Bundles.html#18688" class="Field">1#</a><a id="2224" class="Symbol">)</a>
<a id="2226" href="Algebra.Properties.Semiring.Mult.html#2169" class="Function">×1-homo-*</a> <a id="2236" href="Algebra.Properties.Semiring.Mult.html#2236" class="Bound">m</a> <a id="2238" href="Algebra.Properties.Semiring.Mult.html#2238" class="Bound">n</a> <a id="2240" class="Symbol">=</a> <a id="2242" href="Relation.Binary.Structures.html#1667" class="Function">sym</a> <a id="2246" class="Symbol">(</a><a id="2247" href="Algebra.Properties.Semiring.Mult.html#1775" class="Function">idem-×-homo-*</a> <a id="2261" href="Algebra.Properties.Semiring.Mult.html#2236" class="Bound">m</a> <a id="2263" href="Algebra.Properties.Semiring.Mult.html#2238" class="Bound">n</a> <a id="2265" class="Symbol">(</a><a id="2266" href="Algebra.Structures.html#14716" class="Function">*-identityʳ</a> <a id="2278" href="Algebra.Bundles.html#18688" class="Field">1#</a><a id="2280" class="Symbol">))</a>

<a id="2284" class="Comment">------------------------------------------------------------------------</a>
<a id="2357" class="Comment">-- DEPRECATED NAMES</a>
<a id="2377" class="Comment">------------------------------------------------------------------------</a>
<a id="2450" class="Comment">-- Please use the new names as continuing support for the old names is</a>
<a id="2521" class="Comment">-- not guaranteed.</a>

<a id="2541" class="Comment">-- Version 2.1</a>

<a id="1×-identityʳ"></a><a id="2557" href="Algebra.Properties.Semiring.Mult.html#2557" class="Function">1×-identityʳ</a> <a id="2570" class="Symbol">=</a> <a id="2572" href="Algebra.Properties.Monoid.Mult.html#1595" class="Function">×-homo-1</a>
<a id="2581" class="Symbol">{-#</a> <a id="2585" class="Keyword">WARNING_ON_USAGE</a> <a id="2602" class="Pragma">1×-identityʳ</a>
<a id="2615" class="String">&quot;Warning: 1×-identityʳ was deprecated in v2.1.
Please use ×-homo-1 instead. &quot;</a>
<a id="2693" class="Symbol">#-}</a>
</pre></body></html>