<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Unary.Closure.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Closures of a unary relation with respect to a binary one.</a>
<a id="168" class="Comment">------------------------------------------------------------------------</a>

<a id="242" class="Symbol">{-#</a> <a id="246" class="Keyword">OPTIONS</a> <a id="254" class="Pragma">--cubical-compatible</a> <a id="275" class="Pragma">--safe</a> <a id="282" class="Symbol">#-}</a>

<a id="287" class="Keyword">open</a> <a id="292" class="Keyword">import</a> <a id="299" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="320" class="Keyword">using</a> <a id="326" class="Symbol">(</a><a id="327" href="Relation.Binary.Core.html#896" class="Function">Rel</a><a id="330" class="Symbol">)</a>

<a id="333" class="Keyword">module</a> <a id="340" href="Relation.Unary.Closure.Base.html" class="Module">Relation.Unary.Closure.Base</a> <a id="368" class="Symbol">{</a><a id="369" href="Relation.Unary.Closure.Base.html#369" class="Bound">a</a> <a id="371" href="Relation.Unary.Closure.Base.html#371" class="Bound">b</a><a id="372" class="Symbol">}</a> <a id="374" class="Symbol">{</a><a id="375" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="377" class="Symbol">:</a> <a id="379" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="383" href="Relation.Unary.Closure.Base.html#369" class="Bound">a</a><a id="384" class="Symbol">}</a> <a id="386" class="Symbol">(</a><a id="387" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="389" class="Symbol">:</a> <a id="391" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="395" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="397" href="Relation.Unary.Closure.Base.html#371" class="Bound">b</a><a id="398" class="Symbol">)</a> <a id="400" class="Keyword">where</a>

<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Level.html" class="Module">Level</a> <a id="425" class="Keyword">using</a> <a id="431" class="Symbol">(</a><a id="432" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="435" class="Symbol">)</a>
<a id="437" class="Keyword">open</a> <a id="442" class="Keyword">import</a> <a id="449" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="467" class="Keyword">using</a> <a id="473" class="Symbol">(</a><a id="474" href="Data.Product.Base.html#1244" class="Function">Σ-syntax</a><a id="482" class="Symbol">;</a> <a id="484" href="Data.Product.Base.html#1618" class="Function Operator">_×_</a><a id="487" class="Symbol">;</a> <a id="489" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="492" class="Symbol">;</a> <a id="494" href="Data.Product.Base.html#1932" class="Function Operator">-,_</a><a id="497" class="Symbol">)</a>
<a id="499" class="Keyword">open</a> <a id="504" class="Keyword">import</a> <a id="511" href="Function.Base.html" class="Module">Function.Base</a> <a id="525" class="Keyword">using</a> <a id="531" class="Symbol">(</a><a id="532" href="Function.Base.html#1638" class="Function">flip</a><a id="536" class="Symbol">)</a>
<a id="538" class="Keyword">open</a> <a id="543" class="Keyword">import</a> <a id="550" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a> <a id="578" class="Keyword">using</a> <a id="584" class="Symbol">(</a><a id="585" href="Relation.Binary.Definitions.html#1355" class="Function">Reflexive</a><a id="594" class="Symbol">;</a> <a id="596" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a><a id="606" class="Symbol">)</a>
<a id="608" class="Keyword">open</a> <a id="613" class="Keyword">import</a> <a id="620" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="635" class="Keyword">using</a> <a id="641" class="Symbol">(</a><a id="642" href="Relation.Unary.html#1178" class="Function">Pred</a><a id="646" class="Symbol">)</a>

<a id="649" class="Comment">------------------------------------------------------------------------</a>
<a id="722" class="Comment">-- Definitions</a>

<a id="738" class="Comment">-- Box</a>

<a id="746" class="Comment">-- We start with the definition of □ (&quot;box&quot;) which is named after the</a>
<a id="816" class="Comment">-- box modality in modal logic. `□ T x` states that all the elements one</a>
<a id="889" class="Comment">-- step away from `x` with respect to the relation R satisfy `T`.</a>

<a id="□"></a><a id="956" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="958" class="Symbol">:</a> <a id="960" class="Symbol">∀</a> <a id="962" class="Symbol">{</a><a id="963" href="Relation.Unary.Closure.Base.html#963" class="Bound">t</a><a id="964" class="Symbol">}</a> <a id="966" class="Symbol">→</a> <a id="968" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="973" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="975" href="Relation.Unary.Closure.Base.html#963" class="Bound">t</a> <a id="977" class="Symbol">→</a> <a id="979" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="984" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="986" class="Symbol">(</a><a id="987" href="Relation.Unary.Closure.Base.html#369" class="Bound">a</a> <a id="989" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="991" href="Relation.Unary.Closure.Base.html#371" class="Bound">b</a> <a id="993" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="995" href="Relation.Unary.Closure.Base.html#963" class="Bound">t</a><a id="996" class="Symbol">)</a>
<a id="998" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="1000" href="Relation.Unary.Closure.Base.html#1000" class="Bound">T</a> <a id="1002" href="Relation.Unary.Closure.Base.html#1002" class="Bound">x</a> <a id="1004" class="Symbol">=</a> <a id="1006" class="Symbol">∀</a> <a id="1008" class="Symbol">{</a><a id="1009" href="Relation.Unary.Closure.Base.html#1009" class="Bound">y</a><a id="1010" class="Symbol">}</a> <a id="1012" class="Symbol">→</a> <a id="1014" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="1016" href="Relation.Unary.Closure.Base.html#1002" class="Bound">x</a> <a id="1018" href="Relation.Unary.Closure.Base.html#1009" class="Bound">y</a> <a id="1020" class="Symbol">→</a> <a id="1022" href="Relation.Unary.Closure.Base.html#1000" class="Bound">T</a> <a id="1024" href="Relation.Unary.Closure.Base.html#1009" class="Bound">y</a>

<a id="1027" class="Comment">-- Use cases of □ include:</a>
<a id="1054" class="Comment">-- * The definition of the accessibility predicate corresponding to R:</a>
<a id="1125" class="Comment">--   data Acc (x : A) : Set (a ⊔ b) where</a>
<a id="1167" class="Comment">--     step : □ Acc x → Acc x</a>

<a id="1198" class="Comment">-- * The characterization of stability under weakening: picking R to be</a>
<a id="1270" class="Comment">--   `Data.List.Relation.Sublist.Inductive`, `∀ {Γ} → Tm Γ → □ T Γ`</a>
<a id="1338" class="Comment">--   corresponds to the fact that we have a notion of weakening for `Tm`.</a>

<a id="1413" class="Comment">-- Diamond</a>

<a id="1425" class="Comment">-- We then have the definition of ◇ (&quot;diamond&quot;) which is named after the</a>
<a id="1498" class="Comment">-- diamond modality in modal logic. In modal logic, `◇ T x` states that</a>
<a id="1570" class="Comment">-- there exists an element one step away from x with respect to the</a>
<a id="1638" class="Comment">-- relation R that satisfies T. It is worth noting that the modal logic</a>
<a id="1710" class="Comment">-- metaphor breaks down here: this only is a closure operator if the</a>
<a id="1779" class="Comment">-- step we take is *backwards* with respect to R.</a>

<a id="◇"></a><a id="1830" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="1832" class="Symbol">:</a> <a id="1834" class="Symbol">∀</a> <a id="1836" class="Symbol">{</a><a id="1837" href="Relation.Unary.Closure.Base.html#1837" class="Bound">t</a><a id="1838" class="Symbol">}</a> <a id="1840" class="Symbol">→</a> <a id="1842" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1847" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="1849" href="Relation.Unary.Closure.Base.html#1837" class="Bound">t</a> <a id="1851" class="Symbol">→</a> <a id="1853" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1858" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="1860" class="Symbol">(</a><a id="1861" href="Relation.Unary.Closure.Base.html#369" class="Bound">a</a> <a id="1863" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1865" href="Relation.Unary.Closure.Base.html#371" class="Bound">b</a> <a id="1867" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1869" href="Relation.Unary.Closure.Base.html#1837" class="Bound">t</a><a id="1870" class="Symbol">)</a>
<a id="1872" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="1874" href="Relation.Unary.Closure.Base.html#1874" class="Bound">T</a> <a id="1876" href="Relation.Unary.Closure.Base.html#1876" class="Bound">x</a> <a id="1878" class="Symbol">=</a> <a id="1880" href="Data.Product.Base.html#1244" class="Function">Σ[</a> <a id="1883" href="Relation.Unary.Closure.Base.html#1883" class="Bound">support</a> <a id="1891" href="Data.Product.Base.html#1244" class="Function">∈</a> <a id="1893" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="1895" href="Data.Product.Base.html#1244" class="Function">]</a> <a id="1897" class="Symbol">(</a><a id="1898" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="1900" href="Relation.Unary.Closure.Base.html#1883" class="Bound">support</a> <a id="1908" href="Relation.Unary.Closure.Base.html#1876" class="Bound">x</a> <a id="1910" href="Data.Product.Base.html#1618" class="Function Operator">×</a> <a id="1912" href="Relation.Unary.Closure.Base.html#1874" class="Bound">T</a> <a id="1914" href="Relation.Unary.Closure.Base.html#1883" class="Bound">support</a><a id="1921" class="Symbol">)</a>

<a id="1924" class="Comment">-- Use cases of ◇ include:</a>
<a id="1951" class="Comment">-- * The characterization of strengthening: picking R to be</a>
<a id="2011" class="Comment">--   `Data.List.Relation.Sublist.Inductive`, `∀ {Γ} → Tm Γ → ◇ Tm Γ`</a>
<a id="2080" class="Comment">--   is the type of a function strengthening a term to its support:</a>
<a id="2148" class="Comment">--   all the unused variables are discarded early on by the `related`</a>
<a id="2218" class="Comment">--   proof.</a>
<a id="2230" class="Comment">-- Cf. Conor McBride&#39;s &quot;Everybody&#39;s got to be somewhere&quot; for a more</a>
<a id="2298" class="Comment">-- detailed treatment of such an example.</a>

<a id="2341" class="Comment">-- Closed</a>

<a id="2352" class="Comment">-- Whenever we have a value in one context, we can get one in any</a>
<a id="2418" class="Comment">-- related context.</a>

<a id="2439" class="Keyword">record</a> <a id="Closed"></a><a id="2446" href="Relation.Unary.Closure.Base.html#2446" class="Record">Closed</a> <a id="2453" class="Symbol">{</a><a id="2454" href="Relation.Unary.Closure.Base.html#2454" class="Bound">t</a><a id="2455" class="Symbol">}</a> <a id="2457" class="Symbol">(</a><a id="2458" href="Relation.Unary.Closure.Base.html#2458" class="Bound">T</a> <a id="2460" class="Symbol">:</a> <a id="2462" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="2467" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="2469" href="Relation.Unary.Closure.Base.html#2454" class="Bound">t</a><a id="2470" class="Symbol">)</a> <a id="2472" class="Symbol">:</a> <a id="2474" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2478" class="Symbol">(</a><a id="2479" href="Relation.Unary.Closure.Base.html#369" class="Bound">a</a> <a id="2481" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2483" href="Relation.Unary.Closure.Base.html#371" class="Bound">b</a> <a id="2485" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2487" href="Relation.Unary.Closure.Base.html#2454" class="Bound">t</a><a id="2488" class="Symbol">)</a> <a id="2490" class="Keyword">where</a>
  <a id="2498" class="Keyword">field</a> <a id="Closed.next"></a><a id="2504" href="Relation.Unary.Closure.Base.html#2504" class="Field">next</a> <a id="2509" class="Symbol">:</a> <a id="2511" class="Symbol">∀</a> <a id="2513" class="Symbol">{</a><a id="2514" href="Relation.Unary.Closure.Base.html#2514" class="Bound">x</a><a id="2515" class="Symbol">}</a> <a id="2517" class="Symbol">→</a> <a id="2519" href="Relation.Unary.Closure.Base.html#2458" class="Bound">T</a> <a id="2521" href="Relation.Unary.Closure.Base.html#2514" class="Bound">x</a> <a id="2523" class="Symbol">→</a> <a id="2525" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="2527" href="Relation.Unary.Closure.Base.html#2458" class="Bound">T</a> <a id="2529" href="Relation.Unary.Closure.Base.html#2514" class="Bound">x</a>


<a id="2533" class="Comment">------------------------------------------------------------------------</a>
<a id="2606" class="Comment">-- Basic functions relating □ and ◇</a>

<a id="2643" class="Keyword">module</a> <a id="2650" href="Relation.Unary.Closure.Base.html#2650" class="Module">_</a> <a id="2652" class="Symbol">{</a><a id="2653" href="Relation.Unary.Closure.Base.html#2653" class="Bound">t</a> <a id="2655" href="Relation.Unary.Closure.Base.html#2655" class="Bound">p</a><a id="2656" class="Symbol">}</a> <a id="2658" class="Symbol">{</a><a id="2659" href="Relation.Unary.Closure.Base.html#2659" class="Bound">T</a> <a id="2661" class="Symbol">:</a> <a id="2663" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="2668" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="2670" href="Relation.Unary.Closure.Base.html#2653" class="Bound">t</a><a id="2671" class="Symbol">}</a> <a id="2673" class="Symbol">{</a><a id="2674" href="Relation.Unary.Closure.Base.html#2674" class="Bound">P</a> <a id="2676" class="Symbol">:</a> <a id="2678" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="2683" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="2685" href="Relation.Unary.Closure.Base.html#2655" class="Bound">p</a><a id="2686" class="Symbol">}</a> <a id="2688" class="Keyword">where</a>

  <a id="2697" href="Relation.Unary.Closure.Base.html#2697" class="Function">curry</a> <a id="2703" class="Symbol">:</a> <a id="2705" class="Symbol">(∀</a> <a id="2708" class="Symbol">{</a><a id="2709" href="Relation.Unary.Closure.Base.html#2709" class="Bound">x</a><a id="2710" class="Symbol">}</a> <a id="2712" class="Symbol">→</a> <a id="2714" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="2716" href="Relation.Unary.Closure.Base.html#2659" class="Bound">T</a> <a id="2718" href="Relation.Unary.Closure.Base.html#2709" class="Bound">x</a> <a id="2720" class="Symbol">→</a> <a id="2722" href="Relation.Unary.Closure.Base.html#2674" class="Bound">P</a> <a id="2724" href="Relation.Unary.Closure.Base.html#2709" class="Bound">x</a><a id="2725" class="Symbol">)</a> <a id="2727" class="Symbol">→</a> <a id="2729" class="Symbol">(∀</a> <a id="2732" class="Symbol">{</a><a id="2733" href="Relation.Unary.Closure.Base.html#2733" class="Bound">x</a><a id="2734" class="Symbol">}</a> <a id="2736" class="Symbol">→</a> <a id="2738" href="Relation.Unary.Closure.Base.html#2659" class="Bound">T</a> <a id="2740" href="Relation.Unary.Closure.Base.html#2733" class="Bound">x</a> <a id="2742" class="Symbol">→</a> <a id="2744" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="2746" href="Relation.Unary.Closure.Base.html#2674" class="Bound">P</a> <a id="2748" href="Relation.Unary.Closure.Base.html#2733" class="Bound">x</a><a id="2749" class="Symbol">)</a>
  <a id="2753" href="Relation.Unary.Closure.Base.html#2697" class="Function">curry</a> <a id="2759" href="Relation.Unary.Closure.Base.html#2759" class="Bound">f</a> <a id="2761" href="Relation.Unary.Closure.Base.html#2761" class="Bound">tx</a> <a id="2764" href="Relation.Unary.Closure.Base.html#2764" class="Bound">x∼y</a> <a id="2768" class="Symbol">=</a> <a id="2770" href="Relation.Unary.Closure.Base.html#2759" class="Bound">f</a> <a id="2772" class="Symbol">(</a><a id="2773" href="Data.Product.Base.html#1932" class="Function Operator">-,</a> <a id="2776" href="Relation.Unary.Closure.Base.html#2764" class="Bound">x∼y</a> <a id="2780" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2782" href="Relation.Unary.Closure.Base.html#2761" class="Bound">tx</a><a id="2784" class="Symbol">)</a>

  <a id="2789" href="Relation.Unary.Closure.Base.html#2789" class="Function">uncurry</a> <a id="2797" class="Symbol">:</a> <a id="2799" class="Symbol">(∀</a> <a id="2802" class="Symbol">{</a><a id="2803" href="Relation.Unary.Closure.Base.html#2803" class="Bound">x</a><a id="2804" class="Symbol">}</a> <a id="2806" class="Symbol">→</a> <a id="2808" href="Relation.Unary.Closure.Base.html#2659" class="Bound">T</a> <a id="2810" href="Relation.Unary.Closure.Base.html#2803" class="Bound">x</a> <a id="2812" class="Symbol">→</a> <a id="2814" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="2816" href="Relation.Unary.Closure.Base.html#2674" class="Bound">P</a> <a id="2818" href="Relation.Unary.Closure.Base.html#2803" class="Bound">x</a><a id="2819" class="Symbol">)</a> <a id="2821" class="Symbol">→</a> <a id="2823" class="Symbol">(∀</a> <a id="2826" class="Symbol">{</a><a id="2827" href="Relation.Unary.Closure.Base.html#2827" class="Bound">x</a><a id="2828" class="Symbol">}</a> <a id="2830" class="Symbol">→</a> <a id="2832" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="2834" href="Relation.Unary.Closure.Base.html#2659" class="Bound">T</a> <a id="2836" href="Relation.Unary.Closure.Base.html#2827" class="Bound">x</a> <a id="2838" class="Symbol">→</a> <a id="2840" href="Relation.Unary.Closure.Base.html#2674" class="Bound">P</a> <a id="2842" href="Relation.Unary.Closure.Base.html#2827" class="Bound">x</a><a id="2843" class="Symbol">)</a>
  <a id="2847" href="Relation.Unary.Closure.Base.html#2789" class="Function">uncurry</a> <a id="2855" href="Relation.Unary.Closure.Base.html#2855" class="Bound">f</a> <a id="2857" class="Symbol">(_</a> <a id="2860" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2862" href="Relation.Unary.Closure.Base.html#2862" class="Bound">y∼x</a> <a id="2866" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2868" href="Relation.Unary.Closure.Base.html#2868" class="Bound">ty</a><a id="2870" class="Symbol">)</a> <a id="2872" class="Symbol">=</a> <a id="2874" href="Relation.Unary.Closure.Base.html#2855" class="Bound">f</a> <a id="2876" href="Relation.Unary.Closure.Base.html#2868" class="Bound">ty</a> <a id="2879" href="Relation.Unary.Closure.Base.html#2862" class="Bound">y∼x</a>

<a id="2884" class="Comment">------------------------------------------------------------------------</a>
<a id="2957" class="Comment">-- Properties</a>

<a id="2972" class="Keyword">module</a> <a id="□"></a><a id="2979" href="Relation.Unary.Closure.Base.html#2979" class="Module">□</a> <a id="2981" class="Symbol">{</a><a id="2982" href="Relation.Unary.Closure.Base.html#2982" class="Bound">t</a><a id="2983" class="Symbol">}</a> <a id="2985" class="Symbol">{</a><a id="2986" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="2988" class="Symbol">:</a> <a id="2990" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="2995" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="2997" href="Relation.Unary.Closure.Base.html#2982" class="Bound">t</a><a id="2998" class="Symbol">}</a> <a id="3000" class="Keyword">where</a>

  <a id="□.reindex"></a><a id="3009" href="Relation.Unary.Closure.Base.html#3009" class="Function">reindex</a> <a id="3017" class="Symbol">:</a> <a id="3019" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a> <a id="3030" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3032" class="Symbol">→</a> <a id="3034" class="Symbol">∀</a> <a id="3036" class="Symbol">{</a><a id="3037" href="Relation.Unary.Closure.Base.html#3037" class="Bound">x</a> <a id="3039" href="Relation.Unary.Closure.Base.html#3039" class="Bound">y</a><a id="3040" class="Symbol">}</a> <a id="3042" class="Symbol">→</a> <a id="3044" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3046" href="Relation.Unary.Closure.Base.html#3037" class="Bound">x</a> <a id="3048" href="Relation.Unary.Closure.Base.html#3039" class="Bound">y</a> <a id="3050" class="Symbol">→</a> <a id="3052" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3054" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3056" href="Relation.Unary.Closure.Base.html#3037" class="Bound">x</a> <a id="3058" class="Symbol">→</a> <a id="3060" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3062" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3064" href="Relation.Unary.Closure.Base.html#3039" class="Bound">y</a>
  <a id="3068" href="Relation.Unary.Closure.Base.html#3009" class="Function">reindex</a> <a id="3076" href="Relation.Unary.Closure.Base.html#3076" class="Bound">trans</a> <a id="3082" href="Relation.Unary.Closure.Base.html#3082" class="Bound">x∼y</a> <a id="3086" href="Relation.Unary.Closure.Base.html#3086" class="Bound">□Tx</a> <a id="3090" href="Relation.Unary.Closure.Base.html#3090" class="Bound">y∼z</a> <a id="3094" class="Symbol">=</a> <a id="3096" href="Relation.Unary.Closure.Base.html#3086" class="Bound">□Tx</a> <a id="3100" class="Symbol">(</a><a id="3101" href="Relation.Unary.Closure.Base.html#3076" class="Bound">trans</a> <a id="3107" href="Relation.Unary.Closure.Base.html#3082" class="Bound">x∼y</a> <a id="3111" href="Relation.Unary.Closure.Base.html#3090" class="Bound">y∼z</a><a id="3114" class="Symbol">)</a>

  <a id="3119" class="Comment">-- Provided that R is reflexive and Transitive, □ is a comonad</a>
  <a id="□.map"></a><a id="3184" href="Relation.Unary.Closure.Base.html#3184" class="Function">map</a> <a id="3188" class="Symbol">:</a> <a id="3190" class="Symbol">∀</a> <a id="3192" class="Symbol">{</a><a id="3193" href="Relation.Unary.Closure.Base.html#3193" class="Bound">u</a><a id="3194" class="Symbol">}</a> <a id="3196" class="Symbol">{</a><a id="3197" href="Relation.Unary.Closure.Base.html#3197" class="Bound">U</a> <a id="3199" class="Symbol">:</a> <a id="3201" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="3206" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="3208" href="Relation.Unary.Closure.Base.html#3193" class="Bound">u</a><a id="3209" class="Symbol">}</a> <a id="3211" class="Symbol">{</a><a id="3212" href="Relation.Unary.Closure.Base.html#3212" class="Bound">x</a><a id="3213" class="Symbol">}</a> <a id="3215" class="Symbol">→</a> <a id="3217" class="Symbol">(∀</a> <a id="3220" class="Symbol">{</a><a id="3221" href="Relation.Unary.Closure.Base.html#3221" class="Bound">x</a><a id="3222" class="Symbol">}</a> <a id="3224" class="Symbol">→</a> <a id="3226" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3228" href="Relation.Unary.Closure.Base.html#3221" class="Bound">x</a> <a id="3230" class="Symbol">→</a> <a id="3232" href="Relation.Unary.Closure.Base.html#3197" class="Bound">U</a> <a id="3234" href="Relation.Unary.Closure.Base.html#3221" class="Bound">x</a><a id="3235" class="Symbol">)</a> <a id="3237" class="Symbol">→</a> <a id="3239" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3241" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3243" href="Relation.Unary.Closure.Base.html#3212" class="Bound">x</a> <a id="3245" class="Symbol">→</a> <a id="3247" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3249" href="Relation.Unary.Closure.Base.html#3197" class="Bound">U</a> <a id="3251" href="Relation.Unary.Closure.Base.html#3212" class="Bound">x</a>
  <a id="3255" href="Relation.Unary.Closure.Base.html#3184" class="Function">map</a> <a id="3259" href="Relation.Unary.Closure.Base.html#3259" class="Bound">f</a> <a id="3261" href="Relation.Unary.Closure.Base.html#3261" class="Bound">□Tx</a> <a id="3265" href="Relation.Unary.Closure.Base.html#3265" class="Bound">x~y</a> <a id="3269" class="Symbol">=</a> <a id="3271" href="Relation.Unary.Closure.Base.html#3259" class="Bound">f</a> <a id="3273" class="Symbol">(</a><a id="3274" href="Relation.Unary.Closure.Base.html#3261" class="Bound">□Tx</a> <a id="3278" href="Relation.Unary.Closure.Base.html#3265" class="Bound">x~y</a><a id="3281" class="Symbol">)</a>

  <a id="□.extract"></a><a id="3286" href="Relation.Unary.Closure.Base.html#3286" class="Function">extract</a> <a id="3294" class="Symbol">:</a> <a id="3296" href="Relation.Binary.Definitions.html#1355" class="Function">Reflexive</a> <a id="3306" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3308" class="Symbol">→</a> <a id="3310" class="Symbol">∀</a> <a id="3312" class="Symbol">{</a><a id="3313" href="Relation.Unary.Closure.Base.html#3313" class="Bound">x</a><a id="3314" class="Symbol">}</a> <a id="3316" class="Symbol">→</a> <a id="3318" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3320" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3322" href="Relation.Unary.Closure.Base.html#3313" class="Bound">x</a> <a id="3324" class="Symbol">→</a> <a id="3326" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3328" href="Relation.Unary.Closure.Base.html#3313" class="Bound">x</a>
  <a id="3332" href="Relation.Unary.Closure.Base.html#3286" class="Function">extract</a> <a id="3340" href="Relation.Unary.Closure.Base.html#3340" class="Bound">refl</a> <a id="3345" href="Relation.Unary.Closure.Base.html#3345" class="Bound">□Tx</a> <a id="3349" class="Symbol">=</a> <a id="3351" href="Relation.Unary.Closure.Base.html#3345" class="Bound">□Tx</a> <a id="3355" href="Relation.Unary.Closure.Base.html#3340" class="Bound">refl</a>

  <a id="□.duplicate"></a><a id="3363" href="Relation.Unary.Closure.Base.html#3363" class="Function">duplicate</a> <a id="3373" class="Symbol">:</a> <a id="3375" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a> <a id="3386" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3388" class="Symbol">→</a> <a id="3390" class="Symbol">∀</a> <a id="3392" class="Symbol">{</a><a id="3393" href="Relation.Unary.Closure.Base.html#3393" class="Bound">x</a><a id="3394" class="Symbol">}</a> <a id="3396" class="Symbol">→</a> <a id="3398" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3400" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a> <a id="3402" href="Relation.Unary.Closure.Base.html#3393" class="Bound">x</a> <a id="3404" class="Symbol">→</a> <a id="3406" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3408" class="Symbol">(</a><a id="3409" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3411" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a><a id="3412" class="Symbol">)</a> <a id="3414" href="Relation.Unary.Closure.Base.html#3393" class="Bound">x</a>
  <a id="3418" href="Relation.Unary.Closure.Base.html#3363" class="Function">duplicate</a> <a id="3428" href="Relation.Unary.Closure.Base.html#3428" class="Bound">trans</a> <a id="3434" href="Relation.Unary.Closure.Base.html#3434" class="Bound">□Tx</a> <a id="3438" href="Relation.Unary.Closure.Base.html#3438" class="Bound">x∼y</a> <a id="3442" href="Relation.Unary.Closure.Base.html#3442" class="Bound">y∼z</a> <a id="3446" class="Symbol">=</a> <a id="3448" href="Relation.Unary.Closure.Base.html#3434" class="Bound">□Tx</a> <a id="3452" class="Symbol">(</a><a id="3453" href="Relation.Unary.Closure.Base.html#3428" class="Bound">trans</a> <a id="3459" href="Relation.Unary.Closure.Base.html#3438" class="Bound">x∼y</a> <a id="3463" href="Relation.Unary.Closure.Base.html#3442" class="Bound">y∼z</a><a id="3466" class="Symbol">)</a>

  <a id="3471" class="Comment">-- Provided that R is transitive, □ is a closure operator</a>
  <a id="3531" class="Comment">-- i.e. for any `T`, `□ T` is closed.</a>
  <a id="□.closed"></a><a id="3571" href="Relation.Unary.Closure.Base.html#3571" class="Function">closed</a> <a id="3578" class="Symbol">:</a> <a id="3580" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a> <a id="3591" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3593" class="Symbol">→</a> <a id="3595" href="Relation.Unary.Closure.Base.html#2446" class="Record">Closed</a> <a id="3602" class="Symbol">(</a><a id="3603" href="Relation.Unary.Closure.Base.html#956" class="Function">□</a> <a id="3605" href="Relation.Unary.Closure.Base.html#2986" class="Bound">T</a><a id="3606" class="Symbol">)</a>
  <a id="3610" href="Relation.Unary.Closure.Base.html#3571" class="Function">closed</a> <a id="3617" href="Relation.Unary.Closure.Base.html#3617" class="Bound">trans</a> <a id="3623" class="Symbol">=</a> <a id="3625" class="Keyword">record</a> <a id="3632" class="Symbol">{</a> <a id="3634" href="Relation.Unary.Closure.Base.html#2504" class="Field">next</a> <a id="3639" class="Symbol">=</a> <a id="3641" href="Relation.Unary.Closure.Base.html#3363" class="Function">duplicate</a> <a id="3651" href="Relation.Unary.Closure.Base.html#3617" class="Bound">trans</a> <a id="3657" class="Symbol">}</a>

<a id="3660" class="Keyword">module</a> <a id="◇"></a><a id="3667" href="Relation.Unary.Closure.Base.html#3667" class="Module">◇</a> <a id="3669" class="Symbol">{</a><a id="3670" href="Relation.Unary.Closure.Base.html#3670" class="Bound">t</a><a id="3671" class="Symbol">}</a> <a id="3673" class="Symbol">{</a><a id="3674" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="3676" class="Symbol">:</a> <a id="3678" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="3683" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="3685" href="Relation.Unary.Closure.Base.html#3670" class="Bound">t</a><a id="3686" class="Symbol">}</a> <a id="3688" class="Keyword">where</a>

  <a id="◇.reindex"></a><a id="3697" href="Relation.Unary.Closure.Base.html#3697" class="Function">reindex</a> <a id="3705" class="Symbol">:</a> <a id="3707" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a> <a id="3718" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3720" class="Symbol">→</a> <a id="3722" class="Symbol">∀</a> <a id="3724" class="Symbol">{</a><a id="3725" href="Relation.Unary.Closure.Base.html#3725" class="Bound">x</a> <a id="3727" href="Relation.Unary.Closure.Base.html#3727" class="Bound">y</a><a id="3728" class="Symbol">}</a> <a id="3730" class="Symbol">→</a> <a id="3732" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="3734" href="Relation.Unary.Closure.Base.html#3725" class="Bound">x</a> <a id="3736" href="Relation.Unary.Closure.Base.html#3727" class="Bound">y</a> <a id="3738" class="Symbol">→</a> <a id="3740" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="3742" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="3744" href="Relation.Unary.Closure.Base.html#3725" class="Bound">x</a> <a id="3746" class="Symbol">→</a> <a id="3748" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="3750" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="3752" href="Relation.Unary.Closure.Base.html#3727" class="Bound">y</a>
  <a id="3756" href="Relation.Unary.Closure.Base.html#3697" class="Function">reindex</a> <a id="3764" href="Relation.Unary.Closure.Base.html#3764" class="Bound">trans</a> <a id="3770" href="Relation.Unary.Closure.Base.html#3770" class="Bound">x∼y</a> <a id="3774" class="Symbol">(</a><a id="3775" href="Relation.Unary.Closure.Base.html#3775" class="Bound">z</a> <a id="3777" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3779" href="Relation.Unary.Closure.Base.html#3779" class="Bound">z∼x</a> <a id="3783" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3785" href="Relation.Unary.Closure.Base.html#3785" class="Bound">tz</a><a id="3787" class="Symbol">)</a> <a id="3789" class="Symbol">=</a> <a id="3791" href="Relation.Unary.Closure.Base.html#3775" class="Bound">z</a> <a id="3793" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3795" href="Relation.Unary.Closure.Base.html#3764" class="Bound">trans</a> <a id="3801" href="Relation.Unary.Closure.Base.html#3779" class="Bound">z∼x</a> <a id="3805" href="Relation.Unary.Closure.Base.html#3770" class="Bound">x∼y</a> <a id="3809" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3811" href="Relation.Unary.Closure.Base.html#3785" class="Bound">tz</a>

  <a id="3817" class="Comment">-- Provided that R is reflexive and Transitive, ◇ is a monad</a>
  <a id="◇.map"></a><a id="3880" href="Relation.Unary.Closure.Base.html#3880" class="Function">map</a> <a id="3884" class="Symbol">:</a> <a id="3886" class="Symbol">∀</a> <a id="3888" class="Symbol">{</a><a id="3889" href="Relation.Unary.Closure.Base.html#3889" class="Bound">u</a><a id="3890" class="Symbol">}</a> <a id="3892" class="Symbol">{</a><a id="3893" href="Relation.Unary.Closure.Base.html#3893" class="Bound">U</a> <a id="3895" class="Symbol">:</a> <a id="3897" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="3902" href="Relation.Unary.Closure.Base.html#375" class="Bound">A</a> <a id="3904" href="Relation.Unary.Closure.Base.html#3889" class="Bound">u</a><a id="3905" class="Symbol">}</a> <a id="3907" class="Symbol">{</a><a id="3908" href="Relation.Unary.Closure.Base.html#3908" class="Bound">x</a><a id="3909" class="Symbol">}</a> <a id="3911" class="Symbol">→</a> <a id="3913" class="Symbol">(∀</a> <a id="3916" class="Symbol">{</a><a id="3917" href="Relation.Unary.Closure.Base.html#3917" class="Bound">x</a><a id="3918" class="Symbol">}</a> <a id="3920" class="Symbol">→</a> <a id="3922" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="3924" href="Relation.Unary.Closure.Base.html#3917" class="Bound">x</a> <a id="3926" class="Symbol">→</a> <a id="3928" href="Relation.Unary.Closure.Base.html#3893" class="Bound">U</a> <a id="3930" href="Relation.Unary.Closure.Base.html#3917" class="Bound">x</a><a id="3931" class="Symbol">)</a> <a id="3933" class="Symbol">→</a> <a id="3935" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="3937" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="3939" href="Relation.Unary.Closure.Base.html#3908" class="Bound">x</a> <a id="3941" class="Symbol">→</a> <a id="3943" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="3945" href="Relation.Unary.Closure.Base.html#3893" class="Bound">U</a> <a id="3947" href="Relation.Unary.Closure.Base.html#3908" class="Bound">x</a>
  <a id="3951" href="Relation.Unary.Closure.Base.html#3880" class="Function">map</a> <a id="3955" href="Relation.Unary.Closure.Base.html#3955" class="Bound">f</a> <a id="3957" class="Symbol">(</a><a id="3958" href="Relation.Unary.Closure.Base.html#3958" class="Bound">y</a> <a id="3960" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3962" href="Relation.Unary.Closure.Base.html#3962" class="Bound">y∼x</a> <a id="3966" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3968" href="Relation.Unary.Closure.Base.html#3968" class="Bound">ty</a><a id="3970" class="Symbol">)</a> <a id="3972" class="Symbol">=</a> <a id="3974" href="Relation.Unary.Closure.Base.html#3958" class="Bound">y</a> <a id="3976" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3978" href="Relation.Unary.Closure.Base.html#3962" class="Bound">y∼x</a> <a id="3982" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3984" href="Relation.Unary.Closure.Base.html#3955" class="Bound">f</a> <a id="3986" href="Relation.Unary.Closure.Base.html#3968" class="Bound">ty</a>

  <a id="◇.pure"></a><a id="3992" href="Relation.Unary.Closure.Base.html#3992" class="Function">pure</a> <a id="3997" class="Symbol">:</a> <a id="3999" href="Relation.Binary.Definitions.html#1355" class="Function">Reflexive</a> <a id="4009" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="4011" class="Symbol">→</a> <a id="4013" class="Symbol">∀</a> <a id="4015" class="Symbol">{</a><a id="4016" href="Relation.Unary.Closure.Base.html#4016" class="Bound">x</a><a id="4017" class="Symbol">}</a> <a id="4019" class="Symbol">→</a> <a id="4021" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="4023" href="Relation.Unary.Closure.Base.html#4016" class="Bound">x</a> <a id="4025" class="Symbol">→</a> <a id="4027" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="4029" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="4031" href="Relation.Unary.Closure.Base.html#4016" class="Bound">x</a>
  <a id="4035" href="Relation.Unary.Closure.Base.html#3992" class="Function">pure</a> <a id="4040" href="Relation.Unary.Closure.Base.html#4040" class="Bound">refl</a> <a id="4045" href="Relation.Unary.Closure.Base.html#4045" class="Bound">tx</a> <a id="4048" class="Symbol">=</a> <a id="4050" href="Data.Product.Base.html#1932" class="Function Operator">-,</a> <a id="4053" href="Relation.Unary.Closure.Base.html#4040" class="Bound">refl</a> <a id="4058" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4060" href="Relation.Unary.Closure.Base.html#4045" class="Bound">tx</a>

  <a id="◇.join"></a><a id="4066" href="Relation.Unary.Closure.Base.html#4066" class="Function">join</a> <a id="4071" class="Symbol">:</a> <a id="4073" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a> <a id="4084" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="4086" class="Symbol">→</a> <a id="4088" class="Symbol">∀</a> <a id="4090" class="Symbol">{</a><a id="4091" href="Relation.Unary.Closure.Base.html#4091" class="Bound">x</a><a id="4092" class="Symbol">}</a> <a id="4094" class="Symbol">→</a> <a id="4096" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="4098" class="Symbol">(</a><a id="4099" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="4101" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a><a id="4102" class="Symbol">)</a> <a id="4104" href="Relation.Unary.Closure.Base.html#4091" class="Bound">x</a> <a id="4106" class="Symbol">→</a> <a id="4108" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="4110" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="4112" href="Relation.Unary.Closure.Base.html#4091" class="Bound">x</a>
  <a id="4116" href="Relation.Unary.Closure.Base.html#4066" class="Function">join</a> <a id="4121" href="Relation.Unary.Closure.Base.html#4121" class="Bound">trans</a> <a id="4127" class="Symbol">(_</a> <a id="4130" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4132" href="Relation.Unary.Closure.Base.html#4132" class="Bound">y∼x</a> <a id="4136" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4138" class="Symbol">_</a> <a id="4140" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4142" href="Relation.Unary.Closure.Base.html#4142" class="Bound">z∼y</a> <a id="4146" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4148" href="Relation.Unary.Closure.Base.html#4148" class="Bound">tz</a><a id="4150" class="Symbol">)</a> <a id="4152" class="Symbol">=</a> <a id="4154" class="Symbol">_</a> <a id="4156" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4158" href="Relation.Unary.Closure.Base.html#4121" class="Bound">trans</a> <a id="4164" href="Relation.Unary.Closure.Base.html#4142" class="Bound">z∼y</a> <a id="4168" href="Relation.Unary.Closure.Base.html#4132" class="Bound">y∼x</a> <a id="4172" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4174" href="Relation.Unary.Closure.Base.html#4148" class="Bound">tz</a>

  <a id="4180" class="Comment">-- Provided that R is transitive, ◇ is a closure operator</a>
  <a id="4240" class="Comment">-- i.e. for any `T`, `◇ T` is closed.</a>
  <a id="◇.closed"></a><a id="4280" href="Relation.Unary.Closure.Base.html#4280" class="Function">closed</a> <a id="4287" class="Symbol">:</a> <a id="4289" href="Relation.Binary.Definitions.html#2030" class="Function">Transitive</a> <a id="4300" href="Relation.Unary.Closure.Base.html#387" class="Bound">R</a> <a id="4302" class="Symbol">→</a> <a id="4304" href="Relation.Unary.Closure.Base.html#2446" class="Record">Closed</a> <a id="4311" class="Symbol">(</a><a id="4312" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="4314" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a><a id="4315" class="Symbol">)</a>
  <a id="4319" href="Relation.Unary.Closure.Base.html#4280" class="Function">closed</a> <a id="4326" href="Relation.Unary.Closure.Base.html#4326" class="Bound">trans</a> <a id="4332" class="Symbol">=</a> <a id="4334" class="Keyword">record</a> <a id="4341" class="Symbol">{</a> <a id="4343" href="Relation.Unary.Closure.Base.html#2504" class="Field">next</a> <a id="4348" class="Symbol">=</a> <a id="4350" class="Symbol">λ</a> <a id="4352" href="Relation.Unary.Closure.Base.html#4352" class="Bound">◇Tx</a> <a id="4356" href="Relation.Unary.Closure.Base.html#4356" class="Bound">x∼y</a> <a id="4360" class="Symbol">→</a> <a id="4362" href="Relation.Unary.Closure.Base.html#3697" class="Function">reindex</a> <a id="4370" href="Relation.Unary.Closure.Base.html#4326" class="Bound">trans</a> <a id="4376" href="Relation.Unary.Closure.Base.html#4356" class="Bound">x∼y</a> <a id="4380" href="Relation.Unary.Closure.Base.html#4352" class="Bound">◇Tx</a> <a id="4384" class="Symbol">}</a>

  <a id="◇.run"></a><a id="4389" href="Relation.Unary.Closure.Base.html#4389" class="Function">run</a> <a id="4393" class="Symbol">:</a> <a id="4395" href="Relation.Unary.Closure.Base.html#2446" class="Record">Closed</a> <a id="4402" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="4404" class="Symbol">→</a> <a id="4406" class="Symbol">∀</a> <a id="4408" class="Symbol">{</a><a id="4409" href="Relation.Unary.Closure.Base.html#4409" class="Bound">x</a><a id="4410" class="Symbol">}</a> <a id="4412" class="Symbol">→</a> <a id="4414" href="Relation.Unary.Closure.Base.html#1830" class="Function">◇</a> <a id="4416" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="4418" href="Relation.Unary.Closure.Base.html#4409" class="Bound">x</a> <a id="4420" class="Symbol">→</a> <a id="4422" href="Relation.Unary.Closure.Base.html#3674" class="Bound">T</a> <a id="4424" href="Relation.Unary.Closure.Base.html#4409" class="Bound">x</a>
  <a id="4428" href="Relation.Unary.Closure.Base.html#4389" class="Function">run</a> <a id="4432" href="Relation.Unary.Closure.Base.html#4432" class="Bound">closed</a> <a id="4439" class="Symbol">(_</a> <a id="4442" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4444" href="Relation.Unary.Closure.Base.html#4444" class="Bound">y∼x</a> <a id="4448" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="4450" href="Relation.Unary.Closure.Base.html#4450" class="Bound">ty</a><a id="4452" class="Symbol">)</a> <a id="4454" class="Symbol">=</a> <a id="4456" href="Relation.Unary.Closure.Base.html#2504" class="Field">Closed.next</a> <a id="4468" href="Relation.Unary.Closure.Base.html#4432" class="Bound">closed</a> <a id="4475" href="Relation.Unary.Closure.Base.html#4450" class="Bound">ty</a> <a id="4478" href="Relation.Unary.Closure.Base.html#4444" class="Bound">y∼x</a>
</pre></body></html>