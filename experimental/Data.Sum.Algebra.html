<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Sum.Algebra</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Algebraic properties of sums (disjoint unions)</a>
<a id="156" class="Comment">------------------------------------------------------------------------</a>

<a id="230" class="Symbol">{-#</a> <a id="234" class="Keyword">OPTIONS</a> <a id="242" class="Pragma">--cubical-compatible</a> <a id="263" class="Pragma">--safe</a> <a id="270" class="Symbol">#-}</a>

<a id="275" class="Keyword">module</a> <a id="282" href="Data.Sum.Algebra.html" class="Module">Data.Sum.Algebra</a> <a id="299" class="Keyword">where</a>

<a id="306" class="Keyword">open</a> <a id="311" class="Keyword">import</a> <a id="318" href="Algebra.html" class="Module">Algebra</a>
<a id="326" class="Keyword">open</a> <a id="331" class="Keyword">import</a> <a id="338" href="Data.Empty.Polymorphic.html" class="Module">Data.Empty.Polymorphic</a> <a id="361" class="Keyword">using</a> <a id="367" class="Symbol">(</a><a id="368" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a><a id="369" class="Symbol">)</a>
<a id="371" class="Keyword">open</a> <a id="376" class="Keyword">import</a> <a id="383" href="Data.Product.html" class="Module">Data.Product</a> <a id="396" class="Keyword">using</a> <a id="402" class="Symbol">(</a><a id="403" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="406" class="Symbol">)</a>
<a id="408" class="Keyword">open</a> <a id="413" class="Keyword">import</a> <a id="420" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a>
<a id="434" class="Keyword">open</a> <a id="439" class="Keyword">import</a> <a id="446" href="Data.Sum.Properties.html" class="Module">Data.Sum.Properties</a>
<a id="466" class="Keyword">open</a> <a id="471" class="Keyword">import</a> <a id="478" href="Data.Unit.Polymorphic.html" class="Module">Data.Unit.Polymorphic</a> <a id="500" class="Keyword">using</a> <a id="506" class="Symbol">(</a><a id="507" href="Data.Unit.Polymorphic.Base.html#489" class="Function">⊤</a><a id="508" class="Symbol">;</a> <a id="510" href="Data.Unit.Polymorphic.Base.html#533" class="Function">tt</a><a id="512" class="Symbol">)</a>
<a id="514" class="Keyword">open</a> <a id="519" class="Keyword">import</a> <a id="526" href="Function.Base.html" class="Module">Function.Base</a> <a id="540" class="Keyword">using</a> <a id="546" class="Symbol">(</a><a id="547" href="Function.Base.html#704" class="Function">id</a><a id="549" class="Symbol">;</a> <a id="551" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="554" class="Symbol">)</a>
<a id="556" class="Keyword">open</a> <a id="561" class="Keyword">import</a> <a id="568" href="Function.Properties.Inverse.html" class="Module">Function.Properties.Inverse</a> <a id="596" class="Keyword">using</a> <a id="602" class="Symbol">(</a><a id="603" href="Function.Properties.Inverse.html#1432" class="Function">↔-isEquivalence</a><a id="618" class="Symbol">)</a>
<a id="620" class="Keyword">open</a> <a id="625" class="Keyword">import</a> <a id="632" href="Function.Bundles.html" class="Module">Function.Bundles</a> <a id="649" class="Keyword">using</a> <a id="655" class="Symbol">(</a><a id="656" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a><a id="659" class="Symbol">;</a> <a id="661" href="Function.Bundles.html#6058" class="Record">Inverse</a><a id="668" class="Symbol">;</a> <a id="670" href="Function.Bundles.html#11359" class="Function">mk↔′</a><a id="674" class="Symbol">)</a>
<a id="676" class="Keyword">open</a> <a id="681" class="Keyword">import</a> <a id="688" href="Level.html" class="Module">Level</a> <a id="694" class="Keyword">using</a> <a id="700" class="Symbol">(</a><a id="701" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="706" class="Symbol">;</a> <a id="708" href="Agda.Primitive.html#774" class="Primitive">suc</a><a id="711" class="Symbol">)</a>
<a id="713" class="Keyword">open</a> <a id="718" class="Keyword">import</a> <a id="725" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a>
  <a id="770" class="Keyword">using</a> <a id="776" class="Symbol">(</a><a id="777" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="780" class="Symbol">;</a> <a id="782" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="786" class="Symbol">;</a> <a id="788" href="Relation.Binary.PropositionalEquality.Core.html#1139" class="Function">cong</a><a id="792" class="Symbol">;</a> <a id="794" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a><a id="799" class="Symbol">)</a>

<a id="802" class="Keyword">import</a> <a id="809" href="Function.Definitions.html" class="Module">Function.Definitions</a> <a id="830" class="Symbol">as</a> <a id="833" class="Module">FuncDef</a>

<a id="842" class="Comment">------------------------------------------------------------------------</a>
<a id="915" class="Comment">-- Setup</a>

<a id="925" class="Keyword">private</a>
  <a id="935" class="Keyword">variable</a>
    <a id="948" href="Data.Sum.Algebra.html#948" class="Generalizable">a</a> <a id="950" href="Data.Sum.Algebra.html#950" class="Generalizable">b</a> <a id="952" href="Data.Sum.Algebra.html#952" class="Generalizable">c</a> <a id="954" href="Data.Sum.Algebra.html#954" class="Generalizable">d</a> <a id="956" class="Symbol">:</a> <a id="958" href="Agda.Primitive.html#591" class="Postulate">Level</a>
    <a id="968" href="Data.Sum.Algebra.html#968" class="Generalizable">A</a> <a id="970" class="Symbol">:</a> <a id="972" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="976" href="Data.Sum.Algebra.html#948" class="Generalizable">a</a>
    <a id="982" href="Data.Sum.Algebra.html#982" class="Generalizable">B</a> <a id="984" class="Symbol">:</a> <a id="986" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="990" href="Data.Sum.Algebra.html#950" class="Generalizable">b</a>
    <a id="996" href="Data.Sum.Algebra.html#996" class="Generalizable">C</a> <a id="998" class="Symbol">:</a> <a id="1000" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1004" href="Data.Sum.Algebra.html#952" class="Generalizable">c</a>
    <a id="1010" href="Data.Sum.Algebra.html#1010" class="Generalizable">D</a> <a id="1012" class="Symbol">:</a> <a id="1014" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1018" href="Data.Sum.Algebra.html#954" class="Generalizable">d</a>

  <a id="1023" class="Comment">-- The module is needed because we need to pass `A` and `B` to `FuncDef`</a>
  <a id="1098" class="Keyword">module</a> <a id="1105" href="Data.Sum.Algebra.html#1105" class="Module">_</a> <a id="1107" class="Symbol">{</a><a id="1108" href="Data.Sum.Algebra.html#1108" class="Bound">A</a> <a id="1110" class="Symbol">:</a> <a id="1112" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1116" href="Data.Sum.Algebra.html#948" class="Generalizable">a</a><a id="1117" class="Symbol">}</a> <a id="1119" class="Symbol">{</a><a id="1120" href="Data.Sum.Algebra.html#1120" class="Bound">B</a> <a id="1122" class="Symbol">:</a> <a id="1124" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1128" href="Data.Sum.Algebra.html#950" class="Generalizable">b</a><a id="1129" class="Symbol">}</a> <a id="1131" class="Keyword">where</a>
    <a id="1141" class="Keyword">open</a> <a id="1146" href="Function.Definitions.html" class="Module">FuncDef</a> <a id="1154" class="Symbol">{</a><a id="1155" class="Argument">A</a> <a id="1157" class="Symbol">=</a> <a id="1159" href="Data.Sum.Algebra.html#1108" class="Bound">A</a><a id="1160" class="Symbol">}</a> <a id="1162" class="Symbol">{</a><a id="1163" href="Data.Sum.Algebra.html#1120" class="Bound">B</a><a id="1164" class="Symbol">}</a> <a id="1166" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a> <a id="1170" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a>

  <a id="♯"></a><a id="1177" href="Data.Sum.Algebra.html#1177" class="Function">♯</a> <a id="1179" class="Symbol">:</a> <a id="1181" class="Symbol">{</a><a id="1182" href="Data.Sum.Algebra.html#1182" class="Bound">B</a> <a id="1184" class="Symbol">:</a> <a id="1186" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a> <a id="1188" class="Symbol">{</a><a id="1189" href="Data.Sum.Algebra.html#948" class="Generalizable">a</a><a id="1190" class="Symbol">}</a> <a id="1192" class="Symbol">→</a> <a id="1194" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1198" href="Data.Sum.Algebra.html#950" class="Generalizable">b</a><a id="1199" class="Symbol">}</a> <a id="1201" class="Symbol">→</a> <a id="1203" class="Symbol">(</a><a id="1204" href="Data.Sum.Algebra.html#1204" class="Bound">w</a> <a id="1206" class="Symbol">:</a> <a id="1208" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a><a id="1209" class="Symbol">)</a> <a id="1211" class="Symbol">→</a> <a id="1213" href="Data.Sum.Algebra.html#1182" class="Bound">B</a> <a id="1215" href="Data.Sum.Algebra.html#1204" class="Bound">w</a>
  <a id="1219" href="Data.Sum.Algebra.html#1177" class="Function">♯</a> <a id="1221" class="Symbol">()</a>

<a id="1225" class="Comment">------------------------------------------------------------------------</a>
<a id="1298" class="Comment">-- Algebraic properties</a>

<a id="⊎-cong"></a><a id="1323" href="Data.Sum.Algebra.html#1323" class="Function">⊎-cong</a> <a id="1330" class="Symbol">:</a> <a id="1332" href="Data.Sum.Algebra.html#968" class="Generalizable">A</a> <a id="1334" href="Function.Bundles.html#9230" class="Function Operator">↔</a> <a id="1336" href="Data.Sum.Algebra.html#982" class="Generalizable">B</a> <a id="1338" class="Symbol">→</a> <a id="1340" href="Data.Sum.Algebra.html#996" class="Generalizable">C</a> <a id="1342" href="Function.Bundles.html#9230" class="Function Operator">↔</a> <a id="1344" href="Data.Sum.Algebra.html#1010" class="Generalizable">D</a> <a id="1346" class="Symbol">→</a> <a id="1348" class="Symbol">(</a><a id="1349" href="Data.Sum.Algebra.html#968" class="Generalizable">A</a> <a id="1351" href="Data.Sum.Base.html#743" class="Datatype Operator">⊎</a> <a id="1353" href="Data.Sum.Algebra.html#996" class="Generalizable">C</a><a id="1354" class="Symbol">)</a> <a id="1356" href="Function.Bundles.html#9230" class="Function Operator">↔</a> <a id="1358" class="Symbol">(</a><a id="1359" href="Data.Sum.Algebra.html#982" class="Generalizable">B</a> <a id="1361" href="Data.Sum.Base.html#743" class="Datatype Operator">⊎</a> <a id="1363" href="Data.Sum.Algebra.html#1010" class="Generalizable">D</a><a id="1364" class="Symbol">)</a>
<a id="1366" href="Data.Sum.Algebra.html#1323" class="Function">⊎-cong</a> <a id="1373" href="Data.Sum.Algebra.html#1373" class="Bound">i</a> <a id="1375" href="Data.Sum.Algebra.html#1375" class="Bound">j</a> <a id="1377" class="Symbol">=</a> <a id="1379" href="Function.Bundles.html#11359" class="Function">mk↔′</a> <a id="1384" class="Symbol">(</a><a id="1385" href="Data.Sum.Base.html#1371" class="Function">map</a> <a id="1389" href="Function.Bundles.html#6112" class="Function">I.to</a> <a id="1394" href="Function.Bundles.html#6112" class="Field">J.to</a><a id="1398" class="Symbol">)</a> <a id="1400" class="Symbol">(</a><a id="1401" href="Data.Sum.Base.html#1371" class="Function">map</a> <a id="1405" href="Function.Bundles.html#6136" class="Function">I.from</a> <a id="1412" href="Function.Bundles.html#6136" class="Field">J.from</a><a id="1418" class="Symbol">)</a>
  <a id="1422" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="1424" href="Relation.Binary.PropositionalEquality.Core.html#1139" class="Function">cong</a> <a id="1429" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="1434" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="1436" href="Function.Bundles.html#6282" class="Function">I.inverseˡ</a> <a id="1447" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="1449" href="Relation.Binary.PropositionalEquality.Core.html#1139" class="Function">cong</a> <a id="1454" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="1459" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="1461" href="Function.Bundles.html#6282" class="Function">J.inverseˡ</a> <a id="1472" href="Data.Sum.Base.html#929" class="Function Operator">]</a>
  <a id="1476" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="1478" href="Relation.Binary.PropositionalEquality.Core.html#1139" class="Function">cong</a> <a id="1483" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="1488" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="1490" href="Function.Bundles.html#6344" class="Function">I.inverseʳ</a> <a id="1501" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="1503" href="Relation.Binary.PropositionalEquality.Core.html#1139" class="Function">cong</a> <a id="1508" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="1513" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="1515" href="Function.Bundles.html#6344" class="Function">J.inverseʳ</a> <a id="1526" href="Data.Sum.Base.html#929" class="Function Operator">]</a>
  <a id="1530" class="Keyword">where</a> <a id="1536" class="Keyword">module</a> <a id="1543" href="Data.Sum.Algebra.html#1543" class="Module">I</a> <a id="1545" class="Symbol">=</a> <a id="1547" href="Function.Bundles.html#6058" class="Module">Inverse</a> <a id="1555" href="Data.Sum.Algebra.html#1373" class="Bound">i</a><a id="1556" class="Symbol">;</a> <a id="1558" class="Keyword">module</a> <a id="1565" href="Data.Sum.Algebra.html#1565" class="Module">J</a> <a id="1567" class="Symbol">=</a> <a id="1569" href="Function.Bundles.html#6058" class="Module">Inverse</a> <a id="1577" href="Data.Sum.Algebra.html#1375" class="Bound">j</a>

<a id="1580" class="Comment">-- ⊎ is commutative.</a>
<a id="1601" class="Comment">-- We don&#39;t use Commutative because it isn&#39;t polymorphic enough.</a>
<a id="⊎-comm"></a><a id="1666" href="Data.Sum.Algebra.html#1666" class="Function">⊎-comm</a> <a id="1673" class="Symbol">:</a> <a id="1675" class="Symbol">(</a><a id="1676" href="Data.Sum.Algebra.html#1676" class="Bound">A</a> <a id="1678" class="Symbol">:</a> <a id="1680" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1684" href="Data.Sum.Algebra.html#948" class="Generalizable">a</a><a id="1685" class="Symbol">)</a> <a id="1687" class="Symbol">(</a><a id="1688" href="Data.Sum.Algebra.html#1688" class="Bound">B</a> <a id="1690" class="Symbol">:</a> <a id="1692" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1696" href="Data.Sum.Algebra.html#950" class="Generalizable">b</a><a id="1697" class="Symbol">)</a> <a id="1699" class="Symbol">→</a> <a id="1701" class="Symbol">(</a><a id="1702" href="Data.Sum.Algebra.html#1676" class="Bound">A</a> <a id="1704" href="Data.Sum.Base.html#743" class="Datatype Operator">⊎</a> <a id="1706" href="Data.Sum.Algebra.html#1688" class="Bound">B</a><a id="1707" class="Symbol">)</a> <a id="1709" href="Function.Bundles.html#9230" class="Function Operator">↔</a> <a id="1711" class="Symbol">(</a><a id="1712" href="Data.Sum.Algebra.html#1688" class="Bound">B</a> <a id="1714" href="Data.Sum.Base.html#743" class="Datatype Operator">⊎</a> <a id="1716" href="Data.Sum.Algebra.html#1676" class="Bound">A</a><a id="1717" class="Symbol">)</a>
<a id="1719" href="Data.Sum.Algebra.html#1666" class="Function">⊎-comm</a> <a id="1726" class="Symbol">_</a> <a id="1728" class="Symbol">_</a> <a id="1730" class="Symbol">=</a> <a id="1732" href="Function.Bundles.html#11359" class="Function">mk↔′</a> <a id="1737" href="Data.Sum.Base.html#1303" class="Function">swap</a> <a id="1742" href="Data.Sum.Base.html#1303" class="Function">swap</a> <a id="1747" href="Data.Sum.Properties.html#1259" class="Function">swap-involutive</a> <a id="1763" href="Data.Sum.Properties.html#1259" class="Function">swap-involutive</a>

<a id="1780" class="Keyword">module</a> <a id="1787" href="Data.Sum.Algebra.html#1787" class="Module">_</a> <a id="1789" class="Symbol">(</a><a id="1790" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a> <a id="1792" class="Symbol">:</a> <a id="1794" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="1799" class="Symbol">)</a> <a id="1801" class="Keyword">where</a>

  <a id="1810" class="Comment">-- ⊎ is associative</a>
  <a id="1832" href="Data.Sum.Algebra.html#1832" class="Function">⊎-assoc</a> <a id="1840" class="Symbol">:</a> <a id="1842" href="Algebra.Definitions.html#1433" class="Function">Associative</a> <a id="1854" class="Symbol">{</a><a id="1855" class="Argument">ℓ</a> <a id="1857" class="Symbol">=</a> <a id="1859" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="1860" class="Symbol">}</a> <a id="1862" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="1866" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a>
  <a id="1872" href="Data.Sum.Algebra.html#1832" class="Function">⊎-assoc</a> <a id="1880" class="Symbol">_</a> <a id="1882" class="Symbol">_</a> <a id="1884" class="Symbol">_</a> <a id="1886" class="Symbol">=</a> <a id="1888" href="Function.Bundles.html#11359" class="Function">mk↔′</a> <a id="1893" href="Data.Sum.Base.html#1549" class="Function">assocʳ</a> <a id="1900" href="Data.Sum.Base.html#1622" class="Function">assocˡ</a>
    <a id="1911" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="1913" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="1919" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="1921" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="1923" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="1929" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="1931" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="1937" href="Data.Sum.Base.html#929" class="Function Operator">]</a> <a id="1939" href="Data.Sum.Base.html#929" class="Function Operator">]</a> <a id="1941" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="1943" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="1945" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="1951" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="1953" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="1959" href="Data.Sum.Base.html#929" class="Function Operator">]</a> <a id="1961" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="1963" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="1969" href="Data.Sum.Base.html#929" class="Function Operator">]</a>

  <a id="1974" class="Comment">-- ⊥ is an identity for ⊎</a>
  <a id="2002" href="Data.Sum.Algebra.html#2002" class="Function">⊎-identityˡ</a> <a id="2014" class="Symbol">:</a> <a id="2016" href="Algebra.Definitions.html#1593" class="Function">LeftIdentity</a> <a id="2029" class="Symbol">{</a><a id="2030" class="Argument">ℓ</a> <a id="2032" class="Symbol">=</a> <a id="2034" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="2035" class="Symbol">}</a> <a id="2037" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2041" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a> <a id="2043" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a>
  <a id="2049" href="Data.Sum.Algebra.html#2002" class="Function">⊎-identityˡ</a> <a id="2061" href="Data.Sum.Algebra.html#2061" class="Bound">A</a> <a id="2063" class="Symbol">=</a> <a id="2065" href="Function.Bundles.html#11359" class="Function">mk↔′</a> <a id="2070" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="2072" href="Data.Sum.Algebra.html#1177" class="Function">♯</a> <a id="2074" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="2076" href="Function.Base.html#704" class="Function">id</a> <a id="2079" href="Data.Sum.Base.html#929" class="Function Operator">]</a> <a id="2081" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="2086" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="2092" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="2094" href="Data.Sum.Algebra.html#1177" class="Function">♯</a> <a id="2096" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="2098" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="2104" href="Data.Sum.Base.html#929" class="Function Operator">]</a>

  <a id="2109" href="Data.Sum.Algebra.html#2109" class="Function">⊎-identityʳ</a> <a id="2121" class="Symbol">:</a> <a id="2123" href="Algebra.Definitions.html#1666" class="Function">RightIdentity</a> <a id="2137" class="Symbol">{</a><a id="2138" class="Argument">ℓ</a> <a id="2140" class="Symbol">=</a> <a id="2142" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="2143" class="Symbol">}</a> <a id="2145" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2149" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a> <a id="2151" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a>
  <a id="2157" href="Data.Sum.Algebra.html#2109" class="Function">⊎-identityʳ</a> <a id="2169" class="Symbol">_</a> <a id="2171" class="Symbol">=</a> <a id="2173" href="Function.Bundles.html#11359" class="Function">mk↔′</a> <a id="2178" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="2180" href="Function.Base.html#704" class="Function">id</a> <a id="2183" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="2185" href="Data.Sum.Algebra.html#1177" class="Function">♯</a> <a id="2187" href="Data.Sum.Base.html#929" class="Function Operator">]</a> <a id="2189" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2194" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="2200" href="Data.Sum.Base.html#929" class="Function Operator">[</a> <a id="2202" href="Relation.Binary.PropositionalEquality.Core.html#1206" class="Function">cong′</a> <a id="2208" href="Data.Sum.Base.html#929" class="Function Operator">,</a> <a id="2210" href="Data.Sum.Algebra.html#1177" class="Function">♯</a> <a id="2212" href="Data.Sum.Base.html#929" class="Function Operator">]</a>

  <a id="2217" href="Data.Sum.Algebra.html#2217" class="Function">⊎-identity</a> <a id="2228" class="Symbol">:</a> <a id="2230" href="Algebra.Definitions.html#1741" class="Function">Identity</a> <a id="2239" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2243" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a> <a id="2245" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a>
  <a id="2251" href="Data.Sum.Algebra.html#2217" class="Function">⊎-identity</a> <a id="2262" class="Symbol">=</a> <a id="2264" href="Data.Sum.Algebra.html#2002" class="Function">⊎-identityˡ</a> <a id="2276" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2278" href="Data.Sum.Algebra.html#2109" class="Function">⊎-identityʳ</a>

<a id="2291" class="Comment">------------------------------------------------------------------------</a>
<a id="2364" class="Comment">-- Algebraic structures</a>

  <a id="2391" href="Data.Sum.Algebra.html#2391" class="Function">⊎-isMagma</a> <a id="2401" class="Symbol">:</a> <a id="2403" href="Algebra.Structures.html#1126" class="Record">IsMagma</a> <a id="2411" class="Symbol">{</a><a id="2412" class="Argument">ℓ</a> <a id="2414" class="Symbol">=</a> <a id="2416" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="2417" class="Symbol">}</a> <a id="2419" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2423" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a>
  <a id="2429" href="Data.Sum.Algebra.html#2391" class="Function">⊎-isMagma</a> <a id="2439" class="Symbol">=</a> <a id="2441" class="Keyword">record</a>
    <a id="2452" class="Symbol">{</a> <a id="2454" href="Algebra.Structures.html#1178" class="Field">isEquivalence</a> <a id="2468" class="Symbol">=</a> <a id="2470" href="Function.Properties.Inverse.html#1432" class="Function">↔-isEquivalence</a>
    <a id="2490" class="Symbol">;</a> <a id="2492" href="Algebra.Structures.html#1216" class="Field">∙-cong</a>        <a id="2506" class="Symbol">=</a> <a id="2508" href="Data.Sum.Algebra.html#1323" class="Function">⊎-cong</a>
    <a id="2519" class="Symbol">}</a>

  <a id="2524" href="Data.Sum.Algebra.html#2524" class="Function">⊎-isSemigroup</a> <a id="2538" class="Symbol">:</a> <a id="2540" href="Algebra.Structures.html#2798" class="Record">IsSemigroup</a> <a id="2552" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2556" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a>
  <a id="2562" href="Data.Sum.Algebra.html#2524" class="Function">⊎-isSemigroup</a> <a id="2576" class="Symbol">=</a> <a id="2578" class="Keyword">record</a>
    <a id="2589" class="Symbol">{</a> <a id="2591" href="Algebra.Structures.html#2854" class="Field">isMagma</a> <a id="2599" class="Symbol">=</a> <a id="2601" href="Data.Sum.Algebra.html#2391" class="Function">⊎-isMagma</a>
    <a id="2615" class="Symbol">;</a> <a id="2617" href="Algebra.Structures.html#2878" class="Field">assoc</a>   <a id="2625" class="Symbol">=</a> <a id="2627" href="Data.Sum.Algebra.html#1832" class="Function">⊎-assoc</a>
    <a id="2639" class="Symbol">}</a>

  <a id="2644" href="Data.Sum.Algebra.html#2644" class="Function">⊎-isMonoid</a> <a id="2655" class="Symbol">:</a> <a id="2657" href="Algebra.Structures.html#3875" class="Record">IsMonoid</a> <a id="2666" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2670" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a> <a id="2674" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a>
  <a id="2678" href="Data.Sum.Algebra.html#2644" class="Function">⊎-isMonoid</a> <a id="2689" class="Symbol">=</a> <a id="2691" class="Keyword">record</a>
    <a id="2702" class="Symbol">{</a> <a id="2704" href="Algebra.Structures.html#3936" class="Field">isSemigroup</a> <a id="2716" class="Symbol">=</a> <a id="2718" href="Data.Sum.Algebra.html#2524" class="Function">⊎-isSemigroup</a>
    <a id="2736" class="Symbol">;</a> <a id="2738" href="Algebra.Structures.html#3968" class="Field">identity</a>    <a id="2750" class="Symbol">=</a> <a id="2752" href="Data.Sum.Algebra.html#2002" class="Function">⊎-identityˡ</a> <a id="2764" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2766" href="Data.Sum.Algebra.html#2109" class="Function">⊎-identityʳ</a>
    <a id="2782" class="Symbol">}</a>

  <a id="2787" href="Data.Sum.Algebra.html#2787" class="Function">⊎-isCommutativeMonoid</a> <a id="2809" class="Symbol">:</a> <a id="2811" href="Algebra.Structures.html#4285" class="Record">IsCommutativeMonoid</a> <a id="2831" href="Function.Bundles.html#9230" class="Function Operator">_↔_</a> <a id="2835" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a> <a id="2839" href="Data.Empty.Polymorphic.html#340" class="Function">⊥</a>
  <a id="2843" href="Data.Sum.Algebra.html#2787" class="Function">⊎-isCommutativeMonoid</a> <a id="2865" class="Symbol">=</a> <a id="2867" class="Keyword">record</a>
    <a id="2878" class="Symbol">{</a> <a id="2880" href="Algebra.Structures.html#4357" class="Field">isMonoid</a> <a id="2889" class="Symbol">=</a> <a id="2891" href="Data.Sum.Algebra.html#2644" class="Function">⊎-isMonoid</a>
    <a id="2906" class="Symbol">;</a> <a id="2908" href="Algebra.Structures.html#4385" class="Field">comm</a>     <a id="2917" class="Symbol">=</a> <a id="2919" href="Data.Sum.Algebra.html#1666" class="Function">⊎-comm</a>
    <a id="2930" class="Symbol">}</a>

<a id="2933" class="Comment">------------------------------------------------------------------------</a>
<a id="3006" class="Comment">-- Algebraic bundles</a>

  <a id="3030" href="Data.Sum.Algebra.html#3030" class="Function">⊎-magma</a> <a id="3038" class="Symbol">:</a> <a id="3040" href="Algebra.Bundles.html#1009" class="Record">Magma</a> <a id="3046" class="Symbol">(</a><a id="3047" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="3051" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="3052" class="Symbol">)</a> <a id="3054" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a>
  <a id="3058" href="Data.Sum.Algebra.html#3030" class="Function">⊎-magma</a> <a id="3066" class="Symbol">=</a> <a id="3068" class="Keyword">record</a>
    <a id="3079" class="Symbol">{</a> <a id="3081" href="Algebra.Bundles.html#1161" class="Field">isMagma</a> <a id="3089" class="Symbol">=</a> <a id="3091" href="Data.Sum.Algebra.html#2391" class="Function">⊎-isMagma</a>
    <a id="3105" class="Symbol">}</a>

  <a id="3110" href="Data.Sum.Algebra.html#3110" class="Function">⊎-semigroup</a> <a id="3122" class="Symbol">:</a> <a id="3124" href="Algebra.Bundles.html#4007" class="Record">Semigroup</a> <a id="3134" class="Symbol">(</a><a id="3135" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="3139" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="3140" class="Symbol">)</a> <a id="3142" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a>
  <a id="3146" href="Data.Sum.Algebra.html#3110" class="Function">⊎-semigroup</a> <a id="3158" class="Symbol">=</a> <a id="3160" class="Keyword">record</a>
    <a id="3171" class="Symbol">{</a> <a id="3173" href="Algebra.Bundles.html#4175" class="Field">isSemigroup</a> <a id="3185" class="Symbol">=</a> <a id="3187" href="Data.Sum.Algebra.html#2524" class="Function">⊎-isSemigroup</a>
    <a id="3205" class="Symbol">}</a>

  <a id="3210" href="Data.Sum.Algebra.html#3210" class="Function">⊎-monoid</a> <a id="3219" class="Symbol">:</a> <a id="3221" href="Algebra.Bundles.html#6283" class="Record">Monoid</a> <a id="3228" class="Symbol">(</a><a id="3229" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="3233" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="3234" class="Symbol">)</a> <a id="3236" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a>
  <a id="3240" href="Data.Sum.Algebra.html#3210" class="Function">⊎-monoid</a> <a id="3249" class="Symbol">=</a> <a id="3251" class="Keyword">record</a>
    <a id="3262" class="Symbol">{</a> <a id="3264" href="Algebra.Bundles.html#6462" class="Field">isMonoid</a> <a id="3273" class="Symbol">=</a> <a id="3275" href="Data.Sum.Algebra.html#2644" class="Function">⊎-isMonoid</a>
    <a id="3290" class="Symbol">}</a>

  <a id="3295" href="Data.Sum.Algebra.html#3295" class="Function">⊎-commutativeMonoid</a> <a id="3315" class="Symbol">:</a> <a id="3317" href="Algebra.Bundles.html#6854" class="Record">CommutativeMonoid</a> <a id="3335" class="Symbol">(</a><a id="3336" href="Agda.Primitive.html#774" class="Primitive">suc</a> <a id="3340" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a><a id="3341" class="Symbol">)</a> <a id="3343" href="Data.Sum.Algebra.html#1790" class="Bound">ℓ</a>
  <a id="3347" href="Data.Sum.Algebra.html#3295" class="Function">⊎-commutativeMonoid</a> <a id="3367" class="Symbol">=</a> <a id="3369" class="Keyword">record</a>
    <a id="3380" class="Symbol">{</a> <a id="3382" href="Algebra.Bundles.html#7088" class="Field">isCommutativeMonoid</a> <a id="3402" class="Symbol">=</a> <a id="3404" href="Data.Sum.Algebra.html#2787" class="Function">⊎-isCommutativeMonoid</a>
    <a id="3430" class="Symbol">}</a>
</pre></body></html>