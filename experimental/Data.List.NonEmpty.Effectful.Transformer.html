<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.List.NonEmpty.Effectful.Transformer</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- An effectful view of List</a>
<a id="135" class="Comment">------------------------------------------------------------------------</a>

<a id="209" class="Symbol">{-#</a> <a id="213" class="Keyword">OPTIONS</a> <a id="221" class="Pragma">--cubical-compatible</a> <a id="242" class="Pragma">--safe</a> <a id="249" class="Symbol">#-}</a>

<a id="254" class="Keyword">module</a> <a id="261" href="Data.List.NonEmpty.Effectful.Transformer.html" class="Module">Data.List.NonEmpty.Effectful.Transformer</a> <a id="302" class="Keyword">where</a>

<a id="309" class="Keyword">open</a> <a id="314" class="Keyword">import</a> <a id="321" href="Data.List.NonEmpty.Base.html" class="Module">Data.List.NonEmpty.Base</a> <a id="345" class="Symbol">as</a> <a id="348" class="Module">List⁺</a> <a id="354" class="Keyword">using</a> <a id="360" class="Symbol">(</a><a id="361" href="Data.List.NonEmpty.Base.html#1285" class="Record">List⁺</a><a id="366" class="Symbol">;</a> <a id="368" href="Data.List.NonEmpty.Base.html#1331" class="InductiveConstructor Operator">_∷_</a><a id="371" class="Symbol">)</a>
<a id="373" class="Keyword">open</a> <a id="378" class="Keyword">import</a> <a id="385" href="Effect.Functor.html" class="Module">Effect.Functor</a> <a id="400" class="Keyword">using</a> <a id="406" class="Symbol">(</a><a id="407" href="Effect.Functor.html#602" class="Record">RawFunctor</a><a id="417" class="Symbol">)</a>
<a id="419" class="Keyword">open</a> <a id="424" class="Keyword">import</a> <a id="431" href="Effect.Applicative.html" class="Module">Effect.Applicative</a> <a id="450" class="Keyword">using</a> <a id="456" class="Symbol">(</a><a id="457" href="Effect.Applicative.html#977" class="Record">RawApplicative</a><a id="471" class="Symbol">)</a>
<a id="473" class="Keyword">open</a> <a id="478" class="Keyword">import</a> <a id="485" href="Effect.Monad.html" class="Module">Effect.Monad</a> <a id="498" class="Keyword">using</a> <a id="504" class="Symbol">(</a><a id="505" href="Effect.Monad.html#899" class="Record">RawMonad</a><a id="513" class="Symbol">;</a> <a id="515" href="Effect.Monad.html#3339" class="Function">RawMonadT</a><a id="524" class="Symbol">)</a>
<a id="526" class="Keyword">open</a> <a id="531" class="Keyword">import</a> <a id="538" href="Function.Base.html" class="Module">Function.Base</a> <a id="552" class="Keyword">using</a> <a id="558" class="Symbol">(</a><a id="559" href="Function.Base.html#3626" class="Function Operator">_∘′_</a><a id="563" class="Symbol">;</a> <a id="565" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="568" class="Symbol">;</a> <a id="570" href="Function.Base.html#1974" class="Function Operator">_$_</a><a id="573" class="Symbol">)</a>
<a id="575" class="Keyword">open</a> <a id="580" class="Keyword">import</a> <a id="587" href="Level.html" class="Module">Level</a> <a id="593" class="Keyword">using</a> <a id="599" class="Symbol">(</a><a id="600" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="605" class="Symbol">)</a>

<a id="608" class="Keyword">import</a> <a id="615" href="Data.List.NonEmpty.Effectful.html" class="Module">Data.List.NonEmpty.Effectful</a> <a id="644" class="Symbol">as</a> <a id="647" class="Module">List⁺</a> <a id="653" class="Keyword">using</a> <a id="659" class="Symbol">(</a><a id="660" class="Keyword">module</a> <a id="667" href="Data.List.NonEmpty.Effectful.html#2121" class="Module">TraversableM</a><a id="679" class="Symbol">)</a>

<a id="682" class="Keyword">private</a>
  <a id="692" class="Keyword">variable</a>
    <a id="705" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a> <a id="707" href="Data.List.NonEmpty.Effectful.Transformer.html#707" class="Generalizable">g</a> <a id="709" class="Symbol">:</a> <a id="711" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="721" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a> <a id="723" class="Symbol">:</a> <a id="725" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="729" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a> <a id="731" class="Symbol">→</a> <a id="733" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="737" href="Data.List.NonEmpty.Effectful.Transformer.html#707" class="Generalizable">g</a>

<a id="740" class="Comment">------------------------------------------------------------------------</a>
<a id="813" class="Comment">-- List⁺ monad transformer</a>

<a id="841" class="Keyword">record</a> <a id="List⁺T"></a><a id="848" href="Data.List.NonEmpty.Effectful.Transformer.html#848" class="Record">List⁺T</a> <a id="855" class="Symbol">(</a><a id="856" href="Data.List.NonEmpty.Effectful.Transformer.html#856" class="Bound">M</a> <a id="858" class="Symbol">:</a> <a id="860" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="864" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a> <a id="866" class="Symbol">→</a> <a id="868" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="872" href="Data.List.NonEmpty.Effectful.Transformer.html#707" class="Generalizable">g</a><a id="873" class="Symbol">)</a> <a id="875" class="Symbol">(</a><a id="876" href="Data.List.NonEmpty.Effectful.Transformer.html#876" class="Bound">A</a> <a id="878" class="Symbol">:</a> <a id="880" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="884" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a><a id="885" class="Symbol">)</a> <a id="887" class="Symbol">:</a> <a id="889" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="893" href="Data.List.NonEmpty.Effectful.Transformer.html#872" class="Bound">g</a> <a id="895" class="Keyword">where</a>
  <a id="903" class="Keyword">constructor</a> <a id="mkList⁺T"></a><a id="915" href="Data.List.NonEmpty.Effectful.Transformer.html#915" class="InductiveConstructor">mkList⁺T</a>
  <a id="926" class="Keyword">field</a> <a id="List⁺T.runList⁺T"></a><a id="932" href="Data.List.NonEmpty.Effectful.Transformer.html#932" class="Field">runList⁺T</a> <a id="942" class="Symbol">:</a> <a id="944" href="Data.List.NonEmpty.Effectful.Transformer.html#856" class="Bound">M</a> <a id="946" class="Symbol">(</a><a id="947" href="Data.List.NonEmpty.Base.html#1285" class="Record">List⁺</a> <a id="953" href="Data.List.NonEmpty.Effectful.Transformer.html#876" class="Bound">A</a><a id="954" class="Symbol">)</a>
<a id="956" class="Keyword">open</a> <a id="961" href="Data.List.NonEmpty.Effectful.Transformer.html#848" class="Module">List⁺T</a> <a id="968" class="Keyword">public</a>

<a id="functor"></a><a id="976" href="Data.List.NonEmpty.Effectful.Transformer.html#976" class="Function">functor</a> <a id="984" class="Symbol">:</a> <a id="986" href="Effect.Functor.html#602" class="Record">RawFunctor</a> <a id="997" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a> <a id="999" class="Symbol">→</a> <a id="1001" href="Effect.Functor.html#602" class="Record">RawFunctor</a> <a id="1012" class="Symbol">{</a><a id="1013" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a><a id="1014" class="Symbol">}</a> <a id="1016" class="Symbol">(</a><a id="1017" href="Data.List.NonEmpty.Effectful.Transformer.html#848" class="Record">List⁺T</a> <a id="1024" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a><a id="1025" class="Symbol">)</a>
<a id="1027" href="Data.List.NonEmpty.Effectful.Transformer.html#976" class="Function">functor</a> <a id="1035" href="Data.List.NonEmpty.Effectful.Transformer.html#1035" class="Bound">M</a> <a id="1037" class="Symbol">=</a> <a id="1039" class="Keyword">record</a>
  <a id="1048" class="Symbol">{</a> <a id="1050" href="Effect.Functor.html#710" class="Field Operator">_&lt;$&gt;_</a> <a id="1056" class="Symbol">=</a> <a id="1058" class="Symbol">λ</a> <a id="1060" href="Data.List.NonEmpty.Effectful.Transformer.html#1060" class="Bound">f</a> <a id="1062" class="Symbol">→</a> <a id="1064" href="Data.List.NonEmpty.Effectful.Transformer.html#915" class="InductiveConstructor">mkList⁺T</a> <a id="1073" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1076" class="Symbol">(</a><a id="1077" href="Data.List.NonEmpty.Base.html#2332" class="Function">List⁺.map</a> <a id="1087" href="Data.List.NonEmpty.Effectful.Transformer.html#1060" class="Bound">f</a> <a id="1089" href="Effect.Functor.html#710" class="Field Operator">&lt;$&gt;_</a><a id="1093" class="Symbol">)</a> <a id="1095" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1098" href="Data.List.NonEmpty.Effectful.Transformer.html#932" class="Field">runList⁺T</a>
  <a id="1110" class="Symbol">}</a> <a id="1112" class="Keyword">where</a> <a id="1118" class="Keyword">open</a> <a id="1123" href="Effect.Functor.html#602" class="Module">RawFunctor</a> <a id="1134" href="Data.List.NonEmpty.Effectful.Transformer.html#1035" class="Bound">M</a>

<a id="applicative"></a><a id="1137" href="Data.List.NonEmpty.Effectful.Transformer.html#1137" class="Function">applicative</a> <a id="1149" class="Symbol">:</a> <a id="1151" href="Effect.Applicative.html#977" class="Record">RawApplicative</a> <a id="1166" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a> <a id="1168" class="Symbol">→</a> <a id="1170" href="Effect.Applicative.html#977" class="Record">RawApplicative</a> <a id="1185" class="Symbol">{</a><a id="1186" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a><a id="1187" class="Symbol">}</a> <a id="1189" class="Symbol">(</a><a id="1190" href="Data.List.NonEmpty.Effectful.Transformer.html#848" class="Record">List⁺T</a> <a id="1197" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a><a id="1198" class="Symbol">)</a>
<a id="1200" href="Data.List.NonEmpty.Effectful.Transformer.html#1137" class="Function">applicative</a> <a id="1212" href="Data.List.NonEmpty.Effectful.Transformer.html#1212" class="Bound">M</a> <a id="1214" class="Symbol">=</a> <a id="1216" class="Keyword">record</a>
  <a id="1225" class="Symbol">{</a> <a id="1227" href="Effect.Applicative.html#1115" class="Field">rawFunctor</a> <a id="1238" class="Symbol">=</a> <a id="1240" href="Data.List.NonEmpty.Effectful.Transformer.html#976" class="Function">functor</a> <a id="1248" href="Effect.Applicative.html#1115" class="Field">rawFunctor</a>
  <a id="1261" class="Symbol">;</a> <a id="1263" href="Effect.Applicative.html#1145" class="Field">pure</a> <a id="1268" class="Symbol">=</a> <a id="1270" href="Data.List.NonEmpty.Effectful.Transformer.html#915" class="InductiveConstructor">mkList⁺T</a> <a id="1279" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1282" href="Effect.Applicative.html#1145" class="Field">pure</a> <a id="1287" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1290" href="Data.List.NonEmpty.Base.html#1547" class="Function Operator">List⁺.[_]</a>
  <a id="1302" class="Symbol">;</a> <a id="1304" href="Effect.Applicative.html#1164" class="Field Operator">_&lt;*&gt;_</a>  <a id="1311" class="Symbol">=</a> <a id="1313" class="Symbol">λ</a> <a id="1315" href="Data.List.NonEmpty.Effectful.Transformer.html#1315" class="Bound">mf</a> <a id="1318" href="Data.List.NonEmpty.Effectful.Transformer.html#1318" class="Bound">ma</a> <a id="1321" class="Symbol">→</a> <a id="1323" href="Data.List.NonEmpty.Effectful.Transformer.html#915" class="InductiveConstructor">mkList⁺T</a> <a id="1332" class="Symbol">(</a><a id="1333" href="Data.List.NonEmpty.Base.html#3811" class="Function">List⁺.ap</a> <a id="1342" href="Effect.Functor.html#710" class="Function Operator">&lt;$&gt;</a> <a id="1346" href="Data.List.NonEmpty.Effectful.Transformer.html#932" class="Field">runList⁺T</a> <a id="1356" href="Data.List.NonEmpty.Effectful.Transformer.html#1315" class="Bound">mf</a> <a id="1359" href="Effect.Applicative.html#1164" class="Field Operator">&lt;*&gt;</a> <a id="1363" href="Data.List.NonEmpty.Effectful.Transformer.html#932" class="Field">runList⁺T</a> <a id="1373" href="Data.List.NonEmpty.Effectful.Transformer.html#1318" class="Bound">ma</a><a id="1375" class="Symbol">)</a>
  <a id="1379" class="Symbol">}</a> <a id="1381" class="Keyword">where</a> <a id="1387" class="Keyword">open</a> <a id="1392" href="Effect.Applicative.html#977" class="Module">RawApplicative</a> <a id="1407" href="Data.List.NonEmpty.Effectful.Transformer.html#1212" class="Bound">M</a>

<a id="monad"></a><a id="1410" href="Data.List.NonEmpty.Effectful.Transformer.html#1410" class="Function">monad</a> <a id="1416" class="Symbol">:</a> <a id="1418" href="Effect.Monad.html#899" class="Record">RawMonad</a> <a id="1427" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a> <a id="1429" class="Symbol">→</a> <a id="1431" href="Effect.Monad.html#899" class="Record">RawMonad</a> <a id="1440" class="Symbol">(</a><a id="1441" href="Data.List.NonEmpty.Effectful.Transformer.html#848" class="Record">List⁺T</a> <a id="1448" href="Data.List.NonEmpty.Effectful.Transformer.html#721" class="Generalizable">M</a><a id="1449" class="Symbol">)</a>
<a id="1451" href="Data.List.NonEmpty.Effectful.Transformer.html#1410" class="Function">monad</a> <a id="1457" href="Data.List.NonEmpty.Effectful.Transformer.html#1457" class="Bound">M</a> <a id="1459" class="Symbol">=</a> <a id="1461" class="Keyword">record</a>
  <a id="1470" class="Symbol">{</a> <a id="1472" href="Effect.Monad.html#1015" class="Field">rawApplicative</a> <a id="1487" class="Symbol">=</a> <a id="1489" href="Data.List.NonEmpty.Effectful.Transformer.html#1137" class="Function">applicative</a> <a id="1501" href="Effect.Monad.html#1015" class="Field">rawApplicative</a>
  <a id="1518" class="Symbol">;</a> <a id="1520" href="Effect.Monad.html#1053" class="Field Operator">_&gt;&gt;=_</a> <a id="1526" class="Symbol">=</a> <a id="1528" class="Symbol">λ</a> <a id="1530" href="Data.List.NonEmpty.Effectful.Transformer.html#1530" class="Bound">mas</a> <a id="1534" href="Data.List.NonEmpty.Effectful.Transformer.html#1534" class="Bound">f</a> <a id="1536" class="Symbol">→</a> <a id="1538" href="Data.List.NonEmpty.Effectful.Transformer.html#915" class="InductiveConstructor">mkList⁺T</a> <a id="1547" href="Function.Base.html#1974" class="Function Operator">$</a> <a id="1549" class="Keyword">do</a>
                       <a id="1575" href="Data.List.NonEmpty.Effectful.Transformer.html#1575" class="Bound">as</a> <a id="1578" href="Effect.Monad.html#1053" class="Field Operator">←</a> <a id="1580" href="Data.List.NonEmpty.Effectful.Transformer.html#932" class="Field">runList⁺T</a> <a id="1590" href="Data.List.NonEmpty.Effectful.Transformer.html#1530" class="Bound">mas</a>
                       <a id="1617" href="Data.List.NonEmpty.Base.html#3637" class="Function">List⁺.concat</a> <a id="1630" href="Effect.Functor.html#710" class="Function Operator">&lt;$&gt;</a> <a id="1634" href="Data.List.NonEmpty.Effectful.html#2300" class="Function">mapM</a> <a id="1639" class="Symbol">(</a><a id="1640" href="Data.List.NonEmpty.Effectful.Transformer.html#932" class="Field">runList⁺T</a> <a id="1650" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1653" href="Data.List.NonEmpty.Effectful.Transformer.html#1534" class="Bound">f</a><a id="1654" class="Symbol">)</a> <a id="1656" href="Data.List.NonEmpty.Effectful.Transformer.html#1575" class="Bound">as</a>
  <a id="1661" class="Symbol">}</a> <a id="1663" class="Keyword">where</a> <a id="1669" class="Keyword">open</a> <a id="1674" href="Effect.Monad.html#899" class="Module">RawMonad</a> <a id="1683" href="Data.List.NonEmpty.Effectful.Transformer.html#1457" class="Bound">M</a><a id="1684" class="Symbol">;</a> <a id="1686" class="Keyword">open</a> <a id="1691" href="Data.List.NonEmpty.Effectful.html#2121" class="Module">List⁺.TraversableM</a> <a id="1710" href="Data.List.NonEmpty.Effectful.Transformer.html#1457" class="Bound">M</a>

<a id="monadT"></a><a id="1713" href="Data.List.NonEmpty.Effectful.Transformer.html#1713" class="Function">monadT</a> <a id="1720" class="Symbol">:</a> <a id="1722" href="Effect.Monad.html#3339" class="Function">RawMonadT</a> <a id="1732" class="Symbol">{</a><a id="1733" href="Data.List.NonEmpty.Effectful.Transformer.html#705" class="Generalizable">f</a><a id="1734" class="Symbol">}</a> <a id="1736" class="Symbol">{</a><a id="1737" href="Data.List.NonEmpty.Effectful.Transformer.html#707" class="Generalizable">g</a><a id="1738" class="Symbol">}</a> <a id="1740" href="Data.List.NonEmpty.Effectful.Transformer.html#848" class="Record">List⁺T</a>
<a id="1747" href="Data.List.NonEmpty.Effectful.Transformer.html#1713" class="Function">monadT</a> <a id="1754" href="Data.List.NonEmpty.Effectful.Transformer.html#1754" class="Bound">M</a> <a id="1756" class="Symbol">=</a> <a id="1758" class="Keyword">record</a>
  <a id="1767" class="Symbol">{</a> <a id="1769" href="Effect.Monad.html#3260" class="Field">lift</a> <a id="1774" class="Symbol">=</a> <a id="1776" href="Data.List.NonEmpty.Effectful.Transformer.html#915" class="InductiveConstructor">mkList⁺T</a> <a id="1785" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1788" class="Symbol">(</a><a id="1789" href="Data.List.NonEmpty.Base.html#1547" class="Function Operator">List⁺.[_]</a> <a id="1799" href="Effect.Functor.html#710" class="Function Operator">&lt;$&gt;_</a><a id="1803" class="Symbol">)</a>
  <a id="1807" class="Symbol">;</a> <a id="1809" href="Effect.Monad.html#3282" class="Field">rawMonad</a> <a id="1818" class="Symbol">=</a> <a id="1820" href="Data.List.NonEmpty.Effectful.Transformer.html#1410" class="Function">monad</a> <a id="1826" href="Data.List.NonEmpty.Effectful.Transformer.html#1754" class="Bound">M</a>
  <a id="1830" class="Symbol">}</a> <a id="1832" class="Keyword">where</a> <a id="1838" class="Keyword">open</a> <a id="1843" href="Effect.Monad.html#899" class="Module">RawMonad</a> <a id="1852" href="Data.List.NonEmpty.Effectful.Transformer.html#1754" class="Bound">M</a>
</pre></body></html>