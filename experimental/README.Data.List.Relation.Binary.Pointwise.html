<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.List.Relation.Binary.Pointwise</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Documentation for pointwise lifting of relations over `List`s</a>
<a id="171" class="Comment">------------------------------------------------------------------------</a>

<a id="245" class="Keyword">module</a> <a id="252" href="README.Data.List.Relation.Binary.Pointwise.html" class="Module">README.Data.List.Relation.Binary.Pointwise</a> <a id="295" class="Keyword">where</a>

<a id="302" class="Keyword">open</a> <a id="307" class="Keyword">import</a> <a id="314" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="323" class="Keyword">using</a> <a id="329" class="Symbol">(</a><a id="330" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="331" class="Symbol">;</a> <a id="333" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a><a id="336" class="Symbol">;</a> <a id="338" href="Data.Nat.Base.html#1538" class="InductiveConstructor">s≤s</a><a id="341" class="Symbol">;</a> <a id="343" href="Data.Nat.Base.html#1496" class="InductiveConstructor">z≤n</a><a id="346" class="Symbol">)</a>
<a id="348" class="Keyword">open</a> <a id="353" class="Keyword">import</a> <a id="360" href="Data.List.html" class="Module">Data.List</a> <a id="370" class="Keyword">using</a> <a id="376" class="Symbol">(</a><a id="377" href="Agda.Builtin.List.html#130" class="Datatype">List</a><a id="381" class="Symbol">;</a> <a id="383" href="Data.List.Base.html#7191" class="InductiveConstructor">[]</a><a id="385" class="Symbol">;</a> <a id="387" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a><a id="390" class="Symbol">;</a> <a id="392" href="Data.List.Base.html#4888" class="Function">length</a><a id="398" class="Symbol">)</a>
<a id="400" class="Keyword">open</a> <a id="405" class="Keyword">import</a> <a id="412" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
  <a id="452" class="Keyword">using</a> <a id="458" class="Symbol">(</a><a id="459" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="462" class="Symbol">;</a> <a id="464" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="468" class="Symbol">;</a> <a id="470" href="Relation.Binary.PropositionalEquality.Core.html#1693" class="Function">sym</a><a id="473" class="Symbol">;</a> <a id="475" href="Relation.Binary.PropositionalEquality.Core.html#1139" class="Function">cong</a><a id="479" class="Symbol">;</a> <a id="481" href="Relation.Binary.PropositionalEquality.Properties.html#5399" class="Function">setoid</a><a id="487" class="Symbol">)</a>
<a id="489" class="Keyword">open</a> <a id="494" class="Keyword">import</a> <a id="501" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="518" class="Keyword">using</a> <a id="524" class="Symbol">(</a><a id="525" href="Relation.Nullary.html#665" class="Function Operator">¬_</a><a id="527" class="Symbol">)</a>

<a id="530" class="Comment">------------------------------------------------------------------------</a>
<a id="603" class="Comment">-- Pointwise</a>

<a id="617" class="Comment">-- One of the most basic ways to form a binary relation between two</a>
<a id="685" class="Comment">-- lists of type `List A`, given a binary relation over `A`, is to say</a>
<a id="756" class="Comment">-- that two lists are related if they are the same length and:</a>
<a id="819" class="Comment">--   i) the first elements in the lists are related</a>
<a id="871" class="Comment">--   ii) the second elements in the lists are related</a>
<a id="925" class="Comment">--   iii) the third elements in the lists are related etc.</a>
<a id="984" class="Comment">--   etc.</a>
<a id="994" class="Comment">--</a>
<a id="997" class="Comment">-- A formalisation of this &quot;pointwise&quot; lifting of a relation to lists</a>
<a id="1067" class="Comment">-- is found in:</a>

<a id="1084" class="Keyword">open</a> <a id="1089" class="Keyword">import</a> <a id="1096" href="Data.List.Relation.Binary.Pointwise.html" class="Module">Data.List.Relation.Binary.Pointwise</a>

<a id="1133" class="Comment">-- The same syntax to construct a list (`[]` &amp; `_∷_`) is used to</a>
<a id="1198" class="Comment">-- construct proofs for the `Pointwise` relation. For example if you</a>
<a id="1267" class="Comment">-- want to prove that one list is strictly less than another list:</a>

<a id="lem₁"></a><a id="1335" href="README.Data.List.Relation.Binary.Pointwise.html#1335" class="Function">lem₁</a> <a id="1340" class="Symbol">:</a> <a id="1342" href="Data.List.Relation.Binary.Pointwise.Base.html#784" class="Datatype">Pointwise</a> <a id="1352" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a> <a id="1356" class="Symbol">(</a><a id="1357" class="Number">0</a> <a id="1359" class="InductiveConstructor Operator">∷</a> <a id="1361" class="Number">2</a> <a id="1363" class="InductiveConstructor Operator">∷</a> <a id="1365" class="Number">1</a> <a id="1367" class="InductiveConstructor Operator">∷</a> <a id="1369" class="InductiveConstructor">[]</a><a id="1371" class="Symbol">)</a> <a id="1373" class="Symbol">(</a><a id="1374" class="Number">1</a> <a id="1376" class="InductiveConstructor Operator">∷</a> <a id="1378" class="Number">4</a> <a id="1380" class="InductiveConstructor Operator">∷</a> <a id="1382" class="Number">2</a> <a id="1384" class="InductiveConstructor Operator">∷</a> <a id="1386" class="InductiveConstructor">[]</a><a id="1388" class="Symbol">)</a>
<a id="1390" href="README.Data.List.Relation.Binary.Pointwise.html#1335" class="Function">lem₁</a> <a id="1395" class="Symbol">=</a> <a id="1397" href="README.Data.List.Relation.Binary.Pointwise.html#1428" class="Function">0&lt;1</a> <a id="1401" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">∷</a> <a id="1403" href="README.Data.List.Relation.Binary.Pointwise.html#1444" class="Function">2&lt;4</a> <a id="1407" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">∷</a> <a id="1409" href="README.Data.List.Relation.Binary.Pointwise.html#1472" class="Function">1&lt;2</a> <a id="1413" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">∷</a> <a id="1415" href="Data.List.Relation.Binary.Pointwise.Base.html#893" class="InductiveConstructor">[]</a>
  <a id="1420" class="Keyword">where</a>
  <a id="1428" href="README.Data.List.Relation.Binary.Pointwise.html#1428" class="Function">0&lt;1</a> <a id="1432" class="Symbol">=</a> <a id="1434" href="Data.Nat.Base.html#1538" class="InductiveConstructor">s≤s</a> <a id="1438" href="Data.Nat.Base.html#1496" class="InductiveConstructor">z≤n</a>
  <a id="1444" href="README.Data.List.Relation.Binary.Pointwise.html#1444" class="Function">2&lt;4</a> <a id="1448" class="Symbol">=</a> <a id="1450" href="Data.Nat.Base.html#1538" class="InductiveConstructor">s≤s</a> <a id="1454" class="Symbol">(</a><a id="1455" href="Data.Nat.Base.html#1538" class="InductiveConstructor">s≤s</a> <a id="1459" class="Symbol">(</a><a id="1460" href="Data.Nat.Base.html#1538" class="InductiveConstructor">s≤s</a> <a id="1464" href="Data.Nat.Base.html#1496" class="InductiveConstructor">z≤n</a><a id="1467" class="Symbol">))</a>
  <a id="1472" href="README.Data.List.Relation.Binary.Pointwise.html#1472" class="Function">1&lt;2</a> <a id="1476" class="Symbol">=</a> <a id="1478" href="Data.Nat.Base.html#1538" class="InductiveConstructor">s≤s</a> <a id="1482" href="README.Data.List.Relation.Binary.Pointwise.html#1428" class="Function">0&lt;1</a>

<a id="1487" class="Comment">-- Lists that are related by `Pointwise` must be of the same length.</a>
<a id="1556" class="Comment">-- For example:</a>

<a id="lem₂"></a><a id="1573" href="README.Data.List.Relation.Binary.Pointwise.html#1573" class="Function">lem₂</a> <a id="1578" class="Symbol">:</a> <a id="1580" href="Relation.Nullary.html#665" class="Function Operator">¬</a> <a id="1582" href="Data.List.Relation.Binary.Pointwise.Base.html#784" class="Datatype">Pointwise</a> <a id="1592" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a> <a id="1596" class="Symbol">(</a><a id="1597" class="Number">0</a> <a id="1599" class="InductiveConstructor Operator">∷</a> <a id="1601" class="Number">2</a> <a id="1603" class="InductiveConstructor Operator">∷</a> <a id="1605" class="InductiveConstructor">[]</a><a id="1607" class="Symbol">)</a> <a id="1609" class="Symbol">(</a><a id="1610" class="Number">1</a> <a id="1612" class="InductiveConstructor Operator">∷</a> <a id="1614" class="InductiveConstructor">[]</a><a id="1616" class="Symbol">)</a>
<a id="1618" href="README.Data.List.Relation.Binary.Pointwise.html#1573" class="Function">lem₂</a> <a id="1623" class="Symbol">(</a><a id="1624" href="README.Data.List.Relation.Binary.Pointwise.html#1624" class="Bound">0&lt;1</a> <a id="1628" href="Data.List.Relation.Binary.Pointwise.Base.html#919" class="InductiveConstructor Operator">∷</a> <a id="1630" class="Symbol">())</a>

<a id="1635" class="Comment">-- Proofs about pointwise, including that of the above fact are</a>
<a id="1699" class="Comment">-- also included in the module:</a>

<a id="lem₃"></a><a id="1732" href="README.Data.List.Relation.Binary.Pointwise.html#1732" class="Function">lem₃</a> <a id="1737" class="Symbol">:</a> <a id="1739" class="Symbol">∀</a> <a id="1741" class="Symbol">{</a><a id="1742" href="README.Data.List.Relation.Binary.Pointwise.html#1742" class="Bound">xs</a> <a id="1745" href="README.Data.List.Relation.Binary.Pointwise.html#1745" class="Bound">ys</a><a id="1747" class="Symbol">}</a> <a id="1749" class="Symbol">→</a> <a id="1751" href="Data.List.Relation.Binary.Pointwise.Base.html#784" class="Datatype">Pointwise</a> <a id="1761" href="Data.Nat.Base.html#1583" class="Function Operator">_&lt;_</a> <a id="1765" href="README.Data.List.Relation.Binary.Pointwise.html#1742" class="Bound">xs</a> <a id="1768" href="README.Data.List.Relation.Binary.Pointwise.html#1745" class="Bound">ys</a> <a id="1771" class="Symbol">→</a> <a id="1773" href="Data.List.Base.html#4888" class="Function">length</a> <a id="1780" href="README.Data.List.Relation.Binary.Pointwise.html#1742" class="Bound">xs</a> <a id="1783" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1785" href="Data.List.Base.html#4888" class="Function">length</a> <a id="1792" href="README.Data.List.Relation.Binary.Pointwise.html#1745" class="Bound">ys</a>
<a id="1795" href="README.Data.List.Relation.Binary.Pointwise.html#1732" class="Function">lem₃</a> <a id="1800" class="Symbol">=</a> <a id="1802" href="Data.List.Relation.Binary.Pointwise.html#4786" class="Function">Pointwise-length</a>
</pre></body></html>