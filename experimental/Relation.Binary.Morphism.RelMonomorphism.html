<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Morphism.RelMonomorphism</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Consequences of a monomorphism between binary relations</a>
<a id="165" class="Comment">------------------------------------------------------------------------</a>

<a id="239" class="Comment">-- See Data.Nat.Binary.Properties for examples of how this and similar</a>
<a id="310" class="Comment">-- modules can be used to easily translate properties between types.</a>

<a id="380" class="Symbol">{-#</a> <a id="384" class="Keyword">OPTIONS</a> <a id="392" class="Pragma">--cubical-compatible</a> <a id="413" class="Pragma">--safe</a> <a id="420" class="Symbol">#-}</a>

<a id="425" class="Keyword">open</a> <a id="430" class="Keyword">import</a> <a id="437" href="Function.Base.html" class="Module">Function.Base</a>
<a id="451" class="Keyword">open</a> <a id="456" class="Keyword">import</a> <a id="463" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="479" class="Keyword">open</a> <a id="484" class="Keyword">import</a> <a id="491" href="Relation.Binary.Morphism.html" class="Module">Relation.Binary.Morphism</a>

<a id="517" class="Keyword">module</a> <a id="524" href="Relation.Binary.Morphism.RelMonomorphism.html" class="Module">Relation.Binary.Morphism.RelMonomorphism</a>
  <a id="567" class="Symbol">{</a><a id="568" href="Relation.Binary.Morphism.RelMonomorphism.html#568" class="Bound">a</a> <a id="570" href="Relation.Binary.Morphism.RelMonomorphism.html#570" class="Bound">b</a> <a id="572" href="Relation.Binary.Morphism.RelMonomorphism.html#572" class="Bound">ℓ₁</a> <a id="575" href="Relation.Binary.Morphism.RelMonomorphism.html#575" class="Bound">ℓ₂</a><a id="577" class="Symbol">}</a> <a id="579" class="Symbol">{</a><a id="580" href="Relation.Binary.Morphism.RelMonomorphism.html#580" class="Bound">A</a> <a id="582" class="Symbol">:</a> <a id="584" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="588" href="Relation.Binary.Morphism.RelMonomorphism.html#568" class="Bound">a</a><a id="589" class="Symbol">}</a> <a id="591" class="Symbol">{</a><a id="592" href="Relation.Binary.Morphism.RelMonomorphism.html#592" class="Bound">B</a> <a id="594" class="Symbol">:</a> <a id="596" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="600" href="Relation.Binary.Morphism.RelMonomorphism.html#570" class="Bound">b</a><a id="601" class="Symbol">}</a>
  <a id="605" class="Symbol">{</a><a id="606" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a> <a id="611" class="Symbol">:</a> <a id="613" href="Relation.Binary.Core.html#891" class="Function">Rel</a> <a id="617" href="Relation.Binary.Morphism.RelMonomorphism.html#580" class="Bound">A</a> <a id="619" href="Relation.Binary.Morphism.RelMonomorphism.html#572" class="Bound">ℓ₁</a><a id="621" class="Symbol">}</a> <a id="623" class="Symbol">{</a><a id="624" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="629" class="Symbol">:</a> <a id="631" href="Relation.Binary.Core.html#891" class="Function">Rel</a> <a id="635" href="Relation.Binary.Morphism.RelMonomorphism.html#592" class="Bound">B</a> <a id="637" href="Relation.Binary.Morphism.RelMonomorphism.html#575" class="Bound">ℓ₂</a><a id="639" class="Symbol">}</a>
  <a id="643" class="Symbol">{</a><a id="644" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟦_⟧</a> <a id="648" class="Symbol">:</a> <a id="650" href="Relation.Binary.Morphism.RelMonomorphism.html#580" class="Bound">A</a> <a id="652" class="Symbol">→</a> <a id="654" href="Relation.Binary.Morphism.RelMonomorphism.html#592" class="Bound">B</a><a id="655" class="Symbol">}</a> <a id="657" class="Symbol">(</a><a id="658" href="Relation.Binary.Morphism.RelMonomorphism.html#658" class="Bound">isMonomorphism</a> <a id="673" class="Symbol">:</a> <a id="675" href="Relation.Binary.Morphism.Structures.html#889" class="Record">IsRelMonomorphism</a> <a id="693" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a> <a id="698" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="703" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟦_⟧</a><a id="706" class="Symbol">)</a>
  <a id="710" class="Keyword">where</a>

<a id="717" class="Keyword">open</a> <a id="722" class="Keyword">import</a> <a id="729" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="743" class="Symbol">as</a> <a id="746" class="Module">Sum</a>
<a id="750" class="Keyword">open</a> <a id="755" class="Keyword">import</a> <a id="762" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="779" class="Keyword">using</a> <a id="785" class="Symbol">(</a><a id="786" href="Relation.Nullary.html#1657" class="InductiveConstructor">yes</a><a id="789" class="Symbol">;</a> <a id="791" href="Relation.Nullary.html#1694" class="InductiveConstructor">no</a><a id="793" class="Symbol">)</a>
<a id="795" class="Keyword">open</a> <a id="800" class="Keyword">import</a> <a id="807" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a>

<a id="835" class="Keyword">open</a> <a id="840" href="Relation.Binary.Morphism.Structures.html#889" class="Module">IsRelMonomorphism</a> <a id="858" href="Relation.Binary.Morphism.RelMonomorphism.html#658" class="Bound">isMonomorphism</a>

<a id="874" class="Comment">------------------------------------------------------------------------</a>
<a id="947" class="Comment">-- Properties</a>

<a id="refl"></a><a id="962" href="Relation.Binary.Morphism.RelMonomorphism.html#962" class="Function">refl</a> <a id="967" class="Symbol">:</a> <a id="969" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="979" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="984" class="Symbol">→</a> <a id="986" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="996" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1001" href="Relation.Binary.Morphism.RelMonomorphism.html#962" class="Function">refl</a> <a id="1006" href="Relation.Binary.Morphism.RelMonomorphism.html#1006" class="Bound">refl</a> <a id="1011" class="Symbol">=</a> <a id="1013" href="Relation.Binary.Morphism.Structures.html#1073" class="Field">injective</a> <a id="1023" href="Relation.Binary.Morphism.RelMonomorphism.html#1006" class="Bound">refl</a>

<a id="sym"></a><a id="1029" href="Relation.Binary.Morphism.RelMonomorphism.html#1029" class="Function">sym</a> <a id="1033" class="Symbol">:</a> <a id="1035" href="Relation.Binary.Definitions.html#1507" class="Function">Symmetric</a> <a id="1045" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1050" class="Symbol">→</a> <a id="1052" href="Relation.Binary.Definitions.html#1507" class="Function">Symmetric</a> <a id="1062" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1067" href="Relation.Binary.Morphism.RelMonomorphism.html#1029" class="Function">sym</a> <a id="1071" href="Relation.Binary.Morphism.RelMonomorphism.html#1071" class="Bound">sym</a> <a id="1075" href="Relation.Binary.Morphism.RelMonomorphism.html#1075" class="Bound">x∼y</a> <a id="1079" class="Symbol">=</a> <a id="1081" href="Relation.Binary.Morphism.Structures.html#1073" class="Field">injective</a> <a id="1091" class="Symbol">(</a><a id="1092" href="Relation.Binary.Morphism.RelMonomorphism.html#1071" class="Bound">sym</a> <a id="1096" class="Symbol">(</a><a id="1097" href="Relation.Binary.Morphism.Structures.html#846" class="Function">cong</a> <a id="1102" href="Relation.Binary.Morphism.RelMonomorphism.html#1075" class="Bound">x∼y</a><a id="1105" class="Symbol">))</a>

<a id="trans"></a><a id="1109" href="Relation.Binary.Morphism.RelMonomorphism.html#1109" class="Function">trans</a> <a id="1115" class="Symbol">:</a> <a id="1117" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="1128" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1133" class="Symbol">→</a> <a id="1135" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="1146" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1151" href="Relation.Binary.Morphism.RelMonomorphism.html#1109" class="Function">trans</a> <a id="1157" href="Relation.Binary.Morphism.RelMonomorphism.html#1157" class="Bound">trans</a> <a id="1163" href="Relation.Binary.Morphism.RelMonomorphism.html#1163" class="Bound">x∼y</a> <a id="1167" href="Relation.Binary.Morphism.RelMonomorphism.html#1167" class="Bound">y∼z</a> <a id="1171" class="Symbol">=</a> <a id="1173" href="Relation.Binary.Morphism.Structures.html#1073" class="Field">injective</a> <a id="1183" class="Symbol">(</a><a id="1184" href="Relation.Binary.Morphism.RelMonomorphism.html#1157" class="Bound">trans</a> <a id="1190" class="Symbol">(</a><a id="1191" href="Relation.Binary.Morphism.Structures.html#846" class="Function">cong</a> <a id="1196" href="Relation.Binary.Morphism.RelMonomorphism.html#1163" class="Bound">x∼y</a><a id="1199" class="Symbol">)</a> <a id="1201" class="Symbol">(</a><a id="1202" href="Relation.Binary.Morphism.Structures.html#846" class="Function">cong</a> <a id="1207" href="Relation.Binary.Morphism.RelMonomorphism.html#1167" class="Bound">y∼z</a><a id="1210" class="Symbol">))</a>

<a id="total"></a><a id="1214" href="Relation.Binary.Morphism.RelMonomorphism.html#1214" class="Function">total</a> <a id="1220" class="Symbol">:</a> <a id="1222" href="Relation.Binary.Definitions.html#2593" class="Function">Total</a> <a id="1228" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1233" class="Symbol">→</a> <a id="1235" href="Relation.Binary.Definitions.html#2593" class="Function">Total</a> <a id="1241" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1246" href="Relation.Binary.Morphism.RelMonomorphism.html#1214" class="Function">total</a> <a id="1252" href="Relation.Binary.Morphism.RelMonomorphism.html#1252" class="Bound">total</a> <a id="1258" href="Relation.Binary.Morphism.RelMonomorphism.html#1258" class="Bound">x</a> <a id="1260" href="Relation.Binary.Morphism.RelMonomorphism.html#1260" class="Bound">y</a> <a id="1262" class="Symbol">=</a> <a id="1264" href="Data.Sum.Base.html#1371" class="Function">Sum.map</a> <a id="1272" href="Relation.Binary.Morphism.Structures.html#1073" class="Field">injective</a> <a id="1282" href="Relation.Binary.Morphism.Structures.html#1073" class="Field">injective</a> <a id="1292" class="Symbol">(</a><a id="1293" href="Relation.Binary.Morphism.RelMonomorphism.html#1252" class="Bound">total</a> <a id="1299" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟦</a> <a id="1301" href="Relation.Binary.Morphism.RelMonomorphism.html#1258" class="Bound">x</a> <a id="1303" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟧</a> <a id="1305" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟦</a> <a id="1307" href="Relation.Binary.Morphism.RelMonomorphism.html#1260" class="Bound">y</a> <a id="1309" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟧</a><a id="1310" class="Symbol">)</a>

<a id="asym"></a><a id="1313" href="Relation.Binary.Morphism.RelMonomorphism.html#1313" class="Function">asym</a> <a id="1318" class="Symbol">:</a> <a id="1320" href="Relation.Binary.Definitions.html#2362" class="Function">Asymmetric</a> <a id="1331" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1336" class="Symbol">→</a> <a id="1338" href="Relation.Binary.Definitions.html#2362" class="Function">Asymmetric</a> <a id="1349" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1354" href="Relation.Binary.Morphism.RelMonomorphism.html#1313" class="Function">asym</a> <a id="1359" href="Relation.Binary.Morphism.RelMonomorphism.html#1359" class="Bound">asym</a> <a id="1364" href="Relation.Binary.Morphism.RelMonomorphism.html#1364" class="Bound">x∼y</a> <a id="1368" href="Relation.Binary.Morphism.RelMonomorphism.html#1368" class="Bound">y∼x</a> <a id="1372" class="Symbol">=</a> <a id="1374" href="Relation.Binary.Morphism.RelMonomorphism.html#1359" class="Bound">asym</a> <a id="1379" class="Symbol">(</a><a id="1380" href="Relation.Binary.Morphism.Structures.html#846" class="Function">cong</a> <a id="1385" href="Relation.Binary.Morphism.RelMonomorphism.html#1364" class="Bound">x∼y</a><a id="1388" class="Symbol">)</a> <a id="1390" class="Symbol">(</a><a id="1391" href="Relation.Binary.Morphism.Structures.html#846" class="Function">cong</a> <a id="1396" href="Relation.Binary.Morphism.RelMonomorphism.html#1368" class="Bound">y∼x</a><a id="1399" class="Symbol">)</a>

<a id="dec"></a><a id="1402" href="Relation.Binary.Morphism.RelMonomorphism.html#1402" class="Function">dec</a> <a id="1406" class="Symbol">:</a> <a id="1408" href="Relation.Binary.Definitions.html#5844" class="Function">Decidable</a> <a id="1418" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1423" class="Symbol">→</a> <a id="1425" href="Relation.Binary.Definitions.html#5844" class="Function">Decidable</a> <a id="1435" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1440" href="Relation.Binary.Morphism.RelMonomorphism.html#1402" class="Function">dec</a> <a id="1444" href="Relation.Binary.Morphism.RelMonomorphism.html#1444" class="Bound Operator">_∼?_</a> <a id="1449" href="Relation.Binary.Morphism.RelMonomorphism.html#1449" class="Bound">x</a> <a id="1451" href="Relation.Binary.Morphism.RelMonomorphism.html#1451" class="Bound">y</a> <a id="1453" class="Symbol">=</a> <a id="1455" href="Relation.Nullary.Decidable.Core.html#3630" class="Function">map′</a> <a id="1460" href="Relation.Binary.Morphism.Structures.html#1073" class="Field">injective</a> <a id="1470" href="Relation.Binary.Morphism.Structures.html#846" class="Function">cong</a> <a id="1475" class="Symbol">(</a><a id="1476" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟦</a> <a id="1478" href="Relation.Binary.Morphism.RelMonomorphism.html#1449" class="Bound">x</a> <a id="1480" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟧</a> <a id="1482" href="Relation.Binary.Morphism.RelMonomorphism.html#1444" class="Bound Operator">∼?</a> <a id="1485" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟦</a> <a id="1487" href="Relation.Binary.Morphism.RelMonomorphism.html#1451" class="Bound">y</a> <a id="1489" href="Relation.Binary.Morphism.RelMonomorphism.html#644" class="Bound Operator">⟧</a><a id="1490" class="Symbol">)</a>

<a id="1493" class="Comment">------------------------------------------------------------------------</a>
<a id="1566" class="Comment">-- Structures</a>

<a id="isEquivalence"></a><a id="1581" href="Relation.Binary.Morphism.RelMonomorphism.html#1581" class="Function">isEquivalence</a> <a id="1595" class="Symbol">:</a> <a id="1597" href="Relation.Binary.Structures.html#1531" class="Record">IsEquivalence</a> <a id="1611" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1616" class="Symbol">→</a> <a id="1618" href="Relation.Binary.Structures.html#1531" class="Record">IsEquivalence</a> <a id="1632" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1637" href="Relation.Binary.Morphism.RelMonomorphism.html#1581" class="Function">isEquivalence</a> <a id="1651" href="Relation.Binary.Morphism.RelMonomorphism.html#1651" class="Bound">isEq</a> <a id="1656" class="Symbol">=</a> <a id="1658" class="Keyword">record</a>
  <a id="1667" class="Symbol">{</a> <a id="1669" href="Relation.Binary.Structures.html#1577" class="Field">refl</a>  <a id="1675" class="Symbol">=</a> <a id="1677" href="Relation.Binary.Morphism.RelMonomorphism.html#962" class="Function">refl</a>  <a id="1683" href="Relation.Binary.Structures.html#1577" class="Field">E.refl</a>
  <a id="1692" class="Symbol">;</a> <a id="1694" href="Relation.Binary.Structures.html#1603" class="Field">sym</a>   <a id="1700" class="Symbol">=</a> <a id="1702" href="Relation.Binary.Morphism.RelMonomorphism.html#1029" class="Function">sym</a>   <a id="1708" href="Relation.Binary.Structures.html#1603" class="Field">E.sym</a>
  <a id="1716" class="Symbol">;</a> <a id="1718" href="Relation.Binary.Structures.html#1629" class="Field">trans</a> <a id="1724" class="Symbol">=</a> <a id="1726" href="Relation.Binary.Morphism.RelMonomorphism.html#1109" class="Function">trans</a> <a id="1732" href="Relation.Binary.Structures.html#1629" class="Field">E.trans</a>
  <a id="1742" class="Symbol">}</a> <a id="1744" class="Keyword">where</a> <a id="1750" class="Keyword">module</a> <a id="1757" href="Relation.Binary.Morphism.RelMonomorphism.html#1757" class="Module">E</a> <a id="1759" class="Symbol">=</a> <a id="1761" href="Relation.Binary.Structures.html#1531" class="Module">IsEquivalence</a> <a id="1775" href="Relation.Binary.Morphism.RelMonomorphism.html#1651" class="Bound">isEq</a>

<a id="isDecEquivalence"></a><a id="1781" href="Relation.Binary.Morphism.RelMonomorphism.html#1781" class="Function">isDecEquivalence</a> <a id="1798" class="Symbol">:</a> <a id="1800" href="Relation.Binary.Structures.html#1833" class="Record">IsDecEquivalence</a> <a id="1817" href="Relation.Binary.Morphism.RelMonomorphism.html#624" class="Bound Operator">_∼₂_</a> <a id="1822" class="Symbol">→</a> <a id="1824" href="Relation.Binary.Structures.html#1833" class="Record">IsDecEquivalence</a> <a id="1841" href="Relation.Binary.Morphism.RelMonomorphism.html#606" class="Bound Operator">_∼₁_</a>
<a id="1846" href="Relation.Binary.Morphism.RelMonomorphism.html#1781" class="Function">isDecEquivalence</a> <a id="1863" href="Relation.Binary.Morphism.RelMonomorphism.html#1863" class="Bound">isDecEq</a> <a id="1871" class="Symbol">=</a> <a id="1873" class="Keyword">record</a>
  <a id="1882" class="Symbol">{</a> <a id="1884" href="Relation.Binary.Structures.html#1896" class="Field">isEquivalence</a>  <a id="1899" class="Symbol">=</a> <a id="1901" href="Relation.Binary.Morphism.RelMonomorphism.html#1581" class="Function">isEquivalence</a> <a id="1915" href="Relation.Binary.Structures.html#1896" class="Field">E.isEquivalence</a>
  <a id="1933" class="Symbol">;</a> <a id="1935" href="Relation.Binary.Structures.html#1930" class="Field Operator">_≟_</a>            <a id="1950" class="Symbol">=</a> <a id="1952" href="Relation.Binary.Morphism.RelMonomorphism.html#1402" class="Function">dec</a> <a id="1956" href="Relation.Binary.Structures.html#1930" class="Field Operator">E._≟_</a>
  <a id="1964" class="Symbol">}</a> <a id="1966" class="Keyword">where</a> <a id="1972" class="Keyword">module</a> <a id="1979" href="Relation.Binary.Morphism.RelMonomorphism.html#1979" class="Module">E</a> <a id="1981" class="Symbol">=</a> <a id="1983" href="Relation.Binary.Structures.html#1833" class="Module">IsDecEquivalence</a> <a id="2000" href="Relation.Binary.Morphism.RelMonomorphism.html#1863" class="Bound">isDecEq</a>
</pre></body></html>