<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Colist.Bisimilarity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Bisimilarity for Colists</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Pragma">--without-K</a> <a id="232" class="Pragma">--safe</a> <a id="239" class="Pragma">--sized-types</a> <a id="253" class="Symbol">#-}</a>

<a id="258" class="Keyword">module</a> <a id="265" href="Codata.Colist.Bisimilarity.html" class="Module">Codata.Colist.Bisimilarity</a> <a id="292" class="Keyword">where</a>

<a id="299" class="Keyword">open</a> <a id="304" class="Keyword">import</a> <a id="311" href="Level.html" class="Module">Level</a> <a id="317" class="Keyword">using</a> <a id="323" class="Symbol">(</a><a id="324" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a><a id="327" class="Symbol">)</a>
<a id="329" class="Keyword">open</a> <a id="334" class="Keyword">import</a> <a id="341" href="Size.html" class="Module">Size</a>
<a id="346" class="Keyword">open</a> <a id="351" class="Keyword">import</a> <a id="358" href="Codata.Thunk.html" class="Module">Codata.Thunk</a>
<a id="371" class="Keyword">open</a> <a id="376" class="Keyword">import</a> <a id="383" href="Codata.Colist.html" class="Module">Codata.Colist</a>
<a id="397" class="Keyword">open</a> <a id="402" class="Keyword">import</a> <a id="409" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="425" class="Keyword">open</a> <a id="430" class="Keyword">import</a> <a id="437" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="475" class="Symbol">as</a> <a id="478" class="Module">Eq</a> <a id="481" class="Keyword">using</a> <a id="487" class="Symbol">(</a><a id="488" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">_≡_</a><a id="491" class="Symbol">)</a>

<a id="494" class="Keyword">data</a> <a id="Bisim"></a><a id="499" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="505" class="Symbol">{</a><a id="506" href="Codata.Colist.Bisimilarity.html#506" class="Bound">a</a> <a id="508" href="Codata.Colist.Bisimilarity.html#508" class="Bound">b</a> <a id="510" href="Codata.Colist.Bisimilarity.html#510" class="Bound">r</a><a id="511" class="Symbol">}</a> <a id="513" class="Symbol">{</a><a id="514" href="Codata.Colist.Bisimilarity.html#514" class="Bound">A</a> <a id="516" class="Symbol">:</a> <a id="518" class="PrimitiveType">Set</a> <a id="522" href="Codata.Colist.Bisimilarity.html#506" class="Bound">a</a><a id="523" class="Symbol">}</a> <a id="525" class="Symbol">{</a><a id="526" href="Codata.Colist.Bisimilarity.html#526" class="Bound">B</a> <a id="528" class="Symbol">:</a> <a id="530" class="PrimitiveType">Set</a> <a id="534" href="Codata.Colist.Bisimilarity.html#508" class="Bound">b</a><a id="535" class="Symbol">}</a> <a id="537" class="Symbol">(</a><a id="538" href="Codata.Colist.Bisimilarity.html#538" class="Bound">R</a> <a id="540" class="Symbol">:</a> <a id="542" href="Codata.Colist.Bisimilarity.html#514" class="Bound">A</a> <a id="544" class="Symbol">→</a> <a id="546" href="Codata.Colist.Bisimilarity.html#526" class="Bound">B</a> <a id="548" class="Symbol">→</a> <a id="550" class="PrimitiveType">Set</a> <a id="554" href="Codata.Colist.Bisimilarity.html#510" class="Bound">r</a><a id="555" class="Symbol">)</a> <a id="557" class="Symbol">(</a><a id="558" href="Codata.Colist.Bisimilarity.html#558" class="Bound">i</a> <a id="560" class="Symbol">:</a> <a id="562" href="Agda.Builtin.Size.html#179" class="Postulate">Size</a><a id="566" class="Symbol">)</a> <a id="568" class="Symbol">:</a>
           <a id="581" class="Symbol">(</a><a id="582" href="Codata.Colist.Bisimilarity.html#582" class="Bound">xs</a> <a id="585" class="Symbol">:</a> <a id="587" href="Codata.Colist.html#1017" class="Datatype">Colist</a> <a id="594" href="Codata.Colist.Bisimilarity.html#514" class="Bound">A</a> <a id="596" href="Agda.Builtin.Size.html#275" class="Postulate">∞</a><a id="597" class="Symbol">)</a> <a id="599" class="Symbol">(</a><a id="600" href="Codata.Colist.Bisimilarity.html#600" class="Bound">ys</a> <a id="603" class="Symbol">:</a> <a id="605" href="Codata.Colist.html#1017" class="Datatype">Colist</a> <a id="612" href="Codata.Colist.Bisimilarity.html#526" class="Bound">B</a> <a id="614" href="Agda.Builtin.Size.html#275" class="Postulate">∞</a><a id="615" class="Symbol">)</a> <a id="617" class="Symbol">→</a> <a id="619" class="PrimitiveType">Set</a> <a id="623" class="Symbol">(</a><a id="624" href="Codata.Colist.Bisimilarity.html#510" class="Bound">r</a> <a id="626" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="628" href="Codata.Colist.Bisimilarity.html#506" class="Bound">a</a> <a id="630" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="632" href="Codata.Colist.Bisimilarity.html#508" class="Bound">b</a><a id="633" class="Symbol">)</a> <a id="635" class="Keyword">where</a>
  <a id="Bisim.[]"></a><a id="643" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>  <a id="647" class="Symbol">:</a> <a id="649" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="655" href="Codata.Colist.Bisimilarity.html#538" class="Bound">R</a> <a id="657" href="Codata.Colist.Bisimilarity.html#558" class="Bound">i</a> <a id="659" href="Codata.Colist.html#1067" class="InductiveConstructor">[]</a> <a id="662" href="Codata.Colist.html#1067" class="InductiveConstructor">[]</a>
  <a id="Bisim._∷_"></a><a id="667" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">_∷_</a> <a id="671" class="Symbol">:</a> <a id="673" class="Symbol">∀</a> <a id="675" class="Symbol">{</a><a id="676" href="Codata.Colist.Bisimilarity.html#676" class="Bound">x</a> <a id="678" href="Codata.Colist.Bisimilarity.html#678" class="Bound">y</a> <a id="680" href="Codata.Colist.Bisimilarity.html#680" class="Bound">xs</a> <a id="683" href="Codata.Colist.Bisimilarity.html#683" class="Bound">ys</a><a id="685" class="Symbol">}</a> <a id="687" class="Symbol">→</a> <a id="689" href="Codata.Colist.Bisimilarity.html#538" class="Bound">R</a> <a id="691" href="Codata.Colist.Bisimilarity.html#676" class="Bound">x</a> <a id="693" href="Codata.Colist.Bisimilarity.html#678" class="Bound">y</a> <a id="695" class="Symbol">→</a> <a id="697" href="Codata.Thunk.html#749" class="Function">Thunk^R</a> <a id="705" class="Symbol">(</a><a id="706" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="712" href="Codata.Colist.Bisimilarity.html#538" class="Bound">R</a><a id="713" class="Symbol">)</a> <a id="715" href="Codata.Colist.Bisimilarity.html#558" class="Bound">i</a> <a id="717" href="Codata.Colist.Bisimilarity.html#680" class="Bound">xs</a> <a id="720" href="Codata.Colist.Bisimilarity.html#683" class="Bound">ys</a> <a id="723" class="Symbol">→</a> <a id="725" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="731" href="Codata.Colist.Bisimilarity.html#538" class="Bound">R</a> <a id="733" href="Codata.Colist.Bisimilarity.html#558" class="Bound">i</a> <a id="735" class="Symbol">(</a><a id="736" href="Codata.Colist.Bisimilarity.html#676" class="Bound">x</a> <a id="738" href="Codata.Colist.html#1086" class="InductiveConstructor Operator">∷</a> <a id="740" href="Codata.Colist.Bisimilarity.html#680" class="Bound">xs</a><a id="742" class="Symbol">)</a> <a id="744" class="Symbol">(</a><a id="745" href="Codata.Colist.Bisimilarity.html#678" class="Bound">y</a> <a id="747" href="Codata.Colist.html#1086" class="InductiveConstructor Operator">∷</a> <a id="749" href="Codata.Colist.Bisimilarity.html#683" class="Bound">ys</a><a id="751" class="Symbol">)</a>


<a id="755" class="Keyword">module</a> <a id="762" href="Codata.Colist.Bisimilarity.html#762" class="Module">_</a> <a id="764" class="Symbol">{</a><a id="765" href="Codata.Colist.Bisimilarity.html#765" class="Bound">a</a> <a id="767" href="Codata.Colist.Bisimilarity.html#767" class="Bound">r</a><a id="768" class="Symbol">}</a> <a id="770" class="Symbol">{</a><a id="771" href="Codata.Colist.Bisimilarity.html#771" class="Bound">A</a> <a id="773" class="Symbol">:</a> <a id="775" class="PrimitiveType">Set</a> <a id="779" href="Codata.Colist.Bisimilarity.html#765" class="Bound">a</a><a id="780" class="Symbol">}</a> <a id="782" class="Symbol">{</a><a id="783" href="Codata.Colist.Bisimilarity.html#783" class="Bound">R</a> <a id="785" class="Symbol">:</a> <a id="787" href="Codata.Colist.Bisimilarity.html#771" class="Bound">A</a> <a id="789" class="Symbol">→</a> <a id="791" href="Codata.Colist.Bisimilarity.html#771" class="Bound">A</a> <a id="793" class="Symbol">→</a> <a id="795" class="PrimitiveType">Set</a> <a id="799" href="Codata.Colist.Bisimilarity.html#767" class="Bound">r</a><a id="800" class="Symbol">}</a> <a id="802" class="Keyword">where</a>

 <a id="810" href="Codata.Colist.Bisimilarity.html#810" class="Function">reflexive</a> <a id="820" class="Symbol">:</a> <a id="822" href="Relation.Binary.Core.html#2321" class="Function">Reflexive</a> <a id="832" href="Codata.Colist.Bisimilarity.html#783" class="Bound">R</a> <a id="834" class="Symbol">→</a> <a id="836" class="Symbol">∀</a> <a id="838" class="Symbol">{</a><a id="839" href="Codata.Colist.Bisimilarity.html#839" class="Bound">i</a><a id="840" class="Symbol">}</a> <a id="842" class="Symbol">→</a> <a id="844" href="Relation.Binary.Core.html#2321" class="Function">Reflexive</a> <a id="854" class="Symbol">(</a><a id="855" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="861" href="Codata.Colist.Bisimilarity.html#783" class="Bound">R</a> <a id="863" href="Codata.Colist.Bisimilarity.html#839" class="Bound">i</a><a id="864" class="Symbol">)</a>
 <a id="867" href="Codata.Colist.Bisimilarity.html#810" class="Function">reflexive</a> <a id="877" href="Codata.Colist.Bisimilarity.html#877" class="Bound">refl^R</a> <a id="884" class="Symbol">{</a><a id="885" href="Codata.Colist.Bisimilarity.html#885" class="Bound">i</a><a id="886" class="Symbol">}</a> <a id="888" class="Symbol">{</a><a id="889" href="Codata.Colist.html#1067" class="InductiveConstructor">[]</a><a id="891" class="Symbol">}</a>     <a id="897" class="Symbol">=</a> <a id="899" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>
 <a id="903" href="Codata.Colist.Bisimilarity.html#810" class="Function">reflexive</a> <a id="913" href="Codata.Colist.Bisimilarity.html#913" class="Bound">refl^R</a> <a id="920" class="Symbol">{</a><a id="921" href="Codata.Colist.Bisimilarity.html#921" class="Bound">i</a><a id="922" class="Symbol">}</a> <a id="924" class="Symbol">{</a><a id="925" href="Codata.Colist.Bisimilarity.html#925" class="Bound">r</a> <a id="927" href="Codata.Colist.html#1086" class="InductiveConstructor Operator">∷</a> <a id="929" href="Codata.Colist.Bisimilarity.html#929" class="Bound">rs</a><a id="931" class="Symbol">}</a> <a id="933" class="Symbol">=</a> <a id="935" href="Codata.Colist.Bisimilarity.html#913" class="Bound">refl^R</a> <a id="942" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">∷</a> <a id="944" class="Symbol">λ</a> <a id="946" class="Keyword">where</a> <a id="952" class="Symbol">.</a><a id="953" href="Codata.Thunk.html#544" class="Field">force</a> <a id="959" class="Symbol">→</a> <a id="961" href="Codata.Colist.Bisimilarity.html#810" class="Function">reflexive</a> <a id="971" href="Codata.Colist.Bisimilarity.html#913" class="Bound">refl^R</a>

<a id="979" class="Keyword">module</a> <a id="986" href="Codata.Colist.Bisimilarity.html#986" class="Module">_</a> <a id="988" class="Symbol">{</a><a id="989" href="Codata.Colist.Bisimilarity.html#989" class="Bound">a</a> <a id="991" href="Codata.Colist.Bisimilarity.html#991" class="Bound">b</a><a id="992" class="Symbol">}</a> <a id="994" class="Symbol">{</a><a id="995" href="Codata.Colist.Bisimilarity.html#995" class="Bound">A</a> <a id="997" class="Symbol">:</a> <a id="999" class="PrimitiveType">Set</a> <a id="1003" href="Codata.Colist.Bisimilarity.html#989" class="Bound">a</a><a id="1004" class="Symbol">}</a> <a id="1006" class="Symbol">{</a><a id="1007" href="Codata.Colist.Bisimilarity.html#1007" class="Bound">B</a> <a id="1009" class="Symbol">:</a> <a id="1011" class="PrimitiveType">Set</a> <a id="1015" href="Codata.Colist.Bisimilarity.html#991" class="Bound">b</a><a id="1016" class="Symbol">}</a>
         <a id="1027" class="Symbol">{</a><a id="1028" href="Codata.Colist.Bisimilarity.html#1028" class="Bound">r</a><a id="1029" class="Symbol">}</a> <a id="1031" class="Symbol">{</a><a id="1032" href="Codata.Colist.Bisimilarity.html#1032" class="Bound">P</a> <a id="1034" class="Symbol">:</a> <a id="1036" href="Codata.Colist.Bisimilarity.html#995" class="Bound">A</a> <a id="1038" class="Symbol">→</a> <a id="1040" href="Codata.Colist.Bisimilarity.html#1007" class="Bound">B</a> <a id="1042" class="Symbol">→</a> <a id="1044" class="PrimitiveType">Set</a> <a id="1048" href="Codata.Colist.Bisimilarity.html#1028" class="Bound">r</a><a id="1049" class="Symbol">}</a> <a id="1051" class="Symbol">{</a><a id="1052" href="Codata.Colist.Bisimilarity.html#1052" class="Bound">Q</a> <a id="1054" class="Symbol">:</a> <a id="1056" href="Codata.Colist.Bisimilarity.html#1007" class="Bound">B</a> <a id="1058" class="Symbol">→</a> <a id="1060" href="Codata.Colist.Bisimilarity.html#995" class="Bound">A</a> <a id="1062" class="Symbol">→</a> <a id="1064" class="PrimitiveType">Set</a> <a id="1068" href="Codata.Colist.Bisimilarity.html#1028" class="Bound">r</a><a id="1069" class="Symbol">}</a> <a id="1071" class="Keyword">where</a>

 <a id="1079" href="Codata.Colist.Bisimilarity.html#1079" class="Function">symmetric</a> <a id="1089" class="Symbol">:</a> <a id="1091" href="Relation.Binary.Core.html#2406" class="Function">Sym</a> <a id="1095" href="Codata.Colist.Bisimilarity.html#1032" class="Bound">P</a> <a id="1097" href="Codata.Colist.Bisimilarity.html#1052" class="Bound">Q</a> <a id="1099" class="Symbol">→</a> <a id="1101" class="Symbol">∀</a> <a id="1103" class="Symbol">{</a><a id="1104" href="Codata.Colist.Bisimilarity.html#1104" class="Bound">i</a><a id="1105" class="Symbol">}</a> <a id="1107" class="Symbol">→</a> <a id="1109" href="Relation.Binary.Core.html#2406" class="Function">Sym</a> <a id="1113" class="Symbol">(</a><a id="1114" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="1120" href="Codata.Colist.Bisimilarity.html#1032" class="Bound">P</a> <a id="1122" href="Codata.Colist.Bisimilarity.html#1104" class="Bound">i</a><a id="1123" class="Symbol">)</a> <a id="1125" class="Symbol">(</a><a id="1126" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="1132" href="Codata.Colist.Bisimilarity.html#1052" class="Bound">Q</a> <a id="1134" href="Codata.Colist.Bisimilarity.html#1104" class="Bound">i</a><a id="1135" class="Symbol">)</a>
 <a id="1138" href="Codata.Colist.Bisimilarity.html#1079" class="Function">symmetric</a> <a id="1148" href="Codata.Colist.Bisimilarity.html#1148" class="Bound">sym^PQ</a> <a id="1155" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>       <a id="1164" class="Symbol">=</a> <a id="1166" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>
 <a id="1170" href="Codata.Colist.Bisimilarity.html#1079" class="Function">symmetric</a> <a id="1180" href="Codata.Colist.Bisimilarity.html#1180" class="Bound">sym^PQ</a> <a id="1187" class="Symbol">(</a><a id="1188" href="Codata.Colist.Bisimilarity.html#1188" class="Bound">p</a> <a id="1190" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">∷</a> <a id="1192" href="Codata.Colist.Bisimilarity.html#1192" class="Bound">ps</a><a id="1194" class="Symbol">)</a> <a id="1196" class="Symbol">=</a> <a id="1198" href="Codata.Colist.Bisimilarity.html#1180" class="Bound">sym^PQ</a> <a id="1205" href="Codata.Colist.Bisimilarity.html#1188" class="Bound">p</a> <a id="1207" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">∷</a> <a id="1209" class="Symbol">λ</a> <a id="1211" class="Keyword">where</a> <a id="1217" class="Symbol">.</a><a id="1218" href="Codata.Thunk.html#544" class="Field">force</a> <a id="1224" class="Symbol">→</a> <a id="1226" href="Codata.Colist.Bisimilarity.html#1079" class="Function">symmetric</a> <a id="1236" href="Codata.Colist.Bisimilarity.html#1180" class="Bound">sym^PQ</a> <a id="1243" class="Symbol">(</a><a id="1244" href="Codata.Colist.Bisimilarity.html#1192" class="Bound">ps</a> <a id="1247" class="Symbol">.</a><a id="1248" href="Codata.Thunk.html#544" class="Field">force</a><a id="1253" class="Symbol">)</a>

<a id="1256" class="Keyword">module</a> <a id="1263" href="Codata.Colist.Bisimilarity.html#1263" class="Module">_</a> <a id="1265" class="Symbol">{</a><a id="1266" href="Codata.Colist.Bisimilarity.html#1266" class="Bound">a</a> <a id="1268" href="Codata.Colist.Bisimilarity.html#1268" class="Bound">b</a> <a id="1270" href="Codata.Colist.Bisimilarity.html#1270" class="Bound">c</a><a id="1271" class="Symbol">}</a> <a id="1273" class="Symbol">{</a><a id="1274" href="Codata.Colist.Bisimilarity.html#1274" class="Bound">A</a> <a id="1276" class="Symbol">:</a> <a id="1278" class="PrimitiveType">Set</a> <a id="1282" href="Codata.Colist.Bisimilarity.html#1266" class="Bound">a</a><a id="1283" class="Symbol">}</a> <a id="1285" class="Symbol">{</a><a id="1286" href="Codata.Colist.Bisimilarity.html#1286" class="Bound">B</a> <a id="1288" class="Symbol">:</a> <a id="1290" class="PrimitiveType">Set</a> <a id="1294" href="Codata.Colist.Bisimilarity.html#1268" class="Bound">b</a><a id="1295" class="Symbol">}</a> <a id="1297" class="Symbol">{</a><a id="1298" href="Codata.Colist.Bisimilarity.html#1298" class="Bound">C</a> <a id="1300" class="Symbol">:</a> <a id="1302" class="PrimitiveType">Set</a> <a id="1306" href="Codata.Colist.Bisimilarity.html#1270" class="Bound">c</a><a id="1307" class="Symbol">}</a>
         <a id="1318" class="Symbol">{</a><a id="1319" href="Codata.Colist.Bisimilarity.html#1319" class="Bound">r</a><a id="1320" class="Symbol">}</a> <a id="1322" class="Symbol">{</a><a id="1323" href="Codata.Colist.Bisimilarity.html#1323" class="Bound">P</a> <a id="1325" class="Symbol">:</a> <a id="1327" href="Codata.Colist.Bisimilarity.html#1274" class="Bound">A</a> <a id="1329" class="Symbol">→</a> <a id="1331" href="Codata.Colist.Bisimilarity.html#1286" class="Bound">B</a> <a id="1333" class="Symbol">→</a> <a id="1335" class="PrimitiveType">Set</a> <a id="1339" href="Codata.Colist.Bisimilarity.html#1319" class="Bound">r</a><a id="1340" class="Symbol">}</a> <a id="1342" class="Symbol">{</a><a id="1343" href="Codata.Colist.Bisimilarity.html#1343" class="Bound">Q</a> <a id="1345" class="Symbol">:</a> <a id="1347" href="Codata.Colist.Bisimilarity.html#1286" class="Bound">B</a> <a id="1349" class="Symbol">→</a> <a id="1351" href="Codata.Colist.Bisimilarity.html#1298" class="Bound">C</a> <a id="1353" class="Symbol">→</a> <a id="1355" class="PrimitiveType">Set</a> <a id="1359" href="Codata.Colist.Bisimilarity.html#1319" class="Bound">r</a><a id="1360" class="Symbol">}</a> <a id="1362" class="Symbol">{</a><a id="1363" href="Codata.Colist.Bisimilarity.html#1363" class="Bound">R</a> <a id="1365" class="Symbol">:</a> <a id="1367" href="Codata.Colist.Bisimilarity.html#1274" class="Bound">A</a> <a id="1369" class="Symbol">→</a> <a id="1371" href="Codata.Colist.Bisimilarity.html#1298" class="Bound">C</a> <a id="1373" class="Symbol">→</a> <a id="1375" class="PrimitiveType">Set</a> <a id="1379" href="Codata.Colist.Bisimilarity.html#1319" class="Bound">r</a><a id="1380" class="Symbol">}</a> <a id="1382" class="Keyword">where</a>

 <a id="1390" href="Codata.Colist.Bisimilarity.html#1390" class="Function">transitive</a> <a id="1401" class="Symbol">:</a> <a id="1403" href="Relation.Binary.Core.html#2567" class="Function">Trans</a> <a id="1409" href="Codata.Colist.Bisimilarity.html#1323" class="Bound">P</a> <a id="1411" href="Codata.Colist.Bisimilarity.html#1343" class="Bound">Q</a> <a id="1413" href="Codata.Colist.Bisimilarity.html#1363" class="Bound">R</a> <a id="1415" class="Symbol">→</a> <a id="1417" class="Symbol">∀</a> <a id="1419" class="Symbol">{</a><a id="1420" href="Codata.Colist.Bisimilarity.html#1420" class="Bound">i</a><a id="1421" class="Symbol">}</a> <a id="1423" class="Symbol">→</a> <a id="1425" href="Relation.Binary.Core.html#2567" class="Function">Trans</a> <a id="1431" class="Symbol">(</a><a id="1432" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="1438" href="Codata.Colist.Bisimilarity.html#1323" class="Bound">P</a> <a id="1440" href="Codata.Colist.Bisimilarity.html#1420" class="Bound">i</a><a id="1441" class="Symbol">)</a> <a id="1443" class="Symbol">(</a><a id="1444" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="1450" href="Codata.Colist.Bisimilarity.html#1343" class="Bound">Q</a> <a id="1452" href="Codata.Colist.Bisimilarity.html#1420" class="Bound">i</a><a id="1453" class="Symbol">)</a> <a id="1455" class="Symbol">(</a><a id="1456" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="1462" href="Codata.Colist.Bisimilarity.html#1363" class="Bound">R</a> <a id="1464" href="Codata.Colist.Bisimilarity.html#1420" class="Bound">i</a><a id="1465" class="Symbol">)</a>
 <a id="1468" href="Codata.Colist.Bisimilarity.html#1390" class="Function">transitive</a> <a id="1479" href="Codata.Colist.Bisimilarity.html#1479" class="Bound">trans^PQR</a> <a id="1489" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>       <a id="1498" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>       <a id="1507" class="Symbol">=</a> <a id="1509" href="Codata.Colist.Bisimilarity.html#643" class="InductiveConstructor">[]</a>
 <a id="1513" href="Codata.Colist.Bisimilarity.html#1390" class="Function">transitive</a> <a id="1524" href="Codata.Colist.Bisimilarity.html#1524" class="Bound">trans^PQR</a> <a id="1534" class="Symbol">(</a><a id="1535" href="Codata.Colist.Bisimilarity.html#1535" class="Bound">p</a> <a id="1537" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">∷</a> <a id="1539" href="Codata.Colist.Bisimilarity.html#1539" class="Bound">ps</a><a id="1541" class="Symbol">)</a> <a id="1543" class="Symbol">(</a><a id="1544" href="Codata.Colist.Bisimilarity.html#1544" class="Bound">q</a> <a id="1546" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">∷</a> <a id="1548" href="Codata.Colist.Bisimilarity.html#1548" class="Bound">qs</a><a id="1550" class="Symbol">)</a> <a id="1552" class="Symbol">=</a>
   <a id="1557" href="Codata.Colist.Bisimilarity.html#1524" class="Bound">trans^PQR</a> <a id="1567" href="Codata.Colist.Bisimilarity.html#1535" class="Bound">p</a> <a id="1569" href="Codata.Colist.Bisimilarity.html#1544" class="Bound">q</a> <a id="1571" href="Codata.Colist.Bisimilarity.html#667" class="InductiveConstructor Operator">∷</a> <a id="1573" class="Symbol">λ</a> <a id="1575" class="Keyword">where</a> <a id="1581" class="Symbol">.</a><a id="1582" href="Codata.Thunk.html#544" class="Field">force</a> <a id="1588" class="Symbol">→</a> <a id="1590" href="Codata.Colist.Bisimilarity.html#1390" class="Function">transitive</a> <a id="1601" href="Codata.Colist.Bisimilarity.html#1524" class="Bound">trans^PQR</a> <a id="1611" class="Symbol">(</a><a id="1612" href="Codata.Colist.Bisimilarity.html#1539" class="Bound">ps</a> <a id="1615" class="Symbol">.</a><a id="1616" href="Codata.Thunk.html#544" class="Field">force</a><a id="1621" class="Symbol">)</a> <a id="1623" class="Symbol">(</a><a id="1624" href="Codata.Colist.Bisimilarity.html#1548" class="Bound">qs</a> <a id="1627" class="Symbol">.</a><a id="1628" href="Codata.Thunk.html#544" class="Field">force</a><a id="1633" class="Symbol">)</a>

<a id="1636" class="Comment">-- Pointwise Equality as a Bisimilarity</a>
<a id="1676" class="Comment">------------------------------------------------------------------------</a>

<a id="1750" class="Keyword">module</a> <a id="1757" href="Codata.Colist.Bisimilarity.html#1757" class="Module">_</a> <a id="1759" class="Symbol">{</a><a id="1760" href="Codata.Colist.Bisimilarity.html#1760" class="Bound">ℓ</a><a id="1761" class="Symbol">}</a> <a id="1763" class="Symbol">{</a><a id="1764" href="Codata.Colist.Bisimilarity.html#1764" class="Bound">A</a> <a id="1766" class="Symbol">:</a> <a id="1768" class="PrimitiveType">Set</a> <a id="1772" href="Codata.Colist.Bisimilarity.html#1760" class="Bound">ℓ</a><a id="1773" class="Symbol">}</a> <a id="1775" class="Keyword">where</a>

 <a id="1783" class="Keyword">infix</a> <a id="1789" class="Number">1</a> <a id="1791" href="Codata.Colist.Bisimilarity.html#1798" class="Function Operator">_⊢_≈_</a>
 <a id="1798" href="Codata.Colist.Bisimilarity.html#1798" class="Function Operator">_⊢_≈_</a> <a id="1804" class="Symbol">:</a> <a id="1806" class="Symbol">∀</a> <a id="1808" href="Codata.Colist.Bisimilarity.html#1808" class="Bound">i</a> <a id="1810" class="Symbol">→</a> <a id="1812" href="Codata.Colist.html#1017" class="Datatype">Colist</a> <a id="1819" href="Codata.Colist.Bisimilarity.html#1764" class="Bound">A</a> <a id="1821" href="Agda.Builtin.Size.html#275" class="Postulate">∞</a> <a id="1823" class="Symbol">→</a> <a id="1825" href="Codata.Colist.html#1017" class="Datatype">Colist</a> <a id="1832" href="Codata.Colist.Bisimilarity.html#1764" class="Bound">A</a> <a id="1834" href="Agda.Builtin.Size.html#275" class="Postulate">∞</a> <a id="1836" class="Symbol">→</a> <a id="1838" class="PrimitiveType">Set</a> <a id="1842" href="Codata.Colist.Bisimilarity.html#1760" class="Bound">ℓ</a>
 <a id="1845" href="Codata.Colist.Bisimilarity.html#1798" class="Function Operator">_⊢_≈_</a> <a id="1851" class="Symbol">=</a> <a id="1853" href="Codata.Colist.Bisimilarity.html#499" class="Datatype">Bisim</a> <a id="1859" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">_≡_</a>

 <a id="1865" href="Codata.Colist.Bisimilarity.html#1865" class="Function">refl</a> <a id="1870" class="Symbol">:</a> <a id="1872" class="Symbol">∀</a> <a id="1874" class="Symbol">{</a><a id="1875" href="Codata.Colist.Bisimilarity.html#1875" class="Bound">i</a><a id="1876" class="Symbol">}</a> <a id="1878" class="Symbol">→</a> <a id="1880" href="Relation.Binary.Core.html#2321" class="Function">Reflexive</a> <a id="1890" class="Symbol">(</a><a id="1891" href="Codata.Colist.Bisimilarity.html#1875" class="Bound">i</a> <a id="1893" href="Codata.Colist.Bisimilarity.html#1798" class="Function Operator">⊢_≈_</a><a id="1897" class="Symbol">)</a>
 <a id="1900" href="Codata.Colist.Bisimilarity.html#1865" class="Function">refl</a> <a id="1905" class="Symbol">=</a> <a id="1907" href="Codata.Colist.Bisimilarity.html#810" class="Function">reflexive</a> <a id="1917" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">Eq.refl</a>

 <a id="1927" href="Codata.Colist.Bisimilarity.html#1927" class="Function">sym</a> <a id="1931" class="Symbol">:</a> <a id="1933" class="Symbol">∀</a> <a id="1935" class="Symbol">{</a><a id="1936" href="Codata.Colist.Bisimilarity.html#1936" class="Bound">i</a><a id="1937" class="Symbol">}</a> <a id="1939" class="Symbol">→</a> <a id="1941" href="Relation.Binary.Core.html#2480" class="Function">Symmetric</a> <a id="1951" class="Symbol">(</a><a id="1952" href="Codata.Colist.Bisimilarity.html#1936" class="Bound">i</a> <a id="1954" href="Codata.Colist.Bisimilarity.html#1798" class="Function Operator">⊢_≈_</a><a id="1958" class="Symbol">)</a>
 <a id="1961" href="Codata.Colist.Bisimilarity.html#1927" class="Function">sym</a> <a id="1965" class="Symbol">=</a> <a id="1967" href="Codata.Colist.Bisimilarity.html#1079" class="Function">symmetric</a> <a id="1977" href="Relation.Binary.PropositionalEquality.Core.html#939" class="Function">Eq.sym</a>

 <a id="1986" href="Codata.Colist.Bisimilarity.html#1986" class="Function">trans</a> <a id="1992" class="Symbol">:</a> <a id="1994" class="Symbol">∀</a> <a id="1996" class="Symbol">{</a><a id="1997" href="Codata.Colist.Bisimilarity.html#1997" class="Bound">i</a><a id="1998" class="Symbol">}</a> <a id="2000" class="Symbol">→</a> <a id="2002" href="Relation.Binary.Core.html#2848" class="Function">Transitive</a> <a id="2013" class="Symbol">(</a><a id="2014" href="Codata.Colist.Bisimilarity.html#1997" class="Bound">i</a> <a id="2016" href="Codata.Colist.Bisimilarity.html#1798" class="Function Operator">⊢_≈_</a><a id="2020" class="Symbol">)</a>
 <a id="2023" href="Codata.Colist.Bisimilarity.html#1986" class="Function">trans</a> <a id="2029" class="Symbol">=</a> <a id="2031" href="Codata.Colist.Bisimilarity.html#1390" class="Function">transitive</a> <a id="2042" href="Relation.Binary.PropositionalEquality.Core.html#984" class="Function">Eq.trans</a>
</pre></body></html>