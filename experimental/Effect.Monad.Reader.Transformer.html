<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Effect.Monad.Reader.Transformer</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The reader monad transformer</a>
<a id="138" class="Comment">------------------------------------------------------------------------</a>

<a id="212" class="Symbol">{-#</a> <a id="216" class="Keyword">OPTIONS</a> <a id="224" class="Pragma">--cubical-compatible</a> <a id="245" class="Pragma">--safe</a> <a id="252" class="Symbol">#-}</a>


<a id="258" class="Keyword">module</a> <a id="265" href="Effect.Monad.Reader.Transformer.html" class="Module">Effect.Monad.Reader.Transformer</a> <a id="297" class="Keyword">where</a>

<a id="304" class="Keyword">open</a> <a id="309" class="Keyword">import</a> <a id="316" href="Algebra.html" class="Module">Algebra</a> <a id="324" class="Keyword">using</a> <a id="330" class="Symbol">(</a><a id="331" href="Algebra.Bundles.Raw.html#1534" class="Record">RawMonoid</a><a id="340" class="Symbol">)</a>
<a id="342" class="Keyword">open</a> <a id="347" class="Keyword">import</a> <a id="354" href="Effect.Choice.html" class="Module">Effect.Choice</a> <a id="368" class="Keyword">using</a> <a id="374" class="Symbol">(</a><a id="375" href="Effect.Choice.html#414" class="Record">RawChoice</a><a id="384" class="Symbol">)</a>
<a id="386" class="Keyword">open</a> <a id="391" class="Keyword">import</a> <a id="398" href="Effect.Empty.html" class="Module">Effect.Empty</a> <a id="411" class="Keyword">using</a> <a id="417" class="Symbol">(</a><a id="418" href="Effect.Empty.html#384" class="Record">RawEmpty</a><a id="426" class="Symbol">)</a>
<a id="428" class="Keyword">open</a> <a id="433" class="Keyword">import</a> <a id="440" href="Effect.Functor.html" class="Module">Effect.Functor</a> <a id="455" class="Keyword">using</a> <a id="461" class="Symbol">(</a><a id="462" href="Effect.Functor.html#579" class="Record">RawFunctor</a><a id="472" class="Symbol">)</a>
<a id="474" class="Keyword">open</a> <a id="479" class="Keyword">import</a> <a id="486" href="Effect.Applicative.html" class="Module">Effect.Applicative</a> <a id="505" class="Keyword">using</a> <a id="511" class="Symbol">(</a><a id="512" href="Effect.Applicative.html#979" class="Record">RawApplicative</a><a id="526" class="Symbol">;</a> <a id="528" href="Effect.Applicative.html#2271" class="Record">RawApplicativeZero</a><a id="546" class="Symbol">;</a> <a id="548" href="Effect.Applicative.html#2675" class="Record">RawAlternative</a><a id="562" class="Symbol">)</a>
<a id="564" class="Keyword">open</a> <a id="569" class="Keyword">import</a> <a id="576" href="Effect.Monad.html" class="Module">Effect.Monad</a> <a id="589" class="Keyword">using</a> <a id="595" class="Symbol">(</a><a id="596" href="Effect.Monad.html#779" class="Record">RawMonad</a><a id="604" class="Symbol">;</a> <a id="606" href="Effect.Monad.html#2107" class="Record">RawMonadZero</a><a id="618" class="Symbol">;</a> <a id="620" href="Effect.Monad.html#2545" class="Record">RawMonadPlus</a><a id="632" class="Symbol">;</a> <a id="634" href="Effect.Monad.html#3219" class="Function">RawMonadT</a><a id="643" class="Symbol">)</a>
<a id="645" class="Keyword">open</a> <a id="650" class="Keyword">import</a> <a id="657" href="Function.Base.html" class="Module">Function.Base</a> <a id="671" class="Keyword">using</a> <a id="677" class="Symbol">(</a><a id="678" href="Function.Base.html#3626" class="Function Operator">_∘′_</a><a id="682" class="Symbol">;</a> <a id="684" href="Function.Base.html#725" class="Function">const</a><a id="689" class="Symbol">;</a> <a id="691" href="Function.Base.html#1974" class="Function Operator">_$_</a><a id="694" class="Symbol">)</a>
<a id="696" class="Keyword">open</a> <a id="701" class="Keyword">import</a> <a id="708" href="Level.html" class="Module">Level</a> <a id="714" class="Keyword">using</a> <a id="720" class="Symbol">(</a><a id="721" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="726" class="Symbol">;</a> <a id="728" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="731" class="Symbol">)</a>

<a id="734" class="Keyword">private</a>
  <a id="744" class="Keyword">variable</a>
    <a id="757" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a> <a id="759" href="Effect.Monad.Reader.Transformer.html#759" class="Generalizable">g</a> <a id="761" href="Effect.Monad.Reader.Transformer.html#761" class="Generalizable">g₁</a> <a id="764" href="Effect.Monad.Reader.Transformer.html#764" class="Generalizable">g₂</a> <a id="767" class="Symbol">:</a> <a id="769" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="779" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="781" href="Effect.Monad.Reader.Transformer.html#781" class="Generalizable">R₁</a> <a id="784" href="Effect.Monad.Reader.Transformer.html#784" class="Generalizable">R₂</a> <a id="787" class="Symbol">:</a> <a id="789" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="793" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a>
    <a id="799" href="Effect.Monad.Reader.Transformer.html#799" class="Generalizable">A</a> <a id="801" href="Effect.Monad.Reader.Transformer.html#801" class="Generalizable">B</a> <a id="803" class="Symbol">:</a> <a id="805" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="809" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a>
    <a id="815" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="817" class="Symbol">:</a> <a id="819" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="823" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a> <a id="825" class="Symbol">→</a> <a id="827" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="831" href="Effect.Monad.Reader.Transformer.html#759" class="Generalizable">g</a>

<a id="834" class="Comment">------------------------------------------------------------------------</a>
<a id="907" class="Comment">-- Re-export the basic type definitions</a>

<a id="948" class="Keyword">open</a> <a id="953" class="Keyword">import</a> <a id="960" href="Effect.Monad.Reader.Transformer.Base.html" class="Module">Effect.Monad.Reader.Transformer.Base</a> <a id="997" class="Keyword">public</a>
  <a id="1006" class="Keyword">using</a> <a id="1012" class="Symbol">(</a> <a id="1014" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a>
        <a id="1037" class="Symbol">;</a> <a id="1039" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a>
        <a id="1055" class="Symbol">;</a> <a id="1057" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a>
        <a id="1075" class="Symbol">;</a> <a id="1077" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">runReaderT</a>
        <a id="1096" class="Symbol">)</a>

<a id="1099" class="Comment">------------------------------------------------------------------------</a>
<a id="1172" class="Comment">-- Structure</a>

<a id="functor"></a><a id="1186" href="Effect.Monad.Reader.Transformer.html#1186" class="Function">functor</a> <a id="1194" class="Symbol">:</a> <a id="1196" href="Effect.Functor.html#579" class="Record">RawFunctor</a> <a id="1207" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="1209" class="Symbol">→</a> <a id="1211" href="Effect.Functor.html#579" class="Record">RawFunctor</a> <a id="1222" class="Symbol">(</a><a id="1223" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="1231" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="1233" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="1234" class="Symbol">)</a>
<a id="1236" href="Effect.Monad.Reader.Transformer.html#1186" class="Function">functor</a> <a id="1244" href="Effect.Monad.Reader.Transformer.html#1244" class="Bound">M</a> <a id="1246" class="Symbol">=</a> <a id="1248" class="Keyword">record</a>
  <a id="1257" class="Symbol">{</a> <a id="1259" href="Effect.Functor.html#687" class="Field Operator">_&lt;$&gt;_</a> <a id="1265" class="Symbol">=</a> <a id="1267" class="Symbol">λ</a> <a id="1269" href="Effect.Monad.Reader.Transformer.html#1269" class="Bound">f</a> <a id="1271" href="Effect.Monad.Reader.Transformer.html#1271" class="Bound">ma</a> <a id="1274" class="Symbol">→</a> <a id="1276" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="1286" class="Symbol">(λ</a> <a id="1289" href="Effect.Monad.Reader.Transformer.html#1289" class="Bound">r</a> <a id="1291" class="Symbol">→</a> <a id="1293" href="Effect.Monad.Reader.Transformer.html#1269" class="Bound">f</a> <a id="1295" href="Effect.Functor.html#687" class="Field Operator">&lt;$&gt;</a> <a id="1299" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="1318" href="Effect.Monad.Reader.Transformer.html#1271" class="Bound">ma</a> <a id="1321" href="Effect.Monad.Reader.Transformer.html#1289" class="Bound">r</a><a id="1322" class="Symbol">)</a>
  <a id="1326" class="Symbol">}</a> <a id="1328" class="Keyword">where</a> <a id="1334" class="Keyword">open</a> <a id="1339" href="Effect.Functor.html#579" class="Module">RawFunctor</a> <a id="1350" href="Effect.Monad.Reader.Transformer.html#1244" class="Bound">M</a>

<a id="applicative"></a><a id="1353" href="Effect.Monad.Reader.Transformer.html#1353" class="Function">applicative</a> <a id="1365" class="Symbol">:</a> <a id="1367" href="Effect.Applicative.html#979" class="Record">RawApplicative</a> <a id="1382" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="1384" class="Symbol">→</a> <a id="1386" href="Effect.Applicative.html#979" class="Record">RawApplicative</a> <a id="1401" class="Symbol">(</a><a id="1402" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="1410" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="1412" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="1413" class="Symbol">)</a>
<a id="1415" href="Effect.Monad.Reader.Transformer.html#1353" class="Function">applicative</a> <a id="1427" href="Effect.Monad.Reader.Transformer.html#1427" class="Bound">M</a> <a id="1429" class="Symbol">=</a> <a id="1431" class="Keyword">record</a>
  <a id="1440" class="Symbol">{</a> <a id="1442" href="Effect.Applicative.html#1117" class="Field">rawFunctor</a> <a id="1453" class="Symbol">=</a> <a id="1455" href="Effect.Monad.Reader.Transformer.html#1186" class="Function">functor</a> <a id="1463" href="Effect.Applicative.html#1117" class="Field">rawFunctor</a>
  <a id="1476" class="Symbol">;</a> <a id="1478" href="Effect.Applicative.html#1147" class="Field">pure</a> <a id="1483" class="Symbol">=</a> <a id="1485" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="1495" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1498" href="Function.Base.html#725" class="Function">const</a> <a id="1504" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="1507" href="Effect.Applicative.html#1147" class="Field">pure</a>
  <a id="1514" class="Symbol">;</a> <a id="1516" href="Effect.Applicative.html#1166" class="Field Operator">_&lt;*&gt;_</a> <a id="1522" class="Symbol">=</a> <a id="1524" class="Symbol">λ</a> <a id="1526" href="Effect.Monad.Reader.Transformer.html#1526" class="Bound">mf</a> <a id="1529" href="Effect.Monad.Reader.Transformer.html#1529" class="Bound">mx</a> <a id="1532" class="Symbol">→</a> <a id="1534" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="1544" class="Symbol">(λ</a> <a id="1547" href="Effect.Monad.Reader.Transformer.html#1547" class="Bound">r</a> <a id="1549" class="Symbol">→</a> <a id="1551" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="1570" href="Effect.Monad.Reader.Transformer.html#1526" class="Bound">mf</a> <a id="1573" href="Effect.Monad.Reader.Transformer.html#1547" class="Bound">r</a> <a id="1575" href="Effect.Applicative.html#1166" class="Field Operator">&lt;*&gt;</a> <a id="1579" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="1598" href="Effect.Monad.Reader.Transformer.html#1529" class="Bound">mx</a> <a id="1601" href="Effect.Monad.Reader.Transformer.html#1547" class="Bound">r</a><a id="1602" class="Symbol">)</a>
  <a id="1606" class="Symbol">}</a> <a id="1608" class="Keyword">where</a> <a id="1614" class="Keyword">open</a> <a id="1619" href="Effect.Applicative.html#979" class="Module">RawApplicative</a> <a id="1634" href="Effect.Monad.Reader.Transformer.html#1427" class="Bound">M</a>

<a id="empty"></a><a id="1637" href="Effect.Monad.Reader.Transformer.html#1637" class="Function">empty</a> <a id="1643" class="Symbol">:</a> <a id="1645" href="Effect.Empty.html#384" class="Record">RawEmpty</a> <a id="1654" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="1656" class="Symbol">→</a> <a id="1658" href="Effect.Empty.html#384" class="Record">RawEmpty</a> <a id="1667" class="Symbol">(</a><a id="1668" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="1676" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="1678" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="1679" class="Symbol">)</a>
<a id="1681" href="Effect.Monad.Reader.Transformer.html#1637" class="Function">empty</a> <a id="1687" href="Effect.Monad.Reader.Transformer.html#1687" class="Bound">M</a> <a id="1689" class="Symbol">=</a> <a id="1691" class="Keyword">record</a>
  <a id="1700" class="Symbol">{</a> <a id="1702" href="Effect.Empty.html#451" class="Field">empty</a> <a id="1708" class="Symbol">=</a> <a id="1710" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="1720" class="Symbol">(</a><a id="1721" href="Function.Base.html#725" class="Function">const</a> <a id="1727" class="Symbol">(</a><a id="1728" href="Effect.Empty.html#451" class="Field">RawEmpty.empty</a> <a id="1743" href="Effect.Monad.Reader.Transformer.html#1687" class="Bound">M</a><a id="1744" class="Symbol">))</a>
  <a id="1749" class="Symbol">}</a>

<a id="choice"></a><a id="1752" href="Effect.Monad.Reader.Transformer.html#1752" class="Function">choice</a> <a id="1759" class="Symbol">:</a> <a id="1761" href="Effect.Choice.html#414" class="Record">RawChoice</a> <a id="1771" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="1773" class="Symbol">→</a> <a id="1775" href="Effect.Choice.html#414" class="Record">RawChoice</a> <a id="1785" class="Symbol">(</a><a id="1786" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="1794" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="1796" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="1797" class="Symbol">)</a>
<a id="1799" href="Effect.Monad.Reader.Transformer.html#1752" class="Function">choice</a> <a id="1806" href="Effect.Monad.Reader.Transformer.html#1806" class="Bound">M</a> <a id="1808" class="Symbol">=</a> <a id="1810" class="Keyword">record</a>
  <a id="1819" class="Symbol">{</a> <a id="1821" href="Effect.Choice.html#503" class="Field Operator">_&lt;|&gt;_</a> <a id="1827" class="Symbol">=</a> <a id="1829" class="Symbol">λ</a> <a id="1831" href="Effect.Monad.Reader.Transformer.html#1831" class="Bound">ma₁</a> <a id="1835" href="Effect.Monad.Reader.Transformer.html#1835" class="Bound">ma₂</a> <a id="1839" class="Symbol">→</a> <a id="1841" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="1851" href="Function.Base.html#1974" class="Function Operator">$</a> <a id="1853" class="Symbol">λ</a> <a id="1855" href="Effect.Monad.Reader.Transformer.html#1855" class="Bound">r</a> <a id="1857" class="Symbol">→</a>
            <a id="1871" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="1890" href="Effect.Monad.Reader.Transformer.html#1831" class="Bound">ma₁</a> <a id="1894" href="Effect.Monad.Reader.Transformer.html#1855" class="Bound">r</a>
            <a id="1908" href="Effect.Choice.html#503" class="Field Operator">&lt;|&gt;</a> <a id="1912" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="1931" href="Effect.Monad.Reader.Transformer.html#1835" class="Bound">ma₂</a> <a id="1935" href="Effect.Monad.Reader.Transformer.html#1855" class="Bound">r</a>
  <a id="1939" class="Symbol">}</a> <a id="1941" class="Keyword">where</a> <a id="1947" class="Keyword">open</a> <a id="1952" href="Effect.Choice.html#414" class="Module">RawChoice</a> <a id="1962" href="Effect.Monad.Reader.Transformer.html#1806" class="Bound">M</a>

<a id="applicativeZero"></a><a id="1965" href="Effect.Monad.Reader.Transformer.html#1965" class="Function">applicativeZero</a> <a id="1981" class="Symbol">:</a> <a id="1983" href="Effect.Applicative.html#2271" class="Record">RawApplicativeZero</a> <a id="2002" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="2004" class="Symbol">→</a> <a id="2006" href="Effect.Applicative.html#2271" class="Record">RawApplicativeZero</a> <a id="2025" class="Symbol">(</a><a id="2026" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="2034" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="2036" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="2037" class="Symbol">)</a>
<a id="2039" href="Effect.Monad.Reader.Transformer.html#1965" class="Function">applicativeZero</a> <a id="2055" href="Effect.Monad.Reader.Transformer.html#2055" class="Bound">M</a> <a id="2057" class="Symbol">=</a> <a id="2059" class="Keyword">record</a>
  <a id="2068" class="Symbol">{</a> <a id="2070" href="Effect.Applicative.html#2346" class="Field">rawApplicative</a> <a id="2085" class="Symbol">=</a> <a id="2087" href="Effect.Monad.Reader.Transformer.html#1353" class="Function">applicative</a> <a id="2099" href="Effect.Applicative.html#2346" class="Field">rawApplicative</a>
  <a id="2116" class="Symbol">;</a> <a id="2118" href="Effect.Applicative.html#2384" class="Field">rawEmpty</a> <a id="2127" class="Symbol">=</a> <a id="2129" href="Effect.Monad.Reader.Transformer.html#1637" class="Function">empty</a> <a id="2135" href="Effect.Applicative.html#2384" class="Field">rawEmpty</a>
  <a id="2146" class="Symbol">}</a> <a id="2148" class="Keyword">where</a> <a id="2154" class="Keyword">open</a> <a id="2159" href="Effect.Applicative.html#2271" class="Module">RawApplicativeZero</a> <a id="2178" href="Effect.Monad.Reader.Transformer.html#2055" class="Bound">M</a> <a id="2180" class="Keyword">using</a> <a id="2186" class="Symbol">(</a><a id="2187" href="Effect.Applicative.html#2346" class="Field">rawApplicative</a><a id="2201" class="Symbol">;</a> <a id="2203" href="Effect.Applicative.html#2384" class="Field">rawEmpty</a><a id="2211" class="Symbol">)</a>

<a id="alternative"></a><a id="2214" href="Effect.Monad.Reader.Transformer.html#2214" class="Function">alternative</a> <a id="2226" class="Symbol">:</a> <a id="2228" href="Effect.Applicative.html#2675" class="Record">RawAlternative</a> <a id="2243" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="2245" class="Symbol">→</a> <a id="2247" href="Effect.Applicative.html#2675" class="Record">RawAlternative</a> <a id="2262" class="Symbol">(</a><a id="2263" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="2271" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="2273" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="2274" class="Symbol">)</a>
<a id="2276" href="Effect.Monad.Reader.Transformer.html#2214" class="Function">alternative</a> <a id="2288" href="Effect.Monad.Reader.Transformer.html#2288" class="Bound">M</a> <a id="2290" class="Symbol">=</a> <a id="2292" class="Keyword">record</a>
  <a id="2301" class="Symbol">{</a> <a id="2303" href="Effect.Applicative.html#2746" class="Field">rawApplicativeZero</a> <a id="2322" class="Symbol">=</a> <a id="2324" href="Effect.Monad.Reader.Transformer.html#1965" class="Function">applicativeZero</a> <a id="2340" href="Effect.Applicative.html#2746" class="Field">rawApplicativeZero</a>
  <a id="2361" class="Symbol">;</a> <a id="2363" href="Effect.Applicative.html#2792" class="Field">rawChoice</a> <a id="2373" class="Symbol">=</a> <a id="2375" href="Effect.Monad.Reader.Transformer.html#1752" class="Function">choice</a> <a id="2382" href="Effect.Applicative.html#2792" class="Field">rawChoice</a>
  <a id="2394" class="Symbol">}</a> <a id="2396" class="Keyword">where</a> <a id="2402" class="Keyword">open</a> <a id="2407" href="Effect.Applicative.html#2675" class="Module">RawAlternative</a> <a id="2422" href="Effect.Monad.Reader.Transformer.html#2288" class="Bound">M</a>

<a id="monad"></a><a id="2425" href="Effect.Monad.Reader.Transformer.html#2425" class="Function">monad</a> <a id="2431" class="Symbol">:</a> <a id="2433" href="Effect.Monad.html#779" class="Record">RawMonad</a> <a id="2442" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="2444" class="Symbol">→</a> <a id="2446" href="Effect.Monad.html#779" class="Record">RawMonad</a> <a id="2455" class="Symbol">(</a><a id="2456" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="2464" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="2466" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="2467" class="Symbol">)</a>
<a id="2469" href="Effect.Monad.Reader.Transformer.html#2425" class="Function">monad</a> <a id="2475" href="Effect.Monad.Reader.Transformer.html#2475" class="Bound">M</a> <a id="2477" class="Symbol">=</a> <a id="2479" class="Keyword">record</a>
  <a id="2488" class="Symbol">{</a> <a id="2490" href="Effect.Monad.html#895" class="Field">rawApplicative</a> <a id="2505" class="Symbol">=</a> <a id="2507" href="Effect.Monad.Reader.Transformer.html#1353" class="Function">applicative</a> <a id="2519" href="Effect.Monad.html#895" class="Field">rawApplicative</a>
  <a id="2536" class="Symbol">;</a> <a id="2538" href="Effect.Monad.html#933" class="Field Operator">_&gt;&gt;=_</a> <a id="2544" class="Symbol">=</a> <a id="2546" class="Symbol">λ</a> <a id="2548" href="Effect.Monad.Reader.Transformer.html#2548" class="Bound">ma</a> <a id="2551" href="Effect.Monad.Reader.Transformer.html#2551" class="Bound">f</a> <a id="2553" class="Symbol">→</a> <a id="2555" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="2565" href="Function.Base.html#1974" class="Function Operator">$</a> <a id="2567" class="Symbol">λ</a> <a id="2569" href="Effect.Monad.Reader.Transformer.html#2569" class="Bound">r</a> <a id="2571" class="Symbol">→</a>
              <a id="2587" class="Keyword">do</a> <a id="2590" href="Effect.Monad.Reader.Transformer.html#2590" class="Bound">a</a> <a id="2592" href="Effect.Monad.html#933" class="Field Operator">←</a> <a id="2594" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="2613" href="Effect.Monad.Reader.Transformer.html#2548" class="Bound">ma</a> <a id="2616" href="Effect.Monad.Reader.Transformer.html#2569" class="Bound">r</a>
                 <a id="2635" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="2654" class="Symbol">(</a><a id="2655" href="Effect.Monad.Reader.Transformer.html#2551" class="Bound">f</a> <a id="2657" href="Effect.Monad.Reader.Transformer.html#2590" class="Bound">a</a><a id="2658" class="Symbol">)</a> <a id="2660" href="Effect.Monad.Reader.Transformer.html#2569" class="Bound">r</a>
  <a id="2664" class="Symbol">}</a> <a id="2666" class="Keyword">where</a> <a id="2672" class="Keyword">open</a> <a id="2677" href="Effect.Monad.html#779" class="Module">RawMonad</a> <a id="2686" href="Effect.Monad.Reader.Transformer.html#2475" class="Bound">M</a>

<a id="monadZero"></a><a id="2689" href="Effect.Monad.Reader.Transformer.html#2689" class="Function">monadZero</a> <a id="2699" class="Symbol">:</a> <a id="2701" href="Effect.Monad.html#2107" class="Record">RawMonadZero</a> <a id="2714" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="2716" class="Symbol">→</a> <a id="2718" href="Effect.Monad.html#2107" class="Record">RawMonadZero</a> <a id="2731" class="Symbol">(</a><a id="2732" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="2740" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="2742" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="2743" class="Symbol">)</a>
<a id="2745" href="Effect.Monad.Reader.Transformer.html#2689" class="Function">monadZero</a> <a id="2755" href="Effect.Monad.Reader.Transformer.html#2755" class="Bound">M</a> <a id="2757" class="Symbol">=</a> <a id="2759" class="Keyword">record</a>
  <a id="2768" class="Symbol">{</a> <a id="2770" href="Effect.Monad.html#2176" class="Field">rawMonad</a> <a id="2779" class="Symbol">=</a> <a id="2781" href="Effect.Monad.Reader.Transformer.html#2425" class="Function">monad</a> <a id="2787" class="Symbol">(</a><a id="2788" href="Effect.Monad.html#2176" class="Field">RawMonadZero.rawMonad</a> <a id="2810" href="Effect.Monad.Reader.Transformer.html#2755" class="Bound">M</a><a id="2811" class="Symbol">)</a>
  <a id="2815" class="Symbol">;</a> <a id="2817" href="Effect.Monad.html#2202" class="Field">rawEmpty</a> <a id="2826" class="Symbol">=</a> <a id="2828" href="Effect.Monad.Reader.Transformer.html#1637" class="Function">empty</a> <a id="2834" class="Symbol">(</a><a id="2835" href="Effect.Monad.html#2202" class="Field">RawMonadZero.rawEmpty</a> <a id="2857" href="Effect.Monad.Reader.Transformer.html#2755" class="Bound">M</a><a id="2858" class="Symbol">)</a>
  <a id="2862" class="Symbol">}</a>

<a id="monadPlus"></a><a id="2865" href="Effect.Monad.Reader.Transformer.html#2865" class="Function">monadPlus</a> <a id="2875" class="Symbol">:</a> <a id="2877" href="Effect.Monad.html#2545" class="Record">RawMonadPlus</a> <a id="2890" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="2892" class="Symbol">→</a> <a id="2894" href="Effect.Monad.html#2545" class="Record">RawMonadPlus</a> <a id="2907" class="Symbol">(</a><a id="2908" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="2916" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="2918" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="2919" class="Symbol">)</a>
<a id="2921" href="Effect.Monad.Reader.Transformer.html#2865" class="Function">monadPlus</a> <a id="2931" href="Effect.Monad.Reader.Transformer.html#2931" class="Bound">M</a> <a id="2933" class="Symbol">=</a> <a id="2935" class="Keyword">record</a>
  <a id="2944" class="Symbol">{</a> <a id="2946" href="Effect.Monad.html#2614" class="Field">rawMonadZero</a> <a id="2959" class="Symbol">=</a> <a id="2961" href="Effect.Monad.Reader.Transformer.html#2689" class="Function">monadZero</a> <a id="2971" href="Effect.Monad.html#2614" class="Field">rawMonadZero</a>
  <a id="2986" class="Symbol">;</a> <a id="2988" href="Effect.Monad.html#2648" class="Field">rawChoice</a> <a id="2998" class="Symbol">=</a> <a id="3000" href="Effect.Monad.Reader.Transformer.html#1752" class="Function">choice</a> <a id="3007" href="Effect.Monad.html#2648" class="Field">rawChoice</a>
  <a id="3019" class="Symbol">}</a> <a id="3021" class="Keyword">where</a> <a id="3027" class="Keyword">open</a> <a id="3032" href="Effect.Monad.html#2545" class="Module">RawMonadPlus</a> <a id="3045" href="Effect.Monad.Reader.Transformer.html#2931" class="Bound">M</a>

<a id="3048" class="Comment">------------------------------------------------------------------------</a>
<a id="3121" class="Comment">-- Monad reader transformer specifics</a>

<a id="monadT"></a><a id="3160" href="Effect.Monad.Reader.Transformer.html#3160" class="Function">monadT</a> <a id="3167" class="Symbol">:</a> <a id="3169" href="Effect.Monad.html#3219" class="Function">RawMonadT</a> <a id="3179" class="Symbol">{</a><a id="3180" class="Argument">g₁</a> <a id="3183" class="Symbol">=</a> <a id="3185" href="Effect.Monad.Reader.Transformer.html#761" class="Generalizable">g₁</a><a id="3187" class="Symbol">}</a> <a id="3189" class="Symbol">{</a><a id="3190" class="Argument">g₂</a> <a id="3193" class="Symbol">=</a> <a id="3195" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a> <a id="3197" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="3199" href="Effect.Monad.Reader.Transformer.html#761" class="Generalizable">g₁</a><a id="3201" class="Symbol">}</a> <a id="3203" class="Symbol">(</a><a id="3204" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="3212" class="Symbol">{</a><a id="3213" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a><a id="3214" class="Symbol">}</a> <a id="3216" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a><a id="3217" class="Symbol">)</a>
<a id="3219" href="Effect.Monad.Reader.Transformer.html#3160" class="Function">monadT</a> <a id="3226" href="Effect.Monad.Reader.Transformer.html#3226" class="Bound">M</a> <a id="3228" class="Symbol">=</a> <a id="3230" class="Keyword">record</a>
  <a id="3239" class="Symbol">{</a> <a id="3241" href="Effect.Monad.html#3140" class="Field">lift</a> <a id="3246" class="Symbol">=</a> <a id="3248" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="3258" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="3261" href="Function.Base.html#725" class="Function">const</a>
  <a id="3269" class="Symbol">;</a> <a id="3271" href="Effect.Monad.html#3162" class="Field">rawMonad</a> <a id="3280" class="Symbol">=</a> <a id="3282" href="Effect.Monad.Reader.Transformer.html#2425" class="Function">monad</a> <a id="3288" href="Effect.Monad.Reader.Transformer.html#3226" class="Bound">M</a>
  <a id="3292" class="Symbol">}</a>

<a id="monadReader"></a><a id="3295" href="Effect.Monad.Reader.Transformer.html#3295" class="Function">monadReader</a> <a id="3307" class="Symbol">:</a> <a id="3309" href="Effect.Monad.html#779" class="Record">RawMonad</a> <a id="3318" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="3320" class="Symbol">→</a> <a id="3322" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="3337" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="3339" class="Symbol">(</a><a id="3340" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="3348" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="3350" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="3351" class="Symbol">)</a>
<a id="3353" href="Effect.Monad.Reader.Transformer.html#3295" class="Function">monadReader</a> <a id="3365" href="Effect.Monad.Reader.Transformer.html#3365" class="Bound">M</a> <a id="3367" class="Symbol">=</a> <a id="3369" class="Keyword">record</a>
  <a id="3378" class="Symbol">{</a> <a id="3380" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="3387" class="Symbol">=</a> <a id="3389" class="Symbol">λ</a> <a id="3391" href="Effect.Monad.Reader.Transformer.html#3391" class="Bound">f</a> <a id="3393" class="Symbol">→</a> <a id="3395" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="3405" class="Symbol">(</a><a id="3406" href="Effect.Applicative.html#1147" class="Function">pure</a> <a id="3411" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="3414" href="Effect.Monad.Reader.Transformer.html#3391" class="Bound">f</a><a id="3415" class="Symbol">)</a>
  <a id="3419" class="Symbol">;</a> <a id="3421" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="3427" class="Symbol">=</a> <a id="3429" class="Symbol">λ</a> <a id="3431" href="Effect.Monad.Reader.Transformer.html#3431" class="Bound">f</a> <a id="3433" href="Effect.Monad.Reader.Transformer.html#3433" class="Bound">ma</a> <a id="3436" class="Symbol">→</a> <a id="3438" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="3448" class="Symbol">(</a><a id="3449" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">ReaderT.runReaderT</a> <a id="3468" href="Effect.Monad.Reader.Transformer.html#3433" class="Bound">ma</a> <a id="3471" href="Function.Base.html#3626" class="Function Operator">∘′</a> <a id="3474" href="Effect.Monad.Reader.Transformer.html#3431" class="Bound">f</a><a id="3475" class="Symbol">)</a>
  <a id="3479" class="Symbol">}</a> <a id="3481" class="Keyword">where</a> <a id="3487" class="Keyword">open</a> <a id="3492" href="Effect.Monad.html#779" class="Module">RawMonad</a> <a id="3501" href="Effect.Monad.Reader.Transformer.html#3365" class="Bound">M</a>

<a id="liftReaderT"></a><a id="3504" href="Effect.Monad.Reader.Transformer.html#3504" class="Function">liftReaderT</a> <a id="3516" class="Symbol">:</a> <a id="3518" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="3533" href="Effect.Monad.Reader.Transformer.html#781" class="Generalizable">R₁</a> <a id="3536" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="3538" class="Symbol">→</a>
              <a id="3554" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="3569" href="Effect.Monad.Reader.Transformer.html#781" class="Generalizable">R₁</a> <a id="3572" class="Symbol">(</a><a id="3573" href="Effect.Monad.Reader.Transformer.Base.html#871" class="Record">ReaderT</a> <a id="3581" href="Effect.Monad.Reader.Transformer.html#784" class="Generalizable">R₂</a> <a id="3584" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="3585" class="Symbol">)</a>
<a id="3587" href="Effect.Monad.Reader.Transformer.html#3504" class="Function">liftReaderT</a> <a id="3599" href="Effect.Monad.Reader.Transformer.html#3599" class="Bound">MRead</a> <a id="3605" class="Symbol">=</a> <a id="3607" class="Keyword">record</a>
  <a id="3616" class="Symbol">{</a> <a id="3618" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="3625" class="Symbol">=</a> <a id="3627" class="Symbol">λ</a> <a id="3629" href="Effect.Monad.Reader.Transformer.html#3629" class="Bound">k</a> <a id="3631" class="Symbol">→</a> <a id="3633" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="3643" class="Symbol">(</a><a id="3644" href="Function.Base.html#725" class="Function">const</a> <a id="3650" class="Symbol">(</a><a id="3651" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="3658" href="Effect.Monad.Reader.Transformer.html#3629" class="Bound">k</a><a id="3659" class="Symbol">))</a>
  <a id="3664" class="Symbol">;</a> <a id="3666" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="3672" class="Symbol">=</a> <a id="3674" class="Symbol">λ</a> <a id="3676" href="Effect.Monad.Reader.Transformer.html#3676" class="Bound">f</a> <a id="3678" href="Effect.Monad.Reader.Transformer.html#3678" class="Bound">mx</a> <a id="3681" class="Symbol">→</a> <a id="3683" href="Effect.Monad.Reader.Transformer.Base.html#966" class="InductiveConstructor">mkReaderT</a> <a id="3693" class="Symbol">(λ</a> <a id="3696" href="Effect.Monad.Reader.Transformer.html#3696" class="Bound">r₂</a> <a id="3699" class="Symbol">→</a> <a id="3701" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="3707" href="Effect.Monad.Reader.Transformer.html#3676" class="Bound">f</a> <a id="3709" class="Symbol">(</a><a id="3710" href="Effect.Monad.Reader.Transformer.Base.html#984" class="Field">runReaderT</a> <a id="3721" href="Effect.Monad.Reader.Transformer.html#3678" class="Bound">mx</a> <a id="3724" href="Effect.Monad.Reader.Transformer.html#3696" class="Bound">r₂</a><a id="3726" class="Symbol">))</a>
  <a id="3731" class="Symbol">}</a> <a id="3733" class="Keyword">where</a> <a id="3739" class="Keyword">open</a> <a id="3744" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Module">RawMonadReader</a> <a id="3759" href="Effect.Monad.Reader.Transformer.html#3599" class="Bound">MRead</a>

<a id="3766" class="Keyword">open</a> <a id="3771" class="Keyword">import</a> <a id="3778" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="3796" class="Keyword">using</a> <a id="3802" class="Symbol">(</a><a id="3803" href="Data.Product.Base.html#1618" class="Function Operator">_×_</a><a id="3806" class="Symbol">;</a> <a id="3808" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="3811" class="Symbol">)</a>
<a id="3813" class="Keyword">open</a> <a id="3818" class="Keyword">import</a> <a id="3825" href="Effect.Monad.Writer.Transformer.Base.html" class="Module">Effect.Monad.Writer.Transformer.Base</a>

<a id="liftWriterT"></a><a id="3863" href="Effect.Monad.Reader.Transformer.html#3863" class="Function">liftWriterT</a> <a id="3875" class="Symbol">:</a> <a id="3877" class="Symbol">(</a><a id="3878" href="Effect.Monad.Reader.Transformer.html#3878" class="Bound">MR</a> <a id="3881" class="Symbol">:</a> <a id="3883" href="Algebra.Bundles.Raw.html#1534" class="Record">RawMonoid</a> <a id="3893" href="Effect.Monad.Reader.Transformer.html#757" class="Generalizable">r</a> <a id="3895" href="Effect.Monad.Reader.Transformer.html#759" class="Generalizable">g</a><a id="3896" class="Symbol">)</a> <a id="3898" class="Symbol">→</a>
              <a id="3914" href="Effect.Functor.html#579" class="Record">RawFunctor</a> <a id="3925" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="3927" class="Symbol">→</a>
              <a id="3943" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="3958" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="3960" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="3962" class="Symbol">→</a>
              <a id="3978" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="3993" href="Effect.Monad.Reader.Transformer.html#779" class="Generalizable">R</a> <a id="3995" class="Symbol">(</a><a id="3996" href="Effect.Monad.Writer.Transformer.Base.html#1230" class="Record">WriterT</a> <a id="4004" href="Effect.Monad.Reader.Transformer.html#3878" class="Bound">MR</a> <a id="4007" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="4008" class="Symbol">)</a>
<a id="4010" href="Effect.Monad.Reader.Transformer.html#3863" class="Function">liftWriterT</a> <a id="4022" href="Effect.Monad.Reader.Transformer.html#4022" class="Bound">MR</a> <a id="4025" href="Effect.Monad.Reader.Transformer.html#4025" class="Bound">M</a> <a id="4027" href="Effect.Monad.Reader.Transformer.html#4027" class="Bound">MRead</a> <a id="4033" class="Symbol">=</a> <a id="4035" class="Keyword">record</a>
  <a id="4044" class="Symbol">{</a> <a id="4046" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="4053" class="Symbol">=</a> <a id="4055" class="Symbol">λ</a> <a id="4057" href="Effect.Monad.Reader.Transformer.html#4057" class="Bound">k</a> <a id="4059" class="Symbol">→</a> <a id="4061" href="Effect.Monad.Writer.Transformer.Base.html#1352" class="InductiveConstructor">mkWriterT</a> <a id="4071" class="Symbol">λ</a> <a id="4073" href="Effect.Monad.Reader.Transformer.html#4073" class="Bound">w</a> <a id="4075" class="Symbol">→</a> <a id="4077" class="Symbol">((</a><a id="4079" href="Effect.Monad.Reader.Transformer.html#4073" class="Bound">w</a> <a id="4081" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="4083" class="Symbol">)</a> <a id="4085" href="Effect.Functor.html#687" class="Function Operator">&lt;$&gt;</a> <a id="4089" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="4096" href="Effect.Monad.Reader.Transformer.html#4057" class="Bound">k</a><a id="4097" class="Symbol">)</a>
  <a id="4101" class="Symbol">;</a> <a id="4103" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="4109" class="Symbol">=</a> <a id="4111" class="Symbol">λ</a> <a id="4113" href="Effect.Monad.Reader.Transformer.html#4113" class="Bound">f</a> <a id="4115" href="Effect.Monad.Reader.Transformer.html#4115" class="Bound">mx</a> <a id="4118" class="Symbol">→</a> <a id="4120" href="Effect.Monad.Writer.Transformer.Base.html#1352" class="InductiveConstructor">mkWriterT</a> <a id="4130" class="Symbol">λ</a> <a id="4132" href="Effect.Monad.Reader.Transformer.html#4132" class="Bound">w</a> <a id="4134" class="Symbol">→</a> <a id="4136" class="Symbol">(</a><a id="4137" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="4143" href="Effect.Monad.Reader.Transformer.html#4113" class="Bound">f</a> <a id="4145" class="Symbol">(</a><a id="4146" href="Effect.Monad.Writer.Transformer.Base.html#1413" class="Field">runWriterT</a> <a id="4157" href="Effect.Monad.Reader.Transformer.html#4115" class="Bound">mx</a> <a id="4160" href="Effect.Monad.Reader.Transformer.html#4132" class="Bound">w</a><a id="4161" class="Symbol">))</a>
  <a id="4166" class="Symbol">}</a> <a id="4168" class="Keyword">where</a> <a id="4174" class="Keyword">open</a> <a id="4179" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Module">RawMonadReader</a> <a id="4194" href="Effect.Monad.Reader.Transformer.html#4027" class="Bound">MRead</a>
          <a id="4210" class="Keyword">open</a> <a id="4215" href="Effect.Functor.html#579" class="Module">RawFunctor</a> <a id="4226" href="Effect.Monad.Reader.Transformer.html#4025" class="Bound">M</a>

<a id="4229" class="Keyword">open</a> <a id="4234" class="Keyword">import</a> <a id="4241" href="Effect.Monad.State.Transformer.Base.html" class="Module">Effect.Monad.State.Transformer.Base</a>

<a id="liftStateT"></a><a id="4278" href="Effect.Monad.Reader.Transformer.html#4278" class="Function">liftStateT</a> <a id="4289" class="Symbol">:</a> <a id="4291" href="Effect.Functor.html#579" class="Record">RawFunctor</a> <a id="4302" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="4304" class="Symbol">→</a>
             <a id="4319" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="4334" href="Effect.Monad.Reader.Transformer.html#781" class="Generalizable">R₁</a> <a id="4337" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a> <a id="4339" class="Symbol">→</a>
             <a id="4354" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Record">RawMonadReader</a> <a id="4369" href="Effect.Monad.Reader.Transformer.html#781" class="Generalizable">R₁</a> <a id="4372" class="Symbol">(</a><a id="4373" href="Effect.Monad.State.Transformer.Base.html#1063" class="Record">StateT</a> <a id="4380" href="Effect.Monad.Reader.Transformer.html#784" class="Generalizable">R₂</a> <a id="4383" href="Effect.Monad.Reader.Transformer.html#815" class="Generalizable">M</a><a id="4384" class="Symbol">)</a>
<a id="4386" href="Effect.Monad.Reader.Transformer.html#4278" class="Function">liftStateT</a> <a id="4397" href="Effect.Monad.Reader.Transformer.html#4397" class="Bound">M</a> <a id="4399" href="Effect.Monad.Reader.Transformer.html#4399" class="Bound">MRead</a> <a id="4405" class="Symbol">=</a> <a id="4407" class="Keyword">record</a>
  <a id="4416" class="Symbol">{</a> <a id="4418" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="4425" class="Symbol">=</a> <a id="4427" class="Symbol">λ</a> <a id="4429" href="Effect.Monad.Reader.Transformer.html#4429" class="Bound">k</a> <a id="4431" class="Symbol">→</a> <a id="4433" href="Effect.Monad.State.Transformer.Base.html#1176" class="InductiveConstructor">mkStateT</a> <a id="4442" class="Symbol">(λ</a> <a id="4445" href="Effect.Monad.Reader.Transformer.html#4445" class="Bound">s</a> <a id="4447" class="Symbol">→</a> <a id="4449" class="Symbol">(</a><a id="4450" href="Effect.Monad.Reader.Transformer.html#4445" class="Bound">s</a> <a id="4452" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,_</a><a id="4454" class="Symbol">)</a> <a id="4456" href="Effect.Functor.html#687" class="Function Operator">&lt;$&gt;</a> <a id="4460" href="Effect.Monad.Reader.Transformer.Base.html#674" class="Field">reader</a> <a id="4467" href="Effect.Monad.Reader.Transformer.html#4429" class="Bound">k</a><a id="4468" class="Symbol">)</a>
  <a id="4472" class="Symbol">;</a> <a id="4474" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="4480" class="Symbol">=</a> <a id="4482" class="Symbol">λ</a> <a id="4484" href="Effect.Monad.Reader.Transformer.html#4484" class="Bound">f</a> <a id="4486" href="Effect.Monad.Reader.Transformer.html#4486" class="Bound">mx</a> <a id="4489" class="Symbol">→</a> <a id="4491" href="Effect.Monad.State.Transformer.Base.html#1176" class="InductiveConstructor">mkStateT</a> <a id="4500" class="Symbol">(λ</a> <a id="4503" href="Effect.Monad.Reader.Transformer.html#4503" class="Bound">s</a> <a id="4505" class="Symbol">→</a> <a id="4507" href="Effect.Monad.Reader.Transformer.Base.html#701" class="Field">local</a> <a id="4513" href="Effect.Monad.Reader.Transformer.html#4484" class="Bound">f</a> <a id="4515" class="Symbol">(</a><a id="4516" href="Effect.Monad.State.Transformer.Base.html#1193" class="Field">runStateT</a> <a id="4526" href="Effect.Monad.Reader.Transformer.html#4486" class="Bound">mx</a> <a id="4529" href="Effect.Monad.Reader.Transformer.html#4503" class="Bound">s</a><a id="4530" class="Symbol">))</a>
  <a id="4535" class="Symbol">}</a> <a id="4537" class="Keyword">where</a> <a id="4543" class="Keyword">open</a> <a id="4548" href="Effect.Monad.Reader.Transformer.Base.html#589" class="Module">RawMonadReader</a> <a id="4563" href="Effect.Monad.Reader.Transformer.html#4399" class="Bound">MRead</a>
          <a id="4579" class="Keyword">open</a> <a id="4584" href="Effect.Functor.html#579" class="Module">RawFunctor</a> <a id="4595" href="Effect.Monad.Reader.Transformer.html#4397" class="Bound">M</a>
</pre></body></html>