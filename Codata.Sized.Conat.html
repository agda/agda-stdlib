<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Sized.Conat</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The Conat type and some operations</a>
<a id="144" class="Comment">------------------------------------------------------------------------</a>

<a id="218" class="Symbol">{-#</a> <a id="222" class="Keyword">OPTIONS</a> <a id="230" class="Pragma">--cubical-compatible</a> <a id="251" class="Pragma">--sized-types</a> <a id="265" class="Symbol">#-}</a>

<a id="270" class="Keyword">module</a> <a id="277" href="Codata.Sized.Conat.html" class="Module">Codata.Sized.Conat</a> <a id="296" class="Keyword">where</a>

<a id="303" class="Keyword">open</a> <a id="308" class="Keyword">import</a> <a id="315" href="Size.html" class="Module">Size</a>
<a id="320" class="Keyword">open</a> <a id="325" class="Keyword">import</a> <a id="332" href="Codata.Sized.Thunk.html" class="Module">Codata.Sized.Thunk</a>

<a id="352" class="Keyword">open</a> <a id="357" class="Keyword">import</a> <a id="364" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="378" class="Keyword">using</a> <a id="384" class="Symbol">(</a><a id="385" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="387" class="Symbol">;</a> <a id="389" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a> <a id="394" class="Symbol">;</a> <a id="396" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a><a id="399" class="Symbol">)</a>
<a id="401" class="Keyword">open</a> <a id="406" class="Keyword">import</a> <a id="413" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>

<a id="431" class="Comment">------------------------------------------------------------------------</a>
<a id="504" class="Comment">-- Definition and first values</a>

<a id="536" class="Keyword">data</a> <a id="Conat"></a><a id="541" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="547" class="Symbol">(</a><a id="548" href="Codata.Sized.Conat.html#548" class="Bound">i</a> <a id="550" class="Symbol">:</a> <a id="552" href="Agda.Builtin.Size.html#213" class="Postulate">Size</a><a id="556" class="Symbol">)</a> <a id="558" class="Symbol">:</a> <a id="560" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="564" class="Keyword">where</a>
  <a id="Conat.zero"></a><a id="572" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a> <a id="577" class="Symbol">:</a> <a id="579" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="585" href="Codata.Sized.Conat.html#548" class="Bound">i</a>
  <a id="Conat.suc"></a><a id="589" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="593" class="Symbol">:</a> <a id="595" href="Codata.Sized.Thunk.html#482" class="Record">Thunk</a> <a id="601" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="607" href="Codata.Sized.Conat.html#548" class="Bound">i</a> <a id="609" class="Symbol">→</a> <a id="611" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="617" href="Codata.Sized.Conat.html#548" class="Bound">i</a>

<a id="infinity"></a><a id="620" href="Codata.Sized.Conat.html#620" class="Function">infinity</a> <a id="629" class="Symbol">:</a> <a id="631" class="Symbol">∀</a> <a id="633" class="Symbol">{</a><a id="634" href="Codata.Sized.Conat.html#634" class="Bound">i</a><a id="635" class="Symbol">}</a> <a id="637" class="Symbol">→</a> <a id="639" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="645" href="Codata.Sized.Conat.html#634" class="Bound">i</a>
<a id="647" href="Codata.Sized.Conat.html#620" class="Function">infinity</a> <a id="656" class="Symbol">=</a> <a id="658" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="662" class="Symbol">λ</a> <a id="664" class="Keyword">where</a> <a id="670" class="Symbol">.</a><a id="671" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="677" class="Symbol">→</a> <a id="679" href="Codata.Sized.Conat.html#620" class="Function">infinity</a>

<a id="fromℕ"></a><a id="689" href="Codata.Sized.Conat.html#689" class="Function">fromℕ</a> <a id="695" class="Symbol">:</a> <a id="697" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="699" class="Symbol">→</a> <a id="701" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="707" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a>
<a id="709" href="Codata.Sized.Conat.html#689" class="Function">fromℕ</a> <a id="715" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="723" class="Symbol">=</a> <a id="725" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
<a id="730" href="Codata.Sized.Conat.html#689" class="Function">fromℕ</a> <a id="736" class="Symbol">(</a><a id="737" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="741" href="Codata.Sized.Conat.html#741" class="Bound">n</a><a id="742" class="Symbol">)</a> <a id="744" class="Symbol">=</a> <a id="746" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="750" class="Symbol">λ</a> <a id="752" class="Keyword">where</a> <a id="758" class="Symbol">.</a><a id="759" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="765" class="Symbol">→</a> <a id="767" href="Codata.Sized.Conat.html#689" class="Function">fromℕ</a> <a id="773" href="Codata.Sized.Conat.html#741" class="Bound">n</a>

<a id="776" class="Comment">------------------------------------------------------------------------</a>
<a id="849" class="Comment">-- Arithmetic operations</a>

<a id="pred"></a><a id="875" href="Codata.Sized.Conat.html#875" class="Function">pred</a> <a id="880" class="Symbol">:</a> <a id="882" class="Symbol">∀</a> <a id="884" class="Symbol">{</a><a id="885" href="Codata.Sized.Conat.html#885" class="Bound">i</a><a id="886" class="Symbol">}</a> <a id="888" class="Symbol">{</a><a id="889" href="Codata.Sized.Conat.html#889" class="Bound">j</a> <a id="891" class="Symbol">:</a> <a id="893" href="Agda.Builtin.Size.html#247" class="Postulate Operator">Size&lt;</a> <a id="899" href="Codata.Sized.Conat.html#885" class="Bound">i</a><a id="900" class="Symbol">}</a> <a id="902" class="Symbol">→</a> <a id="904" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="910" href="Codata.Sized.Conat.html#885" class="Bound">i</a> <a id="912" class="Symbol">→</a> <a id="914" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="920" href="Codata.Sized.Conat.html#889" class="Bound">j</a>
<a id="922" href="Codata.Sized.Conat.html#875" class="Function">pred</a> <a id="927" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>    <a id="935" class="Symbol">=</a> <a id="937" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
<a id="942" href="Codata.Sized.Conat.html#875" class="Function">pred</a> <a id="947" class="Symbol">(</a><a id="948" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="952" href="Codata.Sized.Conat.html#952" class="Bound">n</a><a id="953" class="Symbol">)</a> <a id="955" class="Symbol">=</a> <a id="957" href="Codata.Sized.Conat.html#952" class="Bound">n</a> <a id="959" class="Symbol">.</a><a id="960" href="Codata.Sized.Thunk.html#556" class="Field">force</a>

<a id="967" class="Keyword">infixl</a> <a id="974" class="Number">6</a> <a id="976" href="Codata.Sized.Conat.html#1008" class="Function Operator">_∸_</a> <a id="980" href="Codata.Sized.Conat.html#1277" class="Function Operator">_+_</a> <a id="984" href="Codata.Sized.Conat.html#1074" class="Function Operator">_ℕ+_</a> <a id="989" href="Codata.Sized.Conat.html#1168" class="Function Operator">_+ℕ_</a>
<a id="994" class="Keyword">infixl</a> <a id="1001" class="Number">7</a> <a id="1003" href="Codata.Sized.Conat.html#1382" class="Function Operator">_*_</a>

<a id="_∸_"></a><a id="1008" href="Codata.Sized.Conat.html#1008" class="Function Operator">_∸_</a> <a id="1012" class="Symbol">:</a> <a id="1014" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1020" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="1022" class="Symbol">→</a> <a id="1024" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1026" class="Symbol">→</a> <a id="1028" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1034" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a>
<a id="1036" href="Codata.Sized.Conat.html#1036" class="Bound">m</a> <a id="1038" href="Codata.Sized.Conat.html#1008" class="Function Operator">∸</a> <a id="1040" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>  <a id="1046" class="Symbol">=</a> <a id="1048" href="Codata.Sized.Conat.html#1036" class="Bound">m</a>
<a id="1050" href="Codata.Sized.Conat.html#1050" class="Bound">m</a> <a id="1052" href="Codata.Sized.Conat.html#1008" class="Function Operator">∸</a> <a id="1054" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1058" href="Codata.Sized.Conat.html#1058" class="Bound">n</a> <a id="1060" class="Symbol">=</a> <a id="1062" href="Codata.Sized.Conat.html#875" class="Function">pred</a> <a id="1067" href="Codata.Sized.Conat.html#1050" class="Bound">m</a> <a id="1069" href="Codata.Sized.Conat.html#1008" class="Function Operator">∸</a> <a id="1071" href="Codata.Sized.Conat.html#1058" class="Bound">n</a>

<a id="_ℕ+_"></a><a id="1074" href="Codata.Sized.Conat.html#1074" class="Function Operator">_ℕ+_</a> <a id="1079" class="Symbol">:</a> <a id="1081" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1083" class="Symbol">→</a> <a id="1085" class="Symbol">∀</a> <a id="1087" class="Symbol">{</a><a id="1088" href="Codata.Sized.Conat.html#1088" class="Bound">i</a><a id="1089" class="Symbol">}</a> <a id="1091" class="Symbol">→</a> <a id="1093" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1099" href="Codata.Sized.Conat.html#1088" class="Bound">i</a> <a id="1101" class="Symbol">→</a> <a id="1103" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1109" href="Codata.Sized.Conat.html#1088" class="Bound">i</a>
<a id="1111" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>  <a id="1117" href="Codata.Sized.Conat.html#1074" class="Function Operator">ℕ+</a> <a id="1120" href="Codata.Sized.Conat.html#1120" class="Bound">n</a> <a id="1122" class="Symbol">=</a> <a id="1124" href="Codata.Sized.Conat.html#1120" class="Bound">n</a>
<a id="1126" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1130" href="Codata.Sized.Conat.html#1130" class="Bound">m</a> <a id="1132" href="Codata.Sized.Conat.html#1074" class="Function Operator">ℕ+</a> <a id="1135" href="Codata.Sized.Conat.html#1135" class="Bound">n</a> <a id="1137" class="Symbol">=</a> <a id="1139" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1143" class="Symbol">λ</a> <a id="1145" class="Keyword">where</a> <a id="1151" class="Symbol">.</a><a id="1152" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1158" class="Symbol">→</a> <a id="1160" href="Codata.Sized.Conat.html#1130" class="Bound">m</a> <a id="1162" href="Codata.Sized.Conat.html#1074" class="Function Operator">ℕ+</a> <a id="1165" href="Codata.Sized.Conat.html#1135" class="Bound">n</a>

<a id="_+ℕ_"></a><a id="1168" href="Codata.Sized.Conat.html#1168" class="Function Operator">_+ℕ_</a> <a id="1173" class="Symbol">:</a> <a id="1175" class="Symbol">∀</a> <a id="1177" class="Symbol">{</a><a id="1178" href="Codata.Sized.Conat.html#1178" class="Bound">i</a><a id="1179" class="Symbol">}</a> <a id="1181" class="Symbol">→</a> <a id="1183" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1189" href="Codata.Sized.Conat.html#1178" class="Bound">i</a> <a id="1191" class="Symbol">→</a> <a id="1193" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1195" class="Symbol">→</a> <a id="1197" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1203" href="Codata.Sized.Conat.html#1178" class="Bound">i</a>
<a id="1205" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>  <a id="1211" href="Codata.Sized.Conat.html#1168" class="Function Operator">+ℕ</a> <a id="1214" href="Codata.Sized.Conat.html#1214" class="Bound">n</a> <a id="1216" class="Symbol">=</a> <a id="1218" href="Codata.Sized.Conat.html#689" class="Function">fromℕ</a> <a id="1224" href="Codata.Sized.Conat.html#1214" class="Bound">n</a>
<a id="1226" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1230" href="Codata.Sized.Conat.html#1230" class="Bound">m</a> <a id="1232" href="Codata.Sized.Conat.html#1168" class="Function Operator">+ℕ</a> <a id="1235" href="Codata.Sized.Conat.html#1235" class="Bound">n</a> <a id="1237" class="Symbol">=</a> <a id="1239" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1243" class="Symbol">λ</a> <a id="1245" class="Keyword">where</a> <a id="1251" class="Symbol">.</a><a id="1252" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1258" class="Symbol">→</a> <a id="1260" class="Symbol">(</a><a id="1261" href="Codata.Sized.Conat.html#1230" class="Bound">m</a> <a id="1263" class="Symbol">.</a><a id="1264" href="Codata.Sized.Thunk.html#556" class="Field">force</a><a id="1269" class="Symbol">)</a> <a id="1271" href="Codata.Sized.Conat.html#1168" class="Function Operator">+ℕ</a> <a id="1274" href="Codata.Sized.Conat.html#1235" class="Bound">n</a>

<a id="_+_"></a><a id="1277" href="Codata.Sized.Conat.html#1277" class="Function Operator">_+_</a> <a id="1281" class="Symbol">:</a> <a id="1283" class="Symbol">∀</a> <a id="1285" class="Symbol">{</a><a id="1286" href="Codata.Sized.Conat.html#1286" class="Bound">i</a><a id="1287" class="Symbol">}</a> <a id="1289" class="Symbol">→</a> <a id="1291" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1297" href="Codata.Sized.Conat.html#1286" class="Bound">i</a> <a id="1299" class="Symbol">→</a> <a id="1301" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1307" href="Codata.Sized.Conat.html#1286" class="Bound">i</a> <a id="1309" class="Symbol">→</a> <a id="1311" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1317" href="Codata.Sized.Conat.html#1286" class="Bound">i</a>
<a id="1319" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>  <a id="1325" href="Codata.Sized.Conat.html#1277" class="Function Operator">+</a> <a id="1327" href="Codata.Sized.Conat.html#1327" class="Bound">n</a> <a id="1329" class="Symbol">=</a> <a id="1331" href="Codata.Sized.Conat.html#1327" class="Bound">n</a>
<a id="1333" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1337" href="Codata.Sized.Conat.html#1337" class="Bound">m</a> <a id="1339" href="Codata.Sized.Conat.html#1277" class="Function Operator">+</a> <a id="1341" href="Codata.Sized.Conat.html#1341" class="Bound">n</a> <a id="1343" class="Symbol">=</a> <a id="1345" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1349" class="Symbol">λ</a> <a id="1351" class="Keyword">where</a> <a id="1357" class="Symbol">.</a><a id="1358" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1364" class="Symbol">→</a> <a id="1366" class="Symbol">(</a><a id="1367" href="Codata.Sized.Conat.html#1337" class="Bound">m</a> <a id="1369" class="Symbol">.</a><a id="1370" href="Codata.Sized.Thunk.html#556" class="Field">force</a><a id="1375" class="Symbol">)</a> <a id="1377" href="Codata.Sized.Conat.html#1277" class="Function Operator">+</a> <a id="1379" href="Codata.Sized.Conat.html#1341" class="Bound">n</a>

<a id="_*_"></a><a id="1382" href="Codata.Sized.Conat.html#1382" class="Function Operator">_*_</a> <a id="1386" class="Symbol">:</a> <a id="1388" class="Symbol">∀</a> <a id="1390" class="Symbol">{</a><a id="1391" href="Codata.Sized.Conat.html#1391" class="Bound">i</a><a id="1392" class="Symbol">}</a> <a id="1394" class="Symbol">→</a> <a id="1396" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1402" href="Codata.Sized.Conat.html#1391" class="Bound">i</a> <a id="1404" class="Symbol">→</a> <a id="1406" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1412" href="Codata.Sized.Conat.html#1391" class="Bound">i</a> <a id="1414" class="Symbol">→</a> <a id="1416" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1422" href="Codata.Sized.Conat.html#1391" class="Bound">i</a>
<a id="1424" href="Codata.Sized.Conat.html#1424" class="Bound">m</a>     <a id="1430" href="Codata.Sized.Conat.html#1382" class="Function Operator">*</a> <a id="1432" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>  <a id="1438" class="Symbol">=</a> <a id="1440" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
<a id="1445" href="Codata.Sized.Conat.html#572" class="CatchallClause InductiveConstructor">zero</a><a id="1449" class="CatchallClause">  </a><a id="1451" href="Codata.Sized.Conat.html#1382" class="CatchallClause Function Operator">*</a><a id="1452" class="CatchallClause"> </a><a id="1453" href="Codata.Sized.Conat.html#1453" class="CatchallClause Bound">n</a>     <a id="1459" class="Symbol">=</a> <a id="1461" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
<a id="1466" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1470" href="Codata.Sized.Conat.html#1470" class="Bound">m</a> <a id="1472" href="Codata.Sized.Conat.html#1382" class="Function Operator">*</a> <a id="1474" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1478" href="Codata.Sized.Conat.html#1478" class="Bound">n</a> <a id="1480" class="Symbol">=</a> <a id="1482" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1486" class="Symbol">λ</a> <a id="1488" class="Keyword">where</a> <a id="1494" class="Symbol">.</a><a id="1495" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1501" class="Symbol">→</a> <a id="1503" href="Codata.Sized.Conat.html#1478" class="Bound">n</a> <a id="1505" class="Symbol">.</a><a id="1506" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1512" href="Codata.Sized.Conat.html#1277" class="Function Operator">+</a> <a id="1514" class="Symbol">(</a><a id="1515" href="Codata.Sized.Conat.html#1470" class="Bound">m</a> <a id="1517" class="Symbol">.</a><a id="1518" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1524" href="Codata.Sized.Conat.html#1382" class="Function Operator">*</a> <a id="1526" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1530" href="Codata.Sized.Conat.html#1478" class="Bound">n</a><a id="1531" class="Symbol">)</a>

<a id="1534" class="Comment">-- Max and Min</a>

<a id="1550" class="Keyword">infixl</a> <a id="1557" class="Number">6</a> <a id="1559" href="Codata.Sized.Conat.html#1577" class="Function Operator">_⊔_</a>
<a id="1563" class="Keyword">infixl</a> <a id="1570" class="Number">7</a> <a id="1572" href="Codata.Sized.Conat.html#1713" class="Function Operator">_⊓_</a>

<a id="_⊔_"></a><a id="1577" href="Codata.Sized.Conat.html#1577" class="Function Operator">_⊔_</a> <a id="1581" class="Symbol">:</a> <a id="1583" class="Symbol">∀</a> <a id="1585" class="Symbol">{</a><a id="1586" href="Codata.Sized.Conat.html#1586" class="Bound">i</a><a id="1587" class="Symbol">}</a> <a id="1589" class="Symbol">→</a> <a id="1591" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1597" href="Codata.Sized.Conat.html#1586" class="Bound">i</a> <a id="1599" class="Symbol">→</a> <a id="1601" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1607" href="Codata.Sized.Conat.html#1586" class="Bound">i</a> <a id="1609" class="Symbol">→</a> <a id="1611" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1617" href="Codata.Sized.Conat.html#1586" class="Bound">i</a>
<a id="1619" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>  <a id="1625" href="Codata.Sized.Conat.html#1577" class="Function Operator">⊔</a> <a id="1627" href="Codata.Sized.Conat.html#1627" class="Bound">n</a>     <a id="1633" class="Symbol">=</a> <a id="1635" href="Codata.Sized.Conat.html#1627" class="Bound">n</a>
<a id="1637" href="Codata.Sized.Conat.html#1637" class="CatchallClause Bound">m</a><a id="1638" class="CatchallClause">     </a><a id="1643" href="Codata.Sized.Conat.html#1577" class="CatchallClause Function Operator">⊔</a><a id="1644" class="CatchallClause"> </a><a id="1645" href="Codata.Sized.Conat.html#572" class="CatchallClause InductiveConstructor">zero</a>  <a id="1651" class="Symbol">=</a> <a id="1653" href="Codata.Sized.Conat.html#1637" class="Bound">m</a>
<a id="1655" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1659" href="Codata.Sized.Conat.html#1659" class="Bound">m</a> <a id="1661" href="Codata.Sized.Conat.html#1577" class="Function Operator">⊔</a> <a id="1663" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1667" href="Codata.Sized.Conat.html#1667" class="Bound">n</a> <a id="1669" class="Symbol">=</a> <a id="1671" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1675" class="Symbol">λ</a> <a id="1677" class="Keyword">where</a> <a id="1683" class="Symbol">.</a><a id="1684" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1690" class="Symbol">→</a> <a id="1692" href="Codata.Sized.Conat.html#1659" class="Bound">m</a> <a id="1694" class="Symbol">.</a><a id="1695" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1701" href="Codata.Sized.Conat.html#1577" class="Function Operator">⊔</a> <a id="1703" href="Codata.Sized.Conat.html#1667" class="Bound">n</a> <a id="1705" class="Symbol">.</a><a id="1706" href="Codata.Sized.Thunk.html#556" class="Field">force</a>

<a id="_⊓_"></a><a id="1713" href="Codata.Sized.Conat.html#1713" class="Function Operator">_⊓_</a> <a id="1717" class="Symbol">:</a> <a id="1719" class="Symbol">∀</a> <a id="1721" class="Symbol">{</a><a id="1722" href="Codata.Sized.Conat.html#1722" class="Bound">i</a><a id="1723" class="Symbol">}</a> <a id="1725" class="Symbol">→</a> <a id="1727" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1733" href="Codata.Sized.Conat.html#1722" class="Bound">i</a> <a id="1735" class="Symbol">→</a> <a id="1737" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1743" href="Codata.Sized.Conat.html#1722" class="Bound">i</a> <a id="1745" class="Symbol">→</a> <a id="1747" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1753" href="Codata.Sized.Conat.html#1722" class="Bound">i</a>
<a id="1755" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>  <a id="1761" href="Codata.Sized.Conat.html#1713" class="Function Operator">⊓</a> <a id="1763" href="Codata.Sized.Conat.html#1763" class="Bound">n</a>     <a id="1769" class="Symbol">=</a> <a id="1771" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
<a id="1776" href="Codata.Sized.Conat.html#1776" class="CatchallClause Bound">m</a><a id="1777" class="CatchallClause">     </a><a id="1782" href="Codata.Sized.Conat.html#1713" class="CatchallClause Function Operator">⊓</a><a id="1783" class="CatchallClause"> </a><a id="1784" href="Codata.Sized.Conat.html#572" class="CatchallClause InductiveConstructor">zero</a>  <a id="1790" class="Symbol">=</a> <a id="1792" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
<a id="1797" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1801" href="Codata.Sized.Conat.html#1801" class="Bound">m</a> <a id="1803" href="Codata.Sized.Conat.html#1713" class="Function Operator">⊓</a> <a id="1805" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1809" href="Codata.Sized.Conat.html#1809" class="Bound">n</a> <a id="1811" class="Symbol">=</a> <a id="1813" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="1817" class="Symbol">λ</a> <a id="1819" class="Keyword">where</a> <a id="1825" class="Symbol">.</a><a id="1826" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1832" class="Symbol">→</a> <a id="1834" href="Codata.Sized.Conat.html#1801" class="Bound">m</a> <a id="1836" class="Symbol">.</a><a id="1837" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="1843" href="Codata.Sized.Conat.html#1713" class="Function Operator">⊓</a> <a id="1845" href="Codata.Sized.Conat.html#1809" class="Bound">n</a> <a id="1847" class="Symbol">.</a><a id="1848" href="Codata.Sized.Thunk.html#556" class="Field">force</a>

<a id="1855" class="Comment">------------------------------------------------------------------------</a>
<a id="1928" class="Comment">-- Finiteness</a>

<a id="1943" class="Keyword">data</a> <a id="Finite"></a><a id="1948" href="Codata.Sized.Conat.html#1948" class="Datatype">Finite</a> <a id="1955" class="Symbol">:</a> <a id="1957" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="1963" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="1965" class="Symbol">→</a> <a id="1967" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1971" class="Keyword">where</a>
  <a id="Finite.zero"></a><a id="1979" href="Codata.Sized.Conat.html#1979" class="InductiveConstructor">zero</a> <a id="1984" class="Symbol">:</a> <a id="1986" href="Codata.Sized.Conat.html#1948" class="Datatype">Finite</a> <a id="1993" href="Codata.Sized.Conat.html#572" class="InductiveConstructor">zero</a>
  <a id="Finite.suc"></a><a id="2000" href="Codata.Sized.Conat.html#2000" class="InductiveConstructor">suc</a>  <a id="2005" class="Symbol">:</a> <a id="2007" class="Symbol">∀</a> <a id="2009" class="Symbol">{</a><a id="2010" href="Codata.Sized.Conat.html#2010" class="Bound">n</a><a id="2011" class="Symbol">}</a> <a id="2013" class="Symbol">→</a> <a id="2015" href="Codata.Sized.Conat.html#1948" class="Datatype">Finite</a> <a id="2022" class="Symbol">(</a><a id="2023" href="Codata.Sized.Conat.html#2010" class="Bound">n</a> <a id="2025" class="Symbol">.</a><a id="2026" href="Codata.Sized.Thunk.html#556" class="Field">force</a><a id="2031" class="Symbol">)</a> <a id="2033" class="Symbol">→</a> <a id="2035" href="Codata.Sized.Conat.html#1948" class="Datatype">Finite</a> <a id="2042" class="Symbol">(</a><a id="2043" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="2047" href="Codata.Sized.Conat.html#2010" class="Bound">n</a><a id="2048" class="Symbol">)</a>

<a id="toℕ"></a><a id="2051" href="Codata.Sized.Conat.html#2051" class="Function">toℕ</a> <a id="2055" class="Symbol">:</a> <a id="2057" class="Symbol">∀</a> <a id="2059" class="Symbol">{</a><a id="2060" href="Codata.Sized.Conat.html#2060" class="Bound">n</a><a id="2061" class="Symbol">}</a> <a id="2063" class="Symbol">→</a> <a id="2065" href="Codata.Sized.Conat.html#1948" class="Datatype">Finite</a> <a id="2072" href="Codata.Sized.Conat.html#2060" class="Bound">n</a> <a id="2074" class="Symbol">→</a> <a id="2076" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
<a id="2078" href="Codata.Sized.Conat.html#2051" class="Function">toℕ</a> <a id="2082" href="Codata.Sized.Conat.html#1979" class="InductiveConstructor">zero</a>    <a id="2090" class="Symbol">=</a> <a id="2092" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>
<a id="2097" href="Codata.Sized.Conat.html#2051" class="Function">toℕ</a> <a id="2101" class="Symbol">(</a><a id="2102" href="Codata.Sized.Conat.html#2000" class="InductiveConstructor">suc</a> <a id="2106" href="Codata.Sized.Conat.html#2106" class="Bound">n</a><a id="2107" class="Symbol">)</a> <a id="2109" class="Symbol">=</a> <a id="2111" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2115" class="Symbol">(</a><a id="2116" href="Codata.Sized.Conat.html#2051" class="Function">toℕ</a> <a id="2120" href="Codata.Sized.Conat.html#2106" class="Bound">n</a><a id="2121" class="Symbol">)</a>

<a id="¬Finite∞"></a><a id="2124" href="Codata.Sized.Conat.html#2124" class="Function">¬Finite∞</a> <a id="2133" class="Symbol">:</a> <a id="2135" href="Relation.Nullary.Negation.Core.html#698" class="Function Operator">¬</a> <a id="2137" class="Symbol">(</a><a id="2138" href="Codata.Sized.Conat.html#1948" class="Datatype">Finite</a> <a id="2145" href="Codata.Sized.Conat.html#620" class="Function">infinity</a><a id="2153" class="Symbol">)</a>
<a id="2155" href="Codata.Sized.Conat.html#2124" class="Function">¬Finite∞</a> <a id="2164" class="Symbol">(</a><a id="2165" href="Codata.Sized.Conat.html#2000" class="InductiveConstructor">suc</a> <a id="2169" href="Codata.Sized.Conat.html#2169" class="Bound">p</a><a id="2170" class="Symbol">)</a> <a id="2172" class="Symbol">=</a> <a id="2174" href="Codata.Sized.Conat.html#2124" class="Function">¬Finite∞</a> <a id="2183" href="Codata.Sized.Conat.html#2169" class="Bound">p</a>

<a id="2186" class="Comment">------------------------------------------------------------------------</a>
<a id="2259" class="Comment">-- Order wrt to Nat</a>

<a id="2280" class="Keyword">infix</a> <a id="2286" class="Number">4</a> <a id="2288" href="Codata.Sized.Conat.html#2425" class="Function Operator">_ℕ&lt;_</a> <a id="2293" href="Codata.Sized.Conat.html#2471" class="Function Operator">_ℕ≤infinity</a> <a id="2305" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">_ℕ≤_</a>

<a id="2311" class="Keyword">data</a> <a id="_ℕ≤_"></a><a id="2316" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">_ℕ≤_</a> <a id="2321" class="Symbol">:</a> <a id="2323" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2325" class="Symbol">→</a> <a id="2327" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="2333" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="2335" class="Symbol">→</a> <a id="2337" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2341" class="Keyword">where</a>
  <a id="_ℕ≤_.zℕ≤n"></a><a id="2349" href="Codata.Sized.Conat.html#2349" class="InductiveConstructor">zℕ≤n</a> <a id="2354" class="Symbol">:</a> <a id="2356" class="Symbol">∀</a> <a id="2358" class="Symbol">{</a><a id="2359" href="Codata.Sized.Conat.html#2359" class="Bound">n</a><a id="2360" class="Symbol">}</a> <a id="2362" class="Symbol">→</a> <a id="2364" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a> <a id="2369" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">ℕ≤</a> <a id="2372" href="Codata.Sized.Conat.html#2359" class="Bound">n</a>
  <a id="_ℕ≤_.sℕ≤s"></a><a id="2376" href="Codata.Sized.Conat.html#2376" class="InductiveConstructor">sℕ≤s</a> <a id="2381" class="Symbol">:</a> <a id="2383" class="Symbol">∀</a> <a id="2385" class="Symbol">{</a><a id="2386" href="Codata.Sized.Conat.html#2386" class="Bound">k</a> <a id="2388" href="Codata.Sized.Conat.html#2388" class="Bound">n</a><a id="2389" class="Symbol">}</a> <a id="2391" class="Symbol">→</a> <a id="2393" href="Codata.Sized.Conat.html#2386" class="Bound">k</a> <a id="2395" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">ℕ≤</a> <a id="2398" href="Codata.Sized.Conat.html#2388" class="Bound">n</a> <a id="2400" class="Symbol">.</a><a id="2401" href="Codata.Sized.Thunk.html#556" class="Field">force</a> <a id="2407" class="Symbol">→</a> <a id="2409" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2413" href="Codata.Sized.Conat.html#2386" class="Bound">k</a> <a id="2415" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">ℕ≤</a> <a id="2418" href="Codata.Sized.Conat.html#589" class="InductiveConstructor">suc</a> <a id="2422" href="Codata.Sized.Conat.html#2388" class="Bound">n</a>

<a id="_ℕ&lt;_"></a><a id="2425" href="Codata.Sized.Conat.html#2425" class="Function Operator">_ℕ&lt;_</a> <a id="2430" class="Symbol">:</a> <a id="2432" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2434" class="Symbol">→</a> <a id="2436" href="Codata.Sized.Conat.html#541" class="Datatype">Conat</a> <a id="2442" href="Agda.Builtin.Size.html#315" class="Postulate">∞</a> <a id="2444" class="Symbol">→</a> <a id="2446" href="Agda.Primitive.html#388" class="Primitive">Set</a>
<a id="2450" href="Codata.Sized.Conat.html#2450" class="Bound">k</a> <a id="2452" href="Codata.Sized.Conat.html#2425" class="Function Operator">ℕ&lt;</a> <a id="2455" href="Codata.Sized.Conat.html#2455" class="Bound">n</a> <a id="2457" class="Symbol">=</a> <a id="2459" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2463" href="Codata.Sized.Conat.html#2450" class="Bound">k</a> <a id="2465" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">ℕ≤</a> <a id="2468" href="Codata.Sized.Conat.html#2455" class="Bound">n</a>

<a id="_ℕ≤infinity"></a><a id="2471" href="Codata.Sized.Conat.html#2471" class="Function Operator">_ℕ≤infinity</a> <a id="2483" class="Symbol">:</a> <a id="2485" class="Symbol">∀</a> <a id="2487" href="Codata.Sized.Conat.html#2487" class="Bound">k</a> <a id="2489" class="Symbol">→</a> <a id="2491" href="Codata.Sized.Conat.html#2487" class="Bound">k</a> <a id="2493" href="Codata.Sized.Conat.html#2316" class="Datatype Operator">ℕ≤</a> <a id="2496" href="Codata.Sized.Conat.html#620" class="Function">infinity</a>
<a id="2505" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>  <a id="2511" href="Codata.Sized.Conat.html#2471" class="Function Operator">ℕ≤infinity</a> <a id="2522" class="Symbol">=</a> <a id="2524" href="Codata.Sized.Conat.html#2349" class="InductiveConstructor">zℕ≤n</a>
<a id="2529" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2533" href="Codata.Sized.Conat.html#2533" class="Bound">k</a> <a id="2535" href="Codata.Sized.Conat.html#2471" class="Function Operator">ℕ≤infinity</a> <a id="2546" class="Symbol">=</a> <a id="2548" href="Codata.Sized.Conat.html#2376" class="InductiveConstructor">sℕ≤s</a> <a id="2553" class="Symbol">(</a><a id="2554" href="Codata.Sized.Conat.html#2533" class="Bound">k</a> <a id="2556" href="Codata.Sized.Conat.html#2471" class="Function Operator">ℕ≤infinity</a><a id="2566" class="Symbol">)</a>
</pre></body></html>