------------------------------------------------------------------------
-- The Agda standard library
--
-- Indexed binary trees
------------------------------------------------------------------------

{-# OPTIONS --without-K --safe #-}

module Data.Tree.Binary.Indexed where

open import Data.Tree.Binary as T using (Tree)
open import Data.Unit
open import Level
open import Function.Base
open import Data.Nat using (â„•)

private
  variable
    a n nâ‚ l lâ‚ : Level
    N : Set n
    L : Set l
    Nâ‚ : Set nâ‚
    Lâ‚ : Set lâ‚
    A : Set a

------------------------------------------------------------------------
-- Type to represent the size of a tree

ğ•‹ : Set
ğ•‹ = Tree âŠ¤ âŠ¤

pattern ls = T.leaf tt

pattern ns sâ‚ sâ‚‚ = T.node sâ‚ tt sâ‚‚

------------------------------------------------------------------------
-- ITree definition and basic functions

data ITree (N : Set n) (L : Set l) : ğ•‹ â†’ Set (n âŠ” l) where
  leaf : L â†’ ITree N L ls
  node : âˆ€ {sâ‚ sâ‚‚} â†’ ITree N L sâ‚ â†’ N â†’ ITree N L sâ‚‚ â†’ ITree N L (ns sâ‚ sâ‚‚)

map : âˆ€ {s} â†’ (N â†’ Nâ‚) â†’ (L â†’ Lâ‚) â†’ ITree N L s â†’ ITree Nâ‚ Lâ‚ s
map f g (leaf x) = leaf (g x)
map f g (node l m r) = node (map f g l) (f m) (map f g r)

mapâ‚™ : âˆ€ {s} â†’ (N â†’ Nâ‚) â†’ ITree N L s â†’ ITree Nâ‚ L s
mapâ‚™ f t = map f id t

mapâ‚— : âˆ€ {s} â†’ (L â†’ Lâ‚) â†’ ITree N L s â†’ ITree N Lâ‚ s
mapâ‚— g t = map id g t

#nodes : âˆ€ {s} â†’ ITree N L s â†’ â„•
#nodes {s = s} t = T.#nodes s

#leaves : âˆ€ {s} â†’ ITree N L s â†’ â„•
#leaves {s = s} t = T.#leaves s

foldr : âˆ€ {s} â†’ (A â†’ N â†’ A â†’ A) â†’ (L â†’ A) â†’ ITree N L s â†’ A
foldr f g (leaf x) = g x
foldr f g (node l m r) = f (foldr f g l) m (foldr f g r)

------------------------------------------------------------------------
-- Conversion to regular trees

toTree : âˆ€ {s} â†’ ITree N L s â†’ Tree N L
toTree (leaf x) = T.leaf x
toTree (node l m r) = T.node (toTree l) m (toTree r)

------------------------------------------------------------------------
-- Indexed lookups

data IndexLeaf : ğ•‹ â†’ Set where
  here-l : IndexLeaf ls
  il-l : âˆ€ {sâ‚ sâ‚‚} â†’ IndexLeaf sâ‚ â†’ IndexLeaf (ns sâ‚ sâ‚‚)
  il-r : âˆ€ {sâ‚ sâ‚‚} â†’ IndexLeaf sâ‚‚ â†’ IndexLeaf (ns sâ‚ sâ‚‚)

data IndexTree : ğ•‹ â†’ Set where
  here-t : âˆ€ {s} â†’ IndexTree s
  it-l : âˆ€ {sâ‚ sâ‚‚} â†’ IndexTree sâ‚ â†’ IndexTree (ns sâ‚ sâ‚‚)
  it-r : âˆ€ {sâ‚ sâ‚‚} â†’ IndexTree sâ‚‚ â†’ IndexTree (ns sâ‚ sâ‚‚)

infixl 3 _-_

_-_ : (s : ğ•‹) â†’ IndexTree s â†’ ğ•‹
t      - here-t = t
ns l r - it-l i = l - i
ns l r - it-r i = r - i

retrieve-leaf : âˆ€ {s} â†’ ITree N L s â†’ IndexLeaf s â†’ L
retrieve-leaf (leaf x)     here-l   = x
retrieve-leaf (node l m r) (il-l i) = retrieve-leaf l i
retrieve-leaf (node l m r) (il-r i) = retrieve-leaf r i

retrieve-subtree : âˆ€ {s} â†’ ITree N L s â†’ (i : IndexTree s) â†’ ITree N L (s - i)
retrieve-subtree t             here-t  = t
retrieve-subtree (node l m r) (it-l i) = retrieve-subtree l i
retrieve-subtree (node l m r) (it-r i) = retrieve-subtree r i

update-index : âˆ€ {s} â†’ (L â†’ L) â†’ ITree N L s â†’ IndexLeaf s â†’ ITree N L s
update-index f (leaf x)      here-l  = leaf (f x)
update-index f (node l m r) (il-l i) = node (update-index f l i) m r
update-index f (node l m r) (il-r i) = node l m (update-index f r i)
