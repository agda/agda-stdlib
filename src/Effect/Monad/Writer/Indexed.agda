------------------------------------------------------------------------
-- The Agda standard library
--
-- The indexed writer monad
------------------------------------------------------------------------

{-# OPTIONS --cubical-compatible --safe #-}

open import Level

module Effect.Monad.Writer.Indexed (a : Level) where

open import Algebra using (RawMonoid)
open import Data.Product using (_Ã—_; _,_; mapâ‚)
open import Data.Unit.Polymorphic
open import Effect.Applicative.Indexed
open import Effect.Monad
open import Effect.Monad.Indexed
open import Function
open import Function.Identity.Effectful as Id using (Identity)

private
  variable
    w â„“ : Level
    A B I : Set â„“

------------------------------------------------------------------------
-- Indexed writer

IWriterT : (ğ• : RawMonoid w â„“) â†’ IFun I (w âŠ” a) â†’ IFun I (w âŠ” a)
IWriterT ğ• M i j A = M i j (RawMonoid.Carrier ğ• Ã— A)

module _ {M : IFun I (w âŠ” a)} {ğ• : RawMonoid w â„“} where

  open RawMonoid ğ• renaming (Carrier to W)

  ------------------------------------------------------------------------
  -- Indexed writer applicative

  WriterTIApplicative : RawIApplicative M â†’ RawIApplicative (IWriterT ğ• M)
  WriterTIApplicative App = record
    { pure = Î» x â†’ pure (Îµ , x)
    ; _âŠ›_ = Î» m n â†’ go <$> m âŠ› n
    } where
        open RawIApplicative App
        go : W Ã— (A â†’ B) â†’ W Ã— A â†’ W Ã— B
        go (wâ‚ , f) (wâ‚‚ , x) = wâ‚ âˆ™ wâ‚‚ , f x

  WriterTIApplicativeZero : RawIApplicativeZero M â†’
                            RawIApplicativeZero (IWriterT ğ• M)
  WriterTIApplicativeZero App = record
    { applicative = WriterTIApplicative applicative
    ; âˆ… = âˆ…
    } where open RawIApplicativeZero App

  WriterTIAlternative : RawIAlternative M â†’ RawIAlternative (IWriterT ğ• M)
  WriterTIAlternative Alt = record
    { applicativeZero = WriterTIApplicativeZero applicativeZero
    ; _âˆ£_ = _âˆ£_
    } where open RawIAlternative Alt

  ------------------------------------------------------------------------
  -- Indexed writer monad

  WriterTIMonad : RawIMonad M â†’ RawIMonad (IWriterT ğ• M)
  WriterTIMonad Mon = record
    { return = Î» x â†’ return (Îµ , x)
    ; _>>=_ = Î» m f â†’ do
        wâ‚ , x  â† m
        wâ‚‚ , fx â† f x
        return (wâ‚ âˆ™ wâ‚‚ , fx)
    } where open RawIMonad Mon

  WriterTIMonadZero : RawIMonadZero M â†’ RawIMonadZero (IWriterT ğ• M)
  WriterTIMonadZero Mon = record
    { monad = WriterTIMonad monad
    ; applicativeZero = WriterTIApplicativeZero applicativeZero
    } where open RawIMonadZero Mon

  WriterTIMonadPlus : RawIMonadPlus M â†’ RawIMonadPlus (IWriterT ğ• M)
  WriterTIMonadPlus Mon = record
    { monad = WriterTIMonad monad
    ; alternative = WriterTIAlternative alternative
    } where open RawIMonadPlus Mon

------------------------------------------------------------------------
-- Writer monad operations

record RawIMonadWriter {I : Set â„“} (ğ• : RawMonoid w â„“) (M : IFun I (w âŠ” a))
                       : Set (â„“ âŠ” suc (w âŠ” a)) where
  open RawMonoid ğ• renaming (Carrier to W)
  field
    monad  : RawIMonad M
    writer : âˆ€ {i} â†’ (W Ã— A) â†’ M i i A
    listen : âˆ€ {i j} â†’ M i j A â†’ M i j (W Ã— A)
    pass   : âˆ€ {i j} â†’ M i j ((W â†’ W) Ã— A) â†’ M i j A

  open RawIMonad monad public

  tell : âˆ€ {i} â†’ W â†’ M i i âŠ¤
  tell = writer âˆ˜â€² (_, tt)

  listens : âˆ€ {i j} {Z : Set w} â†’ (W â†’ Z) â†’ M i j A â†’ M i j (Z Ã— A)
  listens f m = listen m >>= return âˆ˜â€² mapâ‚ f

  censor : âˆ€ {i j} â†’ (W â†’ W) â†’ M i j A â†’ M i j A
  censor f m = pass (m >>= return âˆ˜â€² (f ,_))


WriterTIMonadWriter : {I : Set â„“} {ğ• : RawMonoid w â„“} {M : IFun I (w âŠ” a)} â†’
                      RawIMonad M â†’ RawIMonadWriter ğ• (IWriterT ğ• M)
WriterTIMonadWriter {ğ• = ğ•} Mon = record
  { monad = WriterTIMonad {ğ• = ğ•} Mon
  ; writer = return
  ; listen = Î» m â†’ do
      w , a â† m
      return (w , w , a)
  ; pass = Î» m â†’ do
      w , f , a â† m
      return (f w , a)
  } where open RawIMonad Mon
          open RawMonoid ğ• renaming (Carrier to W)
