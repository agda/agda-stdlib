------------------------------------------------------------------------
-- The Agda standard library
--
-- The unique morphism to the terminal object,
-- for each of the relevant categories. Since
-- each terminal algebra builds on `Monoid`,
-- possibly with additional (trivial) operations,
-- satisfying additional properties, it suffices to
-- define the morphism on the underlying `RawMonoid`
------------------------------------------------------------------------

{-# OPTIONS --cubical-compatible --safe #-}

open import Level using (Level)

module Algebra.Morphism.Construct.Terminal {c ‚Ñì : Level} where


open import Algebra.Bundles.Raw
  using (RawMonoid; RawGroup; RawNearSemiring; RawSemiring; RawRing)
import Algebra.Morphism.Definitions as MorphismDefinitions
open import Algebra.Morphism.Structures
  using ( module MagmaMorphisms
        ; module MonoidMorphisms
        ; module GroupMorphisms
        ; module NearSemiringMorphisms
        ; module SemiringMorphisms
        ; module RingWithoutOneMorphisms
        ; module RingMorphisms
        )
open import Data.Product.Base using (_,_)
open import Function.Definitions using (StrictlySurjective)
import Relation.Binary.Morphism.Definitions as Definitions
open import Relation.Binary.Morphism.Structures

open import Algebra.Construct.Terminal {c} {‚Ñì}

private
  variable
    a ‚Ñìa : Level


------------------------------------------------------------------------
-- The underlying data of the morphism

module UniqueMorphism (M : RawMonoid a ‚Ñìa) where

  private module M = RawMonoid M
  open MorphismDefinitions M.Carrier ùïÜne.Carrier ùïÜne._‚âà_
  open MagmaMorphisms M.rawMagma rawMagma

  one : M.Carrier ‚Üí ùïÜne.Carrier
  one _ = _

  cong : Definitions.Homomorphic‚ÇÇ M.Carrier ùïÜne.Carrier M._‚âà_ ùïÜne._‚âà_ one
  cong _ = _

  isRelHomomorphism : IsRelHomomorphism M._‚âà_ ùïÜne._‚âà_ one
  isRelHomomorphism = record { cong = cong }

  homo : Homomorphic‚ÇÇ one M._‚àô_ _
  homo _ = _

  Œµ-homo : Homomorphic‚ÇÄ one M.Œµ _
  Œµ-homo = _

  isMagmaHomomorphism : IsMagmaHomomorphism one
  isMagmaHomomorphism = record
    { isRelHomomorphism = isRelHomomorphism
    ; homo = homo
    }

  strictlySurjective : StrictlySurjective ùïÜne._‚âà_ one
  strictlySurjective _ = M.Œµ , _

------------------------------------------------------------------------
-- Monoid

module _ (M : RawMonoid a ‚Ñìa) where

  open MonoidMorphisms M rawMonoid
  open UniqueMorphism M

  isMonoidHomomorphism : IsMonoidHomomorphism one
  isMonoidHomomorphism = record
    { isMagmaHomomorphism = isMagmaHomomorphism
    ; Œµ-homo = Œµ-homo
    }

------------------------------------------------------------------------
-- Group

module _ (G : RawGroup a ‚Ñìa) where

  private module G = RawGroup G
  open GroupMorphisms G rawGroup
  open UniqueMorphism G.rawMonoid

  isGroupHomomorphism : IsGroupHomomorphism one
  isGroupHomomorphism = record
    { isMonoidHomomorphism = isMonoidHomomorphism G.rawMonoid
    ; ‚Åª¬π-homo = Œª _ ‚Üí _
    }

------------------------------------------------------------------------
-- NearSemiring

module _ (N : RawNearSemiring a ‚Ñìa) where

  private module N = RawNearSemiring N
  open NearSemiringMorphisms N rawNearSemiring
  open UniqueMorphism N.+-rawMonoid

  isNearSemiringHomomorphism : IsNearSemiringHomomorphism one
  isNearSemiringHomomorphism = record
    { +-isMonoidHomomorphism = isMonoidHomomorphism N.+-rawMonoid
    ; *-homo = Œª _ _ ‚Üí _
    }

------------------------------------------------------------------------
-- Semiring

module _ (S : RawSemiring a ‚Ñìa) where

  private module S = RawSemiring S
  open SemiringMorphisms S rawSemiring
  open UniqueMorphism S.+-rawMonoid

  isSemiringHomomorphism : IsSemiringHomomorphism one
  isSemiringHomomorphism = record
    { isNearSemiringHomomorphism = isNearSemiringHomomorphism S.rawNearSemiring
    ; 1#-homo = _
    }

------------------------------------------------------------------------
-- Ring

module _ (R : RawRing a ‚Ñìa) where

  private module R = RawRing R
  open RingMorphisms R rawRing
  open UniqueMorphism R.+-rawMonoid

  isRingHomomorphism : IsRingHomomorphism one
  isRingHomomorphism = record
    { isSemiringHomomorphism = isSemiringHomomorphism R.rawSemiring
    ; -‚Äøhomo = Œª _ ‚Üí _
    }
