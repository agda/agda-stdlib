------------------------------------------------------------------------
-- The Agda standard library
--
-- Properties of linear maps.
------------------------------------------------------------------------

{-# OPTIONS --without-K --safe #-}

open import Algebra                   using (CommutativeRing)
open import Algebra.Module            using (Module)
open import Level                     using (Level; _âŠ”_)

module Algebra.Module.Morphism.Linear.Properties
  {r â„“r m â„“m : Level}
  {ring      : CommutativeRing r â„“r}
  (modA modB  : Module ring m â„“m)
  where

import Algebra.Module.Properties          as Properties
import Function.Definitions
import Relation.Binary.Reasoning.Setoid   as Reasoning
import Algebra.Module.Morphism.Structures as MorphismStructures

open import Agda.Builtin.Sigma
open import Axiom.DoubleNegationElimination
open import Data.List
open import Data.Product
  using (Î£; _,_; âˆƒ; Î£-syntax; âˆƒ-syntax; _Ã—_)
open import Relation.Nullary          using (Â¬_)
open import Relation.Nullary.Negation using (contraposition)

open MorphismStructures.ModuleMorphisms modA modB
open Module modA
  using () renaming
  ( Carrierá´¹       to A
  ; _+á´¹_           to _+á´¬_
  ; _*â‚—_           to _Â·á´¬_
  ; -á´¹_            to -á´¬_
  ; _â‰ˆá´¹_           to _â‰ˆá´¬_
  ; 0á´¹             to 0á´¬
  ; +á´¹-comm        to +á´¬-comm
  ; +á´¹-congË¡       to +á´¬-congË¡
  ; +á´¹-congÊ³       to +á´¬-congÊ³
  ; *â‚—-zeroË¡       to Â·á´¬-zeroË¡
  ; -á´¹â€¿cong        to -á´¬â€¿cong
  ; -á´¹â€¿inverseÊ³    to -á´¬â€¿inverseÊ³
  ; -á´¹â€¿involutive  to -á´¬â€¿involutive
  ; uniqueÊ³â€¿-á´¹     to uniqueÊ³â€¿-á´¬
  ; â‰ˆá´¹-setoid      to â‰ˆá´¬-setoid
  ; â‰ˆá´¹-sym         to symá´¬
  ; leftSemimodule to leftSemimoduleá´¬
  )
open Properties leftSemimoduleá´¬
  using () renaming
  ( non-zeroÊ³ to non-zeroÊ³á´¬
  ; non-zeroË¡ to non-zeroË¡á´¬
  )
open Module modB
  using () renaming
  ( Carrierá´¹       to B
  ; _+á´¹_           to _+á´®_
  ; _*â‚—_           to _Â·á´®_
  ; -á´¹_            to -á´®_
  ; _â‰ˆá´¹_           to _â‰ˆá´®_
  ; 0á´¹             to 0á´®
  ; +á´¹-comm        to +á´®-comm
  ; +á´¹-congË¡       to +á´®-congË¡
  ; +á´¹-congÊ³       to +á´®-congÊ³
  ; *â‚—-zeroË¡       to Â·á´®-zeroË¡
  ; -á´¹â€¿cong        to -á´®â€¿cong
  ; -á´¹â€¿inverseÊ³    to -á´®â€¿inverseÊ³
  ; -á´¹â€¿involutive  to -á´®â€¿involutive
  ; uniqueÊ³â€¿-á´¹     to uniqueÊ³â€¿-á´®
  ; â‰ˆá´¹-setoid      to â‰ˆá´®-setoid
  ; â‰ˆá´¹-sym         to symá´®
  ; leftSemimodule to leftSemimoduleá´®
  )
open Properties leftSemimoduleá´®
  using () renaming
  ( non-zeroÊ³ to non-zeroÊ³á´®
  ; non-zeroË¡ to non-zeroË¡á´®
  )
open CommutativeRing ring
  using (_â‰ˆ_; _*_; _+_) renaming
  ( Carrier to S
  ; 0#      to ğŸ˜
  ; 1#      to ğŸ™
  )
open module Reasoningá´¬ = Reasoning â‰ˆá´¬-setoid
  using () renaming
  ( begin_ to beginá´¬_
  ; _âˆ     to _âˆá´¬
  )
infixr 2 step-â‰ˆá´¬
step-â‰ˆá´¬ = Reasoningá´¬.step-â‰ˆ
syntax step-â‰ˆá´¬ x yâ‰ˆz xâ‰ˆy = x â‰ˆá´¬âŸ¨ xâ‰ˆy âŸ© yâ‰ˆz
open module Reasoningá´® = Reasoning â‰ˆá´®-setoid
open Function.Definitions _â‰ˆá´¬_ _â‰ˆá´®_

_â‰‰á´¬_ : A â†’ A â†’ Set â„“m
x â‰‰á´¬ y = Â¬ (x â‰ˆá´¬ y)

_â‰‰á´®_ : B â†’ B â†’ Set â„“m
x â‰‰á´® y = Â¬ (x â‰ˆá´® y)

module _
  {âŸ¦_âŸ§ : A â†’ B}
  (isModuleHomomorphism : IsModuleHomomorphism âŸ¦_âŸ§)
  where

  open IsModuleHomomorphism isModuleHomomorphism

  -- f(x) â‰ˆ 0 iff x â‰ˆ 0, for linear non-trivial f
  fğŸ˜â‰ˆğŸ˜ : {x : A} â†’ âŸ¦ 0á´¬ âŸ§ â‰ˆá´® 0á´®
  fğŸ˜â‰ˆğŸ˜ {x = x} = begin
    âŸ¦ 0á´¬ âŸ§       â‰ˆâŸ¨ âŸ¦âŸ§-cong (symá´¬ (Â·á´¬-zeroË¡ x)) âŸ©
    âŸ¦ (ğŸ˜ Â·á´¬ x) âŸ§ â‰ˆâŸ¨ *â‚—-homo ğŸ˜ x âŸ©
    ğŸ˜ Â·á´® âŸ¦ x âŸ§   â‰ˆâŸ¨ Â·á´®-zeroË¡ âŸ¦ x âŸ§ âŸ©
    0á´® âˆ

  xâ‰ˆğŸ˜â†’fxâ‰ˆğŸ˜ : {x : A} â†’ x â‰ˆá´¬ 0á´¬ â†’ âŸ¦ x âŸ§ â‰ˆá´® 0á´®
  xâ‰ˆğŸ˜â†’fxâ‰ˆğŸ˜ {x = x} xâ‰ˆğŸ˜ = begin
    âŸ¦ x âŸ§  â‰ˆâŸ¨ âŸ¦âŸ§-cong xâ‰ˆğŸ˜ âŸ©
    âŸ¦ 0á´¬ âŸ§ â‰ˆâŸ¨ fğŸ˜â‰ˆğŸ˜ {x = x} âŸ©
    0á´® âˆ

  fxâ‰‰ğŸ˜â†’xâ‰‰ğŸ˜ : {x : A} â†’ âŸ¦ x âŸ§ â‰‰á´® 0á´® â†’ x â‰‰á´¬ 0á´¬
  fxâ‰‰ğŸ˜â†’xâ‰‰ğŸ˜ = contraposition xâ‰ˆğŸ˜â†’fxâ‰ˆğŸ˜

  -- Zero is a unique output of linear map â‰‰ `const ğŸ˜`.
  zero-unique : {x : A} â†’
    Î£[ (s , y) âˆˆ S Ã— A ] ((s Â·á´¬ x â‰ˆá´¬ y) Ã— (âŸ¦ y âŸ§ â‰‰á´® 0á´®)) â†’
    x â‰‰á´¬ 0á´¬ â†’ âŸ¦ x âŸ§ â‰‰á´® 0á´®
  zero-unique {x = x} ((s , y) , (sÂ·xâ‰ˆy , fyâ‰‰ğŸ˜)) xâ‰‰ğŸ˜ =
    non-zeroÊ³á´® sÂ·fxâ‰‰ğŸ˜
    where
    yâ‰‰ğŸ˜     : y â‰‰á´¬ 0á´¬
    yâ‰‰ğŸ˜     = fxâ‰‰ğŸ˜â†’xâ‰‰ğŸ˜ fyâ‰‰ğŸ˜
    fsÂ·xâ‰ˆfy : âŸ¦ (s Â·á´¬ x) âŸ§ â‰ˆá´® âŸ¦ y âŸ§
    fsÂ·xâ‰ˆfy = âŸ¦âŸ§-cong sÂ·xâ‰ˆy
    sÂ·fxâ‰ˆfy : s Â·á´® âŸ¦ x âŸ§ â‰ˆá´® âŸ¦ y âŸ§
    sÂ·fxâ‰ˆfy = begin
      s Â·á´® âŸ¦ x âŸ§   â‰ˆâŸ¨ symá´® (*â‚—-homo s x) âŸ©
      âŸ¦ (s Â·á´¬ x) âŸ§ â‰ˆâŸ¨ fsÂ·xâ‰ˆfy âŸ©
      âŸ¦ y âŸ§ âˆ
    sÂ·fxâ‰‰ğŸ˜  : (s Â·á´® âŸ¦ x âŸ§) â‰‰á´® 0á´®
    sÂ·fxâ‰‰ğŸ˜  = Î» sÂ·fxâ‰ˆğŸ˜ â†’
      fyâ‰‰ğŸ˜ ( begin
             âŸ¦ y âŸ§        â‰ˆâŸ¨ âŸ¦âŸ§-cong (symá´¬ sÂ·xâ‰ˆy) âŸ©
             âŸ¦ (s Â·á´¬ x) âŸ§ â‰ˆâŸ¨ *â‚—-homo s x âŸ©
             s Â·á´® âŸ¦ x âŸ§   â‰ˆâŸ¨ sÂ·fxâ‰ˆğŸ˜ âŸ©
             0á´® âˆ
           )

  -- f is odd (i.e. - f (-x) â‰ˆ - (f x)).
  fx+f-xâ‰ˆğŸ˜ : {x : A} â†’ âŸ¦ x âŸ§ +á´® âŸ¦ (-á´¬ x) âŸ§ â‰ˆá´® 0á´®
  fx+f-xâ‰ˆğŸ˜ {x = x} = begin
    âŸ¦ x âŸ§ +á´® âŸ¦ (-á´¬ x) âŸ§ â‰ˆâŸ¨ symá´® (+á´¹-homo x (-á´¬ x)) âŸ©
    âŸ¦ (x +á´¬ (-á´¬ x)) âŸ§   â‰ˆâŸ¨ âŸ¦âŸ§-cong (-á´¬â€¿inverseÊ³ x) âŸ©
    âŸ¦ 0á´¬ âŸ§              â‰ˆâŸ¨ fğŸ˜â‰ˆğŸ˜ {x = x} âŸ©
    0á´® âˆ

  f-xâ‰ˆ-fx : {x : A} â†’ âŸ¦ (-á´¬ x) âŸ§ â‰ˆá´® -á´® âŸ¦ x âŸ§
  f-xâ‰ˆ-fx {x = x} = uniqueÊ³â€¿-á´® âŸ¦ x âŸ§ âŸ¦ -á´¬ x âŸ§ fx+f-xâ‰ˆğŸ˜

  module _ {dne : DoubleNegationElimination _} where

    fxâ‰ˆğŸ˜â‡’xâ‰ˆğŸ˜ : {x : A} â†’
      Î£[ (s , y) âˆˆ S Ã— A ] ((s Â·á´¬ x â‰ˆá´¬ y) Ã— (âŸ¦ y âŸ§ â‰‰á´® 0á´®)) â†’
      âŸ¦ x âŸ§ â‰ˆá´® 0á´® â†’ x â‰ˆá´¬ 0á´¬
    fxâ‰ˆğŸ˜â‡’xâ‰ˆğŸ˜ {x = x} ((s , y) , (sÂ·xâ‰ˆy , fyâ‰‰ğŸ˜)) fxâ‰ˆğŸ˜ =
      dne Â¬xâ‰‰ğŸ˜
      where
      Â¬xâ‰‰ğŸ˜ : Â¬ (x â‰‰á´¬ 0á´¬)
      Â¬xâ‰‰ğŸ˜ = Î» xâ‰‰ğŸ˜ â†’ zero-unique ((s , y) , (sÂ·xâ‰ˆy , fyâ‰‰ğŸ˜)) xâ‰‰ğŸ˜ fxâ‰ˆğŸ˜

    -- A non-trivial linear function is injective.
    inj-lm : {x y : A} â†’
      Î£[ (s , z) âˆˆ S Ã— A ] ((s Â·á´¬ (x +á´¬ -á´¬ y) â‰ˆá´¬ z) Ã— (âŸ¦ z âŸ§ â‰‰á´® 0á´®)) â†’
      âŸ¦ x âŸ§ â‰ˆá´® âŸ¦ y âŸ§ â†’ x â‰ˆá´¬ y
    inj-lm {x = x} {y = y} ((s , z) , (sÂ·[x-y]â‰ˆz , fzâ‰‰ğŸ˜)) fxâ‰ˆfy =
      beginá´¬
      x         â‰ˆá´¬âŸ¨ xâ‰ˆ--y âŸ©
      -á´¬ (-á´¬ y) â‰ˆá´¬âŸ¨ -á´¬â€¿involutive âŸ©
      y âˆá´¬
      where
      fx-fyâ‰ˆğŸ˜ : âŸ¦ x âŸ§ +á´® (-á´® âŸ¦ y âŸ§) â‰ˆá´® 0á´®
      fx-fyâ‰ˆğŸ˜ = begin
        âŸ¦ x âŸ§ +á´® (-á´® âŸ¦ y âŸ§) â‰ˆâŸ¨ +á´®-congË¡ (-á´®â€¿cong (symá´® fxâ‰ˆfy)) âŸ©
        âŸ¦ x âŸ§ +á´® (-á´® âŸ¦ x âŸ§) â‰ˆâŸ¨ -á´®â€¿inverseÊ³ (âŸ¦ x âŸ§) âŸ©
        0á´® âˆ
      fx-yâ‰ˆğŸ˜ : âŸ¦ (x +á´¬ (-á´¬ y)) âŸ§ â‰ˆá´® 0á´®
      fx-yâ‰ˆğŸ˜ = begin
        âŸ¦ x +á´¬ (-á´¬ y) âŸ§     â‰ˆâŸ¨ +á´¹-homo x (-á´¬ y) âŸ©
        âŸ¦ x âŸ§ +á´® âŸ¦ -á´¬ y âŸ§   â‰ˆâŸ¨ +á´®-congË¡ f-xâ‰ˆ-fx âŸ©
        âŸ¦ x âŸ§ +á´® (-á´® âŸ¦ y âŸ§) â‰ˆâŸ¨ fx-fyâ‰ˆğŸ˜ âŸ©
        0á´® âˆ
      x-yâ‰ˆğŸ˜ : x +á´¬ (-á´¬ y) â‰ˆá´¬ 0á´¬
      x-yâ‰ˆğŸ˜ = fxâ‰ˆğŸ˜â‡’xâ‰ˆğŸ˜ {x = x +á´¬ (-á´¬ y)} ((s , z) , sÂ·[x-y]â‰ˆz , fzâ‰‰ğŸ˜) fx-yâ‰ˆğŸ˜
      xâ‰ˆ--y : x â‰ˆá´¬ -á´¬ (-á´¬ y)
      xâ‰ˆ--y = uniqueÊ³â€¿-á´¬ (-á´¬ y) x
        ( beginá´¬
          -á´¬ y +á´¬ x â‰ˆá´¬âŸ¨ +á´¬-comm (-á´¬ y) x âŸ©
          x +á´¬ -á´¬ y â‰ˆá´¬âŸ¨ x-yâ‰ˆğŸ˜ âŸ©
          0á´¬ âˆá´¬
        )
