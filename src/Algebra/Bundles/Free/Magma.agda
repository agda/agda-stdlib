------------------------------------------------------------------------
-- The Agda standard library
--
-- Definitions of 'pre-free' and 'free' magma
------------------------------------------------------------------------

{-# OPTIONS --cubical-compatible --safe #-}

module Algebra.Bundles.Free.Magma where

open import Algebra.Core
import Algebra.Definitions as Definitions using (Congruentâ‚‚)
import Algebra.Structures as Structures using (IsMagma)
open import Algebra.Morphism.Structures using (IsMagmaHomomorphism)
open import Algebra.Bundles using (Magma)
open import Algebra.Bundles.Raw using (RawMagma)
open import Effect.Functor
open import Effect.Monad
open import Function.Base using (id; _âˆ˜_)
open import Relation.Binary.Core using (Rel)
open import Relation.Binary.Morphism using (IsRelHomomorphism)
open import Level using (Level; _âŠ”_)
open import Relation.Nullary.Negation.Core using (Â¬_)
open import Relation.Binary
  using (Setoid; IsEquivalence; Reflexive; Symmetric; Transitive)
open import Relation.Binary.Morphism.Bundles using (SetoidHomomorphism)
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; congâ‚‚) renaming (refl to â‰¡-refl; isEquivalence to â‰¡-isEquivalence)
import Relation.Binary.Reasoning.Setoid as â‰ˆ-Reasoning

private
  variable
    a â„“a b â„“b c â„“ m â„“m : Level
    A : Set a
    B : Set b
    C : Set c
    ğ“ : Setoid a â„“a
    ğ“‘ : Setoid b â„“b

------------------------------------------------------------------------
-- Syntax: 'pre'-free algebra

module Syntax where

  infixl 7 _âˆ™_

  data Syntax (A : Set a) : Set a where

    var : A â†’ Syntax A
    _âˆ™_ : Opâ‚‚ (Syntax A)

-- Functor instance

  map : (A â†’ B) â†’ Syntax A â†’ Syntax B
  map f (var a) = var (f a)
  map f (s âˆ™ t) = (map f s) âˆ™ (map f t)

  map-id : âˆ€ (t : Syntax A) â†’ map id t â‰¡ t
  map-id (var a) = â‰¡-refl
  map-id (s âˆ™ t) = congâ‚‚ _âˆ™_ (map-id s) (map-id t)

  map-âˆ˜ : (g : A â†’ B) â†’ (f : B â†’ C) â†’ âˆ€ t â†’ map (f âˆ˜ g) t â‰¡ (map f âˆ˜ map g) t
  map-âˆ˜ g f (var a) = â‰¡-refl
  map-âˆ˜ g f (s âˆ™ t) = congâ‚‚ _âˆ™_ (map-âˆ˜ g f s) (map-âˆ˜ g f t)

  syntaxRawFunctor : RawFunctor (Syntax {a})
  syntaxRawFunctor = record { _<$>_ = map }

-- Monad instance

  bind : Syntax A â†’ (A â†’ Syntax B) â†’ Syntax B
  bind (var x) h = h x
  bind (s âˆ™ t) h = bind s h âˆ™ bind t h

  syntaxRawMonad : RawMonad (Syntax {a})
  syntaxRawMonad = mkRawMonad Syntax var bind

------------------------------------------------------------------------
-- parametrised 'equational' theory over the 'pre'-free algebra

module EquationalTheory {A : Set a} (R : Rel A â„“) where

  open Syntax

  infix 4 _â‰ˆ_

  data _â‰ˆ_ : Rel (Syntax A) (a âŠ” â„“)

  open Definitions _â‰ˆ_

  data _â‰ˆ_ where

    var : {a b : A} â†’ (R a b) â†’ var a â‰ˆ var b
    _âˆ™_ : Congruentâ‚‚ _âˆ™_

  refl : Reflexive R â†’ Reflexive _â‰ˆ_
  refl r {var _} = var r
  refl r {_ âˆ™ _} = (refl r) âˆ™ (refl r)

  sym : Symmetric R â†’ Symmetric _â‰ˆ_
  sym s (var r)   = var (s r)
  sym s (râ‚ âˆ™ râ‚‚) = sym s râ‚ âˆ™ sym s râ‚‚

  trans : Transitive R â†’ Transitive _â‰ˆ_
  trans t (var r)   (var s)   = var (t r s)
  trans t (râ‚ âˆ™ râ‚‚) (sâ‚ âˆ™ sâ‚‚) = trans t râ‚ sâ‚ âˆ™ trans t râ‚‚ sâ‚‚

  preservesEquivalence : IsEquivalence R â†’ IsEquivalence _â‰ˆ_
  preservesEquivalence isEq = record
    { refl = refl Eq.refl ; sym = sym Eq.sym ; trans = trans Eq.trans }
    where module Eq = IsEquivalence isEq

------------------------------------------------------------------------
-- Free algebra on a Set
{-
   in the propositional case, we can immediately define the following
   but how best to organise this under the Algebra.Bundles.Free hierarchy?
   e.g. should we distinguish Free.Magma.Setoid from Free.Magma.Propositional?
-}

module FreeRawMagma (A : Set a) where

  open Syntax

  open EquationalTheory {A = A} _â‰¡_

-- inductively-defined equational theory conincides with _â‰¡_

  â‰ˆâ‡’â‰¡ : âˆ€ {m n} â†’ m â‰ˆ n â†’ m â‰¡ n
  â‰ˆâ‡’â‰¡ (var â‰¡-refl) = â‰¡-refl
  â‰ˆâ‡’â‰¡ (eqâ‚ âˆ™ eqâ‚‚) = congâ‚‚ _âˆ™_ (â‰ˆâ‡’â‰¡ eqâ‚) (â‰ˆâ‡’â‰¡ eqâ‚‚)

  â‰¡â‡’â‰ˆ : âˆ€ {m n} â†’ m â‰¡ n â†’ m â‰ˆ n
  â‰¡â‡’â‰ˆ â‰¡-refl = refl â‰¡-refl

  freeRawMagma : RawMagma a a
  freeRawMagma = record { Carrier = Syntax A ; _â‰ˆ_ = _â‰¡_ ; _âˆ™_ = _âˆ™_ }

  open Structures {A = Syntax A} _â‰¡_

  isMagma : IsMagma _âˆ™_
  isMagma = record { isEquivalence = â‰¡-isEquivalence ; âˆ™-cong = congâ‚‚ _âˆ™_ }

  freeMagma : Magma a a
  freeMagma = record { RawMagma freeRawMagma ; isMagma = isMagma }

------------------------------------------------------------------------
-- Free algebra on a Setoid

module FreeMagma (ğ“ : Setoid a â„“a) where

  open Setoid ğ“ renaming (isEquivalence to isEqá´¬; _â‰ˆ_ to _â‰ˆá´¬_)

  open Syntax

  open EquationalTheory _â‰ˆá´¬_ public
    renaming (_â‰ˆ_ to _â‰ˆáµ€_) hiding (refl; sym; trans)

  open Structures _â‰ˆáµ€_

  isMagma : IsMagma  _âˆ™_
  isMagma = record { isEquivalence = preservesEquivalence isEqá´¬ ; âˆ™-cong = _âˆ™_ }

  freeMagma : Magma a (a âŠ” â„“a)
  freeMagma = record { Carrier = Syntax Carrier; _â‰ˆ_ = _â‰ˆáµ€_ ; _âˆ™_ = _âˆ™_ ; isMagma = isMagma }

-- reexport some substructure

  open Magma freeMagma public using (rawMagma; Carrier; _â‰ˆ_)

------------------------------------------------------------------------
-- Semantics: in terms of concrete Magma instances

module _ (ğ“œ : Magma m â„“m) where

  open Magma ğ“œ
    renaming (Carrier to UM; _â‰ˆ_ to _â‰ˆá´¹_; _âˆ™_ to _âˆ™á´¹_
             ; setoid to setoidá´¹; rawMagma to rawMagmaá´¹
             ; isMagma to isMagmaá´¹)
  open â‰ˆ-Reasoning setoidá´¹

  open Syntax

------------------------------------------------------------------------
-- Eval, as the unique fold âŸ¦_âŸ§_ over Syntax

  module Eval (ğ“ : Setoid a â„“a) where

    open Setoid ğ“ renaming (Carrier to UA)

    âŸ¦_âŸ§_ : Syntax UA â†’ (UA â†’ UM) â†’ UM
    âŸ¦ var a âŸ§ Î· = Î· a
    âŸ¦ s âˆ™ t âŸ§ Î· = âŸ¦ s âŸ§ Î· âˆ™á´¹ âŸ¦ t âŸ§ Î·

------------------------------------------------------------------------
-- Any Magma *is* an algebra for the Syntax Functor

  alg : Syntax UM â†’ UM
  alg t = âŸ¦ t âŸ§ id where open Eval setoidá´¹

------------------------------------------------------------------------
-- Properties of âŸ¦_âŸ§_

  module Properties {a â„“a} (ğ“ : Setoid a â„“a) where

    open Setoid ğ“ renaming (Carrier to UA; _â‰ˆ_ to _â‰ˆá´¬_)

    open Eval ğ“ public

    open FreeMagma ğ“ renaming (Carrier to UFA)

    module Existence {Î· : UA â†’ UM} (hom-Î· : IsRelHomomorphism _â‰ˆá´¬_ _â‰ˆá´¹_ Î·) where

      âŸ¦_âŸ§á´¹ : UFA â†’ UM
      âŸ¦_âŸ§á´¹ = âŸ¦_âŸ§ Î·

      open Structures _â‰ˆá´¹_
      open IsMagma isMagmaá´¹ renaming (âˆ™-cong to congá´¹)
      open IsRelHomomorphism hom-Î· renaming (cong to cong-Î·)

      cong-âŸ¦_âŸ§ : âˆ€ {s t} â†’ s â‰ˆ t â†’ âŸ¦ s âŸ§á´¹ â‰ˆá´¹ âŸ¦ t âŸ§á´¹
      cong-âŸ¦ var r âŸ§ = cong-Î· r
      cong-âŸ¦ s âˆ™ t âŸ§ = congá´¹ cong-âŸ¦ s âŸ§ cong-âŸ¦ t âŸ§

      isRelHomomorphism : IsRelHomomorphism _â‰ˆ_ _â‰ˆá´¹_ âŸ¦_âŸ§á´¹
      isRelHomomorphism = record { cong = cong-âŸ¦_âŸ§ }

      isMagmaHomomorphism : IsMagmaHomomorphism rawMagma rawMagmaá´¹ âŸ¦_âŸ§á´¹
      isMagmaHomomorphism = record { isRelHomomorphism = isRelHomomorphism
                                   ; homo = Î» s t â†’ begin âŸ¦ s âŸ§á´¹ âˆ™á´¹ âŸ¦ t âŸ§á´¹ âˆ }

      unfold-âŸ¦_âŸ§á´¹ : âˆ€ t â†’ âŸ¦ t âŸ§á´¹ â‰ˆá´¹ alg (map Î· t)
      unfold-âŸ¦ var a âŸ§á´¹ = begin Î· a âˆ
      unfold-âŸ¦ s âˆ™ t âŸ§á´¹ = congá´¹ unfold-âŸ¦ s âŸ§á´¹ unfold-âŸ¦ t âŸ§á´¹

      module Uniqueness {h : UFA â†’ UM}
        (isHom : IsMagmaHomomorphism rawMagma rawMagmaá´¹ h)
        (hâˆ˜varâ‰ˆá´¹Î· : âˆ€ a â†’ h (var a) â‰ˆá´¹ Î· a) where

        isUniqueâŸ¦_âŸ§á´¹ : âˆ€ t â†’ h t â‰ˆá´¹ âŸ¦ t âŸ§á´¹
        isUniqueâŸ¦ var a âŸ§á´¹ = hâˆ˜varâ‰ˆá´¹Î· a
        isUniqueâŸ¦ s âˆ™ t âŸ§á´¹ = begin
          h (s Syntax.âˆ™ t) â‰ˆâŸ¨ homo s t âŸ©
          h s âˆ™á´¹ h t       â‰ˆâŸ¨ congá´¹ isUniqueâŸ¦ s âŸ§á´¹ isUniqueâŸ¦ t âŸ§á´¹ âŸ©
          âŸ¦ s âŸ§á´¹ âˆ™á´¹ âŸ¦ t âŸ§á´¹  âˆ where open IsMagmaHomomorphism isHom

-- immediate corollary

  open FreeMagma setoidá´¹
  open Properties setoidá´¹

  alg-isMagmaHomomorphism : IsMagmaHomomorphism rawMagma rawMagmaá´¹ alg
  alg-isMagmaHomomorphism = Existence.isMagmaHomomorphism (record { cong = id })

------------------------------------------------------------------------
-- Functoriality of FreeMagma wrt Setoid homomorphisms

module FreeMagmaFunctor (ğ“— : SetoidHomomorphism ğ“ ğ“‘) where

  open Setoid ğ“  renaming (Carrier to UA; _â‰ˆ_ to _â‰ˆá´¬_)
  open Setoid ğ“‘  renaming (Carrier to UB; _â‰ˆ_ to _â‰ˆá´®_)

  open FreeMagma ğ“
    renaming (freeMagma to freeMagmaá´¬; rawMagma to rawMagmaá´¬
             ; Carrier to UFA; _â‰ˆ_ to _â‰ˆáµ€á´¬_; isMagma to isMagmaá´¬)

  open FreeMagma ğ“‘
    renaming (freeMagma to freeMagmaá´®; rawMagma to rawMagmaá´®
             ; Carrier to UFB; _â‰ˆ_ to _â‰ˆáµ€á´®_; isMagma to isMagmaá´®)

  open Properties freeMagmaá´® ğ“

  open SetoidHomomorphism ğ“—

  private
    Î· : UA â†’ UFB
    Î· = Syntax.var âˆ˜ âŸ¦_âŸ§

    hom-Î· : IsRelHomomorphism _â‰ˆá´¬_ _â‰ˆáµ€á´®_ Î·
    hom-Î· = record { cong = EquationalTheory.var âˆ˜ congá´¬á´® }
      where open IsRelHomomorphism isRelHomomorphism renaming (cong to congá´¬á´®)

  map : IsMagmaHomomorphism rawMagmaá´¬ rawMagmaá´® _
  map = Existence.isMagmaHomomorphism hom-Î·

------------------------------------------------------------------------
-- Functoriality of FreeMagmaFunctor.map : TODO

------------------------------------------------------------------------
-- Monad instance: TODO

