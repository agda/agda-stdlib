------------------------------------------------------------------------
-- The Agda standard library
--
-- Definitions of 'pre-free' and 'free' magma
------------------------------------------------------------------------

{-# OPTIONS --cubical-compatible --safe #-}

module Algebra.Bundles.Free.Magma where

open import Algebra.Core
open import Algebra.Bundles using (Magma)
open import Algebra.Bundles.Raw using (RawMagma)
import Algebra.Definitions as Definitions using (Congruentâ‚‚)
import Algebra.Structures as Structures using (IsMagma)
open import Algebra.Morphism.Structures using (IsMagmaHomomorphism)
import Algebra.Morphism.Construct.Identity as Identity
import Algebra.Morphism.Construct.Composition as Compose
open import Effect.Functor
open import Effect.Monad
open import Function.Base using (id; _âˆ˜_)
open import Relation.Binary.Core using (Rel)
open import Relation.Binary.Morphism using (IsRelHomomorphism)
open import Level using (Level; suc; _âŠ”_)
open import Relation.Nullary.Negation.Core using (Â¬_)
open import Relation.Binary
  using (Setoid; IsEquivalence; Reflexive; Symmetric; Transitive)
open import Relation.Binary.Morphism.Bundles using (SetoidHomomorphism)
import Relation.Binary.Morphism.Construct.Identity as Identity
import Relation.Binary.Morphism.Construct.Composition as Compose
open import Relation.Binary.PropositionalEquality
  using (_â‰¡_; _â‰—_; congâ‚‚) renaming (refl to â‰¡-refl; isEquivalence to â‰¡-isEquivalence)
import Relation.Binary.Reasoning.Setoid as â‰ˆ-Reasoning

private
  variable
    â„“ a â„“a b â„“b c â„“c m â„“m n â„“n : Level
    A : Set a
    B : Set b
    C : Set c


------------------------------------------------------------------------
-- Morphisms between Magmas: belongs in its own place
-- Algebra.Morphism.Bundles
-- open import Algebra.Morphism.Bundles using (MagmaHomomorphism)
------------------------------------------------------------------------

record MagmaHomomorphism (ğ“ : Magma a â„“a) (ğ“‘ : Magma b â„“b) : Set (a âŠ” b âŠ” â„“a âŠ” â„“b) where

  open Magma ğ“ renaming (rawMagma to rawMagmaá´¬; setoid to setoidá´¬; Carrier to UA)
  open Magma ğ“‘ renaming (rawMagma to rawMagmaá´®; setoid to setoidá´®; Carrier to UB)

  field
    âŸ¦_âŸ§ : UA â†’ UB

    isMagmaHomomorphism : IsMagmaHomomorphism rawMagmaá´¬ rawMagmaá´® âŸ¦_âŸ§

  open IsMagmaHomomorphism isMagmaHomomorphism public

  setoidHomomorphism : SetoidHomomorphism setoidá´¬ setoidá´®
  setoidHomomorphism = record { âŸ¦_âŸ§ = âŸ¦_âŸ§ ; isRelHomomorphism = isRelHomomorphism }

------------------------------------------------------------------------
-- Syntax: 'pre'-free algebra

module Syntax where

  infixl 7 _âˆ™_

  data Syntax (A : Set a) : Set a where

    var : A â†’ Syntax A
    _âˆ™_ : Opâ‚‚ (Syntax A)

-- Functor instance

  map : (A â†’ B) â†’ Syntax A â†’ Syntax B
  map f (var a) = var (f a)
  map f (s âˆ™ t) = (map f s) âˆ™ (map f t)

  map-id : map {A = A} id â‰— id
  map-id (var a) = â‰¡-refl
  map-id (s âˆ™ t) = congâ‚‚ _âˆ™_ (map-id s) (map-id t)

  map-âˆ˜ : (g : A â†’ B) â†’ (f : B â†’ C) â†’ map (f âˆ˜ g) â‰— (map f âˆ˜ map g)
  map-âˆ˜ g f (var a) = â‰¡-refl
  map-âˆ˜ g f (s âˆ™ t) = congâ‚‚ _âˆ™_ (map-âˆ˜ g f s) (map-âˆ˜ g f t)

  syntaxRawFunctor : RawFunctor (Syntax {a})
  syntaxRawFunctor = record { _<$>_ = map }

-- Monad instance

  bind : Syntax A â†’ (A â†’ Syntax B) â†’ Syntax B
  bind (var x) h = h x
  bind (s âˆ™ t) h = bind s h âˆ™ bind t h

  syntaxRawMonad : RawMonad (Syntax {a})
  syntaxRawMonad = mkRawMonad Syntax var bind


------------------------------------------------------------------------
-- parametrised 'equational' theory over the 'pre'-free algebra

module EquationalTheory {A : Set a} (_â‰ˆá´¬_ : Rel A â„“) where

  open Syntax

  infix 4 _â‰ˆ_

  data _â‰ˆ_ : Rel (Syntax A) (a âŠ” â„“)

  open Definitions _â‰ˆ_

  data _â‰ˆ_ where

    var : {a b : A} â†’ a â‰ˆá´¬ b â†’ var a â‰ˆ var b
    _âˆ™_ : Congruentâ‚‚ _âˆ™_

  refl : Reflexive _â‰ˆá´¬_ â†’ Reflexive _â‰ˆ_
  refl r {var _} = var r
  refl r {_ âˆ™ _} = (refl r) âˆ™ (refl r)

  sym : Symmetric _â‰ˆá´¬_ â†’ Symmetric _â‰ˆ_
  sym s (var sâ‚€)  = var (s sâ‚€)
  sym s (sâ‚ âˆ™ sâ‚‚) = sym s sâ‚ âˆ™ sym s sâ‚‚

  trans : Transitive _â‰ˆá´¬_ â†’ Transitive _â‰ˆ_
  trans t (var râ‚€)  (var sâ‚€)  = var (t râ‚€ sâ‚€)
  trans t (râ‚ âˆ™ râ‚‚) (sâ‚ âˆ™ sâ‚‚) = trans t râ‚ sâ‚ âˆ™ trans t râ‚‚ sâ‚‚

  preservesEquivalence : IsEquivalence _â‰ˆá´¬_ â†’ IsEquivalence _â‰ˆ_
  preservesEquivalence isEq = record
    { refl = refl Eq.refl ; sym = sym Eq.sym ; trans = trans Eq.trans }
    where module Eq = IsEquivalence isEq

  varIsRelHomomorphism : IsRelHomomorphism _â‰ˆá´¬_ _â‰ˆ_ var
  varIsRelHomomorphism = record { cong = var }


------------------------------------------------------------------------
-- Free algebra on a Set
{-
   in the propositional case, we can immediately define the following
   but how best to organise this under the Algebra.Bundles.Free hierarchy?
   e.g. should we distinguish Free.Magma.Setoid from Free.Magma.Propositional?
-}

module FreeRawMagma (A : Set a) where

  open Syntax

  open EquationalTheory {A = A} _â‰¡_

-- inductively-defined equational theory conincides with _â‰¡_

  â‰ˆâ‡’â‰¡ : âˆ€ {m n} â†’ m â‰ˆ n â†’ m â‰¡ n
  â‰ˆâ‡’â‰¡ (var â‰¡-refl) = â‰¡-refl
  â‰ˆâ‡’â‰¡ (eqâ‚ âˆ™ eqâ‚‚) = congâ‚‚ _âˆ™_ (â‰ˆâ‡’â‰¡ eqâ‚) (â‰ˆâ‡’â‰¡ eqâ‚‚)

  â‰¡â‡’â‰ˆ : âˆ€ {m n} â†’ m â‰¡ n â†’ m â‰ˆ n
  â‰¡â‡’â‰ˆ â‰¡-refl = refl â‰¡-refl

  freeRawMagma : RawMagma a a
  freeRawMagma = record { Carrier = Syntax A ; _â‰ˆ_ = _â‰¡_ ; _âˆ™_ = _âˆ™_ }

  open Structures {A = Syntax A} _â‰¡_

  isMagma : IsMagma _âˆ™_
  isMagma = record { isEquivalence = â‰¡-isEquivalence ; âˆ™-cong = congâ‚‚ _âˆ™_ }

  freeMagma : Magma a a
  freeMagma = record { RawMagma freeRawMagma ; isMagma = isMagma }


------------------------------------------------------------------------
-- Free algebra on a Setoid

module FreeMagma (ğ“ : Setoid a â„“a) where

  open Setoid ğ“ renaming (isEquivalence to isEqá´¬; _â‰ˆ_ to _â‰ˆá´¬_)

  open Syntax

  open EquationalTheory _â‰ˆá´¬_ public
    renaming (_â‰ˆ_ to _â‰ˆáµ€_) hiding (refl; sym; trans)

  open Structures _â‰ˆáµ€_

  isMagma : IsMagma  _âˆ™_
  isMagma = record { isEquivalence = preservesEquivalence isEqá´¬ ; âˆ™-cong = _âˆ™_ }

  freeMagma : Magma a (a âŠ” â„“a)
  freeMagma = record { Carrier = Syntax Carrier; _â‰ˆ_ = _â‰ˆáµ€_ ; _âˆ™_ = _âˆ™_ ; isMagma = isMagma }

-- re-export some substructure

  open Magma freeMagma public using (rawMagma; setoid; Carrier; _â‰ˆ_)

  varSetoidHomomorphism : SetoidHomomorphism ğ“ setoid
  varSetoidHomomorphism = record { âŸ¦_âŸ§ = var; isRelHomomorphism = varIsRelHomomorphism }


------------------------------------------------------------------------
-- Semantics: in terms of concrete Magma instances

module _ (ğ“œ : Magma m â„“m) where

  open Magma ğ“œ renaming (setoid to setoidá´¹; Carrier to UM; _âˆ™_ to _âˆ™á´¹_)
  open Syntax

------------------------------------------------------------------------
-- Eval, as the unique fold âŸ¦_âŸ§_ over Syntax

  module Eval (ğ“ : Setoid a â„“a) where

    open Setoid ğ“ renaming (Carrier to UA)

    âŸ¦_âŸ§_ : Syntax UA â†’ (UA â†’ UM) â†’ UM
    âŸ¦ var a âŸ§ Î· = Î· a
    âŸ¦ s âˆ™ t âŸ§ Î· = âŸ¦ s âŸ§ Î· âˆ™á´¹ âŸ¦ t âŸ§ Î·

------------------------------------------------------------------------
-- Any Magma *is* an algebra for the Syntax Functor

  alg : Syntax UM â†’ UM
  alg t = âŸ¦ t âŸ§ id where open Eval setoidá´¹

------------------------------------------------------------------------
-- âŸ¦_âŸ§_ defines the (unique) lifting of Setoid homomorphisms to Magma homomorphisms

module LeftAdjoint {ğ“ : Setoid a â„“a} (ğ“œ : Magma m â„“m)
       (ğ“— :  SetoidHomomorphism ğ“ (Magma.setoid ğ“œ)) where

  open Magma ğ“œ
    renaming (Carrier to UM; _â‰ˆ_ to _â‰ˆá´¹_; _âˆ™_ to _âˆ™á´¹_
             ; setoid to setoidá´¹; rawMagma to rawMagmaá´¹
             ; isMagma to isMagmaá´¹)

  open â‰ˆ-Reasoning setoidá´¹

  open Syntax

  open Setoid ğ“ renaming (Carrier to UA; _â‰ˆ_ to _â‰ˆá´¬_)

  open Eval ğ“œ ğ“ public

  open FreeMagma ğ“ renaming (setoid to FA; Carrier to UFA)

  open SetoidHomomorphism ğ“— renaming (âŸ¦_âŸ§ to Î·; isRelHomomorphism to hom-Î·)

  private

    âŸ¦_âŸ§á´¹ : UFA â†’ UM
    âŸ¦_âŸ§á´¹ = âŸ¦_âŸ§ Î·

  open Structures _â‰ˆá´¹_
  open IsMagma isMagmaá´¹ renaming (âˆ™-cong to congá´¹)
  open IsRelHomomorphism hom-Î· renaming (cong to cong-Î·)

  module Existence where

    private
      algá´¹ = alg ğ“œ

    unfold-âŸ¦_âŸ§á´¹ : âˆ€ t â†’ âŸ¦ t âŸ§á´¹ â‰ˆá´¹ algá´¹ (map Î· t)
    unfold-âŸ¦ var a âŸ§á´¹ = begin Î· a âˆ
    unfold-âŸ¦ s âˆ™ t âŸ§á´¹ = congá´¹ unfold-âŸ¦ s âŸ§á´¹ unfold-âŸ¦ t âŸ§á´¹

    cong-âŸ¦_âŸ§ : âˆ€ {s t} â†’ s â‰ˆ t â†’ âŸ¦ s âŸ§á´¹ â‰ˆá´¹ âŸ¦ t âŸ§á´¹
    cong-âŸ¦ var r âŸ§ = cong-Î· r
    cong-âŸ¦ s âˆ™ t âŸ§ = congá´¹ cong-âŸ¦ s âŸ§ cong-âŸ¦ t âŸ§

    isRelHomomorphismá´¹ : IsRelHomomorphism _â‰ˆ_ _â‰ˆá´¹_ âŸ¦_âŸ§á´¹
    isRelHomomorphismá´¹ = record { cong = cong-âŸ¦_âŸ§ }

    setoidHomomorphismá´¹ : SetoidHomomorphism FA setoidá´¹
    setoidHomomorphismá´¹ = record { âŸ¦_âŸ§ = âŸ¦_âŸ§á´¹ ; isRelHomomorphism = isRelHomomorphismá´¹ }

    isMagmaHomomorphismá´¹ : IsMagmaHomomorphism rawMagma rawMagmaá´¹ âŸ¦_âŸ§á´¹
    isMagmaHomomorphismá´¹ = record { isRelHomomorphism = isRelHomomorphismá´¹
                                  ; homo = Î» s t â†’ begin âŸ¦ s âŸ§á´¹ âˆ™á´¹ âŸ¦ t âŸ§á´¹ âˆ }

    magmaHomomorphismá´¹ : MagmaHomomorphism freeMagma ğ“œ
    magmaHomomorphismá´¹ = record { âŸ¦_âŸ§ = âŸ¦_âŸ§á´¹
                               ; isMagmaHomomorphism = isMagmaHomomorphismá´¹ }

  record Î·-MagmaHomomorphism : Set (suc (a âŠ” m âŠ” â„“a âŠ” â„“m)) where

    field
      magmaHomomorphism : MagmaHomomorphism freeMagma ğ“œ
    open MagmaHomomorphism magmaHomomorphism public renaming (homo to âŸ¦âŸ§-homo)
    field
      âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· : âˆ€ a â†’ âŸ¦ var a âŸ§ â‰ˆá´¹ Î· a

  âŸ¦âŸ§á´¹-Î·-MagmaHomomorphism : Î·-MagmaHomomorphism
  âŸ¦âŸ§á´¹-Î·-MagmaHomomorphism = record { magmaHomomorphism = Existence.magmaHomomorphismá´¹
                                   ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Existence.unfold-âŸ¦_âŸ§á´¹ âˆ˜ var }

  module Uniqueness (Î·-magmaHomomorphism : Î·-MagmaHomomorphism) where

    open Î·-MagmaHomomorphism Î·-magmaHomomorphism

    isUniqueâŸ¦_âŸ§á´¹ : âˆ€ t â†’ âŸ¦ t âŸ§ â‰ˆá´¹ âŸ¦ t âŸ§á´¹
    isUniqueâŸ¦ var a âŸ§á´¹ = âŸ¦ a âŸ§âˆ˜varâ‰ˆá´¹Î·
    isUniqueâŸ¦ s âˆ™ t âŸ§á´¹ = begin
        âŸ¦ s Syntax.âˆ™ t âŸ§  â‰ˆâŸ¨ âŸ¦âŸ§-homo s t âŸ©
        âŸ¦ s âŸ§ âˆ™á´¹ âŸ¦ t âŸ§    â‰ˆâŸ¨ congá´¹ isUniqueâŸ¦ s âŸ§á´¹ isUniqueâŸ¦ t âŸ§á´¹ âŸ©
        âŸ¦ s âŸ§á´¹ âˆ™á´¹ âŸ¦ t âŸ§á´¹  âˆ

  module Corollary (ğ“— ğ“š : Î·-MagmaHomomorphism)
    where
      open Î·-MagmaHomomorphism ğ“— renaming (âŸ¦_âŸ§ to âŸ¦_âŸ§á´´)
      open Î·-MagmaHomomorphism ğ“š renaming (âŸ¦_âŸ§ to âŸ¦_âŸ§á´·)
      open Uniqueness ğ“— renaming (isUniqueâŸ¦_âŸ§á´¹ to isUniqueâŸ¦_âŸ§á´´)
      open Uniqueness ğ“š renaming (isUniqueâŸ¦_âŸ§á´¹ to isUniqueâŸ¦_âŸ§á´·)

      isUniqueâŸ¦_âŸ§ :  âˆ€ t â†’ âŸ¦ t âŸ§á´´ â‰ˆá´¹ âŸ¦ t âŸ§á´·
      isUniqueâŸ¦ t âŸ§ = begin âŸ¦ t âŸ§á´´ â‰ˆâŸ¨ isUniqueâŸ¦ t âŸ§á´´ âŸ© âŸ¦ t âŸ§á´¹ â‰ˆË˜âŸ¨ isUniqueâŸ¦ t âŸ§á´· âŸ© âŸ¦ t âŸ§á´· âˆ

------------------------------------------------------------------------
-- Immediate corollary: alg is in fact a MagmaHomomorphism

module _ (ğ“œ : Magma m â„“m) where
  open Magma ğ“œ renaming (setoid to setoidá´¹; _â‰ˆ_ to _â‰ˆá´¹_; isMagma to isMagmaá´¹)
  open FreeMagma setoidá´¹

  algMagmaHomomorphism : MagmaHomomorphism freeMagma ğ“œ
  algMagmaHomomorphism = Existence.magmaHomomorphismá´¹
    where open LeftAdjoint ğ“œ (Identity.setoidHomomorphism setoidá´¹)


------------------------------------------------------------------------
-- Action of FreeMagma on Setoid homomorphisms

module FreeMagmaFunctor {ğ“ : Setoid a â„“a} {ğ“‘ : Setoid b â„“b}
       (ğ“— : SetoidHomomorphism ğ“ ğ“‘) where

  open FreeMagma ğ“ renaming (freeMagma to freeMagmaá´¬)
  open FreeMagma ğ“‘ renaming (freeMagma to freeMagmaá´®
                             ; varSetoidHomomorphism to ğ“¥á´®)

  mapMagmaHomomorphism : MagmaHomomorphism freeMagmaá´¬ freeMagmaá´®
  mapMagmaHomomorphism = Existence.magmaHomomorphismá´¹
    where open LeftAdjoint freeMagmaá´® (Compose.setoidHomomorphism ğ“— ğ“¥á´®)

------------------------------------------------------------------------
-- Naturality of alg

module Naturality {ğ“œ : Magma m â„“m} {ğ“ : Magma n â„“n} where
  open Magma ğ“œ using () renaming (setoid to setoidá´¹)
  open Magma ğ“ using () renaming (_â‰ˆ_ to _â‰ˆá´º_; refl to reflá´º; trans to transá´º)

  module _ (ğ“• : MagmaHomomorphism ğ“œ ğ“) where
    open MagmaHomomorphism ğ“• using (âŸ¦_âŸ§; isMagmaHomomorphism; setoidHomomorphism)
    open FreeMagmaFunctor setoidHomomorphism using (mapMagmaHomomorphism)
    open MagmaHomomorphism mapMagmaHomomorphism
      renaming (âŸ¦_âŸ§ to map; isMagmaHomomorphism to map-isMagmaHomomorphism; setoidHomomorphism to mapSetoidHomomorphism)
    open FreeMagma setoidá´¹ renaming (freeMagma to freeMagmaá´¹)
    open LeftAdjoint ğ“ setoidHomomorphism
    open MagmaHomomorphism (algMagmaHomomorphism ğ“œ)
      renaming (âŸ¦_âŸ§ to algá´¹; isMagmaHomomorphism to algá´¹-isMagmaHomomorphism)
    open MagmaHomomorphism (algMagmaHomomorphism ğ“)
      renaming (âŸ¦_âŸ§ to algá´º; isMagmaHomomorphism to algá´º-isMagmaHomomorphism)

    naturality : âˆ€ t â†’ âŸ¦ algá´¹ t âŸ§ â‰ˆá´º algá´º (map t)
    naturality = Corollary.isUniqueâŸ¦_âŸ§ ğ“— ğ“š
      where
        H K : MagmaHomomorphism freeMagmaá´¹ ğ“
        H = record { âŸ¦_âŸ§ = âŸ¦_âŸ§ âˆ˜ algá´¹
            ; isMagmaHomomorphism = Compose.isMagmaHomomorphism transá´º algá´¹-isMagmaHomomorphism isMagmaHomomorphism }

        K = record { âŸ¦_âŸ§ = algá´º âˆ˜  map
            ; isMagmaHomomorphism = Compose.isMagmaHomomorphism transá´º map-isMagmaHomomorphism algá´º-isMagmaHomomorphism }

        ğ“— ğ“š : Î·-MagmaHomomorphism
        ğ“— = record { magmaHomomorphism = H ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Î» _ â†’ reflá´º }
        ğ“š = record { magmaHomomorphism = K ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Î» _ â†’ reflá´º }


------------------------------------------------------------------------
-- Functoriality of FreeMagmaFunctor.map : by analogy with naturality

module IdentityLaw (ğ“ : Setoid a â„“a) where

  open FreeMagma ğ“ renaming (varSetoidHomomorphism to ğ“¥)
  open Setoid setoid renaming (_â‰ˆ_ to _â‰ˆFA_; refl to reflFA)

  Id : MagmaHomomorphism freeMagma freeMagma
  Id = record
    { âŸ¦_âŸ§ = id
    ; isMagmaHomomorphism = Identity.isMagmaHomomorphism rawMagma reflFA}

  open FreeMagmaFunctor (Identity.setoidHomomorphism ğ“)
  open MagmaHomomorphism mapMagmaHomomorphism renaming (âŸ¦_âŸ§ to map-Id)

  map-id : âˆ€ t â†’ map-Id t â‰ˆFA t
  map-id = Corollary.isUniqueâŸ¦_âŸ§ ğ“˜á´¬ ğ“˜
    where
      open LeftAdjoint freeMagma ğ“¥
      ğ“˜á´¬ ğ“˜ : Î·-MagmaHomomorphism
      ğ“˜á´¬ = record { magmaHomomorphism = mapMagmaHomomorphism ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Î» _ â†’ reflFA }
      ğ“˜ = record { magmaHomomorphism = Id ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Î» _ â†’ reflFA }

module CompositionLaw
  {ğ“ : Setoid a â„“a} {ğ“‘ : Setoid b â„“b} {ğ“’ : Setoid c â„“c}
  (ğ“— : SetoidHomomorphism ğ“ ğ“‘) (ğ“š : SetoidHomomorphism ğ“‘ ğ“’) where

  ğ“• : SetoidHomomorphism ğ“ ğ“’
  ğ“• = Compose.setoidHomomorphism ğ“— ğ“š

  open FreeMagma ğ“ renaming (freeMagma to freeMagmaA)
  open FreeMagma ğ“‘ renaming (freeMagma to freeMagmaB)
  open FreeMagma ğ“’ renaming (freeMagma to freeMagmaC
                             ; setoid to setoidFC
                             ; varSetoidHomomorphism to ğ“¥)
  open Setoid setoidFC renaming (_â‰ˆ_ to _â‰ˆFC_; refl to reflFC; trans to transFC)
  ğ“¥âˆ˜ğ“• = Compose.setoidHomomorphism ğ“• ğ“¥
  open FreeMagmaFunctor ğ“• renaming (mapMagmaHomomorphism to MapAC)
  open FreeMagmaFunctor ğ“— renaming (mapMagmaHomomorphism to MapAB)
  open FreeMagmaFunctor ğ“š renaming (mapMagmaHomomorphism to MapBC)
  open MagmaHomomorphism MapAC renaming (âŸ¦_âŸ§ to mapAC)
  open MagmaHomomorphism MapAB renaming (âŸ¦_âŸ§ to mapAB; isMagmaHomomorphism to isMagmaAB)
  open MagmaHomomorphism MapBC renaming (âŸ¦_âŸ§ to mapBC; isMagmaHomomorphism to isMagmaBC)

  MapBCâˆ˜MapAB : MagmaHomomorphism freeMagmaA freeMagmaC
  MapBCâˆ˜MapAB = record
    { âŸ¦_âŸ§ = mapBC âˆ˜ mapAB
    ; isMagmaHomomorphism = Compose.isMagmaHomomorphism transFC isMagmaAB isMagmaBC}

  map-âˆ˜ : âˆ€ t â†’ mapAC t â‰ˆFC mapBC (mapAB t)
  map-âˆ˜ = Corollary.isUniqueâŸ¦_âŸ§ ğ“ğ“’ ğ“‘ğ“’âˆ˜ğ“ğ“‘
    where
      open LeftAdjoint freeMagmaC ğ“¥âˆ˜ğ“•
      ğ“ğ“’ ğ“‘ğ“’âˆ˜ğ“ğ“‘ : Î·-MagmaHomomorphism
      ğ“ğ“’ = record { magmaHomomorphism = MapAC ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Î» _ â†’ reflFC }
      ğ“‘ğ“’âˆ˜ğ“ğ“‘ = record { magmaHomomorphism = MapBCâˆ˜MapAB ; âŸ¦_âŸ§âˆ˜varâ‰ˆá´¹Î· = Î» _ â†’ reflFC }


------------------------------------------------------------------------
-- Monad instance, etc.: TODO
