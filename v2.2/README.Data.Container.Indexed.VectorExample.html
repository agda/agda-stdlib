<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.Container.Indexed.VectorExample</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Example showing how to define an indexed container</a>
<a id="160" class="Comment">------------------------------------------------------------------------</a>

<a id="234" class="Symbol">{-#</a> <a id="238" class="Keyword">OPTIONS</a> <a id="246" class="Pragma">--with-K</a> <a id="255" class="Pragma">--safe</a> <a id="262" class="Pragma">--guardedness</a> <a id="276" class="Symbol">#-}</a>

<a id="281" class="Keyword">module</a> <a id="288" href="README.Data.Container.Indexed.VectorExample.html" class="Module">README.Data.Container.Indexed.VectorExample</a> <a id="332" class="Keyword">where</a>

<a id="339" class="Keyword">open</a> <a id="344" class="Keyword">import</a> <a id="351" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="361" class="Keyword">open</a> <a id="366" class="Keyword">import</a> <a id="373" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="384" class="Keyword">open</a> <a id="389" class="Keyword">import</a> <a id="396" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
<a id="410" class="Keyword">open</a> <a id="415" class="Keyword">import</a> <a id="422" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="440" class="Keyword">using</a> <a id="446" class="Symbol">(</a><a id="447" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="450" class="Symbol">)</a>
<a id="452" class="Keyword">open</a> <a id="457" class="Keyword">import</a> <a id="464" href="Function.Base.html" class="Module">Function.Base</a> <a id="478" class="Keyword">using</a> <a id="484" class="Symbol">(</a><a id="485" href="Function.Base.html#4486" class="Function Operator">_∋_</a><a id="488" class="Symbol">)</a>
<a id="490" class="Keyword">open</a> <a id="495" class="Keyword">import</a> <a id="502" href="Data.W.Indexed.html" class="Module">Data.W.Indexed</a>
<a id="517" class="Keyword">open</a> <a id="522" class="Keyword">import</a> <a id="529" href="Data.Container.Indexed.html" class="Module">Data.Container.Indexed</a>
<a id="552" class="Keyword">open</a> <a id="557" class="Keyword">import</a> <a id="564" href="Data.Container.Indexed.WithK.html" class="Module">Data.Container.Indexed.WithK</a>

<a id="594" class="Keyword">module</a> <a id="601" href="README.Data.Container.Indexed.VectorExample.html#601" class="Module">_</a> <a id="603" class="Symbol">{</a><a id="604" href="README.Data.Container.Indexed.VectorExample.html#604" class="Bound">a</a><a id="605" class="Symbol">}</a> <a id="607" class="Symbol">(</a><a id="608" href="README.Data.Container.Indexed.VectorExample.html#608" class="Bound">A</a> <a id="610" class="Symbol">:</a> <a id="612" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="616" href="README.Data.Container.Indexed.VectorExample.html#604" class="Bound">a</a><a id="617" class="Symbol">)</a> <a id="619" class="Keyword">where</a>

<a id="626" class="Comment">------------------------------------------------------------------------</a>
<a id="699" class="Comment">-- Vector as an indexed container</a>

<a id="734" class="Comment">-- An indexed container is defined by three things:</a>
<a id="786" class="Comment">-- 1. Commands the user can emit</a>
<a id="819" class="Comment">-- 2. Responses the indexed container returns to these commands</a>
<a id="883" class="Comment">-- 3. Update of the index based on the command and the response issued.</a>

<a id="956" class="Comment">-- For a vector, commands are constructors, responses are the number</a>
<a id="1025" class="Comment">-- of subvectors (0 if the vector is empty, 1 otherwise) and the</a>
<a id="1090" class="Comment">-- update corresponds to setting the size of the tail (if it exists).</a>
<a id="1160" class="Comment">-- We can formalize these ideas like so:</a>

<a id="1202" class="Comment">-- Depending on the size of the vector, we may have reached the end</a>
<a id="1270" class="Comment">-- already (nil) or we may specify what the head should be (cons).</a>
<a id="1337" class="Comment">-- This is the type of commands.</a>

  <a id="1373" class="Keyword">data</a> <a id="1378" href="README.Data.Container.Indexed.VectorExample.html#1378" class="Datatype">VecC</a> <a id="1383" class="Symbol">:</a> <a id="1385" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1387" class="Symbol">→</a> <a id="1389" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1393" href="README.Data.Container.Indexed.VectorExample.html#604" class="Bound">a</a> <a id="1395" class="Keyword">where</a>
    <a id="1405" href="README.Data.Container.Indexed.VectorExample.html#1405" class="InductiveConstructor">nil</a>  <a id="1410" class="Symbol">:</a>           <a id="1422" href="README.Data.Container.Indexed.VectorExample.html#1378" class="Datatype">VecC</a> <a id="1427" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>
    <a id="1436" href="README.Data.Container.Indexed.VectorExample.html#1436" class="InductiveConstructor">cons</a> <a id="1441" class="Symbol">:</a> <a id="1443" class="Symbol">∀</a> <a id="1445" href="README.Data.Container.Indexed.VectorExample.html#1445" class="Bound">n</a> <a id="1447" class="Symbol">→</a> <a id="1449" href="README.Data.Container.Indexed.VectorExample.html#608" class="Bound">A</a> <a id="1451" class="Symbol">→</a> <a id="1453" href="README.Data.Container.Indexed.VectorExample.html#1378" class="Datatype">VecC</a> <a id="1458" class="Symbol">(</a><a id="1459" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1463" href="README.Data.Container.Indexed.VectorExample.html#1445" class="Bound">n</a><a id="1464" class="Symbol">)</a>

  <a id="1469" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="1473" class="Symbol">:</a> <a id="1475" href="Data.Container.Indexed.Core.html#573" class="Record">Container</a> <a id="1485" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1487" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="1489" href="README.Data.Container.Indexed.VectorExample.html#604" class="Bound">a</a> <a id="1491" class="Symbol">_</a>
  <a id="1495" href="Data.Container.Indexed.Core.html#679" class="Field">Command</a> <a id="1503" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="1507" class="Symbol">=</a> <a id="1509" href="README.Data.Container.Indexed.VectorExample.html#1378" class="Datatype">VecC</a>

<a id="1515" class="Comment">-- We then treat each command independently, specifying both the response and the</a>
<a id="1597" class="Comment">-- next index based on that response.</a>

<a id="1636" class="Comment">-- In the nil case, the response is the empty type: there won&#39;t be any tail. As</a>
<a id="1716" class="Comment">-- a consequence, the next index won&#39;t be needed (and we can rely on the fact the</a>
<a id="1798" class="Comment">-- user will never be able to call it).</a>

  <a id="1841" href="Data.Container.Indexed.Core.html#710" class="Field">Response</a> <a id="1850" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="1854" href="README.Data.Container.Indexed.VectorExample.html#1405" class="InductiveConstructor">nil</a> <a id="1858" class="Symbol">=</a> <a id="1860" href="Data.Empty.html#914" class="Function">⊥</a>
  <a id="1864" href="Data.Container.Indexed.Core.html#751" class="Field">next</a>     <a id="1873" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="1877" href="README.Data.Container.Indexed.VectorExample.html#1405" class="InductiveConstructor">nil</a> <a id="1881" class="Symbol">=</a> <a id="1883" class="Symbol">λ</a> <a id="1885" class="Symbol">()</a>

<a id="1889" class="Comment">-- In the cons case, the response is the unit type: there is exactly one tail. The</a>
<a id="1972" class="Comment">-- next index is the predecessor of the current one. It is handily handed over to</a>
<a id="2054" class="Comment">-- use by `cons`.</a>

  <a id="2075" class="Comment">-- cons</a>
  <a id="2085" href="Data.Container.Indexed.Core.html#710" class="Field">Response</a> <a id="2094" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="2098" class="Symbol">(</a><a id="2099" href="README.Data.Container.Indexed.VectorExample.html#1436" class="InductiveConstructor">cons</a> <a id="2104" href="README.Data.Container.Indexed.VectorExample.html#2104" class="Bound">n</a> <a id="2106" href="README.Data.Container.Indexed.VectorExample.html#2106" class="Bound">a</a><a id="2107" class="Symbol">)</a> <a id="2109" class="Symbol">=</a> <a id="2111" href="Agda.Builtin.Unit.html#175" class="Record">⊤</a>
  <a id="2115" href="Data.Container.Indexed.Core.html#751" class="Field">next</a>     <a id="2124" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="2128" class="Symbol">(</a><a id="2129" href="README.Data.Container.Indexed.VectorExample.html#1436" class="InductiveConstructor">cons</a> <a id="2134" href="README.Data.Container.Indexed.VectorExample.html#2134" class="Bound">n</a> <a id="2136" href="README.Data.Container.Indexed.VectorExample.html#2136" class="Bound">a</a><a id="2137" class="Symbol">)</a> <a id="2139" class="Symbol">=</a> <a id="2141" class="Symbol">λ</a> <a id="2143" href="README.Data.Container.Indexed.VectorExample.html#2143" class="Bound">_</a> <a id="2145" class="Symbol">→</a> <a id="2147" href="README.Data.Container.Indexed.VectorExample.html#2134" class="Bound">n</a>

<a id="2150" class="Comment">-- Finally we can define the type of Vector as the least fixed point of Vec.</a>

  <a id="2230" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="2237" class="Symbol">:</a> <a id="2239" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2241" class="Symbol">→</a> <a id="2243" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2247" href="README.Data.Container.Indexed.VectorExample.html#604" class="Bound">a</a>
  <a id="2251" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="2258" class="Symbol">=</a> <a id="2260" href="Data.Container.Indexed.html#1290" class="Function">μ</a> <a id="2262" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a>

<a id="2267" class="Keyword">module</a> <a id="2274" href="README.Data.Container.Indexed.VectorExample.html#2274" class="Module">_</a> <a id="2276" class="Symbol">{</a><a id="2277" href="README.Data.Container.Indexed.VectorExample.html#2277" class="Bound">a</a><a id="2278" class="Symbol">}</a> <a id="2280" class="Symbol">{</a><a id="2281" href="README.Data.Container.Indexed.VectorExample.html#2281" class="Bound">A</a> <a id="2283" class="Symbol">:</a> <a id="2285" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2289" href="README.Data.Container.Indexed.VectorExample.html#2277" class="Bound">a</a><a id="2290" class="Symbol">}</a> <a id="2292" class="Keyword">where</a>

<a id="2299" class="Comment">-- We can recover the usual constructors by using `sup` to enter the fixpoint</a>
<a id="2377" class="Comment">-- and then using the appropriate pairing of a command &amp; a handler for the</a>
<a id="2452" class="Comment">-- response.</a>

<a id="2466" class="Comment">-- For [], the response is ⊥ which makes it easy to conclude.</a>

  <a id="2531" href="README.Data.Container.Indexed.VectorExample.html#2531" class="Function">[]</a> <a id="2534" class="Symbol">:</a> <a id="2536" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="2543" href="README.Data.Container.Indexed.VectorExample.html#2281" class="Bound">A</a> <a id="2545" class="Number">0</a>
  <a id="2549" href="README.Data.Container.Indexed.VectorExample.html#2531" class="Function">[]</a> <a id="2552" class="Symbol">=</a> <a id="2554" href="Data.W.Indexed.html#590" class="InductiveConstructor">sup</a> <a id="2558" class="Symbol">(</a><a id="2559" href="README.Data.Container.Indexed.VectorExample.html#1405" class="InductiveConstructor">nil</a> <a id="2563" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2565" class="Symbol">λ</a> <a id="2567" class="Symbol">())</a>

<a id="2572" class="Comment">-- For _∷_, the response is ⊤ so we need to pass a tail. We give the one we took</a>
<a id="2653" class="Comment">-- as an argument.</a>

  <a id="2675" class="Keyword">infixr</a> <a id="2682" class="Number">3</a> <a id="2684" href="README.Data.Container.Indexed.VectorExample.html#2690" class="Function Operator">_∷_</a>
  <a id="2690" href="README.Data.Container.Indexed.VectorExample.html#2690" class="Function Operator">_∷_</a> <a id="2694" class="Symbol">:</a> <a id="2696" class="Symbol">∀</a> <a id="2698" class="Symbol">{</a><a id="2699" href="README.Data.Container.Indexed.VectorExample.html#2699" class="Bound">n</a><a id="2700" class="Symbol">}</a> <a id="2702" class="Symbol">→</a> <a id="2704" href="README.Data.Container.Indexed.VectorExample.html#2281" class="Bound">A</a> <a id="2706" class="Symbol">→</a> <a id="2708" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="2715" href="README.Data.Container.Indexed.VectorExample.html#2281" class="Bound">A</a> <a id="2717" href="README.Data.Container.Indexed.VectorExample.html#2699" class="Bound">n</a> <a id="2719" class="Symbol">→</a> <a id="2721" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="2728" href="README.Data.Container.Indexed.VectorExample.html#2281" class="Bound">A</a> <a id="2730" class="Symbol">(</a><a id="2731" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2735" href="README.Data.Container.Indexed.VectorExample.html#2699" class="Bound">n</a><a id="2736" class="Symbol">)</a>
  <a id="2740" href="README.Data.Container.Indexed.VectorExample.html#2740" class="Bound">x</a> <a id="2742" href="README.Data.Container.Indexed.VectorExample.html#2690" class="Function Operator">∷</a> <a id="2744" href="README.Data.Container.Indexed.VectorExample.html#2744" class="Bound">xs</a> <a id="2747" class="Symbol">=</a> <a id="2749" href="Data.W.Indexed.html#590" class="InductiveConstructor">sup</a> <a id="2753" class="Symbol">(</a><a id="2754" href="README.Data.Container.Indexed.VectorExample.html#1436" class="InductiveConstructor">cons</a> <a id="2759" class="Symbol">_</a> <a id="2761" href="README.Data.Container.Indexed.VectorExample.html#2740" class="Bound">x</a> <a id="2763" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2765" class="Symbol">λ</a> <a id="2767" href="README.Data.Container.Indexed.VectorExample.html#2767" class="Bound">_</a> <a id="2769" class="Symbol">→</a> <a id="2771" href="README.Data.Container.Indexed.VectorExample.html#2744" class="Bound">xs</a><a id="2773" class="Symbol">)</a>

<a id="2776" class="Comment">-- We can now use these constructors to build up vectors:</a>

<a id="1⋯3"></a><a id="2835" href="README.Data.Container.Indexed.VectorExample.html#2835" class="Function">1⋯3</a> <a id="2839" class="Symbol">:</a> <a id="2841" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="2848" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2850" class="Number">3</a>
<a id="2852" href="README.Data.Container.Indexed.VectorExample.html#2835" class="Function">1⋯3</a> <a id="2856" class="Symbol">=</a> <a id="2858" class="Number">1</a> <a id="2860" href="README.Data.Container.Indexed.VectorExample.html#2690" class="Function Operator">∷</a> <a id="2862" class="Number">2</a> <a id="2864" href="README.Data.Container.Indexed.VectorExample.html#2690" class="Function Operator">∷</a> <a id="2866" class="Number">3</a> <a id="2868" href="README.Data.Container.Indexed.VectorExample.html#2690" class="Function Operator">∷</a> <a id="2870" href="README.Data.Container.Indexed.VectorExample.html#2531" class="Function">[]</a>





<a id="2878" class="Comment">-- Horrible thing to check the definition of _∈_ is not buggy.</a>
<a id="2941" class="Comment">-- Not sure whether we can say anything interesting about it in the case of Vector...</a>

<a id="3028" class="Keyword">open</a> <a id="3033" class="Keyword">import</a> <a id="3040" href="Relation.Binary.HeterogeneousEquality.html" class="Module">Relation.Binary.HeterogeneousEquality</a>

<a id="3079" href="README.Data.Container.Indexed.VectorExample.html#3079" class="Function">_</a> <a id="3081" class="Symbol">:</a> <a id="3083" href="Data.Container.Indexed.WithK.html#6726" class="Function Operator">_∈_</a> <a id="3087" class="Symbol">{</a><a id="3088" class="Argument">C</a> <a id="3090" class="Symbol">=</a> <a id="3092" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="3096" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="3097" class="Symbol">}</a> <a id="3099" class="Symbol">{</a><a id="3100" class="Argument">X</a> <a id="3102" class="Symbol">=</a> <a id="3104" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="3111" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="3112" class="Symbol">}</a> <a id="3114" href="README.Data.Container.Indexed.VectorExample.html#2835" class="Function">1⋯3</a> <a id="3118" class="Symbol">(</a><a id="3119" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟦</a> <a id="3121" href="README.Data.Container.Indexed.VectorExample.html#1469" class="Function">Vec</a> <a id="3125" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="3127" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟧</a> <a id="3129" class="Symbol">(</a><a id="3130" href="README.Data.Container.Indexed.VectorExample.html#2230" class="Function">Vector</a> <a id="3137" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="3138" class="Symbol">)</a> <a id="3140" class="Number">4</a> <a id="3142" href="Function.Base.html#4486" class="Function Operator">∋</a> <a id="3144" href="README.Data.Container.Indexed.VectorExample.html#1436" class="InductiveConstructor">cons</a> <a id="3149" class="Symbol">_</a> <a id="3151" class="Number">0</a> <a id="3153" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3155" class="Symbol">λ</a> <a id="3157" href="README.Data.Container.Indexed.VectorExample.html#3157" class="Bound">_</a> <a id="3159" class="Symbol">→</a> <a id="3161" href="README.Data.Container.Indexed.VectorExample.html#2835" class="Function">1⋯3</a><a id="3164" class="Symbol">)</a>
<a id="3166" class="Symbol">_</a> <a id="3168" class="Symbol">=</a> <a id="3170" class="Symbol">_</a> <a id="3172" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="3174" href="Relation.Binary.HeterogeneousEquality.Core.html#740" class="InductiveConstructor">refl</a>
</pre></body></html>