<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>README.Data.List.Fresh</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Example use case for a fresh list: sorted list</a>
<a id="156" class="Comment">------------------------------------------------------------------------</a>

<a id="230" class="Keyword">module</a> <a id="237" href="README.Data.List.Fresh.html" class="Module">README.Data.List.Fresh</a> <a id="260" class="Keyword">where</a>

<a id="267" class="Keyword">open</a> <a id="272" class="Keyword">import</a> <a id="279" href="Data.Nat.html" class="Module">Data.Nat</a>
<a id="288" class="Keyword">open</a> <a id="293" class="Keyword">import</a> <a id="300" href="Data.List.Base.html" class="Module">Data.List.Base</a>
<a id="315" class="Keyword">open</a> <a id="320" class="Keyword">import</a> <a id="327" href="Data.List.Fresh.html" class="Module">Data.List.Fresh</a>
<a id="343" class="Keyword">open</a> <a id="348" class="Keyword">import</a> <a id="355" href="Data.List.Relation.Unary.AllPairs.html" class="Module">Data.List.Relation.Unary.AllPairs</a> <a id="389" class="Symbol">as</a> <a id="392" class="Module">AllPairs</a> <a id="401" class="Keyword">using</a> <a id="407" class="Symbol">(</a><a id="408" href="Data.List.Relation.Unary.AllPairs.Core.html#941" class="Datatype">AllPairs</a><a id="416" class="Symbol">)</a>
<a id="418" class="Keyword">open</a> <a id="423" class="Keyword">import</a> <a id="430" href="Data.Product.html" class="Module">Data.Product</a>
<a id="443" class="Keyword">open</a> <a id="448" class="Keyword">import</a> <a id="455" href="Relation.Nary.html" class="Module">Relation.Nary</a> <a id="469" class="Keyword">using</a> <a id="475" class="Symbol">(</a><a id="476" href="Relation.Nary.html#6523" class="Function Operator">⌊_⌋</a><a id="479" class="Symbol">;</a> <a id="481" href="Relation.Nary.html#6710" class="Function">fromWitness</a><a id="492" class="Symbol">)</a>

<a id="495" class="Comment">-- A sorted list of natural numbers can be seen as a fresh list</a>
<a id="559" class="Comment">-- where the notion of freshness is being smaller than all the</a>
<a id="622" class="Comment">-- existing entries</a>

<a id="SortedList"></a><a id="643" href="README.Data.List.Fresh.html#643" class="Function">SortedList</a> <a id="654" class="Symbol">:</a> <a id="656" class="PrimitiveType">Set</a>
<a id="660" href="README.Data.List.Fresh.html#643" class="Function">SortedList</a> <a id="671" class="Symbol">=</a> <a id="673" href="Data.List.Fresh.html#1543" class="Datatype">List#</a> <a id="679" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="681" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a>

<a id="686" href="README.Data.List.Fresh.html#686" class="Function">_</a> <a id="688" class="Symbol">:</a> <a id="690" href="README.Data.List.Fresh.html#643" class="Function">SortedList</a>
<a id="701" class="Symbol">_</a> <a id="703" class="Symbol">=</a> <a id="705" href="Data.List.Fresh.html#1643" class="InductiveConstructor">cons</a> <a id="710" class="Number">0</a> <a id="712" class="Symbol">(</a><a id="713" href="Data.List.Fresh.html#1643" class="InductiveConstructor">cons</a> <a id="718" class="Number">1</a> <a id="720" class="Symbol">(</a><a id="721" href="Data.List.Fresh.html#1643" class="InductiveConstructor">cons</a> <a id="726" class="Number">3</a> <a id="728" class="Symbol">(</a><a id="729" href="Data.List.Fresh.html#1643" class="InductiveConstructor">cons</a> <a id="734" class="Number">10</a> <a id="737" href="Data.List.Fresh.html#1626" class="InductiveConstructor">[]</a> <a id="740" class="Symbol">_)</a>
      <a id="749" class="Symbol">(</a><a id="750" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="754" class="Symbol">(</a><a id="755" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="759" class="Symbol">(</a><a id="760" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="764" class="Symbol">(</a><a id="765" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="769" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a><a id="772" class="Symbol">)))</a> <a id="776" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="778" class="Symbol">_))</a>
      <a id="788" class="Symbol">(</a><a id="789" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="793" class="Symbol">(</a><a id="794" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="798" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a><a id="801" class="Symbol">)</a> <a id="803" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="805" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="809" class="Symbol">(</a><a id="810" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="814" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a><a id="817" class="Symbol">)</a> <a id="819" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="821" class="Symbol">_))</a>
      <a id="831" class="Symbol">(</a><a id="832" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="836" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a> <a id="840" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="842" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="846" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a> <a id="850" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="852" href="Data.Nat.Base.html#1600" class="InductiveConstructor">s≤s</a> <a id="856" href="Data.Nat.Base.html#1558" class="InductiveConstructor">z≤n</a> <a id="860" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="862" class="Symbol">_)</a>

<a id="866" class="Comment">-- Clearly, writing these by hand can pretty quickly become quite cumbersome</a>
<a id="943" class="Comment">-- Luckily, if the notion of freshness we are using is decidable, we can</a>
<a id="1016" class="Comment">-- make most of the proofs inferrable by using the erasure of the relation</a>
<a id="1091" class="Comment">-- rather than the relation itself!</a>

<a id="1128" class="Comment">-- We call this new type *I*SortedList because all the proofs will be implicit.</a>

<a id="ISortedList"></a><a id="1209" href="README.Data.List.Fresh.html#1209" class="Function">ISortedList</a> <a id="1221" class="Symbol">:</a> <a id="1223" class="PrimitiveType">Set</a>
<a id="1227" href="README.Data.List.Fresh.html#1209" class="Function">ISortedList</a> <a id="1239" class="Symbol">=</a> <a id="1241" href="Data.List.Fresh.html#1543" class="Datatype">List#</a> <a id="1247" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a> <a id="1249" href="Relation.Nary.html#6523" class="Function Operator">⌊</a> <a id="1251" href="Data.Nat.Properties.html#10078" class="Function Operator">_&lt;?_</a> <a id="1256" href="Relation.Nary.html#6523" class="Function Operator">⌋</a>

<a id="1259" class="Comment">-- The same example is now much shorter. It looks pretty much like a normal list</a>
<a id="1340" class="Comment">-- except that we know for sure that it is well ordered.</a>

<a id="ins"></a><a id="1398" href="README.Data.List.Fresh.html#1398" class="Function">ins</a> <a id="1402" class="Symbol">:</a> <a id="1404" href="README.Data.List.Fresh.html#1209" class="Function">ISortedList</a>
<a id="1416" href="README.Data.List.Fresh.html#1398" class="Function">ins</a> <a id="1420" class="Symbol">=</a> <a id="1422" class="Number">0</a> <a id="1424" href="Data.List.Fresh.html#2045" class="InductiveConstructor Operator">∷#</a> <a id="1427" class="Number">1</a> <a id="1429" href="Data.List.Fresh.html#2045" class="InductiveConstructor Operator">∷#</a> <a id="1432" class="Number">3</a> <a id="1434" href="Data.List.Fresh.html#2045" class="InductiveConstructor Operator">∷#</a> <a id="1437" class="Number">10</a> <a id="1440" href="Data.List.Fresh.html#2045" class="InductiveConstructor Operator">∷#</a> <a id="1443" href="Data.List.Fresh.html#1626" class="InductiveConstructor">[]</a>

<a id="1447" class="Comment">-- Indeed we can extract the support list together with a proof that it</a>
<a id="1519" class="Comment">-- is ordered thanks to the combined action of toList converting a fresh</a>
<a id="1592" class="Comment">-- list to a pair of a list and a proof and fromWitness which &quot;unerases&quot;</a>
<a id="1665" class="Comment">-- a proof.</a>

<a id="ns"></a><a id="1678" href="README.Data.List.Fresh.html#1678" class="Function">ns</a> <a id="1681" class="Symbol">:</a> <a id="1683" href="Agda.Builtin.List.html#148" class="Datatype">List</a> <a id="1688" href="Agda.Builtin.Nat.html#192" class="Datatype">ℕ</a>
<a id="1690" href="README.Data.List.Fresh.html#1678" class="Function">ns</a> <a id="1693" class="Symbol">=</a> <a id="1695" href="Agda.Builtin.Sigma.html#252" class="Field">proj₁</a> <a id="1701" class="Symbol">(</a><a id="1702" href="Data.List.Fresh.html#6149" class="Function">toList</a> <a id="1709" href="README.Data.List.Fresh.html#1398" class="Function">ins</a><a id="1712" class="Symbol">)</a>

<a id="sorted"></a><a id="1715" href="README.Data.List.Fresh.html#1715" class="Function">sorted</a> <a id="1722" class="Symbol">:</a> <a id="1724" href="Data.List.Relation.Unary.AllPairs.Core.html#941" class="Datatype">AllPairs</a> <a id="1733" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a> <a id="1737" href="README.Data.List.Fresh.html#1678" class="Function">ns</a>
<a id="1740" href="README.Data.List.Fresh.html#1715" class="Function">sorted</a> <a id="1747" class="Symbol">=</a> <a id="1749" href="Data.List.Relation.Unary.AllPairs.html#1568" class="Function">AllPairs.map</a> <a id="1762" class="Symbol">(</a><a id="1763" href="Relation.Nary.html#6710" class="Function">fromWitness</a> <a id="1775" href="Data.Nat.Base.html#1645" class="Function Operator">_&lt;_</a> <a id="1779" href="Data.Nat.Properties.html#10078" class="Function Operator">_&lt;?_</a><a id="1783" class="Symbol">)</a> <a id="1785" class="Symbol">(</a><a id="1786" href="Agda.Builtin.Sigma.html#264" class="Field">proj₂</a> <a id="1792" class="Symbol">(</a><a id="1793" href="Data.List.Fresh.html#6149" class="Function">toList</a> <a id="1800" href="README.Data.List.Fresh.html#1398" class="Function">ins</a><a id="1803" class="Symbol">))</a>

<a id="1807" class="Comment">-- See the following module for an applied use-case of fresh lists</a>
<a id="1874" class="Keyword">open</a> <a id="1879" class="Keyword">import</a> <a id="1886" href="README.Data.Trie.NonDependent.html" class="Module">README.Data.Trie.NonDependent</a>
</pre></body></html>