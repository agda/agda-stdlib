<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Fin.Substitution.Example</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- An example of how Data.Fin.Substitution can be used: a definition</a>
<a id="175" class="Comment">-- of substitution for the untyped λ-calculus, along with some lemmas</a>
<a id="245" class="Comment">------------------------------------------------------------------------</a>

<a id="319" class="Symbol">{-#</a> <a id="323" class="Keyword">OPTIONS</a> <a id="331" class="Pragma">--cubical-compatible</a> <a id="352" class="Pragma">--safe</a> <a id="359" class="Symbol">#-}</a>

<a id="364" class="Keyword">module</a> <a id="371" href="Data.Fin.Substitution.Example.html" class="Module">Data.Fin.Substitution.Example</a> <a id="401" class="Keyword">where</a>

<a id="408" class="Keyword">open</a> <a id="413" class="Keyword">import</a> <a id="420" href="Data.Fin.Substitution.html" class="Module">Data.Fin.Substitution</a>
<a id="442" class="Keyword">open</a> <a id="447" class="Keyword">import</a> <a id="454" href="Data.Fin.Substitution.Lemmas.html" class="Module">Data.Fin.Substitution.Lemmas</a>
<a id="483" class="Keyword">open</a> <a id="488" class="Keyword">import</a> <a id="495" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="509" class="Keyword">hiding</a> <a id="516" class="Symbol">(</a><a id="517" href="Data.Nat.Base.html#5110" class="Function Operator">_/_</a><a id="520" class="Symbol">)</a>
<a id="522" class="Keyword">open</a> <a id="527" class="Keyword">import</a> <a id="534" href="Data.Fin.Base.html" class="Module">Data.Fin.Base</a> <a id="548" class="Keyword">using</a> <a id="554" class="Symbol">(</a><a id="555" href="Data.Fin.Base.html#1274" class="Datatype">Fin</a><a id="558" class="Symbol">)</a>
<a id="560" class="Keyword">open</a> <a id="565" class="Keyword">import</a> <a id="572" href="Data.Vec.Base.html" class="Module">Data.Vec.Base</a>
<a id="586" class="Keyword">open</a> <a id="591" class="Keyword">import</a> <a id="598" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a> <a id="641" class="Symbol">as</a> <a id="644" class="Module">PropEq</a>
  <a id="653" class="Keyword">using</a> <a id="659" class="Symbol">(</a><a id="660" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="663" class="Symbol">;</a> <a id="665" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="669" class="Symbol">;</a> <a id="671" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">sym</a><a id="674" class="Symbol">;</a> <a id="676" href="Relation.Binary.PropositionalEquality.Core.html#1158" class="Function">cong</a><a id="680" class="Symbol">;</a> <a id="682" href="Relation.Binary.PropositionalEquality.Core.html#1395" class="Function">cong₂</a><a id="687" class="Symbol">)</a>
<a id="689" class="Keyword">open</a> <a id="694" href="Relation.Binary.PropositionalEquality.Core.html#2736" class="Module">PropEq.≡-Reasoning</a>
<a id="713" class="Keyword">open</a> <a id="718" class="Keyword">import</a> <a id="725" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html" class="Module">Relation.Binary.Construct.Closure.ReflexiveTransitive</a>
  <a id="781" class="Keyword">using</a> <a id="787" class="Symbol">(</a><a id="788" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#625" class="Datatype">Star</a><a id="792" class="Symbol">;</a> <a id="794" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#686" class="InductiveConstructor">ε</a><a id="795" class="Symbol">;</a> <a id="797" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#713" class="InductiveConstructor Operator">_◅_</a><a id="800" class="Symbol">)</a>

<a id="803" class="Comment">-- A representation of the untyped λ-calculus. Uses de Bruijn indices.</a>

<a id="875" class="Keyword">infixl</a> <a id="882" class="Number">9</a> <a id="884" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">_·_</a>

<a id="889" class="Keyword">data</a> <a id="Tm"></a><a id="894" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="897" class="Symbol">(</a><a id="898" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a> <a id="900" class="Symbol">:</a> <a id="902" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="903" class="Symbol">)</a> <a id="905" class="Symbol">:</a> <a id="907" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="911" class="Keyword">where</a>
  <a id="Tm.var"></a><a id="919" href="Data.Fin.Substitution.Example.html#919" class="InductiveConstructor">var</a> <a id="923" class="Symbol">:</a> <a id="925" class="Symbol">(</a><a id="926" href="Data.Fin.Substitution.Example.html#926" class="Bound">x</a> <a id="928" class="Symbol">:</a> <a id="930" href="Data.Fin.Base.html#1274" class="Datatype">Fin</a> <a id="934" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a><a id="935" class="Symbol">)</a> <a id="937" class="Symbol">→</a> <a id="939" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="942" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a>
  <a id="Tm.ƛ"></a><a id="946" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a>   <a id="950" class="Symbol">:</a> <a id="952" class="Symbol">(</a><a id="953" href="Data.Fin.Substitution.Example.html#953" class="Bound">t</a> <a id="955" class="Symbol">:</a> <a id="957" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="960" class="Symbol">(</a><a id="961" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="965" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a><a id="966" class="Symbol">))</a> <a id="969" class="Symbol">→</a> <a id="971" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="974" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a>
  <a id="Tm._·_"></a><a id="978" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">_·_</a> <a id="982" class="Symbol">:</a> <a id="984" class="Symbol">(</a><a id="985" href="Data.Fin.Substitution.Example.html#985" class="Bound">t₁</a> <a id="988" href="Data.Fin.Substitution.Example.html#988" class="Bound">t₂</a> <a id="991" class="Symbol">:</a> <a id="993" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="996" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a><a id="997" class="Symbol">)</a> <a id="999" class="Symbol">→</a> <a id="1001" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="1004" href="Data.Fin.Substitution.Example.html#898" class="Bound">n</a>

<a id="1007" class="Comment">-- Code for applying substitutions.</a>

<a id="1044" class="Keyword">module</a> <a id="TmApp"></a><a id="1051" href="Data.Fin.Substitution.Example.html#1051" class="Module">TmApp</a> <a id="1057" class="Symbol">{</a><a id="1058" href="Data.Fin.Substitution.Example.html#1058" class="Bound">ℓ</a><a id="1059" class="Symbol">}</a> <a id="1061" class="Symbol">{</a><a id="1062" href="Data.Fin.Substitution.Example.html#1062" class="Bound">T</a> <a id="1064" class="Symbol">:</a> <a id="1066" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1068" class="Symbol">→</a> <a id="1070" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1074" href="Data.Fin.Substitution.Example.html#1058" class="Bound">ℓ</a><a id="1075" class="Symbol">}</a> <a id="1077" class="Symbol">(</a><a id="1078" href="Data.Fin.Substitution.Example.html#1078" class="Bound">l</a> <a id="1080" class="Symbol">:</a> <a id="1082" href="Data.Fin.Substitution.html#3407" class="Record">Lift</a> <a id="1087" href="Data.Fin.Substitution.Example.html#1062" class="Bound">T</a> <a id="1089" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a><a id="1091" class="Symbol">)</a> <a id="1093" class="Keyword">where</a>
  <a id="1101" class="Keyword">open</a> <a id="1106" href="Data.Fin.Substitution.html#3407" class="Module">Lift</a> <a id="1111" href="Data.Fin.Substitution.Example.html#1078" class="Bound">l</a> <a id="1113" class="Keyword">hiding</a> <a id="1120" class="Symbol">(</a><a id="1121" href="Data.Fin.Substitution.html#1439" class="Function">var</a><a id="1124" class="Symbol">)</a>

  <a id="1129" class="Comment">-- Applies a substitution to a term.</a>

  <a id="1169" class="Keyword">infix</a> <a id="1175" class="Number">8</a> <a id="1177" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">_/_</a>

  <a id="TmApp._/_"></a><a id="1184" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">_/_</a> <a id="1188" class="Symbol">:</a> <a id="1190" class="Symbol">∀</a> <a id="1192" class="Symbol">{</a><a id="1193" href="Data.Fin.Substitution.Example.html#1193" class="Bound">m</a> <a id="1195" href="Data.Fin.Substitution.Example.html#1195" class="Bound">n</a><a id="1196" class="Symbol">}</a> <a id="1198" class="Symbol">→</a> <a id="1200" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="1203" href="Data.Fin.Substitution.Example.html#1193" class="Bound">m</a> <a id="1205" class="Symbol">→</a> <a id="1207" href="Data.Fin.Substitution.html#1109" class="Function">Sub</a> <a id="1211" href="Data.Fin.Substitution.Example.html#1062" class="Bound">T</a> <a id="1213" href="Data.Fin.Substitution.Example.html#1193" class="Bound">m</a> <a id="1215" href="Data.Fin.Substitution.Example.html#1195" class="Bound">n</a> <a id="1217" class="Symbol">→</a> <a id="1219" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a> <a id="1222" href="Data.Fin.Substitution.Example.html#1195" class="Bound">n</a>
  <a id="1226" href="Data.Fin.Substitution.Example.html#919" class="InductiveConstructor">var</a> <a id="1230" href="Data.Fin.Substitution.Example.html#1230" class="Bound">x</a>   <a id="1234" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">/</a> <a id="1236" href="Data.Fin.Substitution.Example.html#1236" class="Bound">ρ</a> <a id="1238" class="Symbol">=</a> <a id="1240" href="Data.Fin.Substitution.html#3523" class="Field">lift</a> <a id="1245" class="Symbol">(</a><a id="1246" href="Data.Vec.Base.html#1664" class="Function">lookup</a> <a id="1253" href="Data.Fin.Substitution.Example.html#1236" class="Bound">ρ</a> <a id="1255" href="Data.Fin.Substitution.Example.html#1230" class="Bound">x</a><a id="1256" class="Symbol">)</a>
  <a id="1260" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="1262" href="Data.Fin.Substitution.Example.html#1262" class="Bound">t</a>     <a id="1268" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">/</a> <a id="1270" href="Data.Fin.Substitution.Example.html#1270" class="Bound">ρ</a> <a id="1272" class="Symbol">=</a> <a id="1274" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="1276" class="Symbol">(</a><a id="1277" href="Data.Fin.Substitution.Example.html#1262" class="Bound">t</a> <a id="1279" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">/</a> <a id="1281" href="Data.Fin.Substitution.Example.html#1270" class="Bound">ρ</a> <a id="1283" href="Data.Fin.Substitution.html#1570" class="Function Operator">↑</a><a id="1284" class="Symbol">)</a>
  <a id="1288" href="Data.Fin.Substitution.Example.html#1288" class="Bound">t₁</a> <a id="1291" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="1293" href="Data.Fin.Substitution.Example.html#1293" class="Bound">t₂</a> <a id="1296" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">/</a> <a id="1298" href="Data.Fin.Substitution.Example.html#1298" class="Bound">ρ</a> <a id="1300" class="Symbol">=</a> <a id="1302" class="Symbol">(</a><a id="1303" href="Data.Fin.Substitution.Example.html#1288" class="Bound">t₁</a> <a id="1306" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">/</a> <a id="1308" href="Data.Fin.Substitution.Example.html#1298" class="Bound">ρ</a><a id="1309" class="Symbol">)</a> <a id="1311" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="1313" class="Symbol">(</a><a id="1314" href="Data.Fin.Substitution.Example.html#1293" class="Bound">t₂</a> <a id="1317" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">/</a> <a id="1319" href="Data.Fin.Substitution.Example.html#1298" class="Bound">ρ</a><a id="1320" class="Symbol">)</a>

  <a id="1325" class="Keyword">open</a> <a id="1330" href="Data.Fin.Substitution.html#2271" class="Module">Application</a> <a id="1342" class="Symbol">(</a><a id="1343" class="Keyword">record</a> <a id="1350" class="Symbol">{</a> <a id="1352" href="Data.Fin.Substitution.html#2454" class="Field Operator">_/_</a> <a id="1356" class="Symbol">=</a> <a id="1358" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">_/_</a> <a id="1362" class="Symbol">})</a> <a id="1365" class="Keyword">using</a> <a id="1371" class="Symbol">(</a><a id="1372" href="Data.Fin.Substitution.html#2697" class="Function Operator">_/✶_</a><a id="1376" class="Symbol">)</a>

  <a id="1381" class="Comment">-- Some lemmas about _/_.</a>

  <a id="TmApp.ƛ-/✶-↑✶"></a><a id="1410" href="Data.Fin.Substitution.Example.html#1410" class="Function">ƛ-/✶-↑✶</a> <a id="1418" class="Symbol">:</a> <a id="1420" class="Symbol">∀</a> <a id="1422" href="Data.Fin.Substitution.Example.html#1422" class="Bound">k</a> <a id="1424" class="Symbol">{</a><a id="1425" href="Data.Fin.Substitution.Example.html#1425" class="Bound">m</a> <a id="1427" href="Data.Fin.Substitution.Example.html#1427" class="Bound">n</a> <a id="1429" href="Data.Fin.Substitution.Example.html#1429" class="Bound">t</a><a id="1430" class="Symbol">}</a> <a id="1432" class="Symbol">(</a><a id="1433" href="Data.Fin.Substitution.Example.html#1433" class="Bound">ρs</a> <a id="1436" class="Symbol">:</a> <a id="1438" href="Data.Fin.Substitution.html#1226" class="Function">Subs</a> <a id="1443" href="Data.Fin.Substitution.Example.html#1062" class="Bound">T</a> <a id="1445" href="Data.Fin.Substitution.Example.html#1425" class="Bound">m</a> <a id="1447" href="Data.Fin.Substitution.Example.html#1427" class="Bound">n</a><a id="1448" class="Symbol">)</a> <a id="1450" class="Symbol">→</a>
            <a id="1464" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="1466" href="Data.Fin.Substitution.Example.html#1429" class="Bound">t</a> <a id="1468" href="Data.Fin.Substitution.html#2697" class="Function Operator">/✶</a> <a id="1471" href="Data.Fin.Substitution.Example.html#1433" class="Bound">ρs</a> <a id="1474" href="Data.Fin.Substitution.html#1757" class="Function Operator">↑✶</a> <a id="1477" href="Data.Fin.Substitution.Example.html#1422" class="Bound">k</a> <a id="1479" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1481" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="1483" class="Symbol">(</a><a id="1484" href="Data.Fin.Substitution.Example.html#1429" class="Bound">t</a> <a id="1486" href="Data.Fin.Substitution.html#2697" class="Function Operator">/✶</a> <a id="1489" href="Data.Fin.Substitution.Example.html#1433" class="Bound">ρs</a> <a id="1492" href="Data.Fin.Substitution.html#1757" class="Function Operator">↑✶</a> <a id="1495" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1499" href="Data.Fin.Substitution.Example.html#1422" class="Bound">k</a><a id="1500" class="Symbol">)</a>
  <a id="1504" href="Data.Fin.Substitution.Example.html#1410" class="Function">ƛ-/✶-↑✶</a> <a id="1512" href="Data.Fin.Substitution.Example.html#1512" class="Bound">k</a> <a id="1514" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#686" class="InductiveConstructor">ε</a>        <a id="1523" class="Symbol">=</a> <a id="1525" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
  <a id="1532" href="Data.Fin.Substitution.Example.html#1410" class="Function">ƛ-/✶-↑✶</a> <a id="1540" href="Data.Fin.Substitution.Example.html#1540" class="Bound">k</a> <a id="1542" class="Symbol">(</a><a id="1543" href="Data.Fin.Substitution.Example.html#1543" class="Bound">ρ</a> <a id="1545" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#713" class="InductiveConstructor Operator">◅</a> <a id="1547" href="Data.Fin.Substitution.Example.html#1547" class="Bound">ρs</a><a id="1549" class="Symbol">)</a> <a id="1551" class="Symbol">=</a> <a id="1553" href="Relation.Binary.PropositionalEquality.Core.html#1395" class="Function">cong₂</a> <a id="1559" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">_/_</a> <a id="1563" class="Symbol">(</a><a id="1564" href="Data.Fin.Substitution.Example.html#1410" class="Function">ƛ-/✶-↑✶</a> <a id="1572" href="Data.Fin.Substitution.Example.html#1540" class="Bound">k</a> <a id="1574" href="Data.Fin.Substitution.Example.html#1547" class="Bound">ρs</a><a id="1576" class="Symbol">)</a> <a id="1578" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

  <a id="TmApp.·-/✶-↑✶"></a><a id="1586" href="Data.Fin.Substitution.Example.html#1586" class="Function">·-/✶-↑✶</a> <a id="1594" class="Symbol">:</a> <a id="1596" class="Symbol">∀</a> <a id="1598" href="Data.Fin.Substitution.Example.html#1598" class="Bound">k</a> <a id="1600" class="Symbol">{</a><a id="1601" href="Data.Fin.Substitution.Example.html#1601" class="Bound">m</a> <a id="1603" href="Data.Fin.Substitution.Example.html#1603" class="Bound">n</a> <a id="1605" href="Data.Fin.Substitution.Example.html#1605" class="Bound">t₁</a> <a id="1608" href="Data.Fin.Substitution.Example.html#1608" class="Bound">t₂</a><a id="1610" class="Symbol">}</a> <a id="1612" class="Symbol">(</a><a id="1613" href="Data.Fin.Substitution.Example.html#1613" class="Bound">ρs</a> <a id="1616" class="Symbol">:</a> <a id="1618" href="Data.Fin.Substitution.html#1226" class="Function">Subs</a> <a id="1623" href="Data.Fin.Substitution.Example.html#1062" class="Bound">T</a> <a id="1625" href="Data.Fin.Substitution.Example.html#1601" class="Bound">m</a> <a id="1627" href="Data.Fin.Substitution.Example.html#1603" class="Bound">n</a><a id="1628" class="Symbol">)</a> <a id="1630" class="Symbol">→</a>
            <a id="1644" href="Data.Fin.Substitution.Example.html#1605" class="Bound">t₁</a> <a id="1647" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="1649" href="Data.Fin.Substitution.Example.html#1608" class="Bound">t₂</a> <a id="1652" href="Data.Fin.Substitution.html#2697" class="Function Operator">/✶</a> <a id="1655" href="Data.Fin.Substitution.Example.html#1613" class="Bound">ρs</a> <a id="1658" href="Data.Fin.Substitution.html#1757" class="Function Operator">↑✶</a> <a id="1661" href="Data.Fin.Substitution.Example.html#1598" class="Bound">k</a> <a id="1663" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1665" class="Symbol">(</a><a id="1666" href="Data.Fin.Substitution.Example.html#1605" class="Bound">t₁</a> <a id="1669" href="Data.Fin.Substitution.html#2697" class="Function Operator">/✶</a> <a id="1672" href="Data.Fin.Substitution.Example.html#1613" class="Bound">ρs</a> <a id="1675" href="Data.Fin.Substitution.html#1757" class="Function Operator">↑✶</a> <a id="1678" href="Data.Fin.Substitution.Example.html#1598" class="Bound">k</a><a id="1679" class="Symbol">)</a> <a id="1681" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="1683" class="Symbol">(</a><a id="1684" href="Data.Fin.Substitution.Example.html#1608" class="Bound">t₂</a> <a id="1687" href="Data.Fin.Substitution.html#2697" class="Function Operator">/✶</a> <a id="1690" href="Data.Fin.Substitution.Example.html#1613" class="Bound">ρs</a> <a id="1693" href="Data.Fin.Substitution.html#1757" class="Function Operator">↑✶</a> <a id="1696" href="Data.Fin.Substitution.Example.html#1598" class="Bound">k</a><a id="1697" class="Symbol">)</a>
  <a id="1701" href="Data.Fin.Substitution.Example.html#1586" class="Function">·-/✶-↑✶</a> <a id="1709" href="Data.Fin.Substitution.Example.html#1709" class="Bound">k</a> <a id="1711" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#686" class="InductiveConstructor">ε</a>        <a id="1720" class="Symbol">=</a> <a id="1722" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
  <a id="1729" href="Data.Fin.Substitution.Example.html#1586" class="Function">·-/✶-↑✶</a> <a id="1737" href="Data.Fin.Substitution.Example.html#1737" class="Bound">k</a> <a id="1739" class="Symbol">(</a><a id="1740" href="Data.Fin.Substitution.Example.html#1740" class="Bound">ρ</a> <a id="1742" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#713" class="InductiveConstructor Operator">◅</a> <a id="1744" href="Data.Fin.Substitution.Example.html#1744" class="Bound">ρs</a><a id="1746" class="Symbol">)</a> <a id="1748" class="Symbol">=</a> <a id="1750" href="Relation.Binary.PropositionalEquality.Core.html#1395" class="Function">cong₂</a> <a id="1756" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">_/_</a> <a id="1760" class="Symbol">(</a><a id="1761" href="Data.Fin.Substitution.Example.html#1586" class="Function">·-/✶-↑✶</a> <a id="1769" href="Data.Fin.Substitution.Example.html#1737" class="Bound">k</a> <a id="1771" href="Data.Fin.Substitution.Example.html#1744" class="Bound">ρs</a><a id="1773" class="Symbol">)</a> <a id="1775" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="tmSubst"></a><a id="1781" href="Data.Fin.Substitution.Example.html#1781" class="Function">tmSubst</a> <a id="1789" class="Symbol">:</a> <a id="1791" href="Data.Fin.Substitution.html#3856" class="Record">TermSubst</a> <a id="1801" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a>
<a id="1804" href="Data.Fin.Substitution.Example.html#1781" class="Function">tmSubst</a> <a id="1812" class="Symbol">=</a> <a id="1814" class="Keyword">record</a> <a id="1821" class="Symbol">{</a> <a id="1823" href="Data.Fin.Substitution.html#3907" class="Field">var</a> <a id="1827" class="Symbol">=</a> <a id="1829" href="Data.Fin.Substitution.Example.html#919" class="InductiveConstructor">var</a><a id="1832" class="Symbol">;</a> <a id="1834" href="Data.Fin.Substitution.html#3937" class="Field">app</a> <a id="1838" class="Symbol">=</a> <a id="1840" href="Data.Fin.Substitution.Example.html#1184" class="Function Operator">TmApp._/_</a> <a id="1850" class="Symbol">}</a>

<a id="1853" class="Keyword">open</a> <a id="1858" href="Data.Fin.Substitution.html#3856" class="Module">TermSubst</a> <a id="1868" href="Data.Fin.Substitution.Example.html#1781" class="Function">tmSubst</a> <a id="1876" class="Keyword">hiding</a> <a id="1883" class="Symbol">(</a><a id="1884" href="Data.Fin.Substitution.html#3907" class="Field">var</a><a id="1887" class="Symbol">)</a>

<a id="1890" class="Comment">-- Substitution lemmas.</a>

<a id="tmLemmas"></a><a id="1915" href="Data.Fin.Substitution.Example.html#1915" class="Function">tmLemmas</a> <a id="1924" class="Symbol">:</a> <a id="1926" href="Data.Fin.Substitution.Lemmas.html#17483" class="Record">TermLemmas</a> <a id="1937" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a>
<a id="1940" href="Data.Fin.Substitution.Example.html#1915" class="Function">tmLemmas</a> <a id="1949" class="Symbol">=</a> <a id="1951" class="Keyword">record</a>
  <a id="1960" class="Symbol">{</a> <a id="1962" href="Data.Fin.Substitution.Lemmas.html#17533" class="Field">termSubst</a> <a id="1972" class="Symbol">=</a> <a id="1974" href="Data.Fin.Substitution.Example.html#1781" class="Function">tmSubst</a>
  <a id="1984" class="Symbol">;</a> <a id="1986" href="Data.Fin.Substitution.Lemmas.html#17639" class="Field">app-var</a>   <a id="1996" class="Symbol">=</a> <a id="1998" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>
  <a id="2005" class="Symbol">;</a> <a id="2007" href="Data.Fin.Substitution.Lemmas.html#17769" class="Field">/✶-↑✶</a>     <a id="2017" class="Symbol">=</a> <a id="2019" href="Data.Fin.Substitution.Example.html#2262" class="Function">Lemma./✶-↑✶</a>
  <a id="2033" class="Symbol">}</a>
  <a id="2037" class="Keyword">where</a>
  <a id="2045" class="Keyword">module</a> <a id="2052" href="Data.Fin.Substitution.Example.html#2052" class="Module">Lemma</a> <a id="2058" class="Symbol">{</a><a id="2059" href="Data.Fin.Substitution.Example.html#2059" class="Bound">T₁</a> <a id="2062" href="Data.Fin.Substitution.Example.html#2062" class="Bound">T₂</a><a id="2064" class="Symbol">}</a> <a id="2066" class="Symbol">{</a><a id="2067" href="Data.Fin.Substitution.Example.html#2067" class="Bound">lift₁</a> <a id="2073" class="Symbol">:</a> <a id="2075" href="Data.Fin.Substitution.html#3407" class="Record">Lift</a> <a id="2080" href="Data.Fin.Substitution.Example.html#2059" class="Bound">T₁</a> <a id="2083" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a><a id="2085" class="Symbol">}</a> <a id="2087" class="Symbol">{</a><a id="2088" href="Data.Fin.Substitution.Example.html#2088" class="Bound">lift₂</a> <a id="2094" class="Symbol">:</a> <a id="2096" href="Data.Fin.Substitution.html#3407" class="Record">Lift</a> <a id="2101" href="Data.Fin.Substitution.Example.html#2062" class="Bound">T₂</a> <a id="2104" href="Data.Fin.Substitution.Example.html#894" class="Datatype">Tm</a><a id="2106" class="Symbol">}</a> <a id="2108" class="Keyword">where</a>

    <a id="2119" class="Keyword">open</a> <a id="2124" href="Data.Fin.Substitution.html#4019" class="Module">Lifted</a> <a id="2131" href="Data.Fin.Substitution.Example.html#2067" class="Bound">lift₁</a> <a id="2137" class="Keyword">using</a> <a id="2143" class="Symbol">()</a> <a id="2146" class="Keyword">renaming</a> <a id="2155" class="Symbol">(</a><a id="2156" href="Data.Fin.Substitution.html#1757" class="Function Operator">_↑✶_</a> <a id="2161" class="Symbol">to</a> <a id="2164" class="Function Operator">_↑✶₁_</a><a id="2169" class="Symbol">;</a> <a id="2171" href="Data.Fin.Substitution.html#2697" class="Function Operator">_/✶_</a> <a id="2176" class="Symbol">to</a> <a id="2179" class="Function Operator">_/✶₁_</a><a id="2184" class="Symbol">)</a>
    <a id="2190" class="Keyword">open</a> <a id="2195" href="Data.Fin.Substitution.html#4019" class="Module">Lifted</a> <a id="2202" href="Data.Fin.Substitution.Example.html#2088" class="Bound">lift₂</a> <a id="2208" class="Keyword">using</a> <a id="2214" class="Symbol">()</a> <a id="2217" class="Keyword">renaming</a> <a id="2226" class="Symbol">(</a><a id="2227" href="Data.Fin.Substitution.html#1757" class="Function Operator">_↑✶_</a> <a id="2232" class="Symbol">to</a> <a id="2235" class="Function Operator">_↑✶₂_</a><a id="2240" class="Symbol">;</a> <a id="2242" href="Data.Fin.Substitution.html#2697" class="Function Operator">_/✶_</a> <a id="2247" class="Symbol">to</a> <a id="2250" class="Function Operator">_/✶₂_</a><a id="2255" class="Symbol">)</a>

    <a id="2262" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="2268" class="Symbol">:</a> <a id="2270" class="Symbol">∀</a> <a id="2272" class="Symbol">{</a><a id="2273" href="Data.Fin.Substitution.Example.html#2273" class="Bound">m</a> <a id="2275" href="Data.Fin.Substitution.Example.html#2275" class="Bound">n</a><a id="2276" class="Symbol">}</a> <a id="2278" class="Symbol">(</a><a id="2279" href="Data.Fin.Substitution.Example.html#2279" class="Bound">ρs₁</a> <a id="2283" class="Symbol">:</a> <a id="2285" href="Data.Fin.Substitution.html#1226" class="Function">Subs</a> <a id="2290" href="Data.Fin.Substitution.Example.html#2059" class="Bound">T₁</a> <a id="2293" href="Data.Fin.Substitution.Example.html#2273" class="Bound">m</a> <a id="2295" href="Data.Fin.Substitution.Example.html#2275" class="Bound">n</a><a id="2296" class="Symbol">)</a> <a id="2298" class="Symbol">(</a><a id="2299" href="Data.Fin.Substitution.Example.html#2299" class="Bound">ρs₂</a> <a id="2303" class="Symbol">:</a> <a id="2305" href="Data.Fin.Substitution.html#1226" class="Function">Subs</a> <a id="2310" href="Data.Fin.Substitution.Example.html#2062" class="Bound">T₂</a> <a id="2313" href="Data.Fin.Substitution.Example.html#2273" class="Bound">m</a> <a id="2315" href="Data.Fin.Substitution.Example.html#2275" class="Bound">n</a><a id="2316" class="Symbol">)</a> <a id="2318" class="Symbol">→</a>
            <a id="2332" class="Symbol">(∀</a> <a id="2335" href="Data.Fin.Substitution.Example.html#2335" class="Bound">k</a> <a id="2337" href="Data.Fin.Substitution.Example.html#2337" class="Bound">x</a> <a id="2339" class="Symbol">→</a> <a id="2341" href="Data.Fin.Substitution.Example.html#919" class="InductiveConstructor">var</a> <a id="2345" href="Data.Fin.Substitution.Example.html#2337" class="Bound">x</a> <a id="2347" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2351" href="Data.Fin.Substitution.Example.html#2279" class="Bound">ρs₁</a> <a id="2355" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2359" href="Data.Fin.Substitution.Example.html#2335" class="Bound">k</a> <a id="2361" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2363" href="Data.Fin.Substitution.Example.html#919" class="InductiveConstructor">var</a> <a id="2367" href="Data.Fin.Substitution.Example.html#2337" class="Bound">x</a> <a id="2369" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="2373" href="Data.Fin.Substitution.Example.html#2299" class="Bound">ρs₂</a> <a id="2377" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="2381" href="Data.Fin.Substitution.Example.html#2335" class="Bound">k</a><a id="2382" class="Symbol">)</a> <a id="2384" class="Symbol">→</a>
             <a id="2399" class="Symbol">∀</a> <a id="2401" href="Data.Fin.Substitution.Example.html#2401" class="Bound">k</a> <a id="2403" href="Data.Fin.Substitution.Example.html#2403" class="Bound">t</a> <a id="2405" class="Symbol">→</a> <a id="2407" href="Data.Fin.Substitution.Example.html#2403" class="Bound">t</a>     <a id="2413" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2417" href="Data.Fin.Substitution.Example.html#2279" class="Bound">ρs₁</a> <a id="2421" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2425" href="Data.Fin.Substitution.Example.html#2401" class="Bound">k</a> <a id="2427" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="2429" href="Data.Fin.Substitution.Example.html#2403" class="Bound">t</a>     <a id="2435" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="2439" href="Data.Fin.Substitution.Example.html#2299" class="Bound">ρs₂</a> <a id="2443" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="2447" href="Data.Fin.Substitution.Example.html#2401" class="Bound">k</a>
    <a id="2453" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="2459" href="Data.Fin.Substitution.Example.html#2459" class="Bound">ρs₁</a> <a id="2463" href="Data.Fin.Substitution.Example.html#2463" class="Bound">ρs₂</a> <a id="2467" href="Data.Fin.Substitution.Example.html#2467" class="Bound">hyp</a> <a id="2471" href="Data.Fin.Substitution.Example.html#2471" class="Bound">k</a> <a id="2473" class="Symbol">(</a><a id="2474" href="Data.Fin.Substitution.Example.html#919" class="InductiveConstructor">var</a> <a id="2478" href="Data.Fin.Substitution.Example.html#2478" class="Bound">x</a><a id="2479" class="Symbol">)</a> <a id="2481" class="Symbol">=</a> <a id="2483" href="Data.Fin.Substitution.Example.html#2467" class="Bound">hyp</a> <a id="2487" href="Data.Fin.Substitution.Example.html#2471" class="Bound">k</a> <a id="2489" href="Data.Fin.Substitution.Example.html#2478" class="Bound">x</a>
    <a id="2495" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="2501" href="Data.Fin.Substitution.Example.html#2501" class="Bound">ρs₁</a> <a id="2505" href="Data.Fin.Substitution.Example.html#2505" class="Bound">ρs₂</a> <a id="2509" href="Data.Fin.Substitution.Example.html#2509" class="Bound">hyp</a> <a id="2513" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a> <a id="2515" class="Symbol">(</a><a id="2516" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="2518" href="Data.Fin.Substitution.Example.html#2518" class="Bound">t</a><a id="2519" class="Symbol">)</a>   <a id="2523" class="Symbol">=</a> <a id="2525" href="Relation.Binary.PropositionalEquality.Core.html#2834" class="Function Operator">begin</a>
      <a id="2537" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="2539" href="Data.Fin.Substitution.Example.html#2518" class="Bound">t</a> <a id="2541" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2545" href="Data.Fin.Substitution.Example.html#2501" class="Bound">ρs₁</a> <a id="2549" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2553" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a>        <a id="2562" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="2565" href="Data.Fin.Substitution.Example.html#1410" class="Function">TmApp.ƛ-/✶-↑✶</a> <a id="2579" class="Symbol">_</a> <a id="2581" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a> <a id="2583" href="Data.Fin.Substitution.Example.html#2501" class="Bound">ρs₁</a> <a id="2587" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
      <a id="2595" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="2597" class="Symbol">(</a><a id="2598" href="Data.Fin.Substitution.Example.html#2518" class="Bound">t</a> <a id="2600" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2604" href="Data.Fin.Substitution.Example.html#2501" class="Bound">ρs₁</a> <a id="2608" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2612" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2616" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a><a id="2617" class="Symbol">)</a>  <a id="2620" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="2623" href="Relation.Binary.PropositionalEquality.Core.html#1158" class="Function">cong</a> <a id="2628" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="2630" class="Symbol">(</a><a id="2631" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="2637" href="Data.Fin.Substitution.Example.html#2501" class="Bound">ρs₁</a> <a id="2641" href="Data.Fin.Substitution.Example.html#2505" class="Bound">ρs₂</a> <a id="2645" href="Data.Fin.Substitution.Example.html#2509" class="Bound">hyp</a> <a id="2649" class="Symbol">(</a><a id="2650" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2654" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a><a id="2655" class="Symbol">)</a> <a id="2657" href="Data.Fin.Substitution.Example.html#2518" class="Bound">t</a><a id="2658" class="Symbol">)</a> <a id="2660" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
      <a id="2668" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="2670" class="Symbol">(</a><a id="2671" href="Data.Fin.Substitution.Example.html#2518" class="Bound">t</a> <a id="2673" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="2677" href="Data.Fin.Substitution.Example.html#2505" class="Bound">ρs₂</a> <a id="2681" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="2685" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2689" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a><a id="2690" class="Symbol">)</a>  <a id="2693" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="2696" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">sym</a> <a id="2700" class="Symbol">(</a><a id="2701" href="Data.Fin.Substitution.Example.html#1410" class="Function">TmApp.ƛ-/✶-↑✶</a> <a id="2715" class="Symbol">_</a> <a id="2717" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a> <a id="2719" href="Data.Fin.Substitution.Example.html#2505" class="Bound">ρs₂</a><a id="2722" class="Symbol">)</a> <a id="2724" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
      <a id="2732" href="Data.Fin.Substitution.Example.html#946" class="InductiveConstructor">ƛ</a> <a id="2734" href="Data.Fin.Substitution.Example.html#2518" class="Bound">t</a> <a id="2736" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="2740" href="Data.Fin.Substitution.Example.html#2505" class="Bound">ρs₂</a> <a id="2744" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="2748" href="Data.Fin.Substitution.Example.html#2513" class="Bound">k</a>        <a id="2757" href="Relation.Binary.PropositionalEquality.Core.html#3133" class="Function Operator">∎</a>
    <a id="2763" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="2769" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="2773" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a> <a id="2777" href="Data.Fin.Substitution.Example.html#2777" class="Bound">hyp</a> <a id="2781" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a> <a id="2783" class="Symbol">(</a><a id="2784" href="Data.Fin.Substitution.Example.html#2784" class="Bound">t₁</a> <a id="2787" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="2789" href="Data.Fin.Substitution.Example.html#2789" class="Bound">t₂</a><a id="2791" class="Symbol">)</a> <a id="2793" class="Symbol">=</a> <a id="2795" href="Relation.Binary.PropositionalEquality.Core.html#2834" class="Function Operator">begin</a>
      <a id="2807" href="Data.Fin.Substitution.Example.html#2784" class="Bound">t₁</a> <a id="2810" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="2812" href="Data.Fin.Substitution.Example.html#2789" class="Bound">t₂</a> <a id="2815" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2819" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="2823" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2827" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a>                    <a id="2848" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="2851" href="Data.Fin.Substitution.Example.html#1586" class="Function">TmApp.·-/✶-↑✶</a> <a id="2865" class="Symbol">_</a> <a id="2867" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a> <a id="2869" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="2873" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
      <a id="2881" class="Symbol">(</a><a id="2882" href="Data.Fin.Substitution.Example.html#2784" class="Bound">t₁</a> <a id="2885" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2889" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="2893" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2897" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a><a id="2898" class="Symbol">)</a> <a id="2900" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="2902" class="Symbol">(</a><a id="2903" href="Data.Fin.Substitution.Example.html#2789" class="Bound">t₂</a> <a id="2906" href="Data.Fin.Substitution.Example.html#2179" class="Function Operator">/✶₁</a> <a id="2910" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="2914" href="Data.Fin.Substitution.Example.html#2164" class="Function Operator">↑✶₁</a> <a id="2918" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a><a id="2919" class="Symbol">)</a>  <a id="2922" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="2925" href="Relation.Binary.PropositionalEquality.Core.html#1395" class="Function">cong₂</a> <a id="2931" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">_·_</a> <a id="2935" class="Symbol">(</a><a id="2936" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="2942" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="2946" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a> <a id="2950" href="Data.Fin.Substitution.Example.html#2777" class="Bound">hyp</a> <a id="2954" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a> <a id="2956" href="Data.Fin.Substitution.Example.html#2784" class="Bound">t₁</a><a id="2958" class="Symbol">)</a>
                                                            <a id="3020" class="Symbol">(</a><a id="3021" href="Data.Fin.Substitution.Example.html#2262" class="Function">/✶-↑✶</a> <a id="3027" href="Data.Fin.Substitution.Example.html#2769" class="Bound">ρs₁</a> <a id="3031" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a> <a id="3035" href="Data.Fin.Substitution.Example.html#2777" class="Bound">hyp</a> <a id="3039" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a> <a id="3041" href="Data.Fin.Substitution.Example.html#2789" class="Bound">t₂</a><a id="3043" class="Symbol">)</a> <a id="3045" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
      <a id="3053" class="Symbol">(</a><a id="3054" href="Data.Fin.Substitution.Example.html#2784" class="Bound">t₁</a> <a id="3057" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="3061" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a> <a id="3065" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="3069" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a><a id="3070" class="Symbol">)</a> <a id="3072" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="3074" class="Symbol">(</a><a id="3075" href="Data.Fin.Substitution.Example.html#2789" class="Bound">t₂</a> <a id="3078" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="3082" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a> <a id="3086" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="3090" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a><a id="3091" class="Symbol">)</a>  <a id="3094" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="3097" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">sym</a> <a id="3101" class="Symbol">(</a><a id="3102" href="Data.Fin.Substitution.Example.html#1586" class="Function">TmApp.·-/✶-↑✶</a> <a id="3116" class="Symbol">_</a> <a id="3118" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a> <a id="3120" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a><a id="3123" class="Symbol">)</a> <a id="3125" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
      <a id="3133" href="Data.Fin.Substitution.Example.html#2784" class="Bound">t₁</a> <a id="3136" href="Data.Fin.Substitution.Example.html#978" class="InductiveConstructor Operator">·</a> <a id="3138" href="Data.Fin.Substitution.Example.html#2789" class="Bound">t₂</a> <a id="3141" href="Data.Fin.Substitution.Example.html#2250" class="Function Operator">/✶₂</a> <a id="3145" href="Data.Fin.Substitution.Example.html#2773" class="Bound">ρs₂</a> <a id="3149" href="Data.Fin.Substitution.Example.html#2235" class="Function Operator">↑✶₂</a> <a id="3153" href="Data.Fin.Substitution.Example.html#2781" class="Bound">k</a>                    <a id="3174" href="Relation.Binary.PropositionalEquality.Core.html#3133" class="Function Operator">∎</a>

<a id="3177" class="Keyword">open</a> <a id="3182" href="Data.Fin.Substitution.Lemmas.html#17483" class="Module">TermLemmas</a> <a id="3193" href="Data.Fin.Substitution.Example.html#1915" class="Function">tmLemmas</a> <a id="3202" class="Keyword">public</a> <a id="3209" class="Keyword">hiding</a> <a id="3216" class="Symbol">(</a><a id="3217" href="Data.Fin.Substitution.html#1439" class="Function">var</a><a id="3220" class="Symbol">)</a>
</pre></body></html>