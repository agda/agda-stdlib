<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Function.Equality</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Function setoids and related constructions</a>
<a id="152" class="Comment">------------------------------------------------------------------------</a>

<a id="226" class="Symbol">{-#</a> <a id="230" class="Keyword">OPTIONS</a> <a id="238" class="Pragma">--cubical-compatible</a> <a id="259" class="Pragma">--safe</a> <a id="266" class="Symbol">#-}</a>

<a id="271" class="Comment">-- Note: use of the standard function hierarchy is encouraged. The</a>
<a id="338" class="Comment">-- module `Function` re-exports `Congruent`, `IsBijection` and</a>
<a id="401" class="Comment">-- `Bijection`. The alternative definitions found in this file will</a>
<a id="469" class="Comment">-- eventually be deprecated.</a>

<a id="499" class="Keyword">module</a> <a id="506" href="Function.Equality.html" class="Module">Function.Equality</a> <a id="524" class="Keyword">where</a>

<a id="531" class="Keyword">import</a> <a id="538" href="Function.Base.html" class="Module">Function.Base</a> <a id="552" class="Symbol">as</a> <a id="555" class="Module">Fun</a>
<a id="559" class="Keyword">open</a> <a id="564" class="Keyword">import</a> <a id="571" href="Level.html" class="Module">Level</a> <a id="577" class="Keyword">using</a> <a id="583" class="Symbol">(</a><a id="584" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="589" class="Symbol">;</a> <a id="591" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="594" class="Symbol">)</a>
<a id="596" class="Keyword">open</a> <a id="601" class="Keyword">import</a> <a id="608" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="624" class="Keyword">using</a> <a id="630" class="Symbol">(</a><a id="631" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a><a id="637" class="Symbol">)</a>
<a id="639" class="Keyword">open</a> <a id="644" class="Keyword">import</a> <a id="651" href="Relation.Binary.Indexed.Heterogeneous.html" class="Module">Relation.Binary.Indexed.Heterogeneous</a>
  <a id="691" class="Keyword">using</a> <a id="697" class="Symbol">(</a><a id="698" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#803" class="Record">IndexedSetoid</a><a id="711" class="Symbol">;</a> <a id="713" href="Relation.Binary.Indexed.Heterogeneous.Core.html#1071" class="Function Operator">_=[_]⇒_</a><a id="720" class="Symbol">)</a>
<a id="722" class="Keyword">import</a> <a id="729" href="Relation.Binary.Indexed.Heterogeneous.Construct.Trivial.html" class="Module">Relation.Binary.Indexed.Heterogeneous.Construct.Trivial</a>
  <a id="787" class="Symbol">as</a> <a id="790" class="Module">Trivial</a>

<a id="799" class="Comment">------------------------------------------------------------------------</a>
<a id="872" class="Comment">-- Functions which preserve equality</a>

<a id="910" class="Keyword">record</a> <a id="Π"></a><a id="917" href="Function.Equality.html#917" class="Record">Π</a> <a id="919" class="Symbol">{</a><a id="920" href="Function.Equality.html#920" class="Bound">f₁</a> <a id="923" href="Function.Equality.html#923" class="Bound">f₂</a> <a id="926" href="Function.Equality.html#926" class="Bound">t₁</a> <a id="929" href="Function.Equality.html#929" class="Bound">t₂</a><a id="931" class="Symbol">}</a>
         <a id="942" class="Symbol">(</a><a id="943" href="Function.Equality.html#943" class="Bound">From</a> <a id="948" class="Symbol">:</a> <a id="950" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="957" href="Function.Equality.html#920" class="Bound">f₁</a> <a id="960" href="Function.Equality.html#923" class="Bound">f₂</a><a id="962" class="Symbol">)</a>
         <a id="973" class="Symbol">(</a><a id="974" href="Function.Equality.html#974" class="Bound">To</a> <a id="977" class="Symbol">:</a> <a id="979" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#803" class="Record">IndexedSetoid</a> <a id="993" class="Symbol">(</a><a id="994" href="Relation.Binary.Bundles.html#1104" class="Field">Setoid.Carrier</a> <a id="1009" href="Function.Equality.html#943" class="Bound">From</a><a id="1013" class="Symbol">)</a> <a id="1015" href="Function.Equality.html#926" class="Bound">t₁</a> <a id="1018" href="Function.Equality.html#929" class="Bound">t₂</a><a id="1020" class="Symbol">)</a> <a id="1022" class="Symbol">:</a>
         <a id="1033" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1037" class="Symbol">(</a><a id="1038" href="Function.Equality.html#920" class="Bound">f₁</a> <a id="1041" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1043" href="Function.Equality.html#923" class="Bound">f₂</a> <a id="1046" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1048" href="Function.Equality.html#926" class="Bound">t₁</a> <a id="1051" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1053" href="Function.Equality.html#929" class="Bound">t₂</a><a id="1055" class="Symbol">)</a> <a id="1057" class="Keyword">where</a>
  <a id="1065" class="Keyword">infixl</a> <a id="1072" class="Number">5</a> <a id="1074" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a>
  <a id="1082" class="Keyword">field</a>
    <a id="Π._⟨$⟩_"></a><a id="1092" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1098" class="Symbol">:</a> <a id="1100" class="Symbol">(</a><a id="1101" href="Function.Equality.html#1101" class="Bound">x</a> <a id="1103" class="Symbol">:</a> <a id="1105" href="Relation.Binary.Bundles.html#1104" class="Field">Setoid.Carrier</a> <a id="1120" href="Function.Equality.html#943" class="Bound">From</a><a id="1124" class="Symbol">)</a> <a id="1126" class="Symbol">→</a> <a id="1128" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#893" class="Field">IndexedSetoid.Carrier</a> <a id="1150" href="Function.Equality.html#974" class="Bound">To</a> <a id="1153" href="Function.Equality.html#1101" class="Bound">x</a>
    <a id="Π.cong"></a><a id="1159" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="1165" class="Symbol">:</a> <a id="1167" href="Relation.Binary.Bundles.html#1130" class="Field Operator">Setoid._≈_</a> <a id="1178" href="Function.Equality.html#943" class="Bound">From</a> <a id="1183" href="Relation.Binary.Indexed.Heterogeneous.Core.html#1071" class="Function Operator">=[</a> <a id="1186" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1192" href="Relation.Binary.Indexed.Heterogeneous.Core.html#1071" class="Function Operator">]⇒</a> <a id="1195" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#923" class="Field Operator">IndexedSetoid._≈_</a> <a id="1213" href="Function.Equality.html#974" class="Bound">To</a>

<a id="1217" class="Keyword">open</a> <a id="1222" href="Function.Equality.html#917" class="Module">Π</a> <a id="1224" class="Keyword">public</a>

<a id="1232" class="Keyword">infixr</a> <a id="1239" class="Number">0</a> <a id="1241" href="Function.Equality.html#1246" class="Function Operator">_⟶_</a>

<a id="_⟶_"></a><a id="1246" href="Function.Equality.html#1246" class="Function Operator">_⟶_</a> <a id="1250" class="Symbol">:</a> <a id="1252" class="Symbol">∀</a> <a id="1254" class="Symbol">{</a><a id="1255" href="Function.Equality.html#1255" class="Bound">f₁</a> <a id="1258" href="Function.Equality.html#1258" class="Bound">f₂</a> <a id="1261" href="Function.Equality.html#1261" class="Bound">t₁</a> <a id="1264" href="Function.Equality.html#1264" class="Bound">t₂</a><a id="1266" class="Symbol">}</a> <a id="1268" class="Symbol">→</a> <a id="1270" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1277" href="Function.Equality.html#1255" class="Bound">f₁</a> <a id="1280" href="Function.Equality.html#1258" class="Bound">f₂</a> <a id="1283" class="Symbol">→</a> <a id="1285" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1292" href="Function.Equality.html#1261" class="Bound">t₁</a> <a id="1295" href="Function.Equality.html#1264" class="Bound">t₂</a> <a id="1298" class="Symbol">→</a> <a id="1300" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1304" class="Symbol">_</a>
<a id="1306" href="Function.Equality.html#1306" class="Bound">From</a> <a id="1311" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="1313" href="Function.Equality.html#1313" class="Bound">To</a> <a id="1316" class="Symbol">=</a> <a id="1318" href="Function.Equality.html#917" class="Record">Π</a> <a id="1320" href="Function.Equality.html#1306" class="Bound">From</a> <a id="1325" class="Symbol">(</a><a id="1326" href="Relation.Binary.Indexed.Heterogeneous.Construct.Trivial.html#1333" class="Function">Trivial.indexedSetoid</a> <a id="1348" href="Function.Equality.html#1313" class="Bound">To</a><a id="1350" class="Symbol">)</a>

<a id="1353" class="Comment">------------------------------------------------------------------------</a>
<a id="1426" class="Comment">-- Identity and composition.</a>

<a id="id"></a><a id="1456" href="Function.Equality.html#1456" class="Function">id</a> <a id="1459" class="Symbol">:</a> <a id="1461" class="Symbol">∀</a> <a id="1463" class="Symbol">{</a><a id="1464" href="Function.Equality.html#1464" class="Bound">a₁</a> <a id="1467" href="Function.Equality.html#1467" class="Bound">a₂</a><a id="1469" class="Symbol">}</a> <a id="1471" class="Symbol">{</a><a id="1472" href="Function.Equality.html#1472" class="Bound">A</a> <a id="1474" class="Symbol">:</a> <a id="1476" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1483" href="Function.Equality.html#1464" class="Bound">a₁</a> <a id="1486" href="Function.Equality.html#1467" class="Bound">a₂</a><a id="1488" class="Symbol">}</a> <a id="1490" class="Symbol">→</a> <a id="1492" href="Function.Equality.html#1472" class="Bound">A</a> <a id="1494" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="1496" href="Function.Equality.html#1472" class="Bound">A</a>
<a id="1498" href="Function.Equality.html#1456" class="Function">id</a> <a id="1501" class="Symbol">=</a> <a id="1503" class="Keyword">record</a> <a id="1510" class="Symbol">{</a> <a id="1512" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1518" class="Symbol">=</a> <a id="1520" href="Function.Base.html#704" class="Function">Fun.id</a><a id="1526" class="Symbol">;</a> <a id="1528" href="Function.Equality.html#1159" class="Field">cong</a> <a id="1533" class="Symbol">=</a> <a id="1535" href="Function.Base.html#704" class="Function">Fun.id</a> <a id="1542" class="Symbol">}</a>

<a id="1545" class="Keyword">infixr</a> <a id="1552" class="Number">9</a> <a id="1554" href="Function.Equality.html#1559" class="Function Operator">_∘_</a>

<a id="_∘_"></a><a id="1559" href="Function.Equality.html#1559" class="Function Operator">_∘_</a> <a id="1563" class="Symbol">:</a> <a id="1565" class="Symbol">∀</a> <a id="1567" class="Symbol">{</a><a id="1568" href="Function.Equality.html#1568" class="Bound">a₁</a> <a id="1571" href="Function.Equality.html#1571" class="Bound">a₂</a><a id="1573" class="Symbol">}</a> <a id="1575" class="Symbol">{</a><a id="1576" href="Function.Equality.html#1576" class="Bound">A</a> <a id="1578" class="Symbol">:</a> <a id="1580" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1587" href="Function.Equality.html#1568" class="Bound">a₁</a> <a id="1590" href="Function.Equality.html#1571" class="Bound">a₂</a><a id="1592" class="Symbol">}</a>
        <a id="1602" class="Symbol">{</a><a id="1603" href="Function.Equality.html#1603" class="Bound">b₁</a> <a id="1606" href="Function.Equality.html#1606" class="Bound">b₂</a><a id="1608" class="Symbol">}</a> <a id="1610" class="Symbol">{</a><a id="1611" href="Function.Equality.html#1611" class="Bound">B</a> <a id="1613" class="Symbol">:</a> <a id="1615" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1622" href="Function.Equality.html#1603" class="Bound">b₁</a> <a id="1625" href="Function.Equality.html#1606" class="Bound">b₂</a><a id="1627" class="Symbol">}</a>
        <a id="1637" class="Symbol">{</a><a id="1638" href="Function.Equality.html#1638" class="Bound">c₁</a> <a id="1641" href="Function.Equality.html#1641" class="Bound">c₂</a><a id="1643" class="Symbol">}</a> <a id="1645" class="Symbol">{</a><a id="1646" href="Function.Equality.html#1646" class="Bound">C</a> <a id="1648" class="Symbol">:</a> <a id="1650" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1657" href="Function.Equality.html#1638" class="Bound">c₁</a> <a id="1660" href="Function.Equality.html#1641" class="Bound">c₂</a><a id="1662" class="Symbol">}</a> <a id="1664" class="Symbol">→</a>
      <a id="1672" href="Function.Equality.html#1611" class="Bound">B</a> <a id="1674" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="1676" href="Function.Equality.html#1646" class="Bound">C</a> <a id="1678" class="Symbol">→</a> <a id="1680" href="Function.Equality.html#1576" class="Bound">A</a> <a id="1682" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="1684" href="Function.Equality.html#1611" class="Bound">B</a> <a id="1686" class="Symbol">→</a> <a id="1688" href="Function.Equality.html#1576" class="Bound">A</a> <a id="1690" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="1692" href="Function.Equality.html#1646" class="Bound">C</a>
<a id="1694" href="Function.Equality.html#1694" class="Bound">f</a> <a id="1696" href="Function.Equality.html#1559" class="Function Operator">∘</a> <a id="1698" href="Function.Equality.html#1698" class="Bound">g</a> <a id="1700" class="Symbol">=</a> <a id="1702" class="Keyword">record</a>
  <a id="1711" class="Symbol">{</a> <a id="1713" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1719" class="Symbol">=</a> <a id="1721" href="Function.Base.html#1115" class="Function Operator">Fun._∘_</a> <a id="1729" class="Symbol">(</a><a id="1730" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1736" href="Function.Equality.html#1694" class="Bound">f</a><a id="1737" class="Symbol">)</a> <a id="1739" class="Symbol">(</a><a id="1740" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1746" href="Function.Equality.html#1698" class="Bound">g</a><a id="1747" class="Symbol">)</a>
  <a id="1751" class="Symbol">;</a> <a id="1753" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="1759" class="Symbol">=</a> <a id="1761" href="Function.Base.html#1115" class="Function Operator">Fun._∘_</a> <a id="1769" class="Symbol">(</a><a id="1770" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="1776" href="Function.Equality.html#1694" class="Bound">f</a><a id="1777" class="Symbol">)</a> <a id="1779" class="Symbol">(</a><a id="1780" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="1786" href="Function.Equality.html#1698" class="Bound">g</a><a id="1787" class="Symbol">)</a>
  <a id="1791" class="Symbol">}</a>

<a id="1794" class="Comment">-- Constant equality-preserving function.</a>

<a id="const"></a><a id="1837" href="Function.Equality.html#1837" class="Function">const</a> <a id="1843" class="Symbol">:</a> <a id="1845" class="Symbol">∀</a> <a id="1847" class="Symbol">{</a><a id="1848" href="Function.Equality.html#1848" class="Bound">a₁</a> <a id="1851" href="Function.Equality.html#1851" class="Bound">a₂</a><a id="1853" class="Symbol">}</a> <a id="1855" class="Symbol">{</a><a id="1856" href="Function.Equality.html#1856" class="Bound">A</a> <a id="1858" class="Symbol">:</a> <a id="1860" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1867" href="Function.Equality.html#1848" class="Bound">a₁</a> <a id="1870" href="Function.Equality.html#1851" class="Bound">a₂</a><a id="1872" class="Symbol">}</a>
          <a id="1884" class="Symbol">{</a><a id="1885" href="Function.Equality.html#1885" class="Bound">b₁</a> <a id="1888" href="Function.Equality.html#1888" class="Bound">b₂</a><a id="1890" class="Symbol">}</a> <a id="1892" class="Symbol">{</a><a id="1893" href="Function.Equality.html#1893" class="Bound">B</a> <a id="1895" class="Symbol">:</a> <a id="1897" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="1904" href="Function.Equality.html#1885" class="Bound">b₁</a> <a id="1907" href="Function.Equality.html#1888" class="Bound">b₂</a><a id="1909" class="Symbol">}</a> <a id="1911" class="Symbol">→</a>
        <a id="1921" href="Relation.Binary.Bundles.html#1104" class="Field">Setoid.Carrier</a> <a id="1936" href="Function.Equality.html#1893" class="Bound">B</a> <a id="1938" class="Symbol">→</a> <a id="1940" href="Function.Equality.html#1856" class="Bound">A</a> <a id="1942" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="1944" href="Function.Equality.html#1893" class="Bound">B</a>
<a id="1946" href="Function.Equality.html#1837" class="Function">const</a> <a id="1952" class="Symbol">{</a><a id="1953" class="Argument">B</a> <a id="1955" class="Symbol">=</a> <a id="1957" href="Function.Equality.html#1957" class="Bound">B</a><a id="1958" class="Symbol">}</a> <a id="1960" href="Function.Equality.html#1960" class="Bound">b</a> <a id="1962" class="Symbol">=</a> <a id="1964" class="Keyword">record</a>
  <a id="1973" class="Symbol">{</a> <a id="1975" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="1981" class="Symbol">=</a> <a id="1983" href="Function.Base.html#725" class="Function">Fun.const</a> <a id="1993" href="Function.Equality.html#1960" class="Bound">b</a>
  <a id="1997" class="Symbol">;</a> <a id="1999" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="2005" class="Symbol">=</a> <a id="2007" href="Function.Base.html#725" class="Function">Fun.const</a> <a id="2017" class="Symbol">(</a><a id="2018" href="Relation.Binary.Structures.html#1596" class="Function">Setoid.refl</a> <a id="2030" href="Function.Equality.html#1957" class="Bound">B</a><a id="2031" class="Symbol">)</a>
  <a id="2035" class="Symbol">}</a>

<a id="2038" class="Comment">------------------------------------------------------------------------</a>
<a id="2111" class="Comment">-- Function setoids</a>

<a id="2132" class="Comment">-- Dependent.</a>

<a id="setoid"></a><a id="2147" href="Function.Equality.html#2147" class="Function">setoid</a> <a id="2154" class="Symbol">:</a> <a id="2156" class="Symbol">∀</a> <a id="2158" class="Symbol">{</a><a id="2159" href="Function.Equality.html#2159" class="Bound">f₁</a> <a id="2162" href="Function.Equality.html#2162" class="Bound">f₂</a> <a id="2165" href="Function.Equality.html#2165" class="Bound">t₁</a> <a id="2168" href="Function.Equality.html#2168" class="Bound">t₂</a><a id="2170" class="Symbol">}</a>
         <a id="2181" class="Symbol">(</a><a id="2182" href="Function.Equality.html#2182" class="Bound">From</a> <a id="2187" class="Symbol">:</a> <a id="2189" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="2196" href="Function.Equality.html#2159" class="Bound">f₁</a> <a id="2199" href="Function.Equality.html#2162" class="Bound">f₂</a><a id="2201" class="Symbol">)</a> <a id="2203" class="Symbol">→</a>
         <a id="2214" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#803" class="Record">IndexedSetoid</a> <a id="2228" class="Symbol">(</a><a id="2229" href="Relation.Binary.Bundles.html#1104" class="Field">Setoid.Carrier</a> <a id="2244" href="Function.Equality.html#2182" class="Bound">From</a><a id="2248" class="Symbol">)</a> <a id="2250" href="Function.Equality.html#2165" class="Bound">t₁</a> <a id="2253" href="Function.Equality.html#2168" class="Bound">t₂</a> <a id="2256" class="Symbol">→</a>
         <a id="2267" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="2274" class="Symbol">_</a> <a id="2276" class="Symbol">_</a>
<a id="2278" href="Function.Equality.html#2147" class="Function">setoid</a> <a id="2285" href="Function.Equality.html#2285" class="Bound">From</a> <a id="2290" href="Function.Equality.html#2290" class="Bound">To</a> <a id="2293" class="Symbol">=</a> <a id="2295" class="Keyword">record</a>
  <a id="2304" class="Symbol">{</a> <a id="2306" href="Relation.Binary.Bundles.html#1104" class="Field">Carrier</a>       <a id="2320" class="Symbol">=</a> <a id="2322" href="Function.Equality.html#917" class="Record">Π</a> <a id="2324" href="Function.Equality.html#2285" class="Bound">From</a> <a id="2329" href="Function.Equality.html#2290" class="Bound">To</a>
  <a id="2334" class="Symbol">;</a> <a id="2336" href="Relation.Binary.Bundles.html#1130" class="Field Operator">_≈_</a>           <a id="2350" class="Symbol">=</a> <a id="2352" class="Symbol">λ</a> <a id="2354" href="Function.Equality.html#2354" class="Bound">f</a> <a id="2356" href="Function.Equality.html#2356" class="Bound">g</a> <a id="2358" class="Symbol">→</a> <a id="2360" class="Symbol">∀</a> <a id="2362" class="Symbol">{</a><a id="2363" href="Function.Equality.html#2363" class="Bound">x</a> <a id="2365" href="Function.Equality.html#2365" class="Bound">y</a><a id="2366" class="Symbol">}</a> <a id="2368" class="Symbol">→</a> <a id="2370" href="Function.Equality.html#2363" class="Bound">x</a> <a id="2372" href="Function.Equality.html#2650" class="Function Operator">≈₁</a> <a id="2375" href="Function.Equality.html#2365" class="Bound">y</a> <a id="2377" class="Symbol">→</a> <a id="2379" href="Function.Equality.html#2354" class="Bound">f</a> <a id="2381" href="Function.Equality.html#1092" class="Field Operator">⟨$⟩</a> <a id="2385" href="Function.Equality.html#2363" class="Bound">x</a> <a id="2387" href="Function.Equality.html#2720" class="Field Operator">≈₂</a> <a id="2390" href="Function.Equality.html#2356" class="Bound">g</a> <a id="2392" href="Function.Equality.html#1092" class="Field Operator">⟨$⟩</a> <a id="2396" href="Function.Equality.html#2365" class="Bound">y</a>
  <a id="2400" class="Symbol">;</a> <a id="2402" href="Relation.Binary.Bundles.html#1164" class="Field">isEquivalence</a> <a id="2416" class="Symbol">=</a> <a id="2418" class="Keyword">record</a>
    <a id="2429" class="Symbol">{</a> <a id="2431" href="Relation.Binary.Structures.html#1596" class="Field">refl</a>  <a id="2437" class="Symbol">=</a> <a id="2439" class="Symbol">λ</a> <a id="2441" class="Symbol">{</a><a id="2442" href="Function.Equality.html#2442" class="Bound">f</a><a id="2443" class="Symbol">}</a> <a id="2445" class="Symbol">→</a> <a id="2447" href="Function.Equality.html#1159" class="Field">cong</a> <a id="2452" href="Function.Equality.html#2442" class="Bound">f</a>
    <a id="2458" class="Symbol">;</a> <a id="2460" href="Relation.Binary.Structures.html#1622" class="Field">sym</a>   <a id="2466" class="Symbol">=</a> <a id="2468" class="Symbol">λ</a> <a id="2470" href="Function.Equality.html#2470" class="Bound">f∼g</a> <a id="2474" href="Function.Equality.html#2474" class="Bound">x∼y</a> <a id="2478" class="Symbol">→</a> <a id="2480" href="Relation.Binary.Indexed.Heterogeneous.Structures.html#952" class="Function">To.sym</a> <a id="2487" class="Symbol">(</a><a id="2488" href="Function.Equality.html#2470" class="Bound">f∼g</a> <a id="2492" class="Symbol">(</a><a id="2493" href="Relation.Binary.Structures.html#1622" class="Function">From.sym</a> <a id="2502" href="Function.Equality.html#2474" class="Bound">x∼y</a><a id="2505" class="Symbol">))</a>
    <a id="2512" class="Symbol">;</a> <a id="2514" href="Relation.Binary.Structures.html#1648" class="Field">trans</a> <a id="2520" class="Symbol">=</a> <a id="2522" class="Symbol">λ</a> <a id="2524" href="Function.Equality.html#2524" class="Bound">f∼g</a> <a id="2528" href="Function.Equality.html#2528" class="Bound">g∼h</a> <a id="2532" href="Function.Equality.html#2532" class="Bound">x∼y</a> <a id="2536" class="Symbol">→</a> <a id="2538" href="Relation.Binary.Indexed.Heterogeneous.Structures.html#981" class="Function">To.trans</a> <a id="2547" class="Symbol">(</a><a id="2548" href="Function.Equality.html#2524" class="Bound">f∼g</a> <a id="2552" href="Relation.Binary.Structures.html#1596" class="Function">From.refl</a><a id="2561" class="Symbol">)</a> <a id="2563" class="Symbol">(</a><a id="2564" href="Function.Equality.html#2528" class="Bound">g∼h</a> <a id="2568" href="Function.Equality.html#2532" class="Bound">x∼y</a><a id="2571" class="Symbol">)</a>
    <a id="2577" class="Symbol">}</a>
  <a id="2581" class="Symbol">}</a>
  <a id="2585" class="Keyword">where</a>
  <a id="2593" class="Keyword">open</a> <a id="2598" class="Keyword">module</a> <a id="2605" href="Function.Equality.html#2605" class="Module">From</a> <a id="2610" class="Symbol">=</a> <a id="2612" href="Relation.Binary.Bundles.html#1041" class="Module">Setoid</a> <a id="2619" href="Function.Equality.html#2285" class="Bound">From</a> <a id="2624" class="Keyword">using</a> <a id="2630" class="Symbol">()</a> <a id="2633" class="Keyword">renaming</a> <a id="2642" class="Symbol">(</a><a id="2643" href="Relation.Binary.Bundles.html#1130" class="Function Operator">_≈_</a> <a id="2647" class="Symbol">to</a> <a id="2650" class="Function Operator">_≈₁_</a><a id="2654" class="Symbol">)</a>
  <a id="2658" class="Keyword">open</a> <a id="2663" class="Keyword">module</a> <a id="2670" href="Function.Equality.html#2670" class="Module">To</a> <a id="2673" class="Symbol">=</a> <a id="2675" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#803" class="Module">IndexedSetoid</a> <a id="2689" href="Function.Equality.html#2290" class="Bound">To</a>   <a id="2694" class="Keyword">using</a> <a id="2700" class="Symbol">()</a> <a id="2703" class="Keyword">renaming</a> <a id="2712" class="Symbol">(</a><a id="2713" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#923" class="Field Operator">_≈_</a> <a id="2717" class="Symbol">to</a> <a id="2720" class="Field Operator">_≈₂_</a><a id="2724" class="Symbol">)</a>

<a id="2727" class="Comment">-- Non-dependent.</a>

<a id="2746" class="Keyword">infixr</a> <a id="2753" class="Number">0</a> <a id="2755" href="Function.Equality.html#2760" class="Function Operator">_⇨_</a>

<a id="_⇨_"></a><a id="2760" href="Function.Equality.html#2760" class="Function Operator">_⇨_</a> <a id="2764" class="Symbol">:</a> <a id="2766" class="Symbol">∀</a> <a id="2768" class="Symbol">{</a><a id="2769" href="Function.Equality.html#2769" class="Bound">f₁</a> <a id="2772" href="Function.Equality.html#2772" class="Bound">f₂</a> <a id="2775" href="Function.Equality.html#2775" class="Bound">t₁</a> <a id="2778" href="Function.Equality.html#2778" class="Bound">t₂</a><a id="2780" class="Symbol">}</a> <a id="2782" class="Symbol">→</a> <a id="2784" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="2791" href="Function.Equality.html#2769" class="Bound">f₁</a> <a id="2794" href="Function.Equality.html#2772" class="Bound">f₂</a> <a id="2797" class="Symbol">→</a> <a id="2799" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="2806" href="Function.Equality.html#2775" class="Bound">t₁</a> <a id="2809" href="Function.Equality.html#2778" class="Bound">t₂</a> <a id="2812" class="Symbol">→</a> <a id="2814" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="2821" class="Symbol">_</a> <a id="2823" class="Symbol">_</a>
<a id="2825" href="Function.Equality.html#2825" class="Bound">From</a> <a id="2830" href="Function.Equality.html#2760" class="Function Operator">⇨</a> <a id="2832" href="Function.Equality.html#2832" class="Bound">To</a> <a id="2835" class="Symbol">=</a> <a id="2837" href="Function.Equality.html#2147" class="Function">setoid</a> <a id="2844" href="Function.Equality.html#2825" class="Bound">From</a> <a id="2849" class="Symbol">(</a><a id="2850" href="Relation.Binary.Indexed.Heterogeneous.Construct.Trivial.html#1333" class="Function">Trivial.indexedSetoid</a> <a id="2872" href="Function.Equality.html#2832" class="Bound">To</a><a id="2874" class="Symbol">)</a>

<a id="2877" class="Comment">-- A variant of setoid which uses the propositional equality setoid</a>
<a id="2945" class="Comment">-- for the domain, and a more convenient definition of _≈_.</a>

<a id="≡-setoid"></a><a id="3006" href="Function.Equality.html#3006" class="Function">≡-setoid</a> <a id="3015" class="Symbol">:</a> <a id="3017" class="Symbol">∀</a> <a id="3019" class="Symbol">{</a><a id="3020" href="Function.Equality.html#3020" class="Bound">f</a> <a id="3022" href="Function.Equality.html#3022" class="Bound">t₁</a> <a id="3025" href="Function.Equality.html#3025" class="Bound">t₂</a><a id="3027" class="Symbol">}</a> <a id="3029" class="Symbol">(</a><a id="3030" href="Function.Equality.html#3030" class="Bound">From</a> <a id="3035" class="Symbol">:</a> <a id="3037" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="3041" href="Function.Equality.html#3020" class="Bound">f</a><a id="3042" class="Symbol">)</a> <a id="3044" class="Symbol">→</a> <a id="3046" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#803" class="Record">IndexedSetoid</a> <a id="3060" href="Function.Equality.html#3030" class="Bound">From</a> <a id="3065" href="Function.Equality.html#3022" class="Bound">t₁</a> <a id="3068" href="Function.Equality.html#3025" class="Bound">t₂</a> <a id="3071" class="Symbol">→</a> <a id="3073" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="3080" class="Symbol">_</a> <a id="3082" class="Symbol">_</a>
<a id="3084" href="Function.Equality.html#3006" class="Function">≡-setoid</a> <a id="3093" href="Function.Equality.html#3093" class="Bound">From</a> <a id="3098" href="Function.Equality.html#3098" class="Bound">To</a> <a id="3101" class="Symbol">=</a> <a id="3103" class="Keyword">record</a>
  <a id="3112" class="Symbol">{</a> <a id="3114" href="Relation.Binary.Bundles.html#1104" class="Field">Carrier</a>       <a id="3128" class="Symbol">=</a> <a id="3130" class="Symbol">(</a><a id="3131" href="Function.Equality.html#3131" class="Bound">x</a> <a id="3133" class="Symbol">:</a> <a id="3135" href="Function.Equality.html#3093" class="Bound">From</a><a id="3139" class="Symbol">)</a> <a id="3141" class="Symbol">→</a> <a id="3143" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#893" class="Field">Carrier</a> <a id="3151" href="Function.Equality.html#3131" class="Bound">x</a>
  <a id="3155" class="Symbol">;</a> <a id="3157" href="Relation.Binary.Bundles.html#1130" class="Field Operator">_≈_</a>           <a id="3171" class="Symbol">=</a> <a id="3173" class="Symbol">λ</a> <a id="3175" href="Function.Equality.html#3175" class="Bound">f</a> <a id="3177" href="Function.Equality.html#3177" class="Bound">g</a> <a id="3179" class="Symbol">→</a> <a id="3181" class="Symbol">∀</a> <a id="3183" href="Function.Equality.html#3183" class="Bound">x</a> <a id="3185" class="Symbol">→</a> <a id="3187" href="Function.Equality.html#3175" class="Bound">f</a> <a id="3189" href="Function.Equality.html#3183" class="Bound">x</a> <a id="3191" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#923" class="Field Operator">≈</a> <a id="3193" href="Function.Equality.html#3177" class="Bound">g</a> <a id="3195" href="Function.Equality.html#3183" class="Bound">x</a>
  <a id="3199" class="Symbol">;</a> <a id="3201" href="Relation.Binary.Bundles.html#1164" class="Field">isEquivalence</a> <a id="3215" class="Symbol">=</a> <a id="3217" class="Keyword">record</a>
    <a id="3228" class="Symbol">{</a> <a id="3230" href="Relation.Binary.Structures.html#1596" class="Field">refl</a>  <a id="3236" class="Symbol">=</a> <a id="3238" class="Symbol">λ</a> <a id="3240" class="Symbol">{</a><a id="3241" href="Function.Equality.html#3241" class="Bound">f</a><a id="3242" class="Symbol">}</a> <a id="3244" href="Function.Equality.html#3244" class="Bound">x</a> <a id="3246" class="Symbol">→</a> <a id="3248" href="Relation.Binary.Indexed.Heterogeneous.Structures.html#923" class="Function">refl</a>
    <a id="3257" class="Symbol">;</a> <a id="3259" href="Relation.Binary.Structures.html#1622" class="Field">sym</a>   <a id="3265" class="Symbol">=</a> <a id="3267" class="Symbol">λ</a> <a id="3269" href="Function.Equality.html#3269" class="Bound">f∼g</a> <a id="3273" href="Function.Equality.html#3273" class="Bound">x</a> <a id="3275" class="Symbol">→</a> <a id="3277" href="Relation.Binary.Indexed.Heterogeneous.Structures.html#952" class="Function">sym</a> <a id="3281" class="Symbol">(</a><a id="3282" href="Function.Equality.html#3269" class="Bound">f∼g</a> <a id="3286" href="Function.Equality.html#3273" class="Bound">x</a><a id="3287" class="Symbol">)</a>
    <a id="3293" class="Symbol">;</a> <a id="3295" href="Relation.Binary.Structures.html#1648" class="Field">trans</a> <a id="3301" class="Symbol">=</a> <a id="3303" class="Symbol">λ</a> <a id="3305" href="Function.Equality.html#3305" class="Bound">f∼g</a> <a id="3309" href="Function.Equality.html#3309" class="Bound">g∼h</a> <a id="3313" href="Function.Equality.html#3313" class="Bound">x</a> <a id="3315" class="Symbol">→</a> <a id="3317" href="Relation.Binary.Indexed.Heterogeneous.Structures.html#981" class="Function">trans</a> <a id="3323" class="Symbol">(</a><a id="3324" href="Function.Equality.html#3305" class="Bound">f∼g</a> <a id="3328" href="Function.Equality.html#3313" class="Bound">x</a><a id="3329" class="Symbol">)</a> <a id="3331" class="Symbol">(</a><a id="3332" href="Function.Equality.html#3309" class="Bound">g∼h</a> <a id="3336" href="Function.Equality.html#3313" class="Bound">x</a><a id="3337" class="Symbol">)</a>
    <a id="3343" class="Symbol">}</a>
  <a id="3347" class="Symbol">}</a> <a id="3349" class="Keyword">where</a> <a id="3355" class="Keyword">open</a> <a id="3360" href="Relation.Binary.Indexed.Heterogeneous.Bundles.html#803" class="Module">IndexedSetoid</a> <a id="3374" href="Function.Equality.html#3098" class="Bound">To</a>

<a id="3378" class="Comment">-- Parameter swapping function.</a>

<a id="flip"></a><a id="3411" href="Function.Equality.html#3411" class="Function">flip</a> <a id="3416" class="Symbol">:</a> <a id="3418" class="Symbol">∀</a> <a id="3420" class="Symbol">{</a><a id="3421" href="Function.Equality.html#3421" class="Bound">a₁</a> <a id="3424" href="Function.Equality.html#3424" class="Bound">a₂</a><a id="3426" class="Symbol">}</a> <a id="3428" class="Symbol">{</a><a id="3429" href="Function.Equality.html#3429" class="Bound">A</a> <a id="3431" class="Symbol">:</a> <a id="3433" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="3440" href="Function.Equality.html#3421" class="Bound">a₁</a> <a id="3443" href="Function.Equality.html#3424" class="Bound">a₂</a><a id="3445" class="Symbol">}</a>
         <a id="3456" class="Symbol">{</a><a id="3457" href="Function.Equality.html#3457" class="Bound">b₁</a> <a id="3460" href="Function.Equality.html#3460" class="Bound">b₂</a><a id="3462" class="Symbol">}</a> <a id="3464" class="Symbol">{</a><a id="3465" href="Function.Equality.html#3465" class="Bound">B</a> <a id="3467" class="Symbol">:</a> <a id="3469" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="3476" href="Function.Equality.html#3457" class="Bound">b₁</a> <a id="3479" href="Function.Equality.html#3460" class="Bound">b₂</a><a id="3481" class="Symbol">}</a>
         <a id="3492" class="Symbol">{</a><a id="3493" href="Function.Equality.html#3493" class="Bound">c₁</a> <a id="3496" href="Function.Equality.html#3496" class="Bound">c₂</a><a id="3498" class="Symbol">}</a> <a id="3500" class="Symbol">{</a><a id="3501" href="Function.Equality.html#3501" class="Bound">C</a> <a id="3503" class="Symbol">:</a> <a id="3505" href="Relation.Binary.Bundles.html#1041" class="Record">Setoid</a> <a id="3512" href="Function.Equality.html#3493" class="Bound">c₁</a> <a id="3515" href="Function.Equality.html#3496" class="Bound">c₂</a><a id="3517" class="Symbol">}</a> <a id="3519" class="Symbol">→</a>
       <a id="3528" href="Function.Equality.html#3429" class="Bound">A</a> <a id="3530" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="3532" href="Function.Equality.html#3465" class="Bound">B</a> <a id="3534" href="Function.Equality.html#2760" class="Function Operator">⇨</a> <a id="3536" href="Function.Equality.html#3501" class="Bound">C</a> <a id="3538" class="Symbol">→</a> <a id="3540" href="Function.Equality.html#3465" class="Bound">B</a> <a id="3542" href="Function.Equality.html#1246" class="Function Operator">⟶</a> <a id="3544" href="Function.Equality.html#3429" class="Bound">A</a> <a id="3546" href="Function.Equality.html#2760" class="Function Operator">⇨</a> <a id="3548" href="Function.Equality.html#3501" class="Bound">C</a>
<a id="3550" href="Function.Equality.html#3411" class="Function">flip</a> <a id="3555" class="Symbol">{</a><a id="3556" class="Argument">B</a> <a id="3558" class="Symbol">=</a> <a id="3560" href="Function.Equality.html#3560" class="Bound">B</a><a id="3561" class="Symbol">}</a> <a id="3563" href="Function.Equality.html#3563" class="Bound">f</a> <a id="3565" class="Symbol">=</a> <a id="3567" class="Keyword">record</a>
  <a id="3576" class="Symbol">{</a> <a id="3578" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="3584" class="Symbol">=</a> <a id="3586" class="Symbol">λ</a> <a id="3588" href="Function.Equality.html#3588" class="Bound">b</a> <a id="3590" class="Symbol">→</a> <a id="3592" class="Keyword">record</a>
    <a id="3603" class="Symbol">{</a> <a id="3605" href="Function.Equality.html#1092" class="Field Operator">_⟨$⟩_</a> <a id="3611" class="Symbol">=</a> <a id="3613" class="Symbol">λ</a> <a id="3615" href="Function.Equality.html#3615" class="Bound">a</a> <a id="3617" class="Symbol">→</a> <a id="3619" href="Function.Equality.html#3563" class="Bound">f</a> <a id="3621" href="Function.Equality.html#1092" class="Field Operator">⟨$⟩</a> <a id="3625" href="Function.Equality.html#3615" class="Bound">a</a> <a id="3627" href="Function.Equality.html#1092" class="Field Operator">⟨$⟩</a> <a id="3631" href="Function.Equality.html#3588" class="Bound">b</a>
    <a id="3637" class="Symbol">;</a> <a id="3639" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="3645" class="Symbol">=</a> <a id="3647" class="Symbol">λ</a> <a id="3649" href="Function.Equality.html#3649" class="Bound">a₁≈a₂</a> <a id="3655" class="Symbol">→</a> <a id="3657" href="Function.Equality.html#1159" class="Field">cong</a> <a id="3662" href="Function.Equality.html#3563" class="Bound">f</a> <a id="3664" href="Function.Equality.html#3649" class="Bound">a₁≈a₂</a> <a id="3670" class="Symbol">(</a><a id="3671" href="Relation.Binary.Structures.html#1596" class="Function">Setoid.refl</a> <a id="3683" href="Function.Equality.html#3560" class="Bound">B</a><a id="3684" class="Symbol">)</a> <a id="3686" class="Symbol">}</a>
  <a id="3690" class="Symbol">;</a> <a id="3692" href="Function.Equality.html#1159" class="Field">cong</a>  <a id="3698" class="Symbol">=</a> <a id="3700" class="Symbol">λ</a> <a id="3702" href="Function.Equality.html#3702" class="Bound">b₁≈b₂</a> <a id="3708" href="Function.Equality.html#3708" class="Bound">a₁≈a₂</a> <a id="3714" class="Symbol">→</a> <a id="3716" href="Function.Equality.html#1159" class="Field">cong</a> <a id="3721" href="Function.Equality.html#3563" class="Bound">f</a> <a id="3723" href="Function.Equality.html#3708" class="Bound">a₁≈a₂</a> <a id="3729" href="Function.Equality.html#3702" class="Bound">b₁≈b₂</a>
  <a id="3737" class="Symbol">}</a>
</pre></body></html>