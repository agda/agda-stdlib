<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Solver.Monoid</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Solver for monoid equalities</a>
<a id="138" class="Comment">------------------------------------------------------------------------</a>

<a id="212" class="Symbol">{-#</a> <a id="216" class="Keyword">OPTIONS</a> <a id="224" class="Pragma">--without-K</a> <a id="236" class="Pragma">--safe</a> <a id="243" class="Symbol">#-}</a>

<a id="248" class="Keyword">open</a> <a id="253" class="Keyword">import</a> <a id="260" href="Algebra.html" class="Module">Algebra</a>

<a id="269" class="Keyword">module</a> <a id="276" href="Algebra.Solver.Monoid.html" class="Module">Algebra.Solver.Monoid</a> <a id="298" class="Symbol">{</a><a id="299" href="Algebra.Solver.Monoid.html#299" class="Bound">m₁</a> <a id="302" href="Algebra.Solver.Monoid.html#302" class="Bound">m₂</a><a id="304" class="Symbol">}</a> <a id="306" class="Symbol">(</a><a id="307" href="Algebra.Solver.Monoid.html#307" class="Bound">M</a> <a id="309" class="Symbol">:</a> <a id="311" href="Algebra.html#2651" class="Record">Monoid</a> <a id="318" href="Algebra.Solver.Monoid.html#299" class="Bound">m₁</a> <a id="321" href="Algebra.Solver.Monoid.html#302" class="Bound">m₂</a><a id="323" class="Symbol">)</a> <a id="325" class="Keyword">where</a>

<a id="332" class="Keyword">open</a> <a id="337" class="Keyword">import</a> <a id="344" href="Data.Fin.html" class="Module">Data.Fin</a> <a id="353" class="Symbol">as</a> <a id="356" class="Module">Fin</a> <a id="360" class="Keyword">hiding</a> <a id="367" class="Symbol">(</a><a id="368" href="Data.Fin.Properties.html#1730" class="Function Operator">_≟_</a><a id="371" class="Symbol">)</a>
<a id="373" class="Keyword">import</a> <a id="380" href="Data.Fin.Properties.html" class="Module">Data.Fin.Properties</a> <a id="400" class="Symbol">as</a> <a id="403" class="Module">Fin</a>
<a id="407" class="Keyword">open</a> <a id="412" class="Keyword">import</a> <a id="419" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="434" class="Keyword">hiding</a> <a id="441" class="Symbol">(</a><a id="442" href="Data.List.Base.html#5343" class="Function">lookup</a><a id="448" class="Symbol">)</a>
<a id="450" class="Keyword">import</a> <a id="457" href="Data.List.Relation.Binary.Equality.DecPropositional.html" class="Module">Data.List.Relation.Binary.Equality.DecPropositional</a> <a id="509" class="Symbol">as</a> <a id="512" class="Module">ListEq</a>
<a id="519" class="Keyword">open</a> <a id="524" class="Keyword">import</a> <a id="531" href="Data.Maybe.html" class="Module">Data.Maybe</a> <a id="542" class="Symbol">as</a> <a id="545" class="Module">Maybe</a>
  <a id="553" class="Keyword">using</a> <a id="559" class="Symbol">(</a><a id="560" href="Data.Maybe.Base.html#795" class="Datatype">Maybe</a><a id="565" class="Symbol">;</a> <a id="567" href="Data.Maybe.Base.html#1187" class="Function">decToMaybe</a><a id="577" class="Symbol">;</a> <a id="579" href="Data.Maybe.Base.html#1746" class="Function">From-just</a><a id="588" class="Symbol">;</a> <a id="590" href="Data.Maybe.Base.html#1834" class="Function">from-just</a><a id="599" class="Symbol">)</a>
<a id="601" class="Keyword">open</a> <a id="606" class="Keyword">import</a> <a id="613" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="627" class="Keyword">using</a> <a id="633" class="Symbol">(</a><a id="634" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a><a id="635" class="Symbol">)</a>
<a id="637" class="Keyword">open</a> <a id="642" class="Keyword">import</a> <a id="649" href="Data.Product.html" class="Module">Data.Product</a>
<a id="662" class="Keyword">open</a> <a id="667" class="Keyword">import</a> <a id="674" href="Data.Vec.html" class="Module">Data.Vec</a> <a id="683" class="Keyword">using</a> <a id="689" class="Symbol">(</a><a id="690" href="Data.Vec.html#887" class="Datatype">Vec</a><a id="693" class="Symbol">;</a> <a id="695" href="Data.Vec.html#1443" class="Function">lookup</a><a id="701" class="Symbol">)</a>
<a id="703" class="Keyword">open</a> <a id="708" class="Keyword">import</a> <a id="715" href="Function.html" class="Module">Function</a> <a id="724" class="Keyword">using</a> <a id="730" class="Symbol">(</a><a id="731" href="Function.html#1085" class="Function Operator">_∘_</a><a id="734" class="Symbol">;</a> <a id="736" href="Function.html#1631" class="Function Operator">_$_</a><a id="739" class="Symbol">)</a>
<a id="741" class="Keyword">open</a> <a id="746" class="Keyword">import</a> <a id="753" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="769" class="Keyword">using</a> <a id="775" class="Symbol">(</a><a id="776" href="Relation.Binary.Core.html#5557" class="Function">Decidable</a><a id="785" class="Symbol">)</a>

<a id="788" class="Keyword">open</a> <a id="793" class="Keyword">import</a> <a id="800" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="838" class="Symbol">as</a> <a id="841" class="Module">P</a> <a id="843" class="Keyword">using</a> <a id="849" class="Symbol">(</a><a id="850" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">_≡_</a><a id="853" class="Symbol">)</a>
<a id="855" class="Keyword">import</a> <a id="862" href="Relation.Binary.Reflection.html" class="Module">Relation.Binary.Reflection</a>
<a id="889" class="Keyword">open</a> <a id="894" class="Keyword">import</a> <a id="901" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>
<a id="918" class="Keyword">import</a> <a id="925" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="952" class="Symbol">as</a> <a id="955" class="Module">Dec</a>

<a id="960" class="Keyword">open</a> <a id="965" href="Algebra.html#2651" class="Module">Monoid</a> <a id="972" href="Algebra.Solver.Monoid.html#307" class="Bound">M</a>
<a id="974" class="Keyword">open</a> <a id="979" class="Keyword">import</a> <a id="986" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="1019" href="Algebra.Structures.html#1153" class="Function">setoid</a>

<a id="1027" class="Comment">------------------------------------------------------------------------</a>
<a id="1100" class="Comment">-- Monoid expressions</a>

<a id="1123" class="Comment">-- There is one constructor for every operation, plus one for</a>
<a id="1185" class="Comment">-- variables; there may be at most n variables.</a>

<a id="1234" class="Keyword">infixr</a> <a id="1241" class="Number">5</a> <a id="1243" href="Algebra.Solver.Monoid.html#1318" class="InductiveConstructor Operator">_⊕_</a>

<a id="1248" class="Keyword">data</a> <a id="Expr"></a><a id="1253" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1258" class="Symbol">(</a><a id="1259" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a> <a id="1261" class="Symbol">:</a> <a id="1263" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a><a id="1264" class="Symbol">)</a> <a id="1266" class="Symbol">:</a> <a id="1268" class="PrimitiveType">Set</a> <a id="1272" class="Keyword">where</a>
  <a id="Expr.var"></a><a id="1280" href="Algebra.Solver.Monoid.html#1280" class="InductiveConstructor">var</a> <a id="1284" class="Symbol">:</a> <a id="1286" href="Data.Fin.Base.html#951" class="Datatype">Fin</a> <a id="1290" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a> <a id="1292" class="Symbol">→</a> <a id="1294" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1299" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a>
  <a id="Expr.id"></a><a id="1303" href="Algebra.Solver.Monoid.html#1303" class="InductiveConstructor">id</a>  <a id="1307" class="Symbol">:</a> <a id="1309" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1314" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a>
  <a id="Expr._⊕_"></a><a id="1318" href="Algebra.Solver.Monoid.html#1318" class="InductiveConstructor Operator">_⊕_</a> <a id="1322" class="Symbol">:</a> <a id="1324" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1329" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a> <a id="1331" class="Symbol">→</a> <a id="1333" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1338" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a> <a id="1340" class="Symbol">→</a> <a id="1342" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1347" href="Algebra.Solver.Monoid.html#1259" class="Bound">n</a>

<a id="1350" class="Comment">-- An environment contains one value for every variable.</a>

<a id="Env"></a><a id="1408" href="Algebra.Solver.Monoid.html#1408" class="Function">Env</a> <a id="1412" class="Symbol">:</a> <a id="1414" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a> <a id="1416" class="Symbol">→</a> <a id="1418" class="PrimitiveType">Set</a> <a id="1422" class="Symbol">_</a>
<a id="1424" href="Algebra.Solver.Monoid.html#1408" class="Function">Env</a> <a id="1428" href="Algebra.Solver.Monoid.html#1428" class="Bound">n</a> <a id="1430" class="Symbol">=</a> <a id="1432" href="Data.Vec.html#887" class="Datatype">Vec</a> <a id="1436" href="Algebra.html#2730" class="Field">Carrier</a> <a id="1444" href="Algebra.Solver.Monoid.html#1428" class="Bound">n</a>

<a id="1447" class="Comment">-- The semantics of an expression is a function from an environment to</a>
<a id="1518" class="Comment">-- a value.</a>

<a id="⟦_⟧"></a><a id="1531" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦_⟧</a> <a id="1535" class="Symbol">:</a> <a id="1537" class="Symbol">∀</a> <a id="1539" class="Symbol">{</a><a id="1540" href="Algebra.Solver.Monoid.html#1540" class="Bound">n</a><a id="1541" class="Symbol">}</a> <a id="1543" class="Symbol">→</a> <a id="1545" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="1550" href="Algebra.Solver.Monoid.html#1540" class="Bound">n</a> <a id="1552" class="Symbol">→</a> <a id="1554" href="Algebra.Solver.Monoid.html#1408" class="Function">Env</a> <a id="1558" href="Algebra.Solver.Monoid.html#1540" class="Bound">n</a> <a id="1560" class="Symbol">→</a> <a id="1562" href="Algebra.html#2730" class="Field">Carrier</a>
<a id="1570" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="1572" href="Algebra.Solver.Monoid.html#1280" class="InductiveConstructor">var</a> <a id="1576" href="Algebra.Solver.Monoid.html#1576" class="Bound">x</a>   <a id="1580" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="1582" href="Algebra.Solver.Monoid.html#1582" class="Bound">ρ</a> <a id="1584" class="Symbol">=</a> <a id="1586" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="1593" href="Algebra.Solver.Monoid.html#1582" class="Bound">ρ</a> <a id="1595" href="Algebra.Solver.Monoid.html#1576" class="Bound">x</a>
<a id="1597" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="1599" href="Algebra.Solver.Monoid.html#1303" class="InductiveConstructor">id</a>      <a id="1607" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="1609" href="Algebra.Solver.Monoid.html#1609" class="Bound">ρ</a> <a id="1611" class="Symbol">=</a> <a id="1613" href="Algebra.html#2807" class="Field">ε</a>
<a id="1615" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="1617" href="Algebra.Solver.Monoid.html#1617" class="Bound">e₁</a> <a id="1620" href="Algebra.Solver.Monoid.html#1318" class="InductiveConstructor Operator">⊕</a> <a id="1622" href="Algebra.Solver.Monoid.html#1622" class="Bound">e₂</a> <a id="1625" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="1627" href="Algebra.Solver.Monoid.html#1627" class="Bound">ρ</a> <a id="1629" class="Symbol">=</a> <a id="1631" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="1633" href="Algebra.Solver.Monoid.html#1617" class="Bound">e₁</a> <a id="1636" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="1638" href="Algebra.Solver.Monoid.html#1627" class="Bound">ρ</a> <a id="1640" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="1642" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="1644" href="Algebra.Solver.Monoid.html#1622" class="Bound">e₂</a> <a id="1647" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="1649" href="Algebra.Solver.Monoid.html#1627" class="Bound">ρ</a>

<a id="1652" class="Comment">------------------------------------------------------------------------</a>
<a id="1725" class="Comment">-- Normal forms</a>

<a id="1742" class="Comment">-- A normal form is a list of variables.</a>

<a id="Normal"></a><a id="1784" href="Algebra.Solver.Monoid.html#1784" class="Function">Normal</a> <a id="1791" class="Symbol">:</a> <a id="1793" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a> <a id="1795" class="Symbol">→</a> <a id="1797" class="PrimitiveType">Set</a>
<a id="1801" href="Algebra.Solver.Monoid.html#1784" class="Function">Normal</a> <a id="1808" href="Algebra.Solver.Monoid.html#1808" class="Bound">n</a> <a id="1810" class="Symbol">=</a> <a id="1812" href="Agda.Builtin.List.html#121" class="Datatype">List</a> <a id="1817" class="Symbol">(</a><a id="1818" href="Data.Fin.Base.html#951" class="Datatype">Fin</a> <a id="1822" href="Algebra.Solver.Monoid.html#1808" class="Bound">n</a><a id="1823" class="Symbol">)</a>

<a id="1826" class="Comment">-- The semantics of a normal form.</a>

<a id="⟦_⟧⇓"></a><a id="1862" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦_⟧⇓</a> <a id="1867" class="Symbol">:</a> <a id="1869" class="Symbol">∀</a> <a id="1871" class="Symbol">{</a><a id="1872" href="Algebra.Solver.Monoid.html#1872" class="Bound">n</a><a id="1873" class="Symbol">}</a> <a id="1875" class="Symbol">→</a> <a id="1877" href="Algebra.Solver.Monoid.html#1784" class="Function">Normal</a> <a id="1884" href="Algebra.Solver.Monoid.html#1872" class="Bound">n</a> <a id="1886" class="Symbol">→</a> <a id="1888" href="Algebra.Solver.Monoid.html#1408" class="Function">Env</a> <a id="1892" href="Algebra.Solver.Monoid.html#1872" class="Bound">n</a> <a id="1894" class="Symbol">→</a> <a id="1896" href="Algebra.html#2730" class="Field">Carrier</a>
<a id="1904" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="1906" href="Agda.Builtin.List.html#158" class="InductiveConstructor">[]</a>     <a id="1913" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="1916" href="Algebra.Solver.Monoid.html#1916" class="Bound">ρ</a> <a id="1918" class="Symbol">=</a> <a id="1920" href="Algebra.html#2807" class="Field">ε</a>
<a id="1922" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="1924" href="Algebra.Solver.Monoid.html#1924" class="Bound">x</a> <a id="1926" href="Agda.Builtin.List.html#173" class="InductiveConstructor Operator">∷</a> <a id="1928" href="Algebra.Solver.Monoid.html#1928" class="Bound">nf</a> <a id="1931" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="1934" href="Algebra.Solver.Monoid.html#1934" class="Bound">ρ</a> <a id="1936" class="Symbol">=</a> <a id="1938" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="1945" href="Algebra.Solver.Monoid.html#1934" class="Bound">ρ</a> <a id="1947" href="Algebra.Solver.Monoid.html#1924" class="Bound">x</a> <a id="1949" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="1951" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="1953" href="Algebra.Solver.Monoid.html#1928" class="Bound">nf</a> <a id="1956" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="1959" href="Algebra.Solver.Monoid.html#1934" class="Bound">ρ</a>

<a id="1962" class="Comment">-- A normaliser.</a>

<a id="normalise"></a><a id="1980" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="1990" class="Symbol">:</a> <a id="1992" class="Symbol">∀</a> <a id="1994" class="Symbol">{</a><a id="1995" href="Algebra.Solver.Monoid.html#1995" class="Bound">n</a><a id="1996" class="Symbol">}</a> <a id="1998" class="Symbol">→</a> <a id="2000" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="2005" href="Algebra.Solver.Monoid.html#1995" class="Bound">n</a> <a id="2007" class="Symbol">→</a> <a id="2009" href="Algebra.Solver.Monoid.html#1784" class="Function">Normal</a> <a id="2016" href="Algebra.Solver.Monoid.html#1995" class="Bound">n</a>
<a id="2018" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2028" class="Symbol">(</a><a id="2029" href="Algebra.Solver.Monoid.html#1280" class="InductiveConstructor">var</a> <a id="2033" href="Algebra.Solver.Monoid.html#2033" class="Bound">x</a><a id="2034" class="Symbol">)</a>   <a id="2038" class="Symbol">=</a> <a id="2040" href="Algebra.Solver.Monoid.html#2033" class="Bound">x</a> <a id="2042" href="Agda.Builtin.List.html#173" class="InductiveConstructor Operator">∷</a> <a id="2044" href="Agda.Builtin.List.html#158" class="InductiveConstructor">[]</a>
<a id="2047" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2057" href="Algebra.Solver.Monoid.html#1303" class="InductiveConstructor">id</a>        <a id="2067" class="Symbol">=</a> <a id="2069" href="Agda.Builtin.List.html#158" class="InductiveConstructor">[]</a>
<a id="2072" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2082" class="Symbol">(</a><a id="2083" href="Algebra.Solver.Monoid.html#2083" class="Bound">e₁</a> <a id="2086" href="Algebra.Solver.Monoid.html#1318" class="InductiveConstructor Operator">⊕</a> <a id="2088" href="Algebra.Solver.Monoid.html#2088" class="Bound">e₂</a><a id="2090" class="Symbol">)</a> <a id="2092" class="Symbol">=</a> <a id="2094" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2104" href="Algebra.Solver.Monoid.html#2083" class="Bound">e₁</a> <a id="2107" href="Data.List.Base.html#1570" class="Function Operator">++</a> <a id="2110" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2120" href="Algebra.Solver.Monoid.html#2088" class="Bound">e₂</a>

<a id="2124" class="Comment">-- The normaliser is homomorphic with respect to _++_/_∙_.</a>

<a id="homomorphic"></a><a id="2184" href="Algebra.Solver.Monoid.html#2184" class="Function">homomorphic</a> <a id="2196" class="Symbol">:</a> <a id="2198" class="Symbol">∀</a> <a id="2200" class="Symbol">{</a><a id="2201" href="Algebra.Solver.Monoid.html#2201" class="Bound">n</a><a id="2202" class="Symbol">}</a> <a id="2204" class="Symbol">(</a><a id="2205" href="Algebra.Solver.Monoid.html#2205" class="Bound">nf₁</a> <a id="2209" href="Algebra.Solver.Monoid.html#2209" class="Bound">nf₂</a> <a id="2213" class="Symbol">:</a> <a id="2215" href="Algebra.Solver.Monoid.html#1784" class="Function">Normal</a> <a id="2222" href="Algebra.Solver.Monoid.html#2201" class="Bound">n</a><a id="2223" class="Symbol">)</a> <a id="2225" class="Symbol">(</a><a id="2226" href="Algebra.Solver.Monoid.html#2226" class="Bound">ρ</a> <a id="2228" class="Symbol">:</a> <a id="2230" href="Algebra.Solver.Monoid.html#1408" class="Function">Env</a> <a id="2234" href="Algebra.Solver.Monoid.html#2201" class="Bound">n</a><a id="2235" class="Symbol">)</a> <a id="2237" class="Symbol">→</a>
              <a id="2253" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2255" href="Algebra.Solver.Monoid.html#2205" class="Bound">nf₁</a> <a id="2259" href="Data.List.Base.html#1570" class="Function Operator">++</a> <a id="2262" href="Algebra.Solver.Monoid.html#2209" class="Bound">nf₂</a> <a id="2266" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2269" href="Algebra.Solver.Monoid.html#2226" class="Bound">ρ</a> <a id="2271" href="Algebra.html#2751" class="Field Operator">≈</a> <a id="2273" class="Symbol">(</a><a id="2274" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2276" href="Algebra.Solver.Monoid.html#2205" class="Bound">nf₁</a> <a id="2280" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2283" href="Algebra.Solver.Monoid.html#2226" class="Bound">ρ</a> <a id="2285" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2287" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2289" href="Algebra.Solver.Monoid.html#2209" class="Bound">nf₂</a> <a id="2293" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2296" href="Algebra.Solver.Monoid.html#2226" class="Bound">ρ</a><a id="2297" class="Symbol">)</a>
<a id="2299" href="Algebra.Solver.Monoid.html#2184" class="Function">homomorphic</a> <a id="2311" href="Agda.Builtin.List.html#158" class="InductiveConstructor">[]</a> <a id="2314" href="Algebra.Solver.Monoid.html#2314" class="Bound">nf₂</a> <a id="2318" href="Algebra.Solver.Monoid.html#2318" class="Bound">ρ</a> <a id="2320" class="Symbol">=</a> <a id="2322" href="Relation.Binary.Reasoning.Base.Single.html#859" class="Function Operator">begin</a>
  <a id="2330" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2332" href="Algebra.Solver.Monoid.html#2314" class="Bound">nf₂</a> <a id="2336" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2339" href="Algebra.Solver.Monoid.html#2318" class="Bound">ρ</a>      <a id="2346" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">≈⟨</a> <a id="2349" href="Relation.Binary.Core.html#6884" class="Function">sym</a> <a id="2353" href="Function.html#1631" class="Function Operator">$</a> <a id="2355" href="Algebra.Structures.html#2231" class="Function">identityˡ</a> <a id="2365" class="Symbol">_</a> <a id="2367" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">⟩</a>
  <a id="2371" href="Algebra.html#2807" class="Field">ε</a> <a id="2373" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2375" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2377" href="Algebra.Solver.Monoid.html#2314" class="Bound">nf₂</a> <a id="2381" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2384" href="Algebra.Solver.Monoid.html#2318" class="Bound">ρ</a>  <a id="2387" href="Relation.Binary.Reasoning.Base.Single.html#1295" class="Function Operator">∎</a>
<a id="2389" href="Algebra.Solver.Monoid.html#2184" class="Function">homomorphic</a> <a id="2401" class="Symbol">(</a><a id="2402" href="Algebra.Solver.Monoid.html#2402" class="Bound">x</a> <a id="2404" href="Agda.Builtin.List.html#173" class="InductiveConstructor Operator">∷</a> <a id="2406" href="Algebra.Solver.Monoid.html#2406" class="Bound">nf₁</a><a id="2409" class="Symbol">)</a> <a id="2411" href="Algebra.Solver.Monoid.html#2411" class="Bound">nf₂</a> <a id="2415" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a> <a id="2417" class="Symbol">=</a> <a id="2419" href="Relation.Binary.Reasoning.Base.Single.html#859" class="Function Operator">begin</a>
  <a id="2427" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="2434" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a> <a id="2436" href="Algebra.Solver.Monoid.html#2402" class="Bound">x</a> <a id="2438" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2440" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2442" href="Algebra.Solver.Monoid.html#2406" class="Bound">nf₁</a> <a id="2446" href="Data.List.Base.html#1570" class="Function Operator">++</a> <a id="2449" href="Algebra.Solver.Monoid.html#2411" class="Bound">nf₂</a> <a id="2453" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2456" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a>          <a id="2467" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">≈⟨</a> <a id="2470" href="Algebra.Structures.html#1228" class="Function">∙-congˡ</a> <a id="2478" class="Symbol">(</a><a id="2479" href="Algebra.Solver.Monoid.html#2184" class="Function">homomorphic</a> <a id="2491" href="Algebra.Solver.Monoid.html#2406" class="Bound">nf₁</a> <a id="2495" href="Algebra.Solver.Monoid.html#2411" class="Bound">nf₂</a> <a id="2499" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a><a id="2500" class="Symbol">)</a> <a id="2502" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">⟩</a>
  <a id="2506" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="2513" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a> <a id="2515" href="Algebra.Solver.Monoid.html#2402" class="Bound">x</a> <a id="2517" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2519" class="Symbol">(</a><a id="2520" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2522" href="Algebra.Solver.Monoid.html#2406" class="Bound">nf₁</a> <a id="2526" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2529" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a> <a id="2531" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2533" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2535" href="Algebra.Solver.Monoid.html#2411" class="Bound">nf₂</a> <a id="2539" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2542" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a><a id="2543" class="Symbol">)</a>  <a id="2546" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">≈⟨</a> <a id="2549" href="Relation.Binary.Core.html#6884" class="Function">sym</a> <a id="2553" href="Function.html#1631" class="Function Operator">$</a> <a id="2555" href="Algebra.Structures.html#1437" class="Function">assoc</a> <a id="2561" class="Symbol">_</a> <a id="2563" class="Symbol">_</a> <a id="2565" class="Symbol">_</a> <a id="2567" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">⟩</a>
  <a id="2571" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="2578" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a> <a id="2580" href="Algebra.Solver.Monoid.html#2402" class="Bound">x</a> <a id="2582" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2584" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2586" href="Algebra.Solver.Monoid.html#2406" class="Bound">nf₁</a> <a id="2590" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2593" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a> <a id="2595" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2597" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2599" href="Algebra.Solver.Monoid.html#2411" class="Bound">nf₂</a> <a id="2603" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2606" href="Algebra.Solver.Monoid.html#2415" class="Bound">ρ</a>    <a id="2611" href="Relation.Binary.Reasoning.Base.Single.html#1295" class="Function Operator">∎</a>

<a id="2614" class="Comment">-- The normaliser preserves the semantics of the expression.</a>

<a id="normalise-correct"></a><a id="2676" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a> <a id="2694" class="Symbol">:</a>
  <a id="2698" class="Symbol">∀</a> <a id="2700" class="Symbol">{</a><a id="2701" href="Algebra.Solver.Monoid.html#2701" class="Bound">n</a><a id="2702" class="Symbol">}</a> <a id="2704" class="Symbol">(</a><a id="2705" href="Algebra.Solver.Monoid.html#2705" class="Bound">e</a> <a id="2707" class="Symbol">:</a> <a id="2709" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="2714" href="Algebra.Solver.Monoid.html#2701" class="Bound">n</a><a id="2715" class="Symbol">)</a> <a id="2717" class="Symbol">(</a><a id="2718" href="Algebra.Solver.Monoid.html#2718" class="Bound">ρ</a> <a id="2720" class="Symbol">:</a> <a id="2722" href="Algebra.Solver.Monoid.html#1408" class="Function">Env</a> <a id="2726" href="Algebra.Solver.Monoid.html#2701" class="Bound">n</a><a id="2727" class="Symbol">)</a> <a id="2729" class="Symbol">→</a> <a id="2731" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2733" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2743" href="Algebra.Solver.Monoid.html#2705" class="Bound">e</a> <a id="2745" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2748" href="Algebra.Solver.Monoid.html#2718" class="Bound">ρ</a> <a id="2750" href="Algebra.html#2751" class="Field Operator">≈</a> <a id="2752" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="2754" href="Algebra.Solver.Monoid.html#2705" class="Bound">e</a> <a id="2756" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="2758" href="Algebra.Solver.Monoid.html#2718" class="Bound">ρ</a>
<a id="2760" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a> <a id="2778" class="Symbol">(</a><a id="2779" href="Algebra.Solver.Monoid.html#1280" class="InductiveConstructor">var</a> <a id="2783" href="Algebra.Solver.Monoid.html#2783" class="Bound">x</a><a id="2784" class="Symbol">)</a> <a id="2786" href="Algebra.Solver.Monoid.html#2786" class="Bound">ρ</a> <a id="2788" class="Symbol">=</a> <a id="2790" href="Relation.Binary.Reasoning.Base.Single.html#859" class="Function Operator">begin</a>
  <a id="2798" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="2805" href="Algebra.Solver.Monoid.html#2786" class="Bound">ρ</a> <a id="2807" href="Algebra.Solver.Monoid.html#2783" class="Bound">x</a> <a id="2809" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="2811" href="Algebra.html#2807" class="Field">ε</a>  <a id="2814" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">≈⟨</a> <a id="2817" href="Algebra.Structures.html#2292" class="Function">identityʳ</a> <a id="2827" class="Symbol">_</a> <a id="2829" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">⟩</a>
  <a id="2833" href="Data.Vec.html#1443" class="Function">lookup</a> <a id="2840" href="Algebra.Solver.Monoid.html#2786" class="Bound">ρ</a> <a id="2842" href="Algebra.Solver.Monoid.html#2783" class="Bound">x</a>      <a id="2849" href="Relation.Binary.Reasoning.Base.Single.html#1295" class="Function Operator">∎</a>
<a id="2851" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a> <a id="2869" href="Algebra.Solver.Monoid.html#1303" class="InductiveConstructor">id</a> <a id="2872" href="Algebra.Solver.Monoid.html#2872" class="Bound">ρ</a> <a id="2874" class="Symbol">=</a> <a id="2876" href="Relation.Binary.Reasoning.Base.Single.html#859" class="Function Operator">begin</a>
  <a id="2884" href="Algebra.html#2807" class="Field">ε</a>  <a id="2887" href="Relation.Binary.Reasoning.Base.Single.html#1295" class="Function Operator">∎</a>
<a id="2889" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a> <a id="2907" class="Symbol">(</a><a id="2908" href="Algebra.Solver.Monoid.html#2908" class="Bound">e₁</a> <a id="2911" href="Algebra.Solver.Monoid.html#1318" class="InductiveConstructor Operator">⊕</a> <a id="2913" href="Algebra.Solver.Monoid.html#2913" class="Bound">e₂</a><a id="2915" class="Symbol">)</a> <a id="2917" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a> <a id="2919" class="Symbol">=</a> <a id="2921" href="Relation.Binary.Reasoning.Base.Single.html#859" class="Function Operator">begin</a>
  <a id="2929" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="2931" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2941" href="Algebra.Solver.Monoid.html#2908" class="Bound">e₁</a> <a id="2944" href="Data.List.Base.html#1570" class="Function Operator">++</a> <a id="2947" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2957" href="Algebra.Solver.Monoid.html#2913" class="Bound">e₂</a> <a id="2960" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="2963" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a>        <a id="2972" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">≈⟨</a> <a id="2975" href="Algebra.Solver.Monoid.html#2184" class="Function">homomorphic</a> <a id="2987" class="Symbol">(</a><a id="2988" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="2998" href="Algebra.Solver.Monoid.html#2908" class="Bound">e₁</a><a id="3000" class="Symbol">)</a> <a id="3002" class="Symbol">(</a><a id="3003" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3013" href="Algebra.Solver.Monoid.html#2913" class="Bound">e₂</a><a id="3015" class="Symbol">)</a> <a id="3017" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a> <a id="3019" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">⟩</a>
  <a id="3023" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="3025" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3035" href="Algebra.Solver.Monoid.html#2908" class="Bound">e₁</a> <a id="3038" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="3041" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a> <a id="3043" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="3045" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="3047" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3057" href="Algebra.Solver.Monoid.html#2913" class="Bound">e₂</a> <a id="3060" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="3063" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a>  <a id="3066" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">≈⟨</a> <a id="3069" href="Algebra.Structures.html#1078" class="Function">∙-cong</a> <a id="3076" class="Symbol">(</a><a id="3077" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a> <a id="3095" href="Algebra.Solver.Monoid.html#2908" class="Bound">e₁</a> <a id="3098" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a><a id="3099" class="Symbol">)</a> <a id="3101" class="Symbol">(</a><a id="3102" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a> <a id="3120" href="Algebra.Solver.Monoid.html#2913" class="Bound">e₂</a> <a id="3123" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a><a id="3124" class="Symbol">)</a> <a id="3126" href="Relation.Binary.Reasoning.Base.Single.html#925" class="Function Operator">⟩</a>
  <a id="3130" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="3132" href="Algebra.Solver.Monoid.html#2908" class="Bound">e₁</a> <a id="3135" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="3137" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a> <a id="3139" href="Algebra.html#2780" class="Field Operator">∙</a> <a id="3141" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="3143" href="Algebra.Solver.Monoid.html#2913" class="Bound">e₂</a> <a id="3146" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="3148" href="Algebra.Solver.Monoid.html#2917" class="Bound">ρ</a>                        <a id="3173" href="Relation.Binary.Reasoning.Base.Single.html#1295" class="Function Operator">∎</a>

<a id="3176" class="Comment">------------------------------------------------------------------------</a>
<a id="3249" class="Comment">-- &quot;Tactics&quot;</a>

<a id="3263" class="Keyword">open</a> <a id="3268" class="Keyword">module</a> <a id="R"></a><a id="3275" href="Algebra.Solver.Monoid.html#3275" class="Module">R</a> <a id="3277" class="Symbol">=</a> <a id="3279" href="Relation.Binary.Reflection.html" class="Module">Relation.Binary.Reflection</a>
                  <a id="3324" href="Algebra.Structures.html#1153" class="Function">setoid</a> <a id="3331" href="Algebra.Solver.Monoid.html#1280" class="InductiveConstructor">var</a> <a id="3335" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦_⟧</a> <a id="3339" class="Symbol">(</a><a id="3340" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦_⟧⇓</a> <a id="3345" href="Function.html#1085" class="Function Operator">∘</a> <a id="3347" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a><a id="3356" class="Symbol">)</a> <a id="3358" href="Algebra.Solver.Monoid.html#2676" class="Function">normalise-correct</a>
  <a id="3378" class="Keyword">public</a> <a id="3385" class="Keyword">using</a> <a id="3391" class="Symbol">(</a><a id="3392" href="Relation.Binary.Reflection.html#2423" class="Function">solve</a><a id="3397" class="Symbol">;</a> <a id="3399" href="Relation.Binary.Reflection.html#3532" class="Function Operator">_⊜_</a><a id="3402" class="Symbol">)</a>

<a id="3405" class="Comment">-- We can decide if two normal forms are /syntactically/ equal.</a>

<a id="3470" class="Keyword">infix</a> <a id="3476" class="Number">5</a> <a id="3478" href="Algebra.Solver.Monoid.html#3483" class="Function Operator">_≟_</a>

<a id="_≟_"></a><a id="3483" href="Algebra.Solver.Monoid.html#3483" class="Function Operator">_≟_</a> <a id="3487" class="Symbol">:</a> <a id="3489" class="Symbol">∀</a> <a id="3491" class="Symbol">{</a><a id="3492" href="Algebra.Solver.Monoid.html#3492" class="Bound">n</a><a id="3493" class="Symbol">}</a> <a id="3495" class="Symbol">→</a> <a id="3497" href="Relation.Binary.Core.html#5557" class="Function">Decidable</a> <a id="3507" class="Symbol">{</a><a id="3508" class="Argument">A</a> <a id="3510" class="Symbol">=</a> <a id="3512" href="Algebra.Solver.Monoid.html#1784" class="Function">Normal</a> <a id="3519" href="Algebra.Solver.Monoid.html#3492" class="Bound">n</a><a id="3520" class="Symbol">}</a> <a id="3522" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">_≡_</a>
<a id="3526" href="Algebra.Solver.Monoid.html#3526" class="Bound">nf₁</a> <a id="3530" href="Algebra.Solver.Monoid.html#3483" class="Function Operator">≟</a> <a id="3532" href="Algebra.Solver.Monoid.html#3532" class="Bound">nf₂</a> <a id="3536" class="Symbol">=</a> <a id="3538" href="Relation.Nullary.Decidable.html#2002" class="Function">Dec.map′</a> <a id="3547" href="Data.List.Relation.Binary.Equality.Propositional.html#800" class="Function">≋⇒≡</a> <a id="3551" href="Data.List.Relation.Binary.Equality.Propositional.html#896" class="Function">≡⇒≋</a> <a id="3555" class="Symbol">(</a><a id="3556" href="Algebra.Solver.Monoid.html#3526" class="Bound">nf₁</a> <a id="3560" href="Data.List.Relation.Binary.Equality.DecSetoid.html#871" class="Function Operator">≋?</a> <a id="3563" href="Algebra.Solver.Monoid.html#3532" class="Bound">nf₂</a><a id="3566" class="Symbol">)</a>
  <a id="3570" class="Keyword">where</a> <a id="3576" class="Keyword">open</a> <a id="3581" href="Data.List.Relation.Binary.Equality.DecPropositional.html" class="Module">ListEq</a> <a id="3588" href="Data.Fin.Properties.html#1730" class="Function Operator">Fin._≟_</a>

<a id="3597" class="Comment">-- We can also give a sound, but not necessarily complete, procedure</a>
<a id="3666" class="Comment">-- for determining if two expressions have the same semantics.</a>

<a id="prove′"></a><a id="3730" href="Algebra.Solver.Monoid.html#3730" class="Function">prove′</a> <a id="3737" class="Symbol">:</a> <a id="3739" class="Symbol">∀</a> <a id="3741" class="Symbol">{</a><a id="3742" href="Algebra.Solver.Monoid.html#3742" class="Bound">n</a><a id="3743" class="Symbol">}</a> <a id="3745" class="Symbol">(</a><a id="3746" href="Algebra.Solver.Monoid.html#3746" class="Bound">e₁</a> <a id="3749" href="Algebra.Solver.Monoid.html#3749" class="Bound">e₂</a> <a id="3752" class="Symbol">:</a> <a id="3754" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="3759" href="Algebra.Solver.Monoid.html#3742" class="Bound">n</a><a id="3760" class="Symbol">)</a> <a id="3762" class="Symbol">→</a> <a id="3764" href="Data.Maybe.Base.html#795" class="Datatype">Maybe</a> <a id="3770" class="Symbol">(∀</a> <a id="3773" href="Algebra.Solver.Monoid.html#3773" class="Bound">ρ</a> <a id="3775" class="Symbol">→</a> <a id="3777" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="3779" href="Algebra.Solver.Monoid.html#3746" class="Bound">e₁</a> <a id="3782" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="3784" href="Algebra.Solver.Monoid.html#3773" class="Bound">ρ</a> <a id="3786" href="Algebra.html#2751" class="Field Operator">≈</a> <a id="3788" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="3790" href="Algebra.Solver.Monoid.html#3749" class="Bound">e₂</a> <a id="3793" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="3795" href="Algebra.Solver.Monoid.html#3773" class="Bound">ρ</a><a id="3796" class="Symbol">)</a>
<a id="3798" href="Algebra.Solver.Monoid.html#3730" class="Function">prove′</a> <a id="3805" href="Algebra.Solver.Monoid.html#3805" class="Bound">e₁</a> <a id="3808" href="Algebra.Solver.Monoid.html#3808" class="Bound">e₂</a> <a id="3811" class="Symbol">=</a>
  <a id="3815" href="Data.Maybe.Base.html#1948" class="Function">Maybe.map</a> <a id="3825" href="Algebra.Solver.Monoid.html#3884" class="Function">lemma</a> <a id="3831" href="Function.html#1631" class="Function Operator">$</a> <a id="3833" href="Data.Maybe.Base.html#1187" class="Function">decToMaybe</a> <a id="3844" class="Symbol">(</a><a id="3845" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3855" href="Algebra.Solver.Monoid.html#3805" class="Bound">e₁</a> <a id="3858" href="Algebra.Solver.Monoid.html#3483" class="Function Operator">≟</a> <a id="3860" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3870" href="Algebra.Solver.Monoid.html#3808" class="Bound">e₂</a><a id="3872" class="Symbol">)</a>
  <a id="3876" class="Keyword">where</a>
  <a id="3884" href="Algebra.Solver.Monoid.html#3884" class="Function">lemma</a> <a id="3890" class="Symbol">:</a> <a id="3892" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3902" href="Algebra.Solver.Monoid.html#3805" class="Bound">e₁</a> <a id="3905" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="3907" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="3917" href="Algebra.Solver.Monoid.html#3808" class="Bound">e₂</a> <a id="3920" class="Symbol">→</a> <a id="3922" class="Symbol">∀</a> <a id="3924" href="Algebra.Solver.Monoid.html#3924" class="Bound">ρ</a> <a id="3926" class="Symbol">→</a> <a id="3928" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="3930" href="Algebra.Solver.Monoid.html#3805" class="Bound">e₁</a> <a id="3933" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="3935" href="Algebra.Solver.Monoid.html#3924" class="Bound">ρ</a> <a id="3937" href="Algebra.html#2751" class="Field Operator">≈</a> <a id="3939" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟦</a> <a id="3941" href="Algebra.Solver.Monoid.html#3808" class="Bound">e₂</a> <a id="3944" href="Algebra.Solver.Monoid.html#1531" class="Function Operator">⟧</a> <a id="3946" href="Algebra.Solver.Monoid.html#3924" class="Bound">ρ</a>
  <a id="3950" href="Algebra.Solver.Monoid.html#3884" class="Function">lemma</a> <a id="3956" href="Algebra.Solver.Monoid.html#3956" class="Bound">eq</a> <a id="3959" href="Algebra.Solver.Monoid.html#3959" class="Bound">ρ</a> <a id="3961" class="Symbol">=</a>
    <a id="3967" href="Relation.Binary.Reflection.html#1737" class="Function">R.prove</a> <a id="3975" href="Algebra.Solver.Monoid.html#3959" class="Bound">ρ</a> <a id="3977" href="Algebra.Solver.Monoid.html#3805" class="Bound">e₁</a> <a id="3980" href="Algebra.Solver.Monoid.html#3808" class="Bound">e₂</a> <a id="3983" class="Symbol">(</a><a id="3984" href="Relation.Binary.Reasoning.Base.Single.html#859" class="Function Operator">begin</a>
      <a id="3996" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="3998" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="4008" href="Algebra.Solver.Monoid.html#3805" class="Bound">e₁</a> <a id="4011" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="4014" href="Algebra.Solver.Monoid.html#3959" class="Bound">ρ</a>  <a id="4017" href="Relation.Binary.Reasoning.Base.Single.html#1034" class="Function Operator">≡⟨</a> <a id="4020" href="Relation.Binary.PropositionalEquality.Core.html#1090" class="Function">P.cong</a> <a id="4027" class="Symbol">(λ</a> <a id="4030" href="Algebra.Solver.Monoid.html#4030" class="Bound">e</a> <a id="4032" class="Symbol">→</a> <a id="4034" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="4036" href="Algebra.Solver.Monoid.html#4030" class="Bound">e</a> <a id="4038" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="4041" href="Algebra.Solver.Monoid.html#3959" class="Bound">ρ</a><a id="4042" class="Symbol">)</a> <a id="4044" href="Algebra.Solver.Monoid.html#3956" class="Bound">eq</a> <a id="4047" href="Relation.Binary.Reasoning.Base.Single.html#1034" class="Function Operator">⟩</a>
      <a id="4055" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟦</a> <a id="4057" href="Algebra.Solver.Monoid.html#1980" class="Function">normalise</a> <a id="4067" href="Algebra.Solver.Monoid.html#3808" class="Bound">e₂</a> <a id="4070" href="Algebra.Solver.Monoid.html#1862" class="Function Operator">⟧⇓</a> <a id="4073" href="Algebra.Solver.Monoid.html#3959" class="Bound">ρ</a>  <a id="4076" href="Relation.Binary.Reasoning.Base.Single.html#1295" class="Function Operator">∎</a><a id="4077" class="Symbol">)</a>

<a id="4080" class="Comment">-- This procedure can be combined with from-just.</a>

<a id="prove"></a><a id="4131" href="Algebra.Solver.Monoid.html#4131" class="Function">prove</a> <a id="4137" class="Symbol">:</a> <a id="4139" class="Symbol">∀</a> <a id="4141" href="Algebra.Solver.Monoid.html#4141" class="Bound">n</a> <a id="4143" class="Symbol">(</a><a id="4144" href="Algebra.Solver.Monoid.html#4144" class="Bound">es</a> <a id="4147" class="Symbol">:</a> <a id="4149" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="4154" href="Algebra.Solver.Monoid.html#4141" class="Bound">n</a> <a id="4156" href="Data.Product.html#1162" class="Function Operator">×</a> <a id="4158" href="Algebra.Solver.Monoid.html#1253" class="Datatype">Expr</a> <a id="4163" href="Algebra.Solver.Monoid.html#4141" class="Bound">n</a><a id="4164" class="Symbol">)</a> <a id="4166" class="Symbol">→</a>
        <a id="4176" href="Data.Maybe.Base.html#1746" class="Function">From-just</a> <a id="4186" class="Symbol">(</a><a id="4187" href="Data.Product.html#3098" class="Function">uncurry</a> <a id="4195" href="Algebra.Solver.Monoid.html#3730" class="Function">prove′</a> <a id="4202" href="Algebra.Solver.Monoid.html#4144" class="Bound">es</a><a id="4204" class="Symbol">)</a>
<a id="4206" href="Algebra.Solver.Monoid.html#4131" class="Function">prove</a> <a id="4212" class="Symbol">_</a> <a id="4214" class="Symbol">=</a> <a id="4216" href="Data.Maybe.Base.html#1834" class="Function">from-just</a> <a id="4226" href="Function.html#1085" class="Function Operator">∘</a> <a id="4228" href="Data.Product.html#3098" class="Function">uncurry</a> <a id="4236" href="Algebra.Solver.Monoid.html#3730" class="Function">prove′</a>
</pre></body></html>