<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.W</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- W-types</a>
<a id="117" class="Comment">------------------------------------------------------------------------</a>

<a id="191" class="Symbol">{-#</a> <a id="195" class="Keyword">OPTIONS</a> <a id="203" class="Pragma">--without-K</a> <a id="215" class="Pragma">--safe</a> <a id="222" class="Symbol">#-}</a>

<a id="227" class="Keyword">module</a> <a id="234" href="Data.W.html" class="Module">Data.W</a> <a id="241" class="Keyword">where</a>

<a id="248" class="Keyword">open</a> <a id="253" class="Keyword">import</a> <a id="260" href="Level.html" class="Module">Level</a>
<a id="266" class="Keyword">open</a> <a id="271" class="Keyword">import</a> <a id="278" href="Function.html" class="Module">Function</a>
<a id="287" class="Keyword">open</a> <a id="292" class="Keyword">import</a> <a id="299" href="Data.Product.html" class="Module">Data.Product</a> <a id="312" class="Keyword">hiding</a> <a id="319" class="Symbol">(</a><a id="320" href="Data.Product.html#2190" class="Function">map</a><a id="323" class="Symbol">)</a>
<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="Data.Container.Core.html" class="Module">Data.Container.Core</a> <a id="357" class="Keyword">hiding</a> <a id="364" class="Symbol">(</a><a id="365" href="Data.Container.Core.html#875" class="Function">map</a><a id="368" class="Symbol">)</a>
<a id="370" class="Keyword">open</a> <a id="375" class="Keyword">import</a> <a id="382" href="Data.Container.Relation.Unary.All.html" class="Module">Data.Container.Relation.Unary.All</a> <a id="416" class="Keyword">using</a> <a id="422" class="Symbol">(</a><a id="423" href="Data.Container.Relation.Unary.All.html#543" class="Record">□</a><a id="424" class="Symbol">;</a> <a id="426" href="Data.Container.Relation.Unary.All.html#662" class="InductiveConstructor">all</a><a id="429" class="Symbol">)</a>
<a id="431" class="Keyword">open</a> <a id="436" class="Keyword">import</a> <a id="443" href="Relation.Nullary.html" class="Module">Relation.Nullary</a>
<a id="460" class="Keyword">open</a> <a id="465" class="Keyword">import</a> <a id="472" href="Agda.Builtin.Equality.html" class="Module">Agda.Builtin.Equality</a>

<a id="495" class="Comment">-- The family of W-types.</a>

<a id="522" class="Keyword">data</a> <a id="W"></a><a id="527" href="Data.W.html#527" class="Datatype">W</a> <a id="529" class="Symbol">{</a><a id="530" href="Data.W.html#530" class="Bound">s</a> <a id="532" href="Data.W.html#532" class="Bound">p</a><a id="533" class="Symbol">}</a> <a id="535" class="Symbol">(</a><a id="536" href="Data.W.html#536" class="Bound">C</a> <a id="538" class="Symbol">:</a> <a id="540" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="550" href="Data.W.html#530" class="Bound">s</a> <a id="552" href="Data.W.html#532" class="Bound">p</a><a id="553" class="Symbol">)</a> <a id="555" class="Symbol">:</a> <a id="557" class="PrimitiveType">Set</a> <a id="561" class="Symbol">(</a><a id="562" href="Data.W.html#530" class="Bound">s</a> <a id="564" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="566" href="Data.W.html#532" class="Bound">p</a><a id="567" class="Symbol">)</a> <a id="569" class="Keyword">where</a>
  <a id="W.sup"></a><a id="577" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="581" class="Symbol">:</a> <a id="583" href="Data.Container.Core.html#750" class="Function Operator">⟦</a> <a id="585" href="Data.W.html#536" class="Bound">C</a> <a id="587" href="Data.Container.Core.html#750" class="Function Operator">⟧</a> <a id="589" class="Symbol">(</a><a id="590" href="Data.W.html#527" class="Datatype">W</a> <a id="592" href="Data.W.html#536" class="Bound">C</a><a id="593" class="Symbol">)</a> <a id="595" class="Symbol">→</a> <a id="597" href="Data.W.html#527" class="Datatype">W</a> <a id="599" href="Data.W.html#536" class="Bound">C</a>

<a id="602" class="Keyword">module</a> <a id="609" href="Data.W.html#609" class="Module">_</a> <a id="611" class="Symbol">{</a><a id="612" href="Data.W.html#612" class="Bound">s</a> <a id="614" href="Data.W.html#614" class="Bound">p</a><a id="615" class="Symbol">}</a> <a id="617" class="Symbol">{</a><a id="618" href="Data.W.html#618" class="Bound">C</a> <a id="620" class="Symbol">:</a> <a id="622" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="632" href="Data.W.html#612" class="Bound">s</a> <a id="634" href="Data.W.html#614" class="Bound">p</a><a id="635" class="Symbol">}</a> <a id="637" class="Symbol">{</a><a id="638" href="Data.W.html#638" class="Bound">s</a> <a id="640" class="Symbol">:</a> <a id="642" href="Data.Container.Core.html#633" class="Field">Shape</a> <a id="648" href="Data.W.html#618" class="Bound">C</a><a id="649" class="Symbol">}</a> <a id="651" class="Symbol">{</a><a id="652" href="Data.W.html#652" class="Bound">f</a> <a id="654" class="Symbol">:</a> <a id="656" href="Data.Container.Core.html#654" class="Field">Position</a> <a id="665" href="Data.W.html#618" class="Bound">C</a> <a id="667" href="Data.W.html#638" class="Bound">s</a> <a id="669" class="Symbol">→</a> <a id="671" href="Data.W.html#527" class="Datatype">W</a> <a id="673" href="Data.W.html#618" class="Bound">C</a><a id="674" class="Symbol">}</a> <a id="676" class="Keyword">where</a>

 <a id="684" href="Data.W.html#684" class="Function">sup-injective₁</a> <a id="699" class="Symbol">:</a> <a id="701" class="Symbol">∀</a> <a id="703" class="Symbol">{</a><a id="704" href="Data.W.html#704" class="Bound">t</a> <a id="706" href="Data.W.html#706" class="Bound">g</a><a id="707" class="Symbol">}</a> <a id="709" class="Symbol">→</a> <a id="711" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="715" class="Symbol">(</a><a id="716" href="Data.W.html#638" class="Bound">s</a> <a id="718" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="720" href="Data.W.html#652" class="Bound">f</a><a id="721" class="Symbol">)</a> <a id="723" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="725" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="729" class="Symbol">(</a><a id="730" href="Data.W.html#704" class="Bound">t</a> <a id="732" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="734" href="Data.W.html#706" class="Bound">g</a><a id="735" class="Symbol">)</a> <a id="737" class="Symbol">→</a> <a id="739" href="Data.W.html#638" class="Bound">s</a> <a id="741" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="743" href="Data.W.html#704" class="Bound">t</a>
 <a id="746" href="Data.W.html#684" class="Function">sup-injective₁</a> <a id="761" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a> <a id="766" class="Symbol">=</a> <a id="768" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a>

 <a id="775" class="Comment">-- See also Data.W.WithK.sup-injective₂.</a>

<a id="817" class="Comment">-- Projections.</a>

<a id="834" class="Keyword">module</a> <a id="841" href="Data.W.html#841" class="Module">_</a> <a id="843" class="Symbol">{</a><a id="844" href="Data.W.html#844" class="Bound">s</a> <a id="846" href="Data.W.html#846" class="Bound">p</a><a id="847" class="Symbol">}</a> <a id="849" class="Symbol">{</a><a id="850" href="Data.W.html#850" class="Bound">C</a> <a id="852" class="Symbol">:</a> <a id="854" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="864" href="Data.W.html#844" class="Bound">s</a> <a id="866" href="Data.W.html#846" class="Bound">p</a><a id="867" class="Symbol">}</a> <a id="869" class="Keyword">where</a>

  <a id="878" href="Data.W.html#878" class="Function">head</a> <a id="883" class="Symbol">:</a> <a id="885" href="Data.W.html#527" class="Datatype">W</a> <a id="887" href="Data.W.html#850" class="Bound">C</a> <a id="889" class="Symbol">→</a> <a id="891" href="Data.Container.Core.html#633" class="Field">Shape</a> <a id="897" href="Data.W.html#850" class="Bound">C</a>
  <a id="901" href="Data.W.html#878" class="Function">head</a> <a id="906" class="Symbol">(</a><a id="907" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="911" class="Symbol">(</a><a id="912" href="Data.W.html#912" class="Bound">x</a> <a id="914" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="916" href="Data.W.html#916" class="Bound">f</a><a id="917" class="Symbol">))</a> <a id="920" class="Symbol">=</a> <a id="922" href="Data.W.html#912" class="Bound">x</a>

  <a id="927" href="Data.W.html#927" class="Function">tail</a> <a id="932" class="Symbol">:</a> <a id="934" class="Symbol">(</a><a id="935" href="Data.W.html#935" class="Bound">x</a> <a id="937" class="Symbol">:</a> <a id="939" href="Data.W.html#527" class="Datatype">W</a> <a id="941" href="Data.W.html#850" class="Bound">C</a><a id="942" class="Symbol">)</a> <a id="944" class="Symbol">→</a> <a id="946" href="Data.Container.Core.html#654" class="Field">Position</a> <a id="955" href="Data.W.html#850" class="Bound">C</a> <a id="957" class="Symbol">(</a><a id="958" href="Data.W.html#878" class="Function">head</a> <a id="963" href="Data.W.html#935" class="Bound">x</a><a id="964" class="Symbol">)</a> <a id="966" class="Symbol">→</a> <a id="968" href="Data.W.html#527" class="Datatype">W</a> <a id="970" href="Data.W.html#850" class="Bound">C</a>
  <a id="974" href="Data.W.html#927" class="Function">tail</a> <a id="979" class="Symbol">(</a><a id="980" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="984" class="Symbol">(</a><a id="985" href="Data.W.html#985" class="Bound">x</a> <a id="987" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="989" href="Data.W.html#989" class="Bound">f</a><a id="990" class="Symbol">))</a> <a id="993" class="Symbol">=</a> <a id="995" href="Data.W.html#989" class="Bound">f</a>

<a id="998" class="Comment">-- map</a>

<a id="1006" class="Keyword">module</a> <a id="1013" href="Data.W.html#1013" class="Module">_</a> <a id="1015" class="Symbol">{</a><a id="1016" href="Data.W.html#1016" class="Bound">s₁</a> <a id="1019" href="Data.W.html#1019" class="Bound">s₂</a> <a id="1022" href="Data.W.html#1022" class="Bound">p₁</a> <a id="1025" href="Data.W.html#1025" class="Bound">p₂</a><a id="1027" class="Symbol">}</a> <a id="1029" class="Symbol">{</a><a id="1030" href="Data.W.html#1030" class="Bound">C₁</a> <a id="1033" class="Symbol">:</a> <a id="1035" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1045" href="Data.W.html#1016" class="Bound">s₁</a> <a id="1048" href="Data.W.html#1022" class="Bound">p₁</a><a id="1050" class="Symbol">}</a> <a id="1052" class="Symbol">{</a><a id="1053" href="Data.W.html#1053" class="Bound">C₂</a> <a id="1056" class="Symbol">:</a> <a id="1058" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1068" href="Data.W.html#1019" class="Bound">s₂</a> <a id="1071" href="Data.W.html#1025" class="Bound">p₂</a><a id="1073" class="Symbol">}</a>
         <a id="1084" class="Symbol">(</a><a id="1085" href="Data.W.html#1085" class="Bound">m</a> <a id="1087" class="Symbol">:</a> <a id="1089" href="Data.W.html#1030" class="Bound">C₁</a> <a id="1092" href="Data.Container.Core.html#1049" class="Record Operator">⇒</a> <a id="1094" href="Data.W.html#1053" class="Bound">C₂</a><a id="1096" class="Symbol">)</a> <a id="1098" class="Keyword">where</a>

  <a id="1107" href="Data.W.html#1107" class="Function">map</a> <a id="1111" class="Symbol">:</a> <a id="1113" href="Data.W.html#527" class="Datatype">W</a> <a id="1115" href="Data.W.html#1030" class="Bound">C₁</a> <a id="1118" class="Symbol">→</a> <a id="1120" href="Data.W.html#527" class="Datatype">W</a> <a id="1122" href="Data.W.html#1053" class="Bound">C₂</a>
  <a id="1127" href="Data.W.html#1107" class="Function">map</a> <a id="1131" class="Symbol">(</a><a id="1132" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1136" class="Symbol">(</a><a id="1137" href="Data.W.html#1137" class="Bound">x</a> <a id="1139" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1141" href="Data.W.html#1141" class="Bound">f</a><a id="1142" class="Symbol">))</a> <a id="1145" class="Symbol">=</a> <a id="1147" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1151" class="Symbol">(</a><a id="1152" href="Data.Container.Core.html#1281" class="Function Operator">⟪</a> <a id="1154" href="Data.W.html#1085" class="Bound">m</a> <a id="1156" href="Data.Container.Core.html#1281" class="Function Operator">⟫</a> <a id="1158" class="Symbol">(</a><a id="1159" href="Data.W.html#1137" class="Bound">x</a> <a id="1161" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1163" class="Symbol">λ</a> <a id="1165" href="Data.W.html#1165" class="Bound">p</a> <a id="1167" class="Symbol">→</a> <a id="1169" href="Data.W.html#1107" class="Function">map</a> <a id="1173" class="Symbol">(</a><a id="1174" href="Data.W.html#1141" class="Bound">f</a> <a id="1176" href="Data.W.html#1165" class="Bound">p</a><a id="1177" class="Symbol">)))</a>

<a id="1182" class="Comment">-- induction</a>

<a id="1196" class="Keyword">module</a> <a id="1203" href="Data.W.html#1203" class="Module">_</a> <a id="1205" class="Symbol">{</a><a id="1206" href="Data.W.html#1206" class="Bound">s</a> <a id="1208" href="Data.W.html#1208" class="Bound">p</a> <a id="1210" href="Data.W.html#1210" class="Bound">ℓ</a><a id="1211" class="Symbol">}</a> <a id="1213" class="Symbol">{</a><a id="1214" href="Data.W.html#1214" class="Bound">C</a> <a id="1216" class="Symbol">:</a> <a id="1218" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1228" href="Data.W.html#1206" class="Bound">s</a> <a id="1230" href="Data.W.html#1208" class="Bound">p</a><a id="1231" class="Symbol">}</a> <a id="1233" class="Symbol">(</a><a id="1234" href="Data.W.html#1234" class="Bound">P</a> <a id="1236" class="Symbol">:</a> <a id="1238" href="Data.W.html#527" class="Datatype">W</a> <a id="1240" href="Data.W.html#1214" class="Bound">C</a> <a id="1242" class="Symbol">→</a> <a id="1244" class="PrimitiveType">Set</a> <a id="1248" href="Data.W.html#1210" class="Bound">ℓ</a><a id="1249" class="Symbol">)</a>
         <a id="1260" class="Symbol">(</a><a id="1261" href="Data.W.html#1261" class="Bound">alg</a> <a id="1265" class="Symbol">:</a> <a id="1267" class="Symbol">∀</a> <a id="1269" class="Symbol">{</a><a id="1270" href="Data.W.html#1270" class="Bound">t</a><a id="1271" class="Symbol">}</a> <a id="1273" class="Symbol">→</a> <a id="1275" href="Data.Container.Relation.Unary.All.html#543" class="Record">□</a> <a id="1277" href="Data.W.html#1214" class="Bound">C</a> <a id="1279" href="Data.W.html#1234" class="Bound">P</a> <a id="1281" href="Data.W.html#1270" class="Bound">t</a> <a id="1283" class="Symbol">→</a> <a id="1285" href="Data.W.html#1234" class="Bound">P</a> <a id="1287" class="Symbol">(</a><a id="1288" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1292" href="Data.W.html#1270" class="Bound">t</a><a id="1293" class="Symbol">))</a> <a id="1296" class="Keyword">where</a>

 <a id="1304" href="Data.W.html#1304" class="Function">induction</a> <a id="1314" class="Symbol">:</a> <a id="1316" class="Symbol">(</a><a id="1317" href="Data.W.html#1317" class="Bound">w</a> <a id="1319" class="Symbol">:</a> <a id="1321" href="Data.W.html#527" class="Datatype">W</a> <a id="1323" href="Data.W.html#1214" class="Bound">C</a><a id="1324" class="Symbol">)</a> <a id="1326" class="Symbol">→</a> <a id="1328" href="Data.W.html#1234" class="Bound">P</a> <a id="1330" href="Data.W.html#1317" class="Bound">w</a>
 <a id="1333" href="Data.W.html#1304" class="Function">induction</a> <a id="1343" class="Symbol">(</a><a id="1344" href="Data.W.html#577" class="InductiveConstructor">sup</a> <a id="1348" class="Symbol">(</a><a id="1349" href="Data.W.html#1349" class="Bound">s</a> <a id="1351" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="1353" href="Data.W.html#1353" class="Bound">f</a><a id="1354" class="Symbol">))</a> <a id="1357" class="Symbol">=</a> <a id="1359" href="Data.W.html#1261" class="Bound">alg</a> <a id="1363" href="Function.html#1927" class="Function Operator">$</a> <a id="1365" href="Data.Container.Relation.Unary.All.html#662" class="InductiveConstructor">all</a> <a id="1369" class="Symbol">(</a><a id="1370" href="Data.W.html#1304" class="Function">induction</a> <a id="1380" href="Function.html#805" class="Function Operator">∘</a> <a id="1382" href="Data.W.html#1353" class="Bound">f</a><a id="1383" class="Symbol">)</a>

<a id="1386" class="Keyword">module</a> <a id="1393" href="Data.W.html#1393" class="Module">_</a> <a id="1395" class="Symbol">{</a><a id="1396" href="Data.W.html#1396" class="Bound">s</a> <a id="1398" href="Data.W.html#1398" class="Bound">p</a> <a id="1400" href="Data.W.html#1400" class="Bound">ℓ</a><a id="1401" class="Symbol">}</a> <a id="1403" class="Symbol">{</a><a id="1404" href="Data.W.html#1404" class="Bound">C</a> <a id="1406" class="Symbol">:</a> <a id="1408" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1418" href="Data.W.html#1396" class="Bound">s</a> <a id="1420" href="Data.W.html#1398" class="Bound">p</a><a id="1421" class="Symbol">}</a> <a id="1423" class="Symbol">(</a><a id="1424" class="Keyword">open</a> <a id="1429" href="Data.Container.Core.html#553" class="Module">Container</a> <a id="1439" href="Data.W.html#1404" class="Bound">C</a><a id="1440" class="Symbol">)</a>
         <a id="1451" class="Symbol">{</a><a id="1452" href="Data.W.html#1452" class="Bound">P</a> <a id="1454" class="Symbol">:</a> <a id="1456" class="PrimitiveType">Set</a> <a id="1460" href="Data.W.html#1400" class="Bound">ℓ</a><a id="1461" class="Symbol">}</a> <a id="1463" class="Symbol">(</a><a id="1464" href="Data.W.html#1464" class="Bound">alg</a> <a id="1468" class="Symbol">:</a> <a id="1470" href="Data.Container.Core.html#750" class="Function Operator">⟦</a> <a id="1472" href="Data.W.html#1404" class="Bound">C</a> <a id="1474" href="Data.Container.Core.html#750" class="Function Operator">⟧</a> <a id="1476" href="Data.W.html#1452" class="Bound">P</a> <a id="1478" class="Symbol">→</a> <a id="1480" href="Data.W.html#1452" class="Bound">P</a><a id="1481" class="Symbol">)</a> <a id="1483" class="Keyword">where</a>

 <a id="1491" href="Data.W.html#1491" class="Function">foldr</a> <a id="1497" class="Symbol">:</a> <a id="1499" href="Data.W.html#527" class="Datatype">W</a> <a id="1501" href="Data.W.html#1404" class="Bound">C</a> <a id="1503" class="Symbol">→</a> <a id="1505" href="Data.W.html#1452" class="Bound">P</a>
 <a id="1508" href="Data.W.html#1491" class="Function">foldr</a> <a id="1514" class="Symbol">=</a> <a id="1516" href="Data.W.html#1304" class="Function">induction</a> <a id="1526" class="Symbol">(</a><a id="1527" href="Function.html#1145" class="Function">const</a> <a id="1533" href="Data.W.html#1452" class="Bound">P</a><a id="1534" class="Symbol">)</a> <a id="1536" class="Symbol">(</a><a id="1537" href="Data.W.html#1464" class="Bound">alg</a> <a id="1541" href="Function.html#805" class="Function Operator">∘</a> <a id="1543" href="Data.Product.html#1937" class="Function Operator">-,_</a> <a id="1547" href="Function.html#805" class="Function Operator">∘</a> <a id="1549" href="Data.Container.Relation.Unary.All.html#674" class="Field">□.proof</a><a id="1556" class="Symbol">)</a>

<a id="1559" class="Comment">-- If Position is always inhabited, then W_C is empty.</a>

<a id="1615" class="Keyword">module</a> <a id="1622" href="Data.W.html#1622" class="Module">_</a> <a id="1624" class="Symbol">{</a><a id="1625" href="Data.W.html#1625" class="Bound">s</a> <a id="1627" href="Data.W.html#1627" class="Bound">p</a><a id="1628" class="Symbol">}</a> <a id="1630" class="Symbol">{</a><a id="1631" href="Data.W.html#1631" class="Bound">C</a> <a id="1633" class="Symbol">:</a> <a id="1635" href="Data.Container.Core.html#553" class="Record">Container</a> <a id="1645" href="Data.W.html#1625" class="Bound">s</a> <a id="1647" href="Data.W.html#1627" class="Bound">p</a><a id="1648" class="Symbol">}</a> <a id="1650" class="Keyword">where</a>

  <a id="1659" href="Data.W.html#1659" class="Function">inhabited⇒empty</a> <a id="1675" class="Symbol">:</a> <a id="1677" class="Symbol">(∀</a> <a id="1680" href="Data.W.html#1680" class="Bound">s</a> <a id="1682" class="Symbol">→</a> <a id="1684" href="Data.Container.Core.html#654" class="Field">Position</a> <a id="1693" href="Data.W.html#1631" class="Bound">C</a> <a id="1695" href="Data.W.html#1680" class="Bound">s</a><a id="1696" class="Symbol">)</a> <a id="1698" class="Symbol">→</a> <a id="1700" href="Relation.Nullary.html#500" class="Function Operator">¬</a> <a id="1702" href="Data.W.html#527" class="Datatype">W</a> <a id="1704" href="Data.W.html#1631" class="Bound">C</a>
  <a id="1708" href="Data.W.html#1659" class="Function">inhabited⇒empty</a> <a id="1724" href="Data.W.html#1724" class="Bound">b</a> <a id="1726" class="Symbol">=</a> <a id="1728" href="Data.W.html#1491" class="Function">foldr</a> <a id="1734" class="Symbol">((</a><a id="1736" href="Function.html#1927" class="Function Operator">_$</a> <a id="1739" href="Data.W.html#1724" class="Bound">b</a> <a id="1741" class="Symbol">_)</a> <a id="1744" href="Function.html#805" class="Function Operator">∘</a> <a id="1746" href="Agda.Builtin.Sigma.html#237" class="Field">proj₂</a><a id="1751" class="Symbol">)</a>
</pre></body></html>