<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Rational.Base</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Rational numbers</a>
<a id="126" class="Comment">------------------------------------------------------------------------</a>

<a id="200" class="Symbol">{-#</a> <a id="204" class="Keyword">OPTIONS</a> <a id="212" class="Pragma">--without-K</a> <a id="224" class="Pragma">--safe</a> <a id="231" class="Symbol">#-}</a>

<a id="236" class="Keyword">module</a> <a id="243" href="Data.Rational.Base.html" class="Module">Data.Rational.Base</a> <a id="262" class="Keyword">where</a>

<a id="269" class="Keyword">open</a> <a id="274" class="Keyword">import</a> <a id="281" href="Function.html" class="Module">Function</a> <a id="290" class="Keyword">using</a> <a id="296" class="Symbol">(</a><a id="297" href="Function.html#708" class="Function">id</a><a id="299" class="Symbol">)</a>
<a id="301" class="Keyword">open</a> <a id="306" class="Keyword">import</a> <a id="313" href="Data.Integer.html" class="Module">Data.Integer</a> <a id="326" class="Symbol">as</a> <a id="329" class="Module">ℤ</a> <a id="331" class="Keyword">using</a> <a id="337" class="Symbol">(</a><a id="338" href="Agda.Builtin.Int.html#219" class="Datatype">ℤ</a><a id="339" class="Symbol">;</a> <a id="341" href="Data.Integer.Base.html#1075" class="Function Operator">∣_∣</a><a id="344" class="Symbol">;</a> <a id="346" href="Agda.Builtin.Int.html#237" class="InductiveConstructor">+_</a><a id="348" class="Symbol">;</a> <a id="350" href="Agda.Builtin.Int.html#264" class="InductiveConstructor">-[1+_]</a><a id="356" class="Symbol">)</a>
<a id="358" class="Keyword">open</a> <a id="363" class="Keyword">import</a> <a id="370" href="Data.Nat.GCD.html" class="Module">Data.Nat.GCD</a>
<a id="383" class="Keyword">open</a> <a id="388" class="Keyword">import</a> <a id="395" href="Data.Nat.Divisibility.html" class="Module">Data.Nat.Divisibility</a> <a id="417" class="Symbol">as</a> <a id="420" class="Module">ℕDiv</a> <a id="425" class="Keyword">using</a> <a id="431" class="Symbol">(</a><a id="432" href="Data.Nat.Divisibility.html#1324" class="InductiveConstructor">divides</a><a id="439" class="Symbol">;</a> <a id="441" href="Data.Nat.Divisibility.html#3223" class="Function">0∣⇒≡0</a><a id="446" class="Symbol">)</a>
<a id="448" class="Keyword">open</a> <a id="453" class="Keyword">import</a> <a id="460" href="Data.Nat.Coprimality.html" class="Module">Data.Nat.Coprimality</a> <a id="481" class="Symbol">as</a> <a id="484" class="Module">C</a> <a id="486" class="Keyword">using</a> <a id="492" class="Symbol">(</a><a id="493" href="Data.Nat.Coprimality.html#999" class="Function">Coprime</a><a id="500" class="Symbol">;</a> <a id="502" href="Data.Nat.Coprimality.html#2498" class="Function">Bézout-coprime</a><a id="516" class="Symbol">)</a>
<a id="518" class="Keyword">open</a> <a id="523" class="Keyword">import</a> <a id="530" href="Data.Nat.html" class="Module">Data.Nat</a> <a id="539" class="Symbol">as</a> <a id="542" class="Module">ℕ</a> <a id="544" class="Keyword">using</a> <a id="550" class="Symbol">(</a><a id="551" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a><a id="552" class="Symbol">;</a> <a id="554" href="Agda.Builtin.Nat.html#183" class="InductiveConstructor">zero</a><a id="558" class="Symbol">;</a> <a id="560" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a><a id="563" class="Symbol">)</a>
<a id="565" class="Keyword">open</a> <a id="570" class="Keyword">import</a> <a id="577" href="Data.Product.html" class="Module">Data.Product</a>
<a id="590" class="Keyword">open</a> <a id="595" class="Keyword">import</a> <a id="602" href="Level.html" class="Module">Level</a> <a id="608" class="Keyword">using</a> <a id="614" class="Symbol">(</a><a id="615" href="Level.html#506" class="Function">0ℓ</a><a id="617" class="Symbol">)</a>
<a id="619" class="Keyword">open</a> <a id="624" class="Keyword">import</a> <a id="631" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="658" class="Keyword">using</a> <a id="664" class="Symbol">(</a><a id="665" href="Relation.Nullary.Decidable.html#990" class="Function">False</a><a id="670" class="Symbol">)</a>
<a id="672" class="Keyword">open</a> <a id="677" class="Keyword">import</a> <a id="684" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="710" class="Keyword">using</a> <a id="716" class="Symbol">(</a><a id="717" href="Relation.Nullary.Negation.html#596" class="Function">contradiction</a><a id="730" class="Symbol">)</a>
<a id="732" class="Keyword">open</a> <a id="737" class="Keyword">import</a> <a id="744" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="760" class="Keyword">using</a> <a id="766" class="Symbol">(</a><a id="767" href="Relation.Binary.Core.html#976" class="Function">Rel</a><a id="770" class="Symbol">)</a>
<a id="772" class="Keyword">open</a> <a id="777" class="Keyword">import</a> <a id="784" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
  <a id="824" class="Keyword">using</a> <a id="830" class="Symbol">(</a><a id="831" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">_≡_</a><a id="834" class="Symbol">;</a> <a id="836" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a><a id="840" class="Symbol">;</a> <a id="842" href="Relation.Binary.PropositionalEquality.Core.html#1017" class="Function">subst</a><a id="847" class="Symbol">;</a> <a id="849" href="Relation.Binary.PropositionalEquality.Core.html#1084" class="Function">cong</a><a id="853" class="Symbol">;</a> <a id="855" href="Relation.Binary.PropositionalEquality.html#1362" class="Function">cong₂</a><a id="860" class="Symbol">;</a> <a id="862" class="Keyword">module</a> <a id="869" href="Relation.Binary.PropositionalEquality.Core.html#2567" class="Module">≡-Reasoning</a><a id="880" class="Symbol">)</a>

<a id="883" class="Keyword">open</a> <a id="888" href="Relation.Binary.PropositionalEquality.Core.html#2567" class="Module">≡-Reasoning</a>

<a id="901" class="Comment">------------------------------------------------------------------------</a>
<a id="974" class="Comment">-- Rational numbers in reduced form. Note that there is exactly one</a>
<a id="1042" class="Comment">-- way to represent every rational number.</a>

<a id="1086" class="Keyword">record</a> <a id="ℚ"></a><a id="1093" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="1095" class="Symbol">:</a> <a id="1097" class="PrimitiveType">Set</a> <a id="1101" class="Keyword">where</a>
  <a id="1109" class="Keyword">constructor</a> <a id="ℚ.mkℚ"></a><a id="1121" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a>
  <a id="1127" class="Keyword">field</a>
    <a id="ℚ.numerator"></a><a id="1137" href="Data.Rational.Base.html#1137" class="Field">numerator</a>     <a id="1151" class="Symbol">:</a> <a id="1153" href="Agda.Builtin.Int.html#219" class="Datatype">ℤ</a>
    <a id="ℚ.denominator-1"></a><a id="1159" href="Data.Rational.Base.html#1159" class="Field">denominator-1</a> <a id="1173" class="Symbol">:</a> <a id="1175" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a>
    <a id="1181" class="Symbol">.</a><a id="ℚ.isCoprime"></a><a id="1182" href="Data.Rational.Base.html#1182" class="Field">isCoprime</a>    <a id="1195" class="Symbol">:</a> <a id="1197" href="Data.Nat.Coprimality.html#999" class="Function">Coprime</a> <a id="1205" href="Data.Integer.Base.html#1075" class="Function Operator">∣</a> <a id="1207" href="Data.Rational.Base.html#1137" class="Field">numerator</a> <a id="1217" href="Data.Integer.Base.html#1075" class="Function Operator">∣</a> <a id="1219" class="Symbol">(</a><a id="1220" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="1224" href="Data.Rational.Base.html#1159" class="Field">denominator-1</a><a id="1237" class="Symbol">)</a>

  <a id="ℚ.denominatorℕ"></a><a id="1242" href="Data.Rational.Base.html#1242" class="Function">denominatorℕ</a> <a id="1255" class="Symbol">:</a> <a id="1257" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a>
  <a id="1261" href="Data.Rational.Base.html#1242" class="Function">denominatorℕ</a> <a id="1274" class="Symbol">=</a> <a id="1276" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="1280" href="Data.Rational.Base.html#1159" class="Field">denominator-1</a>

  <a id="ℚ.denominator"></a><a id="1297" href="Data.Rational.Base.html#1297" class="Function">denominator</a> <a id="1309" class="Symbol">:</a> <a id="1311" href="Agda.Builtin.Int.html#219" class="Datatype">ℤ</a>
  <a id="1315" href="Data.Rational.Base.html#1297" class="Function">denominator</a> <a id="1327" class="Symbol">=</a> <a id="1329" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="1331" href="Data.Rational.Base.html#1242" class="Function">denominatorℕ</a>

<a id="1345" class="Keyword">open</a> <a id="1350" href="Data.Rational.Base.html#1093" class="Module">ℚ</a> <a id="1352" class="Keyword">public</a> <a id="1359" class="Keyword">using</a> <a id="1365" class="Symbol">()</a>
  <a id="1370" class="Keyword">renaming</a>
  <a id="1381" class="Symbol">(</a> <a id="1383" href="Data.Rational.Base.html#1137" class="Field">numerator</a>    <a id="1396" class="Symbol">to</a> <a id="1399" href="Data.Rational.Base.html#1137" class="Field">↥_</a>
  <a id="1404" class="Symbol">;</a> <a id="1406" href="Data.Rational.Base.html#1297" class="Function">denominator</a>  <a id="1419" class="Symbol">to</a> <a id="1422" href="Data.Rational.Base.html#1297" class="Function">↧_</a>
  <a id="1427" class="Symbol">;</a> <a id="1429" href="Data.Rational.Base.html#1242" class="Function">denominatorℕ</a> <a id="1442" class="Symbol">to</a> <a id="1445" href="Data.Rational.Base.html#1242" class="Function">↧ₙ_</a>
  <a id="1451" class="Symbol">)</a>

<a id="1454" class="Comment">------------------------------------------------------------------------</a>
<a id="1527" class="Comment">-- Equality of rational numbers (coincides with _≡_)</a>

<a id="1581" class="Keyword">infix</a> <a id="1587" class="Number">4</a> <a id="1589" href="Data.Rational.Base.html#1594" class="Function Operator">_≃_</a>

<a id="_≃_"></a><a id="1594" href="Data.Rational.Base.html#1594" class="Function Operator">_≃_</a> <a id="1598" class="Symbol">:</a> <a id="1600" href="Relation.Binary.Core.html#976" class="Function">Rel</a> <a id="1604" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="1606" href="Level.html#506" class="Function">0ℓ</a>
<a id="1609" href="Data.Rational.Base.html#1609" class="Bound">p</a> <a id="1611" href="Data.Rational.Base.html#1594" class="Function Operator">≃</a> <a id="1613" href="Data.Rational.Base.html#1613" class="Bound">q</a> <a id="1615" class="Symbol">=</a> <a id="1617" class="Symbol">(</a><a id="1618" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="1620" href="Data.Rational.Base.html#1609" class="Bound">p</a> <a id="1622" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="1626" href="Data.Rational.Base.html#1297" class="Function Operator">↧</a> <a id="1628" href="Data.Rational.Base.html#1613" class="Bound">q</a><a id="1629" class="Symbol">)</a> <a id="1631" href="Agda.Builtin.Equality.html#125" class="Datatype Operator">≡</a> <a id="1633" class="Symbol">(</a><a id="1634" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="1636" href="Data.Rational.Base.html#1613" class="Bound">q</a> <a id="1638" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="1642" href="Data.Rational.Base.html#1297" class="Function Operator">↧</a> <a id="1644" href="Data.Rational.Base.html#1609" class="Bound">p</a><a id="1645" class="Symbol">)</a>

<a id="1648" class="Comment">------------------------------------------------------------------------</a>
<a id="1721" class="Comment">-- Ordering of rationals</a>

<a id="1747" class="Keyword">infix</a> <a id="1753" class="Number">4</a> <a id="1755" href="Data.Rational.Base.html#1765" class="Datatype Operator">_≤_</a>

<a id="1760" class="Keyword">data</a> <a id="_≤_"></a><a id="1765" href="Data.Rational.Base.html#1765" class="Datatype Operator">_≤_</a> <a id="1769" class="Symbol">:</a> <a id="1771" href="Relation.Binary.Core.html#976" class="Function">Rel</a> <a id="1775" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="1777" href="Level.html#506" class="Function">0ℓ</a> <a id="1780" class="Keyword">where</a>
  <a id="_≤_.*≤*"></a><a id="1788" href="Data.Rational.Base.html#1788" class="InductiveConstructor">*≤*</a> <a id="1792" class="Symbol">:</a> <a id="1794" class="Symbol">∀</a> <a id="1796" class="Symbol">{</a><a id="1797" href="Data.Rational.Base.html#1797" class="Bound">p</a> <a id="1799" href="Data.Rational.Base.html#1799" class="Bound">q</a><a id="1800" class="Symbol">}</a> <a id="1802" class="Symbol">→</a> <a id="1804" class="Symbol">(</a><a id="1805" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="1807" href="Data.Rational.Base.html#1797" class="Bound">p</a> <a id="1809" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="1813" href="Data.Rational.Base.html#1297" class="Function Operator">↧</a> <a id="1815" href="Data.Rational.Base.html#1799" class="Bound">q</a><a id="1816" class="Symbol">)</a> <a id="1818" href="Data.Integer.Base.html#2863" class="Datatype Operator">ℤ.≤</a> <a id="1822" class="Symbol">(</a><a id="1823" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="1825" href="Data.Rational.Base.html#1799" class="Bound">q</a> <a id="1827" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="1831" href="Data.Rational.Base.html#1297" class="Function Operator">↧</a> <a id="1833" href="Data.Rational.Base.html#1797" class="Bound">p</a><a id="1834" class="Symbol">)</a> <a id="1836" class="Symbol">→</a> <a id="1838" href="Data.Rational.Base.html#1797" class="Bound">p</a> <a id="1840" href="Data.Rational.Base.html#1765" class="Datatype Operator">≤</a> <a id="1842" href="Data.Rational.Base.html#1799" class="Bound">q</a>

<a id="1845" class="Comment">------------------------------------------------------------------------</a>
<a id="1918" class="Comment">-- Negation</a>

<a id="1931" class="Keyword">pattern</a> <a id="+0"></a><a id="1939" href="Data.Rational.Base.html#1939" class="InductiveConstructor">+0</a>       <a id="1948" class="Symbol">=</a> <a id="1950" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="1952" class="Number">0</a>
<a id="1954" class="Keyword">pattern</a> <a id="+[1+_]"></a><a id="1962" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">+[1+_]</a> <a id="1969" href="Data.Rational.Base.html#1979" class="Bound">n</a> <a id="1971" class="Symbol">=</a> <a id="1973" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="1975" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="1979" href="Data.Rational.Base.html#1979" class="Bound">n</a>

<a id="-_"></a><a id="1982" href="Data.Rational.Base.html#1982" class="Function Operator">-_</a> <a id="1985" class="Symbol">:</a> <a id="1987" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="1989" class="Symbol">→</a> <a id="1991" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="1993" href="Data.Rational.Base.html#1982" class="Function Operator">-</a> <a id="1995" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="1999" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">-[1+</a> <a id="2004" href="Data.Rational.Base.html#2004" class="Bound">n</a> <a id="2006" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">]</a> <a id="2008" href="Data.Rational.Base.html#2008" class="Bound">d</a> <a id="2010" href="Data.Rational.Base.html#2010" class="Bound">prf</a> <a id="2014" class="Symbol">=</a> <a id="2016" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2020" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">+[1+</a> <a id="2025" href="Data.Rational.Base.html#2004" class="Bound">n</a> <a id="2027" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">]</a> <a id="2029" href="Data.Rational.Base.html#2008" class="Bound">d</a> <a id="2031" href="Data.Rational.Base.html#2010" class="Bound">prf</a>
<a id="2035" href="Data.Rational.Base.html#1982" class="Function Operator">-</a> <a id="2037" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2041" href="Data.Rational.Base.html#1939" class="InductiveConstructor">+0</a>       <a id="2050" href="Data.Rational.Base.html#2050" class="Bound">d</a> <a id="2052" href="Data.Rational.Base.html#2052" class="Bound">prf</a> <a id="2056" class="Symbol">=</a> <a id="2058" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2062" href="Data.Rational.Base.html#1939" class="InductiveConstructor">+0</a>       <a id="2071" href="Data.Rational.Base.html#2050" class="Bound">d</a> <a id="2073" href="Data.Rational.Base.html#2052" class="Bound">prf</a>
<a id="2077" href="Data.Rational.Base.html#1982" class="Function Operator">-</a> <a id="2079" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2083" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">+[1+</a> <a id="2088" href="Data.Rational.Base.html#2088" class="Bound">n</a> <a id="2090" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">]</a> <a id="2092" href="Data.Rational.Base.html#2092" class="Bound">d</a> <a id="2094" href="Data.Rational.Base.html#2094" class="Bound">prf</a> <a id="2098" class="Symbol">=</a> <a id="2100" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2104" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">-[1+</a> <a id="2109" href="Data.Rational.Base.html#2088" class="Bound">n</a> <a id="2111" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">]</a> <a id="2113" href="Data.Rational.Base.html#2092" class="Bound">d</a> <a id="2115" href="Data.Rational.Base.html#2094" class="Bound">prf</a>

<a id="2120" class="Comment">------------------------------------------------------------------------</a>
<a id="2193" class="Comment">-- Constructing rationals</a>

<a id="2220" class="Keyword">infix</a> <a id="2226" class="Number">4</a> <a id="2228" href="Data.Rational.Base.html#2232" class="Function Operator">_≢0</a>
<a id="_≢0"></a><a id="2232" href="Data.Rational.Base.html#2232" class="Function Operator">_≢0</a> <a id="2236" class="Symbol">:</a> <a id="2238" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a> <a id="2240" class="Symbol">→</a> <a id="2242" class="PrimitiveType">Set</a>
<a id="2246" href="Data.Rational.Base.html#2246" class="Bound">n</a> <a id="2248" href="Data.Rational.Base.html#2232" class="Function Operator">≢0</a> <a id="2251" class="Symbol">=</a> <a id="2253" href="Relation.Nullary.Decidable.html#990" class="Function">False</a> <a id="2259" class="Symbol">(</a><a id="2260" href="Data.Rational.Base.html#2246" class="Bound">n</a> <a id="2262" href="Data.Nat.Properties.html#2147" class="Function Operator">ℕ.≟</a> <a id="2266" class="Number">0</a><a id="2267" class="Symbol">)</a>

<a id="2270" class="Comment">-- A constructor for ℚ that takes two natural numbers, say 6 and 21,</a>
<a id="2339" class="Comment">-- and returns them in a normalized form, e.g. say 2 and 7</a>

<a id="normalize"></a><a id="2399" href="Data.Rational.Base.html#2399" class="Function">normalize</a> <a id="2409" class="Symbol">:</a> <a id="2411" class="Symbol">∀</a> <a id="2413" href="Data.Rational.Base.html#2413" class="Bound">m</a> <a id="2415" href="Data.Rational.Base.html#2415" class="Bound">n</a> <a id="2417" class="Symbol">.{</a><a id="2419" href="Data.Rational.Base.html#2419" class="Bound">m≢0</a> <a id="2423" class="Symbol">:</a> <a id="2425" href="Data.Rational.Base.html#2413" class="Bound">m</a> <a id="2427" href="Data.Rational.Base.html#2232" class="Function Operator">≢0</a><a id="2429" class="Symbol">}</a> <a id="2431" class="Symbol">.{</a><a id="2433" href="Data.Rational.Base.html#2433" class="Bound">n≢0</a> <a id="2437" class="Symbol">:</a> <a id="2439" href="Data.Rational.Base.html#2415" class="Bound">n</a> <a id="2441" href="Data.Rational.Base.html#2232" class="Function Operator">≢0</a><a id="2443" class="Symbol">}</a> <a id="2445" class="Symbol">→</a> <a id="2447" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="2449" href="Data.Rational.Base.html#2399" class="Function">normalize</a> <a id="2459" class="Symbol">(</a><a id="2460" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2464" href="Data.Rational.Base.html#2464" class="Bound">m</a><a id="2465" class="Symbol">)</a> <a id="2467" class="Symbol">(</a><a id="2468" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2472" href="Data.Rational.Base.html#2472" class="Bound">n</a><a id="2473" class="Symbol">)</a> <a id="2475" class="Keyword">with</a> <a id="2480" href="Data.Nat.GCD.html#6086" class="Function">gcd</a> <a id="2484" class="Symbol">(</a><a id="2485" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2489" href="Data.Rational.Base.html#2464" class="Bound">m</a><a id="2490" class="Symbol">)</a> <a id="2492" class="Symbol">(</a><a id="2493" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2497" href="Data.Rational.Base.html#2472" class="Bound">n</a><a id="2498" class="Symbol">)</a>
<a id="2500" class="Symbol">...</a> <a id="2504" class="Symbol">|</a> <a id="2506" href="Agda.Builtin.Nat.html#183" class="InductiveConstructor">zero</a>  <a id="2512" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2514" href="Data.Nat.GCD.html#976" class="InductiveConstructor">GCD.is</a> <a id="2521" class="Symbol">(</a><a id="2522" href="Data.Rational.Base.html#2522" class="Bound">1+m∣0</a> <a id="2528" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2530" class="Symbol">_)</a> <a id="2533" class="Symbol">_</a> <a id="2535" class="Symbol">=</a> <a id="2537" href="Relation.Nullary.Negation.html#596" class="Function">contradiction</a> <a id="2551" class="Symbol">(</a><a id="2552" href="Data.Nat.Divisibility.html#3223" class="Function">0∣⇒≡0</a> <a id="2558" href="Data.Rational.Base.html#2522" class="Bound">1+m∣0</a><a id="2563" class="Symbol">)</a> <a id="2565" class="Symbol">λ()</a>
<a id="2569" class="Symbol">...</a> <a id="2573" class="Symbol">|</a> <a id="2575" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2579" href="Data.Rational.Base.html#2579" class="Bound">g</a> <a id="2581" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2583" href="Data.Rational.Base.html#2583" class="Bound">G</a><a id="2584" class="Symbol">@(</a><a id="2586" href="Data.Nat.GCD.html#976" class="InductiveConstructor">GCD.is</a> <a id="2593" class="Symbol">(</a><a id="2594" href="Data.Nat.Divisibility.html#1324" class="InductiveConstructor">divides</a> <a id="2602" class="Symbol">(</a><a id="2603" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2607" href="Data.Rational.Base.html#2607" class="Bound">p</a><a id="2608" class="Symbol">)</a> <a id="2610" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a> <a id="2615" href="Agda.Builtin.Sigma.html#209" class="InductiveConstructor Operator">,</a> <a id="2617" href="Data.Nat.Divisibility.html#1324" class="InductiveConstructor">divides</a> <a id="2625" class="Symbol">(</a><a id="2626" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2630" href="Data.Rational.Base.html#2630" class="Bound">q</a><a id="2631" class="Symbol">)</a> <a id="2633" href="Agda.Builtin.Equality.html#182" class="InductiveConstructor">refl</a><a id="2637" class="Symbol">)</a> <a id="2639" class="Symbol">_)</a>
  <a id="2644" class="Symbol">=</a> <a id="2646" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2650" class="Symbol">(</a><a id="2651" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="2653" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="2657" href="Data.Rational.Base.html#2607" class="Bound">p</a><a id="2658" class="Symbol">)</a> <a id="2660" href="Data.Rational.Base.html#2630" class="Bound">q</a> <a id="2662" class="Symbol">(</a><a id="2663" href="Data.Nat.Coprimality.html#2498" class="Function">Bézout-coprime</a> <a id="2678" class="Symbol">(</a><a id="2679" href="Data.Nat.GCD.html#5894" class="Function">Bézout.identity</a> <a id="2695" href="Data.Rational.Base.html#2583" class="Bound">G</a><a id="2696" class="Symbol">))</a>

<a id="2700" class="Comment">-- A constructor for ℚ that (unlike mkℚ) automatically normalises it&#39;s</a>
<a id="2771" class="Comment">-- arguments. See the constants section below for how to use this operator.</a>

<a id="2848" class="Keyword">infixl</a> <a id="2855" class="Number">7</a> <a id="2857" href="Data.Rational.Base.html#2862" class="Function Operator">_/_</a>

<a id="_/_"></a><a id="2862" href="Data.Rational.Base.html#2862" class="Function Operator">_/_</a> <a id="2866" class="Symbol">:</a> <a id="2868" class="Symbol">(</a><a id="2869" href="Data.Rational.Base.html#2869" class="Bound">n</a> <a id="2871" class="Symbol">:</a> <a id="2873" href="Agda.Builtin.Int.html#219" class="Datatype">ℤ</a><a id="2874" class="Symbol">)</a> <a id="2876" class="Symbol">(</a><a id="2877" href="Data.Rational.Base.html#2877" class="Bound">d</a> <a id="2879" class="Symbol">:</a> <a id="2881" href="Agda.Builtin.Nat.html#165" class="Datatype">ℕ</a><a id="2882" class="Symbol">)</a> <a id="2884" class="Symbol">→</a> <a id="2886" class="Symbol">.{</a><a id="2888" href="Data.Rational.Base.html#2888" class="Bound">d≢0</a> <a id="2892" class="Symbol">:</a> <a id="2894" href="Data.Rational.Base.html#2877" class="Bound">d</a> <a id="2896" href="Data.Rational.Base.html#2232" class="Function Operator">≢0</a><a id="2898" class="Symbol">}</a> <a id="2900" class="Symbol">→</a> <a id="2902" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="2904" href="Data.Rational.Base.html#2862" class="Function Operator">_/_</a> <a id="2908" href="Data.Rational.Base.html#1939" class="InductiveConstructor">+0</a>       <a id="2917" href="Data.Rational.Base.html#2917" class="Bound">d</a> <a id="2919" class="Symbol">{</a><a id="2920" href="Data.Rational.Base.html#2920" class="Bound">d≢0</a><a id="2923" class="Symbol">}</a> <a id="2925" class="Symbol">=</a> <a id="2927" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="2931" href="Data.Rational.Base.html#1939" class="InductiveConstructor">+0</a> <a id="2934" class="Number">0</a> <a id="2936" class="Symbol">(</a><a id="2937" href="Data.Nat.Coprimality.html#1966" class="Function">C.sym</a> <a id="2943" class="Symbol">(</a><a id="2944" href="Data.Nat.Coprimality.html#2040" class="Function">C.1-coprimeTo</a> <a id="2958" class="Number">0</a><a id="2959" class="Symbol">))</a>
<a id="2962" href="Data.Rational.Base.html#2862" class="Function Operator">_/_</a> <a id="2966" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">+[1+</a> <a id="2971" href="Data.Rational.Base.html#2971" class="Bound">n</a> <a id="2973" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">]</a> <a id="2975" href="Data.Rational.Base.html#2975" class="Bound">d</a> <a id="2977" class="Symbol">{</a><a id="2978" href="Data.Rational.Base.html#2978" class="Bound">d≢0</a><a id="2981" class="Symbol">}</a> <a id="2983" class="Symbol">=</a>   <a id="2987" href="Data.Rational.Base.html#2399" class="Function">normalize</a> <a id="2997" class="Symbol">(</a><a id="2998" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="3002" href="Data.Rational.Base.html#2971" class="Bound">n</a><a id="3003" class="Symbol">)</a> <a id="3005" href="Data.Rational.Base.html#2975" class="Bound">d</a> <a id="3007" class="Symbol">{_}</a> <a id="3011" class="Symbol">{</a><a id="3012" href="Data.Rational.Base.html#2978" class="Bound">d≢0</a><a id="3015" class="Symbol">}</a>
<a id="3017" href="Data.Rational.Base.html#2862" class="Function Operator">_/_</a> <a id="3021" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">-[1+</a> <a id="3026" href="Data.Rational.Base.html#3026" class="Bound">n</a> <a id="3028" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">]</a> <a id="3030" href="Data.Rational.Base.html#3030" class="Bound">d</a> <a id="3032" class="Symbol">{</a><a id="3033" href="Data.Rational.Base.html#3033" class="Bound">d≢0</a><a id="3036" class="Symbol">}</a> <a id="3038" class="Symbol">=</a> <a id="3040" href="Data.Rational.Base.html#1982" class="Function Operator">-</a> <a id="3042" href="Data.Rational.Base.html#2399" class="Function">normalize</a> <a id="3052" class="Symbol">(</a><a id="3053" href="Agda.Builtin.Nat.html#196" class="InductiveConstructor">suc</a> <a id="3057" href="Data.Rational.Base.html#3026" class="Bound">n</a><a id="3058" class="Symbol">)</a> <a id="3060" href="Data.Rational.Base.html#3030" class="Bound">d</a> <a id="3062" class="Symbol">{_}</a> <a id="3066" class="Symbol">{</a><a id="3067" href="Data.Rational.Base.html#3033" class="Bound">d≢0</a><a id="3070" class="Symbol">}</a>

<a id="3073" class="Comment">------------------------------------------------------------------------------</a>
<a id="3152" class="Comment">-- Some constants</a>

<a id="0ℚ"></a><a id="3171" href="Data.Rational.Base.html#3171" class="Function">0ℚ</a> <a id="3174" class="Symbol">:</a> <a id="3176" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3178" href="Data.Rational.Base.html#3171" class="Function">0ℚ</a> <a id="3181" class="Symbol">=</a> <a id="3183" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="3185" class="Number">0</a> <a id="3187" href="Data.Rational.Base.html#2862" class="Function Operator">/</a> <a id="3189" class="Number">1</a>

<a id="1ℚ"></a><a id="3192" href="Data.Rational.Base.html#3192" class="Function">1ℚ</a> <a id="3195" class="Symbol">:</a> <a id="3197" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3199" href="Data.Rational.Base.html#3192" class="Function">1ℚ</a> <a id="3202" class="Symbol">=</a> <a id="3204" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="3206" class="Number">1</a> <a id="3208" href="Data.Rational.Base.html#2862" class="Function Operator">/</a> <a id="3210" class="Number">1</a>

<a id="½"></a><a id="3213" href="Data.Rational.Base.html#3213" class="Function">½</a> <a id="3215" class="Symbol">:</a> <a id="3217" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3219" href="Data.Rational.Base.html#3213" class="Function">½</a> <a id="3221" class="Symbol">=</a> <a id="3223" href="Agda.Builtin.Int.html#237" class="InductiveConstructor Operator">+</a> <a id="3225" class="Number">1</a> <a id="3227" href="Data.Rational.Base.html#2862" class="Function Operator">/</a> <a id="3229" class="Number">2</a>

<a id="-½"></a><a id="3232" href="Data.Rational.Base.html#3232" class="Function">-½</a> <a id="3235" class="Symbol">:</a> <a id="3237" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3239" href="Data.Rational.Base.html#3232" class="Function">-½</a> <a id="3242" class="Symbol">=</a> <a id="3244" href="Data.Rational.Base.html#1982" class="Function Operator">-</a> <a id="3246" href="Data.Rational.Base.html#3213" class="Function">½</a>

<a id="3249" class="Comment">------------------------------------------------------------------------------</a>
<a id="3328" class="Comment">-- Operations on rationals</a>

<a id="3356" class="Keyword">infix</a>  <a id="3363" class="Number">8</a> <a id="3365" href="Data.Rational.Base.html#1982" class="Function Operator">-_</a> <a id="3368" href="Data.Rational.Base.html#3684" class="Function Operator">1/_</a>
<a id="3372" class="Keyword">infixl</a> <a id="3379" class="Number">7</a> <a id="3381" href="Data.Rational.Base.html#3512" class="Function Operator">_*_</a> <a id="3385" href="Data.Rational.Base.html#3891" class="Function Operator">_÷_</a>
<a id="3389" class="Keyword">infixl</a> <a id="3396" class="Number">6</a> <a id="3398" href="Data.Rational.Base.html#3585" class="Function Operator">_-_</a> <a id="3402" href="Data.Rational.Base.html#3420" class="Function Operator">_+_</a>

<a id="3407" class="Comment">-- addition</a>

<a id="_+_"></a><a id="3420" href="Data.Rational.Base.html#3420" class="Function Operator">_+_</a> <a id="3424" class="Symbol">:</a> <a id="3426" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="3428" class="Symbol">→</a> <a id="3430" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="3432" class="Symbol">→</a> <a id="3434" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3436" href="Data.Rational.Base.html#3436" class="Bound">p</a> <a id="3438" href="Data.Rational.Base.html#3420" class="Function Operator">+</a> <a id="3440" href="Data.Rational.Base.html#3440" class="Bound">q</a> <a id="3442" class="Symbol">=</a> <a id="3444" class="Symbol">(</a><a id="3445" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="3447" href="Data.Rational.Base.html#3436" class="Bound">p</a> <a id="3449" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="3453" href="Data.Rational.Base.html#1297" class="Function Operator">↧</a> <a id="3455" href="Data.Rational.Base.html#3440" class="Bound">q</a> <a id="3457" href="Data.Integer.Base.html#2057" class="Function Operator">ℤ.+</a> <a id="3461" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="3463" href="Data.Rational.Base.html#3440" class="Bound">q</a> <a id="3465" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="3469" href="Data.Rational.Base.html#1297" class="Function Operator">↧</a> <a id="3471" href="Data.Rational.Base.html#3436" class="Bound">p</a><a id="3472" class="Symbol">)</a> <a id="3474" href="Data.Rational.Base.html#2862" class="Function Operator">/</a> <a id="3476" class="Symbol">(</a><a id="3477" href="Data.Rational.Base.html#1242" class="Function Operator">↧ₙ</a> <a id="3480" href="Data.Rational.Base.html#3436" class="Bound">p</a> <a id="3482" href="Agda.Builtin.Nat.html#501" class="Primitive Operator">ℕ.*</a> <a id="3486" href="Data.Rational.Base.html#1242" class="Function Operator">↧ₙ</a> <a id="3489" href="Data.Rational.Base.html#3440" class="Bound">q</a><a id="3490" class="Symbol">)</a>

<a id="3493" class="Comment">-- multiplication</a>

<a id="_*_"></a><a id="3512" href="Data.Rational.Base.html#3512" class="Function Operator">_*_</a> <a id="3516" class="Symbol">:</a> <a id="3518" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="3520" class="Symbol">→</a> <a id="3522" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="3524" class="Symbol">→</a> <a id="3526" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3528" href="Data.Rational.Base.html#3528" class="Bound">p</a> <a id="3530" href="Data.Rational.Base.html#3512" class="Function Operator">*</a> <a id="3532" href="Data.Rational.Base.html#3532" class="Bound">q</a> <a id="3534" class="Symbol">=</a> <a id="3536" class="Symbol">(</a><a id="3537" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="3539" href="Data.Rational.Base.html#3528" class="Bound">p</a> <a id="3541" href="Data.Integer.Base.html#2389" class="Function Operator">ℤ.*</a> <a id="3545" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="3547" href="Data.Rational.Base.html#3532" class="Bound">q</a><a id="3548" class="Symbol">)</a> <a id="3550" href="Data.Rational.Base.html#2862" class="Function Operator">/</a> <a id="3552" class="Symbol">(</a><a id="3553" href="Data.Rational.Base.html#1242" class="Function Operator">↧ₙ</a> <a id="3556" href="Data.Rational.Base.html#3528" class="Bound">p</a> <a id="3558" href="Agda.Builtin.Nat.html#501" class="Primitive Operator">ℕ.*</a> <a id="3562" href="Data.Rational.Base.html#1242" class="Function Operator">↧ₙ</a> <a id="3565" href="Data.Rational.Base.html#3532" class="Bound">q</a><a id="3566" class="Symbol">)</a>

<a id="3569" class="Comment">-- subtraction</a>

<a id="_-_"></a><a id="3585" href="Data.Rational.Base.html#3585" class="Function Operator">_-_</a> <a id="3589" class="Symbol">:</a> <a id="3591" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="3593" class="Symbol">→</a> <a id="3595" href="Data.Rational.Base.html#1093" class="Record">ℚ</a> <a id="3597" class="Symbol">→</a> <a id="3599" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3601" href="Data.Rational.Base.html#3601" class="Bound">p</a> <a id="3603" href="Data.Rational.Base.html#3585" class="Function Operator">-</a> <a id="3605" href="Data.Rational.Base.html#3605" class="Bound">q</a> <a id="3607" class="Symbol">=</a> <a id="3609" href="Data.Rational.Base.html#3601" class="Bound">p</a> <a id="3611" href="Data.Rational.Base.html#3420" class="Function Operator">+</a> <a id="3613" class="Symbol">(</a><a id="3614" href="Data.Rational.Base.html#1982" class="Function Operator">-</a> <a id="3616" href="Data.Rational.Base.html#3605" class="Bound">q</a><a id="3617" class="Symbol">)</a>

<a id="3620" class="Comment">-- reciprocal: requires a proof that the numerator is not zero</a>

<a id="1/_"></a><a id="3684" href="Data.Rational.Base.html#3684" class="Function Operator">1/_</a> <a id="3688" class="Symbol">:</a> <a id="3690" class="Symbol">(</a><a id="3691" href="Data.Rational.Base.html#3691" class="Bound">p</a> <a id="3693" class="Symbol">:</a> <a id="3695" href="Data.Rational.Base.html#1093" class="Record">ℚ</a><a id="3696" class="Symbol">)</a> <a id="3698" class="Symbol">→</a> <a id="3700" class="Symbol">.{</a><a id="3702" href="Data.Rational.Base.html#3702" class="Bound">n≢0</a> <a id="3706" class="Symbol">:</a> <a id="3708" href="Data.Integer.Base.html#1075" class="Function Operator">∣</a> <a id="3710" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="3712" href="Data.Rational.Base.html#3691" class="Bound">p</a> <a id="3714" href="Data.Integer.Base.html#1075" class="Function Operator">∣</a> <a id="3716" href="Data.Rational.Base.html#2232" class="Function Operator">≢0</a><a id="3718" class="Symbol">}</a> <a id="3720" class="Symbol">→</a> <a id="3722" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3724" href="Data.Rational.Base.html#3684" class="Function Operator">1/</a> <a id="3727" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="3731" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">+[1+</a> <a id="3736" href="Data.Rational.Base.html#3736" class="Bound">n</a> <a id="3738" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">]</a> <a id="3740" href="Data.Rational.Base.html#3740" class="Bound">d</a> <a id="3742" href="Data.Rational.Base.html#3742" class="Bound">prf</a> <a id="3746" class="Symbol">=</a> <a id="3748" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="3752" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">+[1+</a> <a id="3757" href="Data.Rational.Base.html#3740" class="Bound">d</a> <a id="3759" href="Data.Rational.Base.html#1962" class="InductiveConstructor Operator">]</a> <a id="3761" href="Data.Rational.Base.html#3736" class="Bound">n</a> <a id="3763" class="Symbol">(</a><a id="3764" href="Data.Nat.Coprimality.html#1966" class="Function">C.sym</a> <a id="3770" href="Data.Rational.Base.html#3742" class="Bound">prf</a><a id="3773" class="Symbol">)</a>
<a id="3775" href="Data.Rational.Base.html#3684" class="Function Operator">1/</a> <a id="3778" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="3782" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">-[1+</a> <a id="3787" href="Data.Rational.Base.html#3787" class="Bound">n</a> <a id="3789" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">]</a> <a id="3791" href="Data.Rational.Base.html#3791" class="Bound">d</a> <a id="3793" href="Data.Rational.Base.html#3793" class="Bound">prf</a> <a id="3797" class="Symbol">=</a> <a id="3799" href="Data.Rational.Base.html#1121" class="InductiveConstructor">mkℚ</a> <a id="3803" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">-[1+</a> <a id="3808" href="Data.Rational.Base.html#3791" class="Bound">d</a> <a id="3810" href="Agda.Builtin.Int.html#264" class="InductiveConstructor Operator">]</a> <a id="3812" href="Data.Rational.Base.html#3787" class="Bound">n</a> <a id="3814" class="Symbol">(</a><a id="3815" href="Data.Nat.Coprimality.html#1966" class="Function">C.sym</a> <a id="3821" href="Data.Rational.Base.html#3793" class="Bound">prf</a><a id="3824" class="Symbol">)</a>

<a id="3827" class="Comment">-- division: requires a proof that the denominator is not zero</a>

<a id="_÷_"></a><a id="3891" href="Data.Rational.Base.html#3891" class="Function Operator">_÷_</a> <a id="3895" class="Symbol">:</a> <a id="3897" class="Symbol">(</a><a id="3898" href="Data.Rational.Base.html#3898" class="Bound">p</a> <a id="3900" href="Data.Rational.Base.html#3900" class="Bound">q</a> <a id="3902" class="Symbol">:</a> <a id="3904" href="Data.Rational.Base.html#1093" class="Record">ℚ</a><a id="3905" class="Symbol">)</a> <a id="3907" class="Symbol">→</a> <a id="3909" class="Symbol">.{</a><a id="3911" href="Data.Rational.Base.html#3911" class="Bound">n≢0</a> <a id="3915" class="Symbol">:</a> <a id="3917" href="Data.Integer.Base.html#1075" class="Function Operator">∣</a> <a id="3919" href="Data.Rational.Base.html#1137" class="Field Operator">↥</a> <a id="3921" href="Data.Rational.Base.html#3900" class="Bound">q</a> <a id="3923" href="Data.Integer.Base.html#1075" class="Function Operator">∣</a> <a id="3925" href="Data.Rational.Base.html#2232" class="Function Operator">≢0</a><a id="3927" class="Symbol">}</a> <a id="3929" class="Symbol">→</a> <a id="3931" href="Data.Rational.Base.html#1093" class="Record">ℚ</a>
<a id="3933" class="Symbol">(</a><a id="3934" href="Data.Rational.Base.html#3934" class="Bound">p</a> <a id="3936" href="Data.Rational.Base.html#3891" class="Function Operator">÷</a> <a id="3938" href="Data.Rational.Base.html#3938" class="Bound">q</a><a id="3939" class="Symbol">)</a> <a id="3941" class="Symbol">{</a><a id="3942" href="Data.Rational.Base.html#3942" class="Bound">n≢0</a><a id="3945" class="Symbol">}</a> <a id="3947" class="Symbol">=</a> <a id="3949" href="Data.Rational.Base.html#3934" class="Bound">p</a> <a id="3951" href="Data.Rational.Base.html#3512" class="Function Operator">*</a> <a id="3953" class="Symbol">(</a><a id="3954" href="Data.Rational.Base.html#3684" class="Function Operator">1/_</a> <a id="3958" href="Data.Rational.Base.html#3938" class="Bound">q</a> <a id="3960" class="Symbol">{</a><a id="3961" href="Data.Rational.Base.html#3942" class="Bound">n≢0</a><a id="3964" class="Symbol">})</a>
</pre></body></html>