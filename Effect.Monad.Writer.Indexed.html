<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Effect.Monad.Writer.Indexed</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- The indexed writer monad</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Pragma">--without-K</a> <a id="232" class="Pragma">--safe</a> <a id="239" class="Symbol">#-}</a>

<a id="244" class="Keyword">open</a> <a id="249" class="Keyword">import</a> <a id="256" href="Level.html" class="Module">Level</a>

<a id="263" class="Keyword">module</a> <a id="270" href="Effect.Monad.Writer.Indexed.html" class="Module">Effect.Monad.Writer.Indexed</a> <a id="298" class="Symbol">(</a><a id="299" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a> <a id="301" class="Symbol">:</a> <a id="303" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="308" class="Symbol">)</a> <a id="310" class="Keyword">where</a>

<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="Algebra.html" class="Module">Algebra</a> <a id="337" class="Keyword">using</a> <a id="343" class="Symbol">(</a><a id="344" href="Algebra.Bundles.Raw.html#1096" class="Record">RawMonoid</a><a id="353" class="Symbol">)</a>
<a id="355" class="Keyword">open</a> <a id="360" class="Keyword">import</a> <a id="367" href="Data.Product.html" class="Module">Data.Product</a> <a id="380" class="Keyword">using</a> <a id="386" class="Symbol">(</a><a id="387" href="Data.Product.Base.html#1109" class="Function Operator">_Ã—_</a><a id="390" class="Symbol">;</a> <a id="392" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,_</a><a id="395" class="Symbol">;</a> <a id="397" href="Data.Product.Base.html#1803" class="Function">mapâ‚</a><a id="401" class="Symbol">)</a>
<a id="403" class="Keyword">open</a> <a id="408" class="Keyword">import</a> <a id="415" href="Data.Unit.Polymorphic.html" class="Module">Data.Unit.Polymorphic</a>
<a id="437" class="Keyword">open</a> <a id="442" class="Keyword">import</a> <a id="449" href="Effect.Applicative.Indexed.html" class="Module">Effect.Applicative.Indexed</a>
<a id="476" class="Keyword">open</a> <a id="481" class="Keyword">import</a> <a id="488" href="Effect.Monad.html" class="Module">Effect.Monad</a>
<a id="501" class="Keyword">open</a> <a id="506" class="Keyword">import</a> <a id="513" href="Effect.Monad.Indexed.html" class="Module">Effect.Monad.Indexed</a>
<a id="534" class="Keyword">open</a> <a id="539" class="Keyword">import</a> <a id="546" href="Function.html" class="Module">Function</a>
<a id="555" class="Keyword">open</a> <a id="560" class="Keyword">import</a> <a id="567" href="Function.Identity.Effectful.html" class="Module">Function.Identity.Effectful</a> <a id="595" class="Symbol">as</a> <a id="598" class="Module">Id</a> <a id="601" class="Keyword">using</a> <a id="607" class="Symbol">(</a><a id="608" href="Function.Identity.Effectful.html#529" class="Function">Identity</a><a id="616" class="Symbol">)</a>

<a id="619" class="Keyword">private</a>
  <a id="629" class="Keyword">variable</a>
    <a id="642" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="644" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a> <a id="646" class="Symbol">:</a> <a id="648" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="658" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a> <a id="660" href="Effect.Monad.Writer.Indexed.html#660" class="Generalizable">B</a> <a id="662" href="Effect.Monad.Writer.Indexed.html#662" class="Generalizable">I</a> <a id="664" class="Symbol">:</a> <a id="666" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="670" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a>

<a id="673" class="Comment">------------------------------------------------------------------------</a>
<a id="746" class="Comment">-- Indexed writer</a>

<a id="IWriterT"></a><a id="765" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="774" class="Symbol">:</a> <a id="776" class="Symbol">(</a><a id="777" href="Effect.Monad.Writer.Indexed.html#777" class="Bound">ğ•</a> <a id="779" class="Symbol">:</a> <a id="781" href="Algebra.Bundles.Raw.html#1096" class="Record">RawMonoid</a> <a id="791" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="793" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a><a id="794" class="Symbol">)</a> <a id="796" class="Symbol">â†’</a> <a id="798" href="Effect.Applicative.Indexed.html#657" class="Function">IFun</a> <a id="803" href="Effect.Monad.Writer.Indexed.html#662" class="Generalizable">I</a> <a id="805" class="Symbol">(</a><a id="806" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="808" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="810" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a><a id="811" class="Symbol">)</a> <a id="813" class="Symbol">â†’</a> <a id="815" href="Effect.Applicative.Indexed.html#657" class="Function">IFun</a> <a id="820" href="Effect.Monad.Writer.Indexed.html#662" class="Generalizable">I</a> <a id="822" class="Symbol">(</a><a id="823" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="825" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="827" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a><a id="828" class="Symbol">)</a>
<a id="830" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="839" href="Effect.Monad.Writer.Indexed.html#839" class="Bound">ğ•</a> <a id="841" href="Effect.Monad.Writer.Indexed.html#841" class="Bound">M</a> <a id="843" href="Effect.Monad.Writer.Indexed.html#843" class="Bound">i</a> <a id="845" href="Effect.Monad.Writer.Indexed.html#845" class="Bound">j</a> <a id="847" href="Effect.Monad.Writer.Indexed.html#847" class="Bound">A</a> <a id="849" class="Symbol">=</a> <a id="851" href="Effect.Monad.Writer.Indexed.html#841" class="Bound">M</a> <a id="853" href="Effect.Monad.Writer.Indexed.html#843" class="Bound">i</a> <a id="855" href="Effect.Monad.Writer.Indexed.html#845" class="Bound">j</a> <a id="857" class="Symbol">(</a><a id="858" href="Algebra.Bundles.Raw.html#1178" class="Field">RawMonoid.Carrier</a> <a id="876" href="Effect.Monad.Writer.Indexed.html#839" class="Bound">ğ•</a> <a id="878" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="880" href="Effect.Monad.Writer.Indexed.html#847" class="Bound">A</a><a id="881" class="Symbol">)</a>

<a id="884" class="Keyword">module</a> <a id="891" href="Effect.Monad.Writer.Indexed.html#891" class="Module">_</a> <a id="893" class="Symbol">{</a><a id="894" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="896" class="Symbol">:</a> <a id="898" href="Effect.Applicative.Indexed.html#657" class="Function">IFun</a> <a id="903" href="Effect.Monad.Writer.Indexed.html#662" class="Generalizable">I</a> <a id="905" class="Symbol">(</a><a id="906" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="908" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="910" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a><a id="911" class="Symbol">)}</a> <a id="914" class="Symbol">{</a><a id="915" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="917" class="Symbol">:</a> <a id="919" href="Algebra.Bundles.Raw.html#1096" class="Record">RawMonoid</a> <a id="929" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="931" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a><a id="932" class="Symbol">}</a> <a id="934" class="Keyword">where</a>

  <a id="943" class="Keyword">open</a> <a id="948" href="Algebra.Bundles.Raw.html#1096" class="Module">RawMonoid</a> <a id="958" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="960" class="Keyword">renaming</a> <a id="969" class="Symbol">(</a><a id="970" href="Algebra.Bundles.Raw.html#1178" class="Field">Carrier</a> <a id="978" class="Symbol">to</a> <a id="981" class="Field">W</a><a id="982" class="Symbol">)</a>

  <a id="987" class="Comment">------------------------------------------------------------------------</a>
  <a id="1062" class="Comment">-- Indexed writer applicative</a>

  <a id="1095" href="Effect.Monad.Writer.Indexed.html#1095" class="Function">WriterTIApplicative</a> <a id="1115" class="Symbol">:</a> <a id="1117" href="Effect.Applicative.Indexed.html#848" class="Record">RawIApplicative</a> <a id="1133" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="1135" class="Symbol">â†’</a> <a id="1137" href="Effect.Applicative.Indexed.html#848" class="Record">RawIApplicative</a> <a id="1153" class="Symbol">(</a><a id="1154" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="1163" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="1165" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a><a id="1166" class="Symbol">)</a>
  <a id="1170" href="Effect.Monad.Writer.Indexed.html#1095" class="Function">WriterTIApplicative</a> <a id="1190" href="Effect.Monad.Writer.Indexed.html#1190" class="Bound">App</a> <a id="1194" class="Symbol">=</a> <a id="1196" class="Keyword">record</a>
    <a id="1207" class="Symbol">{</a> <a id="1209" href="Effect.Applicative.Indexed.html#991" class="Field">pure</a> <a id="1214" class="Symbol">=</a> <a id="1216" class="Symbol">Î»</a> <a id="1218" href="Effect.Monad.Writer.Indexed.html#1218" class="Bound">x</a> <a id="1220" class="Symbol">â†’</a> <a id="1222" href="Effect.Applicative.Indexed.html#991" class="Field">pure</a> <a id="1227" class="Symbol">(</a><a id="1228" href="Algebra.Bundles.Raw.html#1252" class="Field">Îµ</a> <a id="1230" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1232" href="Effect.Monad.Writer.Indexed.html#1218" class="Bound">x</a><a id="1233" class="Symbol">)</a>
    <a id="1239" class="Symbol">;</a> <a id="1241" href="Effect.Applicative.Indexed.html#1022" class="Field Operator">_âŠ›_</a> <a id="1245" class="Symbol">=</a> <a id="1247" class="Symbol">Î»</a> <a id="1249" href="Effect.Monad.Writer.Indexed.html#1249" class="Bound">m</a> <a id="1251" href="Effect.Monad.Writer.Indexed.html#1251" class="Bound">n</a> <a id="1253" class="Symbol">â†’</a> <a id="1255" href="Effect.Monad.Writer.Indexed.html#1321" class="Function">go</a> <a id="1258" href="Effect.Functor.html#678" class="Function Operator">&lt;$&gt;</a> <a id="1262" href="Effect.Monad.Writer.Indexed.html#1249" class="Bound">m</a> <a id="1264" href="Effect.Applicative.Indexed.html#1022" class="Field Operator">âŠ›</a> <a id="1266" href="Effect.Monad.Writer.Indexed.html#1251" class="Bound">n</a>
    <a id="1272" class="Symbol">}</a> <a id="1274" class="Keyword">where</a>
        <a id="1288" class="Keyword">open</a> <a id="1293" href="Effect.Applicative.Indexed.html#848" class="Module">RawIApplicative</a> <a id="1309" href="Effect.Monad.Writer.Indexed.html#1190" class="Bound">App</a>
        <a id="1321" href="Effect.Monad.Writer.Indexed.html#1321" class="Function">go</a> <a id="1324" class="Symbol">:</a> <a id="1326" href="Effect.Monad.Writer.Indexed.html#981" class="Field">W</a> <a id="1328" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="1330" class="Symbol">(</a><a id="1331" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a> <a id="1333" class="Symbol">â†’</a> <a id="1335" href="Effect.Monad.Writer.Indexed.html#660" class="Generalizable">B</a><a id="1336" class="Symbol">)</a> <a id="1338" class="Symbol">â†’</a> <a id="1340" href="Effect.Monad.Writer.Indexed.html#981" class="Field">W</a> <a id="1342" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="1344" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a> <a id="1346" class="Symbol">â†’</a> <a id="1348" href="Effect.Monad.Writer.Indexed.html#981" class="Field">W</a> <a id="1350" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="1352" href="Effect.Monad.Writer.Indexed.html#660" class="Generalizable">B</a>
        <a id="1362" href="Effect.Monad.Writer.Indexed.html#1321" class="Function">go</a> <a id="1365" class="Symbol">(</a><a id="1366" href="Effect.Monad.Writer.Indexed.html#1366" class="Bound">wâ‚</a> <a id="1369" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1371" href="Effect.Monad.Writer.Indexed.html#1371" class="Bound">f</a><a id="1372" class="Symbol">)</a> <a id="1374" class="Symbol">(</a><a id="1375" href="Effect.Monad.Writer.Indexed.html#1375" class="Bound">wâ‚‚</a> <a id="1378" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1380" href="Effect.Monad.Writer.Indexed.html#1380" class="Bound">x</a><a id="1381" class="Symbol">)</a> <a id="1383" class="Symbol">=</a> <a id="1385" href="Effect.Monad.Writer.Indexed.html#1366" class="Bound">wâ‚</a> <a id="1388" href="Algebra.Bundles.Raw.html#1226" class="Field Operator">âˆ™</a> <a id="1390" href="Effect.Monad.Writer.Indexed.html#1375" class="Bound">wâ‚‚</a> <a id="1393" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="1395" href="Effect.Monad.Writer.Indexed.html#1371" class="Bound">f</a> <a id="1397" href="Effect.Monad.Writer.Indexed.html#1380" class="Bound">x</a>

  <a id="1402" href="Effect.Monad.Writer.Indexed.html#1402" class="Function">WriterTIApplicativeZero</a> <a id="1426" class="Symbol">:</a> <a id="1428" href="Effect.Applicative.Indexed.html#1809" class="Record">RawIApplicativeZero</a> <a id="1448" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="1450" class="Symbol">â†’</a>
                            <a id="1480" href="Effect.Applicative.Indexed.html#1809" class="Record">RawIApplicativeZero</a> <a id="1500" class="Symbol">(</a><a id="1501" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="1510" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="1512" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a><a id="1513" class="Symbol">)</a>
  <a id="1517" href="Effect.Monad.Writer.Indexed.html#1402" class="Function">WriterTIApplicativeZero</a> <a id="1541" href="Effect.Monad.Writer.Indexed.html#1541" class="Bound">App</a> <a id="1545" class="Symbol">=</a> <a id="1547" class="Keyword">record</a>
    <a id="1558" class="Symbol">{</a> <a id="1560" href="Effect.Applicative.Indexed.html#1906" class="Field">applicative</a> <a id="1572" class="Symbol">=</a> <a id="1574" href="Effect.Monad.Writer.Indexed.html#1095" class="Function">WriterTIApplicative</a> <a id="1594" href="Effect.Applicative.Indexed.html#1906" class="Field">applicative</a>
    <a id="1610" class="Symbol">;</a> <a id="1612" href="Effect.Applicative.Indexed.html#1942" class="Field">âˆ…</a> <a id="1614" class="Symbol">=</a> <a id="1616" href="Effect.Applicative.Indexed.html#1942" class="Field">âˆ…</a>
    <a id="1622" class="Symbol">}</a> <a id="1624" class="Keyword">where</a> <a id="1630" class="Keyword">open</a> <a id="1635" href="Effect.Applicative.Indexed.html#1809" class="Module">RawIApplicativeZero</a> <a id="1655" href="Effect.Monad.Writer.Indexed.html#1541" class="Bound">App</a>

  <a id="1662" href="Effect.Monad.Writer.Indexed.html#1662" class="Function">WriterTIAlternative</a> <a id="1682" class="Symbol">:</a> <a id="1684" href="Effect.Applicative.Indexed.html#2146" class="Record">RawIAlternative</a> <a id="1700" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="1702" class="Symbol">â†’</a> <a id="1704" href="Effect.Applicative.Indexed.html#2146" class="Record">RawIAlternative</a> <a id="1720" class="Symbol">(</a><a id="1721" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="1730" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="1732" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a><a id="1733" class="Symbol">)</a>
  <a id="1737" href="Effect.Monad.Writer.Indexed.html#1662" class="Function">WriterTIAlternative</a> <a id="1757" href="Effect.Monad.Writer.Indexed.html#1757" class="Bound">Alt</a> <a id="1761" class="Symbol">=</a> <a id="1763" class="Keyword">record</a>
    <a id="1774" class="Symbol">{</a> <a id="1776" href="Effect.Applicative.Indexed.html#2254" class="Field">applicativeZero</a> <a id="1792" class="Symbol">=</a> <a id="1794" href="Effect.Monad.Writer.Indexed.html#1402" class="Function">WriterTIApplicativeZero</a> <a id="1818" href="Effect.Applicative.Indexed.html#2254" class="Field">applicativeZero</a>
    <a id="1838" class="Symbol">;</a> <a id="1840" href="Effect.Applicative.Indexed.html#2298" class="Field Operator">_âˆ£_</a> <a id="1844" class="Symbol">=</a> <a id="1846" href="Effect.Applicative.Indexed.html#2298" class="Field Operator">_âˆ£_</a>
    <a id="1854" class="Symbol">}</a> <a id="1856" class="Keyword">where</a> <a id="1862" class="Keyword">open</a> <a id="1867" href="Effect.Applicative.Indexed.html#2146" class="Module">RawIAlternative</a> <a id="1883" href="Effect.Monad.Writer.Indexed.html#1757" class="Bound">Alt</a>

  <a id="1890" class="Comment">------------------------------------------------------------------------</a>
  <a id="1965" class="Comment">-- Indexed writer monad</a>

  <a id="1992" href="Effect.Monad.Writer.Indexed.html#1992" class="Function">WriterTIMonad</a> <a id="2006" class="Symbol">:</a> <a id="2008" href="Effect.Monad.Indexed.html#520" class="Record">RawIMonad</a> <a id="2018" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="2020" class="Symbol">â†’</a> <a id="2022" href="Effect.Monad.Indexed.html#520" class="Record">RawIMonad</a> <a id="2032" class="Symbol">(</a><a id="2033" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="2042" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="2044" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a><a id="2045" class="Symbol">)</a>
  <a id="2049" href="Effect.Monad.Writer.Indexed.html#1992" class="Function">WriterTIMonad</a> <a id="2063" href="Effect.Monad.Writer.Indexed.html#2063" class="Bound">Mon</a> <a id="2067" class="Symbol">=</a> <a id="2069" class="Keyword">record</a>
    <a id="2080" class="Symbol">{</a> <a id="2082" href="Effect.Monad.Indexed.html#645" class="Field">return</a> <a id="2089" class="Symbol">=</a> <a id="2091" class="Symbol">Î»</a> <a id="2093" href="Effect.Monad.Writer.Indexed.html#2093" class="Bound">x</a> <a id="2095" class="Symbol">â†’</a> <a id="2097" href="Effect.Monad.Indexed.html#645" class="Field">return</a> <a id="2104" class="Symbol">(</a><a id="2105" href="Algebra.Bundles.Raw.html#1252" class="Field">Îµ</a> <a id="2107" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2109" href="Effect.Monad.Writer.Indexed.html#2093" class="Bound">x</a><a id="2110" class="Symbol">)</a>
    <a id="2116" class="Symbol">;</a> <a id="2118" href="Effect.Monad.Indexed.html#678" class="Field Operator">_&gt;&gt;=_</a> <a id="2124" class="Symbol">=</a> <a id="2126" class="Symbol">Î»</a> <a id="2128" href="Effect.Monad.Writer.Indexed.html#2128" class="Bound">m</a> <a id="2130" href="Effect.Monad.Writer.Indexed.html#2130" class="Bound">f</a> <a id="2132" class="Symbol">â†’</a> <a id="2134" class="Keyword">do</a>
        <a id="2145" href="Effect.Monad.Writer.Indexed.html#2145" class="Bound">wâ‚</a> <a id="2148" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2150" href="Effect.Monad.Writer.Indexed.html#2150" class="Bound">x</a>  <a id="2153" href="Effect.Monad.Indexed.html#678" class="Field Operator">â†</a> <a id="2155" href="Effect.Monad.Writer.Indexed.html#2128" class="Bound">m</a>
        <a id="2165" href="Effect.Monad.Writer.Indexed.html#2165" class="Bound">wâ‚‚</a> <a id="2168" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2170" href="Effect.Monad.Writer.Indexed.html#2170" class="Bound">fx</a> <a id="2173" href="Effect.Monad.Indexed.html#678" class="Field Operator">â†</a> <a id="2175" href="Effect.Monad.Writer.Indexed.html#2130" class="Bound">f</a> <a id="2177" href="Effect.Monad.Writer.Indexed.html#2150" class="Bound">x</a>
        <a id="2187" href="Effect.Monad.Indexed.html#645" class="Field">return</a> <a id="2194" class="Symbol">(</a><a id="2195" href="Effect.Monad.Writer.Indexed.html#2145" class="Bound">wâ‚</a> <a id="2198" href="Algebra.Bundles.Raw.html#1226" class="Field Operator">âˆ™</a> <a id="2200" href="Effect.Monad.Writer.Indexed.html#2165" class="Bound">wâ‚‚</a> <a id="2203" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="2205" href="Effect.Monad.Writer.Indexed.html#2170" class="Bound">fx</a><a id="2207" class="Symbol">)</a>
    <a id="2213" class="Symbol">}</a> <a id="2215" class="Keyword">where</a> <a id="2221" class="Keyword">open</a> <a id="2226" href="Effect.Monad.Indexed.html#520" class="Module">RawIMonad</a> <a id="2236" href="Effect.Monad.Writer.Indexed.html#2063" class="Bound">Mon</a>

  <a id="2243" href="Effect.Monad.Writer.Indexed.html#2243" class="Function">WriterTIMonadZero</a> <a id="2261" class="Symbol">:</a> <a id="2263" href="Effect.Monad.Indexed.html#1471" class="Record">RawIMonadZero</a> <a id="2277" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="2279" class="Symbol">â†’</a> <a id="2281" href="Effect.Monad.Indexed.html#1471" class="Record">RawIMonadZero</a> <a id="2295" class="Symbol">(</a><a id="2296" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="2305" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="2307" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a><a id="2308" class="Symbol">)</a>
  <a id="2312" href="Effect.Monad.Writer.Indexed.html#2243" class="Function">WriterTIMonadZero</a> <a id="2330" href="Effect.Monad.Writer.Indexed.html#2330" class="Bound">Mon</a> <a id="2334" class="Symbol">=</a> <a id="2336" class="Keyword">record</a>
    <a id="2347" class="Symbol">{</a> <a id="2349" href="Effect.Monad.Indexed.html#1548" class="Field">monad</a> <a id="2355" class="Symbol">=</a> <a id="2357" href="Effect.Monad.Writer.Indexed.html#1992" class="Function">WriterTIMonad</a> <a id="2371" href="Effect.Monad.Indexed.html#1548" class="Field">monad</a>
    <a id="2381" class="Symbol">;</a> <a id="2383" href="Effect.Monad.Indexed.html#1582" class="Field">applicativeZero</a> <a id="2399" class="Symbol">=</a> <a id="2401" href="Effect.Monad.Writer.Indexed.html#1402" class="Function">WriterTIApplicativeZero</a> <a id="2425" href="Effect.Monad.Indexed.html#1582" class="Field">applicativeZero</a>
    <a id="2445" class="Symbol">}</a> <a id="2447" class="Keyword">where</a> <a id="2453" class="Keyword">open</a> <a id="2458" href="Effect.Monad.Indexed.html#1471" class="Module">RawIMonadZero</a> <a id="2472" href="Effect.Monad.Writer.Indexed.html#2330" class="Bound">Mon</a>

  <a id="2479" href="Effect.Monad.Writer.Indexed.html#2479" class="Function">WriterTIMonadPlus</a> <a id="2497" class="Symbol">:</a> <a id="2499" href="Effect.Monad.Indexed.html#1721" class="Record">RawIMonadPlus</a> <a id="2513" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a> <a id="2515" class="Symbol">â†’</a> <a id="2517" href="Effect.Monad.Indexed.html#1721" class="Record">RawIMonadPlus</a> <a id="2531" class="Symbol">(</a><a id="2532" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="2541" href="Effect.Monad.Writer.Indexed.html#915" class="Bound">ğ•</a> <a id="2543" href="Effect.Monad.Writer.Indexed.html#894" class="Bound">M</a><a id="2544" class="Symbol">)</a>
  <a id="2548" href="Effect.Monad.Writer.Indexed.html#2479" class="Function">WriterTIMonadPlus</a> <a id="2566" href="Effect.Monad.Writer.Indexed.html#2566" class="Bound">Mon</a> <a id="2570" class="Symbol">=</a> <a id="2572" class="Keyword">record</a>
    <a id="2583" class="Symbol">{</a> <a id="2585" href="Effect.Monad.Indexed.html#1798" class="Field">monad</a> <a id="2591" class="Symbol">=</a> <a id="2593" href="Effect.Monad.Writer.Indexed.html#1992" class="Function">WriterTIMonad</a> <a id="2607" href="Effect.Monad.Indexed.html#1798" class="Field">monad</a>
    <a id="2617" class="Symbol">;</a> <a id="2619" href="Effect.Monad.Indexed.html#1828" class="Field">alternative</a> <a id="2631" class="Symbol">=</a> <a id="2633" href="Effect.Monad.Writer.Indexed.html#1662" class="Function">WriterTIAlternative</a> <a id="2653" href="Effect.Monad.Indexed.html#1828" class="Field">alternative</a>
    <a id="2669" class="Symbol">}</a> <a id="2671" class="Keyword">where</a> <a id="2677" class="Keyword">open</a> <a id="2682" href="Effect.Monad.Indexed.html#1721" class="Module">RawIMonadPlus</a> <a id="2696" href="Effect.Monad.Writer.Indexed.html#2566" class="Bound">Mon</a>

<a id="2701" class="Comment">------------------------------------------------------------------------</a>
<a id="2774" class="Comment">-- Writer monad operations</a>

<a id="2802" class="Keyword">record</a> <a id="RawIMonadWriter"></a><a id="2809" href="Effect.Monad.Writer.Indexed.html#2809" class="Record">RawIMonadWriter</a> <a id="2825" class="Symbol">{</a><a id="2826" href="Effect.Monad.Writer.Indexed.html#2826" class="Bound">I</a> <a id="2828" class="Symbol">:</a> <a id="2830" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="2834" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a><a id="2835" class="Symbol">}</a> <a id="2837" class="Symbol">(</a><a id="2838" href="Effect.Monad.Writer.Indexed.html#2838" class="Bound">ğ•</a> <a id="2840" class="Symbol">:</a> <a id="2842" href="Algebra.Bundles.Raw.html#1096" class="Record">RawMonoid</a> <a id="2852" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="2854" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a><a id="2855" class="Symbol">)</a> <a id="2857" class="Symbol">(</a><a id="2858" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="2860" class="Symbol">:</a> <a id="2862" href="Effect.Applicative.Indexed.html#657" class="Function">IFun</a> <a id="2867" href="Effect.Monad.Writer.Indexed.html#2826" class="Bound">I</a> <a id="2869" class="Symbol">(</a><a id="2870" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="2872" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="2874" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a><a id="2875" class="Symbol">))</a>
                       <a id="2901" class="Symbol">:</a> <a id="2903" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="2907" class="Symbol">(</a><a id="2908" href="Effect.Monad.Writer.Indexed.html#2834" class="Bound">â„“</a> <a id="2910" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="2912" href="Agda.Primitive.html#780" class="Primitive">suc</a> <a id="2916" class="Symbol">(</a><a id="2917" href="Effect.Monad.Writer.Indexed.html#2852" class="Bound">w</a> <a id="2919" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="2921" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a><a id="2922" class="Symbol">))</a> <a id="2925" class="Keyword">where</a>
  <a id="2933" class="Keyword">open</a> <a id="2938" href="Algebra.Bundles.Raw.html#1096" class="Module">RawMonoid</a> <a id="2948" href="Effect.Monad.Writer.Indexed.html#2838" class="Bound">ğ•</a> <a id="2950" class="Keyword">renaming</a> <a id="2959" class="Symbol">(</a><a id="2960" href="Algebra.Bundles.Raw.html#1178" class="Field">Carrier</a> <a id="2968" class="Symbol">to</a> <a id="2971" class="Field">W</a><a id="2972" class="Symbol">)</a>
  <a id="2976" class="Keyword">field</a>
    <a id="RawIMonadWriter.monad"></a><a id="2986" href="Effect.Monad.Writer.Indexed.html#2986" class="Field">monad</a>  <a id="2993" class="Symbol">:</a> <a id="2995" href="Effect.Monad.Indexed.html#520" class="Record">RawIMonad</a> <a id="3005" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a>
    <a id="RawIMonadWriter.writer"></a><a id="3011" href="Effect.Monad.Writer.Indexed.html#3011" class="Field">writer</a> <a id="3018" class="Symbol">:</a> <a id="3020" class="Symbol">âˆ€</a> <a id="3022" class="Symbol">{</a><a id="3023" href="Effect.Monad.Writer.Indexed.html#3023" class="Bound">i</a><a id="3024" class="Symbol">}</a> <a id="3026" class="Symbol">â†’</a> <a id="3028" class="Symbol">(</a><a id="3029" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a> <a id="3031" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="3033" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a><a id="3034" class="Symbol">)</a> <a id="3036" class="Symbol">â†’</a> <a id="3038" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3040" href="Effect.Monad.Writer.Indexed.html#3023" class="Bound">i</a> <a id="3042" href="Effect.Monad.Writer.Indexed.html#3023" class="Bound">i</a> <a id="3044" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a>
    <a id="RawIMonadWriter.listen"></a><a id="3050" href="Effect.Monad.Writer.Indexed.html#3050" class="Field">listen</a> <a id="3057" class="Symbol">:</a> <a id="3059" class="Symbol">âˆ€</a> <a id="3061" class="Symbol">{</a><a id="3062" href="Effect.Monad.Writer.Indexed.html#3062" class="Bound">i</a> <a id="3064" href="Effect.Monad.Writer.Indexed.html#3064" class="Bound">j</a><a id="3065" class="Symbol">}</a> <a id="3067" class="Symbol">â†’</a> <a id="3069" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3071" href="Effect.Monad.Writer.Indexed.html#3062" class="Bound">i</a> <a id="3073" href="Effect.Monad.Writer.Indexed.html#3064" class="Bound">j</a> <a id="3075" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a> <a id="3077" class="Symbol">â†’</a> <a id="3079" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3081" href="Effect.Monad.Writer.Indexed.html#3062" class="Bound">i</a> <a id="3083" href="Effect.Monad.Writer.Indexed.html#3064" class="Bound">j</a> <a id="3085" class="Symbol">(</a><a id="3086" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a> <a id="3088" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="3090" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a><a id="3091" class="Symbol">)</a>
    <a id="RawIMonadWriter.pass"></a><a id="3097" href="Effect.Monad.Writer.Indexed.html#3097" class="Field">pass</a>   <a id="3104" class="Symbol">:</a> <a id="3106" class="Symbol">âˆ€</a> <a id="3108" class="Symbol">{</a><a id="3109" href="Effect.Monad.Writer.Indexed.html#3109" class="Bound">i</a> <a id="3111" href="Effect.Monad.Writer.Indexed.html#3111" class="Bound">j</a><a id="3112" class="Symbol">}</a> <a id="3114" class="Symbol">â†’</a> <a id="3116" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3118" href="Effect.Monad.Writer.Indexed.html#3109" class="Bound">i</a> <a id="3120" href="Effect.Monad.Writer.Indexed.html#3111" class="Bound">j</a> <a id="3122" class="Symbol">((</a><a id="3124" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a> <a id="3126" class="Symbol">â†’</a> <a id="3128" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a><a id="3129" class="Symbol">)</a> <a id="3131" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="3133" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a><a id="3134" class="Symbol">)</a> <a id="3136" class="Symbol">â†’</a> <a id="3138" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3140" href="Effect.Monad.Writer.Indexed.html#3109" class="Bound">i</a> <a id="3142" href="Effect.Monad.Writer.Indexed.html#3111" class="Bound">j</a> <a id="3144" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a>

  <a id="3149" class="Keyword">open</a> <a id="3154" href="Effect.Monad.Indexed.html#520" class="Module">RawIMonad</a> <a id="3164" href="Effect.Monad.Writer.Indexed.html#2986" class="Field">monad</a> <a id="3170" class="Keyword">public</a>

  <a id="RawIMonadWriter.tell"></a><a id="3180" href="Effect.Monad.Writer.Indexed.html#3180" class="Function">tell</a> <a id="3185" class="Symbol">:</a> <a id="3187" class="Symbol">âˆ€</a> <a id="3189" class="Symbol">{</a><a id="3190" href="Effect.Monad.Writer.Indexed.html#3190" class="Bound">i</a><a id="3191" class="Symbol">}</a> <a id="3193" class="Symbol">â†’</a> <a id="3195" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a> <a id="3197" class="Symbol">â†’</a> <a id="3199" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3201" href="Effect.Monad.Writer.Indexed.html#3190" class="Bound">i</a> <a id="3203" href="Effect.Monad.Writer.Indexed.html#3190" class="Bound">i</a> <a id="3205" href="Data.Unit.Polymorphic.Base.html#480" class="Function">âŠ¤</a>
  <a id="3209" href="Effect.Monad.Writer.Indexed.html#3180" class="Function">tell</a> <a id="3214" class="Symbol">=</a> <a id="3216" href="Effect.Monad.Writer.Indexed.html#3011" class="Field">writer</a> <a id="3223" href="Function.Base.html#3637" class="Function Operator">âˆ˜â€²</a> <a id="3226" class="Symbol">(</a><a id="3227" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,</a> <a id="3230" href="Data.Unit.Polymorphic.Base.html#524" class="Function">tt</a><a id="3232" class="Symbol">)</a>

  <a id="RawIMonadWriter.listens"></a><a id="3237" href="Effect.Monad.Writer.Indexed.html#3237" class="Function">listens</a> <a id="3245" class="Symbol">:</a> <a id="3247" class="Symbol">âˆ€</a> <a id="3249" class="Symbol">{</a><a id="3250" href="Effect.Monad.Writer.Indexed.html#3250" class="Bound">i</a> <a id="3252" href="Effect.Monad.Writer.Indexed.html#3252" class="Bound">j</a><a id="3253" class="Symbol">}</a> <a id="3255" class="Symbol">{</a><a id="3256" href="Effect.Monad.Writer.Indexed.html#3256" class="Bound">Z</a> <a id="3258" class="Symbol">:</a> <a id="3260" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="3264" href="Effect.Monad.Writer.Indexed.html#2852" class="Bound">w</a><a id="3265" class="Symbol">}</a> <a id="3267" class="Symbol">â†’</a> <a id="3269" class="Symbol">(</a><a id="3270" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a> <a id="3272" class="Symbol">â†’</a> <a id="3274" href="Effect.Monad.Writer.Indexed.html#3256" class="Bound">Z</a><a id="3275" class="Symbol">)</a> <a id="3277" class="Symbol">â†’</a> <a id="3279" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3281" href="Effect.Monad.Writer.Indexed.html#3250" class="Bound">i</a> <a id="3283" href="Effect.Monad.Writer.Indexed.html#3252" class="Bound">j</a> <a id="3285" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a> <a id="3287" class="Symbol">â†’</a> <a id="3289" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3291" href="Effect.Monad.Writer.Indexed.html#3250" class="Bound">i</a> <a id="3293" href="Effect.Monad.Writer.Indexed.html#3252" class="Bound">j</a> <a id="3295" class="Symbol">(</a><a id="3296" href="Effect.Monad.Writer.Indexed.html#3256" class="Bound">Z</a> <a id="3298" href="Data.Product.Base.html#1109" class="Function Operator">Ã—</a> <a id="3300" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a><a id="3301" class="Symbol">)</a>
  <a id="3305" href="Effect.Monad.Writer.Indexed.html#3237" class="Function">listens</a> <a id="3313" href="Effect.Monad.Writer.Indexed.html#3313" class="Bound">f</a> <a id="3315" href="Effect.Monad.Writer.Indexed.html#3315" class="Bound">m</a> <a id="3317" class="Symbol">=</a> <a id="3319" href="Effect.Monad.Writer.Indexed.html#3050" class="Field">listen</a> <a id="3326" href="Effect.Monad.Writer.Indexed.html#3315" class="Bound">m</a> <a id="3328" href="Effect.Monad.Indexed.html#678" class="Function Operator">&gt;&gt;=</a> <a id="3332" href="Effect.Monad.Indexed.html#645" class="Function">return</a> <a id="3339" href="Function.Base.html#3637" class="Function Operator">âˆ˜â€²</a> <a id="3342" href="Data.Product.Base.html#1803" class="Function">mapâ‚</a> <a id="3347" href="Effect.Monad.Writer.Indexed.html#3313" class="Bound">f</a>

  <a id="RawIMonadWriter.censor"></a><a id="3352" href="Effect.Monad.Writer.Indexed.html#3352" class="Function">censor</a> <a id="3359" class="Symbol">:</a> <a id="3361" class="Symbol">âˆ€</a> <a id="3363" class="Symbol">{</a><a id="3364" href="Effect.Monad.Writer.Indexed.html#3364" class="Bound">i</a> <a id="3366" href="Effect.Monad.Writer.Indexed.html#3366" class="Bound">j</a><a id="3367" class="Symbol">}</a> <a id="3369" class="Symbol">â†’</a> <a id="3371" class="Symbol">(</a><a id="3372" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a> <a id="3374" class="Symbol">â†’</a> <a id="3376" href="Effect.Monad.Writer.Indexed.html#2971" class="Function">W</a><a id="3377" class="Symbol">)</a> <a id="3379" class="Symbol">â†’</a> <a id="3381" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3383" href="Effect.Monad.Writer.Indexed.html#3364" class="Bound">i</a> <a id="3385" href="Effect.Monad.Writer.Indexed.html#3366" class="Bound">j</a> <a id="3387" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a> <a id="3389" class="Symbol">â†’</a> <a id="3391" href="Effect.Monad.Writer.Indexed.html#2858" class="Bound">M</a> <a id="3393" href="Effect.Monad.Writer.Indexed.html#3364" class="Bound">i</a> <a id="3395" href="Effect.Monad.Writer.Indexed.html#3366" class="Bound">j</a> <a id="3397" href="Effect.Monad.Writer.Indexed.html#658" class="Generalizable">A</a>
  <a id="3401" href="Effect.Monad.Writer.Indexed.html#3352" class="Function">censor</a> <a id="3408" href="Effect.Monad.Writer.Indexed.html#3408" class="Bound">f</a> <a id="3410" href="Effect.Monad.Writer.Indexed.html#3410" class="Bound">m</a> <a id="3412" class="Symbol">=</a> <a id="3414" href="Effect.Monad.Writer.Indexed.html#3097" class="Field">pass</a> <a id="3419" class="Symbol">(</a><a id="3420" href="Effect.Monad.Writer.Indexed.html#3410" class="Bound">m</a> <a id="3422" href="Effect.Monad.Indexed.html#678" class="Function Operator">&gt;&gt;=</a> <a id="3426" href="Effect.Monad.Indexed.html#645" class="Function">return</a> <a id="3433" href="Function.Base.html#3637" class="Function Operator">âˆ˜â€²</a> <a id="3436" class="Symbol">(</a><a id="3437" href="Effect.Monad.Writer.Indexed.html#3408" class="Bound">f</a> <a id="3439" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,_</a><a id="3441" class="Symbol">))</a>


<a id="WriterTIMonadWriter"></a><a id="3446" href="Effect.Monad.Writer.Indexed.html#3446" class="Function">WriterTIMonadWriter</a> <a id="3466" class="Symbol">:</a> <a id="3468" class="Symbol">{</a><a id="3469" href="Effect.Monad.Writer.Indexed.html#3469" class="Bound">I</a> <a id="3471" class="Symbol">:</a> <a id="3473" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="3477" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a><a id="3478" class="Symbol">}</a> <a id="3480" class="Symbol">{</a><a id="3481" href="Effect.Monad.Writer.Indexed.html#3481" class="Bound">ğ•</a> <a id="3483" class="Symbol">:</a> <a id="3485" href="Algebra.Bundles.Raw.html#1096" class="Record">RawMonoid</a> <a id="3495" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="3497" href="Effect.Monad.Writer.Indexed.html#644" class="Generalizable">â„“</a><a id="3498" class="Symbol">}</a> <a id="3500" class="Symbol">{</a><a id="3501" href="Effect.Monad.Writer.Indexed.html#3501" class="Bound">M</a> <a id="3503" class="Symbol">:</a> <a id="3505" href="Effect.Applicative.Indexed.html#657" class="Function">IFun</a> <a id="3510" href="Effect.Monad.Writer.Indexed.html#3469" class="Bound">I</a> <a id="3512" class="Symbol">(</a><a id="3513" href="Effect.Monad.Writer.Indexed.html#642" class="Generalizable">w</a> <a id="3515" href="Agda.Primitive.html#810" class="Primitive Operator">âŠ”</a> <a id="3517" href="Effect.Monad.Writer.Indexed.html#299" class="Bound">a</a><a id="3518" class="Symbol">)}</a> <a id="3521" class="Symbol">â†’</a>
                      <a id="3545" href="Effect.Monad.Indexed.html#520" class="Record">RawIMonad</a> <a id="3555" href="Effect.Monad.Writer.Indexed.html#3501" class="Bound">M</a> <a id="3557" class="Symbol">â†’</a> <a id="3559" href="Effect.Monad.Writer.Indexed.html#2809" class="Record">RawIMonadWriter</a> <a id="3575" href="Effect.Monad.Writer.Indexed.html#3481" class="Bound">ğ•</a> <a id="3577" class="Symbol">(</a><a id="3578" href="Effect.Monad.Writer.Indexed.html#765" class="Function">IWriterT</a> <a id="3587" href="Effect.Monad.Writer.Indexed.html#3481" class="Bound">ğ•</a> <a id="3589" href="Effect.Monad.Writer.Indexed.html#3501" class="Bound">M</a><a id="3590" class="Symbol">)</a>
<a id="3592" href="Effect.Monad.Writer.Indexed.html#3446" class="Function">WriterTIMonadWriter</a> <a id="3612" class="Symbol">{</a><a id="3613" class="Argument">ğ•</a> <a id="3615" class="Symbol">=</a> <a id="3617" href="Effect.Monad.Writer.Indexed.html#3617" class="Bound">ğ•</a><a id="3618" class="Symbol">}</a> <a id="3620" href="Effect.Monad.Writer.Indexed.html#3620" class="Bound">Mon</a> <a id="3624" class="Symbol">=</a> <a id="3626" class="Keyword">record</a>
  <a id="3635" class="Symbol">{</a> <a id="3637" href="Effect.Monad.Writer.Indexed.html#2986" class="Field">monad</a> <a id="3643" class="Symbol">=</a> <a id="3645" href="Effect.Monad.Writer.Indexed.html#1992" class="Function">WriterTIMonad</a> <a id="3659" class="Symbol">{</a><a id="3660" class="Argument">ğ•</a> <a id="3662" class="Symbol">=</a> <a id="3664" href="Effect.Monad.Writer.Indexed.html#3617" class="Bound">ğ•</a><a id="3665" class="Symbol">}</a> <a id="3667" href="Effect.Monad.Writer.Indexed.html#3620" class="Bound">Mon</a>
  <a id="3673" class="Symbol">;</a> <a id="3675" href="Effect.Monad.Writer.Indexed.html#3011" class="Field">writer</a> <a id="3682" class="Symbol">=</a> <a id="3684" href="Effect.Monad.Indexed.html#645" class="Field">return</a>
  <a id="3693" class="Symbol">;</a> <a id="3695" href="Effect.Monad.Writer.Indexed.html#3050" class="Field">listen</a> <a id="3702" class="Symbol">=</a> <a id="3704" class="Symbol">Î»</a> <a id="3706" href="Effect.Monad.Writer.Indexed.html#3706" class="Bound">m</a> <a id="3708" class="Symbol">â†’</a> <a id="3710" class="Keyword">do</a>
      <a id="3719" href="Effect.Monad.Writer.Indexed.html#3719" class="Bound">w</a> <a id="3721" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="3723" href="Effect.Monad.Writer.Indexed.html#3723" class="Bound">a</a> <a id="3725" href="Effect.Monad.Indexed.html#678" class="Field Operator">â†</a> <a id="3727" href="Effect.Monad.Writer.Indexed.html#3706" class="Bound">m</a>
      <a id="3735" href="Effect.Monad.Indexed.html#645" class="Field">return</a> <a id="3742" class="Symbol">(</a><a id="3743" href="Effect.Monad.Writer.Indexed.html#3719" class="Bound">w</a> <a id="3745" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="3747" href="Effect.Monad.Writer.Indexed.html#3719" class="Bound">w</a> <a id="3749" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="3751" href="Effect.Monad.Writer.Indexed.html#3723" class="Bound">a</a><a id="3752" class="Symbol">)</a>
  <a id="3756" class="Symbol">;</a> <a id="3758" href="Effect.Monad.Writer.Indexed.html#3097" class="Field">pass</a> <a id="3763" class="Symbol">=</a> <a id="3765" class="Symbol">Î»</a> <a id="3767" href="Effect.Monad.Writer.Indexed.html#3767" class="Bound">m</a> <a id="3769" class="Symbol">â†’</a> <a id="3771" class="Keyword">do</a>
      <a id="3780" href="Effect.Monad.Writer.Indexed.html#3780" class="Bound">w</a> <a id="3782" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="3784" href="Effect.Monad.Writer.Indexed.html#3784" class="Bound">f</a> <a id="3786" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="3788" href="Effect.Monad.Writer.Indexed.html#3788" class="Bound">a</a> <a id="3790" href="Effect.Monad.Indexed.html#678" class="Field Operator">â†</a> <a id="3792" href="Effect.Monad.Writer.Indexed.html#3767" class="Bound">m</a>
      <a id="3800" href="Effect.Monad.Indexed.html#645" class="Field">return</a> <a id="3807" class="Symbol">(</a><a id="3808" href="Effect.Monad.Writer.Indexed.html#3784" class="Bound">f</a> <a id="3810" href="Effect.Monad.Writer.Indexed.html#3780" class="Bound">w</a> <a id="3812" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">,</a> <a id="3814" href="Effect.Monad.Writer.Indexed.html#3788" class="Bound">a</a><a id="3815" class="Symbol">)</a>
  <a id="3819" class="Symbol">}</a> <a id="3821" class="Keyword">where</a> <a id="3827" class="Keyword">open</a> <a id="3832" href="Effect.Monad.Indexed.html#520" class="Module">RawIMonad</a> <a id="3842" href="Effect.Monad.Writer.Indexed.html#3620" class="Bound">Mon</a>
          <a id="3856" class="Keyword">open</a> <a id="3861" href="Algebra.Bundles.Raw.html#1096" class="Module">RawMonoid</a> <a id="3871" href="Effect.Monad.Writer.Indexed.html#3617" class="Bound">ğ•</a> <a id="3873" class="Keyword">renaming</a> <a id="3882" class="Symbol">(</a><a id="3883" href="Algebra.Bundles.Raw.html#1178" class="Field">Carrier</a> <a id="3891" class="Symbol">to</a> <a id="3894" class="Field">W</a><a id="3895" class="Symbol">)</a>
</pre></body></html>