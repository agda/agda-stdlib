<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Morphism.RelMonomorphism</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Consequences of a monomorphism between binary relations</a>
<a id="165" class="Comment">------------------------------------------------------------------------</a>

<a id="239" class="Comment">-- See Data.Nat.Binary.Properties for examples of how this and similar</a>
<a id="310" class="Comment">-- modules can be used to easily translate properties between types.</a>

<a id="380" class="Symbol">{-#</a> <a id="384" class="Keyword">OPTIONS</a> <a id="392" class="Pragma">--cubical-compatible</a> <a id="413" class="Pragma">--safe</a> <a id="420" class="Symbol">#-}</a>

<a id="425" class="Keyword">open</a> <a id="430" class="Keyword">import</a> <a id="437" href="Function.Base.html" class="Module">Function.Base</a>
<a id="451" class="Keyword">open</a> <a id="456" class="Keyword">import</a> <a id="463" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="484" class="Keyword">using</a> <a id="490" class="Symbol">(</a><a id="491" href="Relation.Binary.Core.html#896" class="Function">Rel</a><a id="494" class="Symbol">)</a>
<a id="496" class="Keyword">open</a> <a id="501" class="Keyword">import</a> <a id="508" href="Relation.Binary.Structures.html" class="Module">Relation.Binary.Structures</a> <a id="535" class="Keyword">using</a> <a id="541" class="Symbol">(</a><a id="542" href="Relation.Binary.Structures.html#1550" class="Record">IsEquivalence</a><a id="555" class="Symbol">;</a> <a id="557" href="Relation.Binary.Structures.html#1852" class="Record">IsDecEquivalence</a><a id="573" class="Symbol">)</a>
<a id="575" class="Keyword">open</a> <a id="580" class="Keyword">import</a> <a id="587" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a>
  <a id="617" class="Keyword">using</a> <a id="623" class="Symbol">(</a><a id="624" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a><a id="633" class="Symbol">;</a> <a id="635" href="Relation.Binary.Definitions.html#1587" class="Function">Symmetric</a><a id="644" class="Symbol">;</a> <a id="646" href="Relation.Binary.Definitions.html#2103" class="Function">Transitive</a><a id="656" class="Symbol">;</a> <a id="658" href="Relation.Binary.Definitions.html#2910" class="Function">Total</a><a id="663" class="Symbol">;</a> <a id="665" href="Relation.Binary.Definitions.html#2590" class="Function">Asymmetric</a><a id="675" class="Symbol">;</a> <a id="677" href="Relation.Binary.Definitions.html#6161" class="Function">Decidable</a><a id="686" class="Symbol">)</a>
<a id="688" class="Keyword">open</a> <a id="693" class="Keyword">import</a> <a id="700" href="Relation.Binary.Morphism.html" class="Module">Relation.Binary.Morphism</a>

<a id="726" class="Keyword">module</a> <a id="733" href="Relation.Binary.Morphism.RelMonomorphism.html" class="Module">Relation.Binary.Morphism.RelMonomorphism</a>
  <a id="776" class="Symbol">{</a><a id="777" href="Relation.Binary.Morphism.RelMonomorphism.html#777" class="Bound">a</a> <a id="779" href="Relation.Binary.Morphism.RelMonomorphism.html#779" class="Bound">b</a> <a id="781" href="Relation.Binary.Morphism.RelMonomorphism.html#781" class="Bound">ℓ₁</a> <a id="784" href="Relation.Binary.Morphism.RelMonomorphism.html#784" class="Bound">ℓ₂</a><a id="786" class="Symbol">}</a> <a id="788" class="Symbol">{</a><a id="789" href="Relation.Binary.Morphism.RelMonomorphism.html#789" class="Bound">A</a> <a id="791" class="Symbol">:</a> <a id="793" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="797" href="Relation.Binary.Morphism.RelMonomorphism.html#777" class="Bound">a</a><a id="798" class="Symbol">}</a> <a id="800" class="Symbol">{</a><a id="801" href="Relation.Binary.Morphism.RelMonomorphism.html#801" class="Bound">B</a> <a id="803" class="Symbol">:</a> <a id="805" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="809" href="Relation.Binary.Morphism.RelMonomorphism.html#779" class="Bound">b</a><a id="810" class="Symbol">}</a>
  <a id="814" class="Symbol">{</a><a id="815" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a> <a id="820" class="Symbol">:</a> <a id="822" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="826" href="Relation.Binary.Morphism.RelMonomorphism.html#789" class="Bound">A</a> <a id="828" href="Relation.Binary.Morphism.RelMonomorphism.html#781" class="Bound">ℓ₁</a><a id="830" class="Symbol">}</a> <a id="832" class="Symbol">{</a><a id="833" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="838" class="Symbol">:</a> <a id="840" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="844" href="Relation.Binary.Morphism.RelMonomorphism.html#801" class="Bound">B</a> <a id="846" href="Relation.Binary.Morphism.RelMonomorphism.html#784" class="Bound">ℓ₂</a><a id="848" class="Symbol">}</a>
  <a id="852" class="Symbol">{</a><a id="853" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟦_⟧</a> <a id="857" class="Symbol">:</a> <a id="859" href="Relation.Binary.Morphism.RelMonomorphism.html#789" class="Bound">A</a> <a id="861" class="Symbol">→</a> <a id="863" href="Relation.Binary.Morphism.RelMonomorphism.html#801" class="Bound">B</a><a id="864" class="Symbol">}</a> <a id="866" class="Symbol">(</a><a id="867" href="Relation.Binary.Morphism.RelMonomorphism.html#867" class="Bound">isMonomorphism</a> <a id="882" class="Symbol">:</a> <a id="884" href="Relation.Binary.Morphism.Structures.html#925" class="Record">IsRelMonomorphism</a> <a id="902" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a> <a id="907" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="912" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟦_⟧</a><a id="915" class="Symbol">)</a>
  <a id="919" class="Keyword">where</a>

<a id="926" class="Keyword">open</a> <a id="931" class="Keyword">import</a> <a id="938" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="952" class="Symbol">as</a> <a id="955" class="Module">Sum</a>
<a id="959" class="Keyword">open</a> <a id="964" class="Keyword">import</a> <a id="971" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="998" class="Keyword">using</a> <a id="1004" class="Symbol">(</a><a id="1005" href="Relation.Nullary.Decidable.Core.html#1618" class="InductiveConstructor">yes</a><a id="1008" class="Symbol">;</a> <a id="1010" href="Relation.Nullary.Decidable.Core.html#1655" class="InductiveConstructor">no</a><a id="1012" class="Symbol">)</a>
<a id="1014" class="Keyword">open</a> <a id="1019" class="Keyword">import</a> <a id="1026" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a>

<a id="1054" class="Keyword">open</a> <a id="1059" href="Relation.Binary.Morphism.Structures.html#925" class="Module">IsRelMonomorphism</a> <a id="1077" href="Relation.Binary.Morphism.RelMonomorphism.html#867" class="Bound">isMonomorphism</a>

<a id="1093" class="Comment">------------------------------------------------------------------------</a>
<a id="1166" class="Comment">-- Properties</a>

<a id="refl"></a><a id="1181" href="Relation.Binary.Morphism.RelMonomorphism.html#1181" class="Function">refl</a> <a id="1186" class="Symbol">:</a> <a id="1188" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a> <a id="1198" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1203" class="Symbol">→</a> <a id="1205" href="Relation.Binary.Definitions.html#1428" class="Function">Reflexive</a> <a id="1215" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1220" href="Relation.Binary.Morphism.RelMonomorphism.html#1181" class="Function">refl</a> <a id="1225" href="Relation.Binary.Morphism.RelMonomorphism.html#1225" class="Bound">refl</a> <a id="1230" class="Symbol">=</a> <a id="1232" href="Relation.Binary.Morphism.Structures.html#1109" class="Field">injective</a> <a id="1242" href="Relation.Binary.Morphism.RelMonomorphism.html#1225" class="Bound">refl</a>

<a id="sym"></a><a id="1248" href="Relation.Binary.Morphism.RelMonomorphism.html#1248" class="Function">sym</a> <a id="1252" class="Symbol">:</a> <a id="1254" href="Relation.Binary.Definitions.html#1587" class="Function">Symmetric</a> <a id="1264" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1269" class="Symbol">→</a> <a id="1271" href="Relation.Binary.Definitions.html#1587" class="Function">Symmetric</a> <a id="1281" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1286" href="Relation.Binary.Morphism.RelMonomorphism.html#1248" class="Function">sym</a> <a id="1290" href="Relation.Binary.Morphism.RelMonomorphism.html#1290" class="Bound">sym</a> <a id="1294" href="Relation.Binary.Morphism.RelMonomorphism.html#1294" class="Bound">x∼y</a> <a id="1298" class="Symbol">=</a> <a id="1300" href="Relation.Binary.Morphism.Structures.html#1109" class="Field">injective</a> <a id="1310" class="Symbol">(</a><a id="1311" href="Relation.Binary.Morphism.RelMonomorphism.html#1290" class="Bound">sym</a> <a id="1315" class="Symbol">(</a><a id="1316" href="Relation.Binary.Morphism.Structures.html#882" class="Function">cong</a> <a id="1321" href="Relation.Binary.Morphism.RelMonomorphism.html#1294" class="Bound">x∼y</a><a id="1324" class="Symbol">))</a>

<a id="trans"></a><a id="1328" href="Relation.Binary.Morphism.RelMonomorphism.html#1328" class="Function">trans</a> <a id="1334" class="Symbol">:</a> <a id="1336" href="Relation.Binary.Definitions.html#2103" class="Function">Transitive</a> <a id="1347" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1352" class="Symbol">→</a> <a id="1354" href="Relation.Binary.Definitions.html#2103" class="Function">Transitive</a> <a id="1365" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1370" href="Relation.Binary.Morphism.RelMonomorphism.html#1328" class="Function">trans</a> <a id="1376" href="Relation.Binary.Morphism.RelMonomorphism.html#1376" class="Bound">trans</a> <a id="1382" href="Relation.Binary.Morphism.RelMonomorphism.html#1382" class="Bound">x∼y</a> <a id="1386" href="Relation.Binary.Morphism.RelMonomorphism.html#1386" class="Bound">y∼z</a> <a id="1390" class="Symbol">=</a> <a id="1392" href="Relation.Binary.Morphism.Structures.html#1109" class="Field">injective</a> <a id="1402" class="Symbol">(</a><a id="1403" href="Relation.Binary.Morphism.RelMonomorphism.html#1376" class="Bound">trans</a> <a id="1409" class="Symbol">(</a><a id="1410" href="Relation.Binary.Morphism.Structures.html#882" class="Function">cong</a> <a id="1415" href="Relation.Binary.Morphism.RelMonomorphism.html#1382" class="Bound">x∼y</a><a id="1418" class="Symbol">)</a> <a id="1420" class="Symbol">(</a><a id="1421" href="Relation.Binary.Morphism.Structures.html#882" class="Function">cong</a> <a id="1426" href="Relation.Binary.Morphism.RelMonomorphism.html#1386" class="Bound">y∼z</a><a id="1429" class="Symbol">))</a>

<a id="total"></a><a id="1433" href="Relation.Binary.Morphism.RelMonomorphism.html#1433" class="Function">total</a> <a id="1439" class="Symbol">:</a> <a id="1441" href="Relation.Binary.Definitions.html#2910" class="Function">Total</a> <a id="1447" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1452" class="Symbol">→</a> <a id="1454" href="Relation.Binary.Definitions.html#2910" class="Function">Total</a> <a id="1460" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1465" href="Relation.Binary.Morphism.RelMonomorphism.html#1433" class="Function">total</a> <a id="1471" href="Relation.Binary.Morphism.RelMonomorphism.html#1471" class="Bound">total</a> <a id="1477" href="Relation.Binary.Morphism.RelMonomorphism.html#1477" class="Bound">x</a> <a id="1479" href="Relation.Binary.Morphism.RelMonomorphism.html#1479" class="Bound">y</a> <a id="1481" class="Symbol">=</a> <a id="1483" href="Data.Sum.Base.html#1253" class="Function">Sum.map</a> <a id="1491" href="Relation.Binary.Morphism.Structures.html#1109" class="Field">injective</a> <a id="1501" href="Relation.Binary.Morphism.Structures.html#1109" class="Field">injective</a> <a id="1511" class="Symbol">(</a><a id="1512" href="Relation.Binary.Morphism.RelMonomorphism.html#1471" class="Bound">total</a> <a id="1518" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟦</a> <a id="1520" href="Relation.Binary.Morphism.RelMonomorphism.html#1477" class="Bound">x</a> <a id="1522" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟧</a> <a id="1524" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟦</a> <a id="1526" href="Relation.Binary.Morphism.RelMonomorphism.html#1479" class="Bound">y</a> <a id="1528" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟧</a><a id="1529" class="Symbol">)</a>

<a id="asym"></a><a id="1532" href="Relation.Binary.Morphism.RelMonomorphism.html#1532" class="Function">asym</a> <a id="1537" class="Symbol">:</a> <a id="1539" href="Relation.Binary.Definitions.html#2590" class="Function">Asymmetric</a> <a id="1550" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1555" class="Symbol">→</a> <a id="1557" href="Relation.Binary.Definitions.html#2590" class="Function">Asymmetric</a> <a id="1568" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1573" href="Relation.Binary.Morphism.RelMonomorphism.html#1532" class="Function">asym</a> <a id="1578" href="Relation.Binary.Morphism.RelMonomorphism.html#1578" class="Bound">asym</a> <a id="1583" href="Relation.Binary.Morphism.RelMonomorphism.html#1583" class="Bound">x∼y</a> <a id="1587" href="Relation.Binary.Morphism.RelMonomorphism.html#1587" class="Bound">y∼x</a> <a id="1591" class="Symbol">=</a> <a id="1593" href="Relation.Binary.Morphism.RelMonomorphism.html#1578" class="Bound">asym</a> <a id="1598" class="Symbol">(</a><a id="1599" href="Relation.Binary.Morphism.Structures.html#882" class="Function">cong</a> <a id="1604" href="Relation.Binary.Morphism.RelMonomorphism.html#1583" class="Bound">x∼y</a><a id="1607" class="Symbol">)</a> <a id="1609" class="Symbol">(</a><a id="1610" href="Relation.Binary.Morphism.Structures.html#882" class="Function">cong</a> <a id="1615" href="Relation.Binary.Morphism.RelMonomorphism.html#1587" class="Bound">y∼x</a><a id="1618" class="Symbol">)</a>

<a id="dec"></a><a id="1621" href="Relation.Binary.Morphism.RelMonomorphism.html#1621" class="Function">dec</a> <a id="1625" class="Symbol">:</a> <a id="1627" href="Relation.Binary.Definitions.html#6161" class="Function">Decidable</a> <a id="1637" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1642" class="Symbol">→</a> <a id="1644" href="Relation.Binary.Definitions.html#6161" class="Function">Decidable</a> <a id="1654" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1659" href="Relation.Binary.Morphism.RelMonomorphism.html#1621" class="Function">dec</a> <a id="1663" href="Relation.Binary.Morphism.RelMonomorphism.html#1663" class="Bound Operator">_∼?_</a> <a id="1668" href="Relation.Binary.Morphism.RelMonomorphism.html#1668" class="Bound">x</a> <a id="1670" href="Relation.Binary.Morphism.RelMonomorphism.html#1670" class="Bound">y</a> <a id="1672" class="Symbol">=</a> <a id="1674" href="Relation.Nullary.Decidable.Core.html#4901" class="Function">map′</a> <a id="1679" href="Relation.Binary.Morphism.Structures.html#1109" class="Field">injective</a> <a id="1689" href="Relation.Binary.Morphism.Structures.html#882" class="Function">cong</a> <a id="1694" class="Symbol">(</a><a id="1695" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟦</a> <a id="1697" href="Relation.Binary.Morphism.RelMonomorphism.html#1668" class="Bound">x</a> <a id="1699" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟧</a> <a id="1701" href="Relation.Binary.Morphism.RelMonomorphism.html#1663" class="Bound Operator">∼?</a> <a id="1704" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟦</a> <a id="1706" href="Relation.Binary.Morphism.RelMonomorphism.html#1670" class="Bound">y</a> <a id="1708" href="Relation.Binary.Morphism.RelMonomorphism.html#853" class="Bound Operator">⟧</a><a id="1709" class="Symbol">)</a>

<a id="1712" class="Comment">------------------------------------------------------------------------</a>
<a id="1785" class="Comment">-- Structures</a>

<a id="isEquivalence"></a><a id="1800" href="Relation.Binary.Morphism.RelMonomorphism.html#1800" class="Function">isEquivalence</a> <a id="1814" class="Symbol">:</a> <a id="1816" href="Relation.Binary.Structures.html#1550" class="Record">IsEquivalence</a> <a id="1830" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="1835" class="Symbol">→</a> <a id="1837" href="Relation.Binary.Structures.html#1550" class="Record">IsEquivalence</a> <a id="1851" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="1856" href="Relation.Binary.Morphism.RelMonomorphism.html#1800" class="Function">isEquivalence</a> <a id="1870" href="Relation.Binary.Morphism.RelMonomorphism.html#1870" class="Bound">isEq</a> <a id="1875" class="Symbol">=</a> <a id="1877" class="Keyword">record</a>
  <a id="1886" class="Symbol">{</a> <a id="1888" href="Relation.Binary.Structures.html#1596" class="Field">refl</a>  <a id="1894" class="Symbol">=</a> <a id="1896" href="Relation.Binary.Morphism.RelMonomorphism.html#1181" class="Function">refl</a>  <a id="1902" href="Relation.Binary.Structures.html#1596" class="Field">E.refl</a>
  <a id="1911" class="Symbol">;</a> <a id="1913" href="Relation.Binary.Structures.html#1622" class="Field">sym</a>   <a id="1919" class="Symbol">=</a> <a id="1921" href="Relation.Binary.Morphism.RelMonomorphism.html#1248" class="Function">sym</a>   <a id="1927" href="Relation.Binary.Structures.html#1622" class="Field">E.sym</a>
  <a id="1935" class="Symbol">;</a> <a id="1937" href="Relation.Binary.Structures.html#1648" class="Field">trans</a> <a id="1943" class="Symbol">=</a> <a id="1945" href="Relation.Binary.Morphism.RelMonomorphism.html#1328" class="Function">trans</a> <a id="1951" href="Relation.Binary.Structures.html#1648" class="Field">E.trans</a>
  <a id="1961" class="Symbol">}</a> <a id="1963" class="Keyword">where</a> <a id="1969" class="Keyword">module</a> <a id="1976" href="Relation.Binary.Morphism.RelMonomorphism.html#1976" class="Module">E</a> <a id="1978" class="Symbol">=</a> <a id="1980" href="Relation.Binary.Structures.html#1550" class="Module">IsEquivalence</a> <a id="1994" href="Relation.Binary.Morphism.RelMonomorphism.html#1870" class="Bound">isEq</a>

<a id="isDecEquivalence"></a><a id="2000" href="Relation.Binary.Morphism.RelMonomorphism.html#2000" class="Function">isDecEquivalence</a> <a id="2017" class="Symbol">:</a> <a id="2019" href="Relation.Binary.Structures.html#1852" class="Record">IsDecEquivalence</a> <a id="2036" href="Relation.Binary.Morphism.RelMonomorphism.html#833" class="Bound Operator">_∼₂_</a> <a id="2041" class="Symbol">→</a> <a id="2043" href="Relation.Binary.Structures.html#1852" class="Record">IsDecEquivalence</a> <a id="2060" href="Relation.Binary.Morphism.RelMonomorphism.html#815" class="Bound Operator">_∼₁_</a>
<a id="2065" href="Relation.Binary.Morphism.RelMonomorphism.html#2000" class="Function">isDecEquivalence</a> <a id="2082" href="Relation.Binary.Morphism.RelMonomorphism.html#2082" class="Bound">isDecEq</a> <a id="2090" class="Symbol">=</a> <a id="2092" class="Keyword">record</a>
  <a id="2101" class="Symbol">{</a> <a id="2103" href="Relation.Binary.Structures.html#1915" class="Field">isEquivalence</a>  <a id="2118" class="Symbol">=</a> <a id="2120" href="Relation.Binary.Morphism.RelMonomorphism.html#1800" class="Function">isEquivalence</a> <a id="2134" href="Relation.Binary.Structures.html#1915" class="Field">E.isEquivalence</a>
  <a id="2152" class="Symbol">;</a> <a id="2154" href="Relation.Binary.Structures.html#1949" class="Field Operator">_≟_</a>            <a id="2169" class="Symbol">=</a> <a id="2171" href="Relation.Binary.Morphism.RelMonomorphism.html#1621" class="Function">dec</a> <a id="2175" href="Relation.Binary.Structures.html#1949" class="Field Operator">E._≟_</a>
  <a id="2183" class="Symbol">}</a> <a id="2185" class="Keyword">where</a> <a id="2191" class="Keyword">module</a> <a id="2198" href="Relation.Binary.Morphism.RelMonomorphism.html#2198" class="Module">E</a> <a id="2200" class="Symbol">=</a> <a id="2202" href="Relation.Binary.Structures.html#1852" class="Module">IsDecEquivalence</a> <a id="2219" href="Relation.Binary.Morphism.RelMonomorphism.html#2082" class="Bound">isDecEq</a>
</pre></body></html>