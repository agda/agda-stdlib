<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Conat.Bisimilarity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Bisimilarity for Conats</a>
<a id="133" class="Comment">------------------------------------------------------------------------</a>

<a id="207" class="Symbol">{-#</a> <a id="211" class="Keyword">OPTIONS</a> <a id="219" class="Pragma">--without-K</a> <a id="231" class="Pragma">--safe</a> <a id="238" class="Pragma">--sized-types</a> <a id="252" class="Symbol">#-}</a>

<a id="257" class="Keyword">module</a> <a id="264" href="Codata.Conat.Bisimilarity.html" class="Module">Codata.Conat.Bisimilarity</a> <a id="290" class="Keyword">where</a>

<a id="297" class="Keyword">open</a> <a id="302" class="Keyword">import</a> <a id="309" href="Level.html" class="Module">Level</a> <a id="315" class="Keyword">using</a> <a id="321" class="Symbol">(</a><a id="322" href="Level.html#512" class="Function">0ℓ</a><a id="324" class="Symbol">)</a>
<a id="326" class="Keyword">open</a> <a id="331" class="Keyword">import</a> <a id="338" href="Size.html" class="Module">Size</a>
<a id="343" class="Keyword">open</a> <a id="348" class="Keyword">import</a> <a id="355" href="Codata.Thunk.html" class="Module">Codata.Thunk</a>
<a id="368" class="Keyword">open</a> <a id="373" class="Keyword">import</a> <a id="380" href="Codata.Conat.html" class="Module">Codata.Conat</a>
<a id="393" class="Keyword">open</a> <a id="398" class="Keyword">import</a> <a id="405" href="Relation.Binary.html" class="Module">Relation.Binary</a>

<a id="422" class="Keyword">infix</a> <a id="428" class="Number">1</a> <a id="430" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">_⊢_≈_</a>
<a id="436" class="Keyword">data</a> <a id="_⊢_≈_"></a><a id="441" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">_⊢_≈_</a> <a id="447" href="Codata.Conat.Bisimilarity.html#447" class="Bound">i</a> <a id="449" class="Symbol">:</a> <a id="451" class="Symbol">(</a><a id="452" href="Codata.Conat.Bisimilarity.html#452" class="Bound">m</a> <a id="454" href="Codata.Conat.Bisimilarity.html#454" class="Bound">n</a> <a id="456" class="Symbol">:</a> <a id="458" href="Codata.Conat.html#527" class="Datatype">Conat</a> <a id="464" href="Agda.Builtin.Size.html#302" class="Postulate">∞</a><a id="465" class="Symbol">)</a> <a id="467" class="Symbol">→</a> <a id="469" class="PrimitiveType">Set</a> <a id="473" class="Keyword">where</a>
  <a id="_⊢_≈_.zero"></a><a id="481" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a> <a id="486" class="Symbol">:</a> <a id="488" href="Codata.Conat.Bisimilarity.html#447" class="Bound">i</a> <a id="490" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="492" href="Codata.Conat.html#558" class="InductiveConstructor">zero</a> <a id="497" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="499" href="Codata.Conat.html#558" class="InductiveConstructor">zero</a>
  <a id="_⊢_≈_.suc"></a><a id="506" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a>  <a id="511" class="Symbol">:</a> <a id="513" class="Symbol">∀</a> <a id="515" class="Symbol">{</a><a id="516" href="Codata.Conat.Bisimilarity.html#516" class="Bound">m</a> <a id="518" href="Codata.Conat.Bisimilarity.html#518" class="Bound">n</a><a id="519" class="Symbol">}</a> <a id="521" class="Symbol">→</a> <a id="523" href="Codata.Thunk.html#749" class="Function">Thunk^R</a> <a id="531" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">_⊢_≈_</a> <a id="537" href="Codata.Conat.Bisimilarity.html#447" class="Bound">i</a> <a id="539" href="Codata.Conat.Bisimilarity.html#516" class="Bound">m</a> <a id="541" href="Codata.Conat.Bisimilarity.html#518" class="Bound">n</a> <a id="543" class="Symbol">→</a> <a id="545" href="Codata.Conat.Bisimilarity.html#447" class="Bound">i</a> <a id="547" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="549" href="Codata.Conat.html#575" class="InductiveConstructor">suc</a> <a id="553" href="Codata.Conat.Bisimilarity.html#516" class="Bound">m</a> <a id="555" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="557" href="Codata.Conat.html#575" class="InductiveConstructor">suc</a> <a id="561" href="Codata.Conat.Bisimilarity.html#518" class="Bound">n</a>

<a id="refl"></a><a id="564" href="Codata.Conat.Bisimilarity.html#564" class="Function">refl</a> <a id="569" class="Symbol">:</a> <a id="571" class="Symbol">∀</a> <a id="573" class="Symbol">{</a><a id="574" href="Codata.Conat.Bisimilarity.html#574" class="Bound">i</a> <a id="576" href="Codata.Conat.Bisimilarity.html#576" class="Bound">m</a><a id="577" class="Symbol">}</a> <a id="579" class="Symbol">→</a> <a id="581" href="Codata.Conat.Bisimilarity.html#574" class="Bound">i</a> <a id="583" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="585" href="Codata.Conat.Bisimilarity.html#576" class="Bound">m</a> <a id="587" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="589" href="Codata.Conat.Bisimilarity.html#576" class="Bound">m</a>
<a id="591" href="Codata.Conat.Bisimilarity.html#564" class="Function">refl</a> <a id="596" class="Symbol">{</a><a id="597" class="Argument">m</a> <a id="599" class="Symbol">=</a> <a id="601" href="Codata.Conat.html#558" class="InductiveConstructor">zero</a><a id="605" class="Symbol">}</a>  <a id="608" class="Symbol">=</a> <a id="610" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>
<a id="615" href="Codata.Conat.Bisimilarity.html#564" class="Function">refl</a> <a id="620" class="Symbol">{</a><a id="621" class="Argument">m</a> <a id="623" class="Symbol">=</a> <a id="625" href="Codata.Conat.html#575" class="InductiveConstructor">suc</a> <a id="629" href="Codata.Conat.Bisimilarity.html#629" class="Bound">m</a><a id="630" class="Symbol">}</a> <a id="632" class="Symbol">=</a> <a id="634" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="638" class="Symbol">λ</a> <a id="640" class="Keyword">where</a> <a id="646" class="Symbol">.</a><a id="647" href="Codata.Thunk.html#544" class="Field">force</a> <a id="653" class="Symbol">→</a> <a id="655" href="Codata.Conat.Bisimilarity.html#564" class="Function">refl</a>

<a id="sym"></a><a id="661" href="Codata.Conat.Bisimilarity.html#661" class="Function">sym</a> <a id="665" class="Symbol">:</a> <a id="667" class="Symbol">∀</a> <a id="669" class="Symbol">{</a><a id="670" href="Codata.Conat.Bisimilarity.html#670" class="Bound">i</a> <a id="672" href="Codata.Conat.Bisimilarity.html#672" class="Bound">m</a> <a id="674" href="Codata.Conat.Bisimilarity.html#674" class="Bound">n</a><a id="675" class="Symbol">}</a> <a id="677" class="Symbol">→</a> <a id="679" href="Codata.Conat.Bisimilarity.html#670" class="Bound">i</a> <a id="681" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="683" href="Codata.Conat.Bisimilarity.html#672" class="Bound">m</a> <a id="685" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="687" href="Codata.Conat.Bisimilarity.html#674" class="Bound">n</a> <a id="689" class="Symbol">→</a> <a id="691" href="Codata.Conat.Bisimilarity.html#670" class="Bound">i</a> <a id="693" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="695" href="Codata.Conat.Bisimilarity.html#674" class="Bound">n</a> <a id="697" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="699" href="Codata.Conat.Bisimilarity.html#672" class="Bound">m</a>
<a id="701" href="Codata.Conat.Bisimilarity.html#661" class="Function">sym</a> <a id="705" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>     <a id="714" class="Symbol">=</a> <a id="716" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>
<a id="721" href="Codata.Conat.Bisimilarity.html#661" class="Function">sym</a> <a id="725" class="Symbol">(</a><a id="726" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="730" href="Codata.Conat.Bisimilarity.html#730" class="Bound">eq</a><a id="732" class="Symbol">)</a> <a id="734" class="Symbol">=</a> <a id="736" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="740" class="Symbol">λ</a> <a id="742" class="Keyword">where</a> <a id="748" class="Symbol">.</a><a id="749" href="Codata.Thunk.html#544" class="Field">force</a> <a id="755" class="Symbol">→</a> <a id="757" href="Codata.Conat.Bisimilarity.html#661" class="Function">sym</a> <a id="761" class="Symbol">(</a><a id="762" href="Codata.Conat.Bisimilarity.html#730" class="Bound">eq</a> <a id="765" class="Symbol">.</a><a id="766" href="Codata.Thunk.html#544" class="Field">force</a><a id="771" class="Symbol">)</a>

<a id="trans"></a><a id="774" href="Codata.Conat.Bisimilarity.html#774" class="Function">trans</a> <a id="780" class="Symbol">:</a> <a id="782" class="Symbol">∀</a> <a id="784" class="Symbol">{</a><a id="785" href="Codata.Conat.Bisimilarity.html#785" class="Bound">i</a> <a id="787" href="Codata.Conat.Bisimilarity.html#787" class="Bound">m</a> <a id="789" href="Codata.Conat.Bisimilarity.html#789" class="Bound">n</a> <a id="791" href="Codata.Conat.Bisimilarity.html#791" class="Bound">p</a><a id="792" class="Symbol">}</a> <a id="794" class="Symbol">→</a> <a id="796" href="Codata.Conat.Bisimilarity.html#785" class="Bound">i</a> <a id="798" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="800" href="Codata.Conat.Bisimilarity.html#787" class="Bound">m</a> <a id="802" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="804" href="Codata.Conat.Bisimilarity.html#789" class="Bound">n</a> <a id="806" class="Symbol">→</a> <a id="808" href="Codata.Conat.Bisimilarity.html#785" class="Bound">i</a> <a id="810" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="812" href="Codata.Conat.Bisimilarity.html#789" class="Bound">n</a> <a id="814" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="816" href="Codata.Conat.Bisimilarity.html#791" class="Bound">p</a> <a id="818" class="Symbol">→</a> <a id="820" href="Codata.Conat.Bisimilarity.html#785" class="Bound">i</a> <a id="822" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="824" href="Codata.Conat.Bisimilarity.html#787" class="Bound">m</a> <a id="826" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="828" href="Codata.Conat.Bisimilarity.html#791" class="Bound">p</a>
<a id="830" href="Codata.Conat.Bisimilarity.html#774" class="Function">trans</a> <a id="836" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>      <a id="846" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>      <a id="856" class="Symbol">=</a> <a id="858" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>
<a id="863" href="Codata.Conat.Bisimilarity.html#774" class="Function">trans</a> <a id="869" class="Symbol">(</a><a id="870" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="874" href="Codata.Conat.Bisimilarity.html#874" class="Bound">eq₁</a><a id="877" class="Symbol">)</a> <a id="879" class="Symbol">(</a><a id="880" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="884" href="Codata.Conat.Bisimilarity.html#884" class="Bound">eq₂</a><a id="887" class="Symbol">)</a> <a id="889" class="Symbol">=</a> <a id="891" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="895" class="Symbol">λ</a> <a id="897" class="Keyword">where</a> <a id="903" class="Symbol">.</a><a id="904" href="Codata.Thunk.html#544" class="Field">force</a> <a id="910" class="Symbol">→</a> <a id="912" href="Codata.Conat.Bisimilarity.html#774" class="Function">trans</a> <a id="918" class="Symbol">(</a><a id="919" href="Codata.Conat.Bisimilarity.html#874" class="Bound">eq₁</a> <a id="923" class="Symbol">.</a><a id="924" href="Codata.Thunk.html#544" class="Field">force</a><a id="929" class="Symbol">)</a> <a id="931" class="Symbol">(</a><a id="932" href="Codata.Conat.Bisimilarity.html#884" class="Bound">eq₂</a> <a id="936" class="Symbol">.</a><a id="937" href="Codata.Thunk.html#544" class="Field">force</a><a id="942" class="Symbol">)</a>

<a id="isEquivalence"></a><a id="945" href="Codata.Conat.Bisimilarity.html#945" class="Function">isEquivalence</a> <a id="959" class="Symbol">:</a> <a id="961" class="Symbol">∀</a> <a id="963" class="Symbol">{</a><a id="964" href="Codata.Conat.Bisimilarity.html#964" class="Bound">i</a><a id="965" class="Symbol">}</a> <a id="967" class="Symbol">→</a> <a id="969" href="Relation.Binary.Structures.html#1522" class="Record">IsEquivalence</a> <a id="983" class="Symbol">(</a><a id="984" href="Codata.Conat.Bisimilarity.html#964" class="Bound">i</a> <a id="986" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢_≈_</a><a id="990" class="Symbol">)</a>
<a id="992" href="Codata.Conat.Bisimilarity.html#945" class="Function">isEquivalence</a> <a id="1006" class="Symbol">=</a> <a id="1008" class="Keyword">record</a>
  <a id="1017" class="Symbol">{</a> <a id="1019" href="Relation.Binary.Structures.html#1568" class="Field">refl</a>  <a id="1025" class="Symbol">=</a> <a id="1027" href="Codata.Conat.Bisimilarity.html#564" class="Function">refl</a>
  <a id="1034" class="Symbol">;</a> <a id="1036" href="Relation.Binary.Structures.html#1594" class="Field">sym</a>   <a id="1042" class="Symbol">=</a> <a id="1044" href="Codata.Conat.Bisimilarity.html#661" class="Function">sym</a>
  <a id="1050" class="Symbol">;</a> <a id="1052" href="Relation.Binary.Structures.html#1620" class="Field">trans</a> <a id="1058" class="Symbol">=</a> <a id="1060" href="Codata.Conat.Bisimilarity.html#774" class="Function">trans</a>
  <a id="1068" class="Symbol">}</a>

<a id="setoid"></a><a id="1071" href="Codata.Conat.Bisimilarity.html#1071" class="Function">setoid</a> <a id="1078" class="Symbol">:</a> <a id="1080" href="Agda.Builtin.Size.html#206" class="Postulate">Size</a> <a id="1085" class="Symbol">→</a> <a id="1087" href="Relation.Binary.Bundles.html#1009" class="Record">Setoid</a> <a id="1094" href="Level.html#512" class="Function">0ℓ</a> <a id="1097" href="Level.html#512" class="Function">0ℓ</a>
<a id="1100" href="Codata.Conat.Bisimilarity.html#1071" class="Function">setoid</a> <a id="1107" href="Codata.Conat.Bisimilarity.html#1107" class="Bound">i</a> <a id="1109" class="Symbol">=</a> <a id="1111" class="Keyword">record</a>
  <a id="1120" class="Symbol">{</a> <a id="1122" href="Relation.Binary.Bundles.html#1132" class="Field">isEquivalence</a> <a id="1136" class="Symbol">=</a> <a id="1138" href="Codata.Conat.Bisimilarity.html#945" class="Function">isEquivalence</a> <a id="1152" class="Symbol">{</a><a id="1153" class="Argument">i</a> <a id="1155" class="Symbol">=</a> <a id="1157" href="Codata.Conat.Bisimilarity.html#1107" class="Bound">i</a><a id="1158" class="Symbol">}</a>
  <a id="1162" class="Symbol">}</a>

<a id="1165" class="Keyword">module</a> <a id="≈-Reasoning"></a><a id="1172" href="Codata.Conat.Bisimilarity.html#1172" class="Module">≈-Reasoning</a> <a id="1184" class="Symbol">{</a><a id="1185" href="Codata.Conat.Bisimilarity.html#1185" class="Bound">i</a><a id="1186" class="Symbol">}</a> <a id="1188" class="Keyword">where</a>

  <a id="1197" class="Keyword">open</a> <a id="1202" class="Keyword">import</a> <a id="1209" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="1242" class="Symbol">(</a><a id="1243" href="Codata.Conat.Bisimilarity.html#1071" class="Function">setoid</a> <a id="1250" href="Codata.Conat.Bisimilarity.html#1185" class="Bound">i</a><a id="1251" class="Symbol">)</a> <a id="1253" class="Keyword">public</a>

<a id="1261" class="Keyword">infix</a> <a id="1267" class="Number">1</a> <a id="1269" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">_⊢_≲_</a>
<a id="1275" class="Keyword">data</a> <a id="_⊢_≲_"></a><a id="1280" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">_⊢_≲_</a> <a id="1286" href="Codata.Conat.Bisimilarity.html#1286" class="Bound">i</a> <a id="1288" class="Symbol">:</a> <a id="1290" class="Symbol">(</a><a id="1291" href="Codata.Conat.Bisimilarity.html#1291" class="Bound">m</a> <a id="1293" href="Codata.Conat.Bisimilarity.html#1293" class="Bound">n</a> <a id="1295" class="Symbol">:</a> <a id="1297" href="Codata.Conat.html#527" class="Datatype">Conat</a> <a id="1303" href="Agda.Builtin.Size.html#302" class="Postulate">∞</a><a id="1304" class="Symbol">)</a> <a id="1306" class="Symbol">→</a> <a id="1308" class="PrimitiveType">Set</a> <a id="1312" class="Keyword">where</a>
  <a id="_⊢_≲_.z≲n"></a><a id="1320" href="Codata.Conat.Bisimilarity.html#1320" class="InductiveConstructor">z≲n</a> <a id="1324" class="Symbol">:</a> <a id="1326" class="Symbol">∀</a> <a id="1328" class="Symbol">{</a><a id="1329" href="Codata.Conat.Bisimilarity.html#1329" class="Bound">n</a><a id="1330" class="Symbol">}</a> <a id="1332" class="Symbol">→</a> <a id="1334" href="Codata.Conat.Bisimilarity.html#1286" class="Bound">i</a> <a id="1336" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1338" href="Codata.Conat.html#558" class="InductiveConstructor">zero</a> <a id="1343" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1345" href="Codata.Conat.Bisimilarity.html#1329" class="Bound">n</a>
  <a id="_⊢_≲_.s≲s"></a><a id="1349" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1353" class="Symbol">:</a> <a id="1355" class="Symbol">∀</a> <a id="1357" class="Symbol">{</a><a id="1358" href="Codata.Conat.Bisimilarity.html#1358" class="Bound">m</a> <a id="1360" href="Codata.Conat.Bisimilarity.html#1360" class="Bound">n</a><a id="1361" class="Symbol">}</a> <a id="1363" class="Symbol">→</a> <a id="1365" href="Codata.Thunk.html#749" class="Function">Thunk^R</a> <a id="1373" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">_⊢_≲_</a> <a id="1379" href="Codata.Conat.Bisimilarity.html#1286" class="Bound">i</a> <a id="1381" href="Codata.Conat.Bisimilarity.html#1358" class="Bound">m</a> <a id="1383" href="Codata.Conat.Bisimilarity.html#1360" class="Bound">n</a> <a id="1385" class="Symbol">→</a> <a id="1387" href="Codata.Conat.Bisimilarity.html#1286" class="Bound">i</a> <a id="1389" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1391" href="Codata.Conat.html#575" class="InductiveConstructor">suc</a> <a id="1395" href="Codata.Conat.Bisimilarity.html#1358" class="Bound">m</a> <a id="1397" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1399" href="Codata.Conat.html#575" class="InductiveConstructor">suc</a> <a id="1403" href="Codata.Conat.Bisimilarity.html#1360" class="Bound">n</a>

<a id="≈⇒≲"></a><a id="1406" href="Codata.Conat.Bisimilarity.html#1406" class="Function">≈⇒≲</a> <a id="1410" class="Symbol">:</a> <a id="1412" class="Symbol">∀</a> <a id="1414" class="Symbol">{</a><a id="1415" href="Codata.Conat.Bisimilarity.html#1415" class="Bound">i</a> <a id="1417" href="Codata.Conat.Bisimilarity.html#1417" class="Bound">m</a> <a id="1419" href="Codata.Conat.Bisimilarity.html#1419" class="Bound">n</a><a id="1420" class="Symbol">}</a> <a id="1422" class="Symbol">→</a> <a id="1424" href="Codata.Conat.Bisimilarity.html#1415" class="Bound">i</a> <a id="1426" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="1428" href="Codata.Conat.Bisimilarity.html#1417" class="Bound">m</a> <a id="1430" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="1432" href="Codata.Conat.Bisimilarity.html#1419" class="Bound">n</a> <a id="1434" class="Symbol">→</a> <a id="1436" href="Codata.Conat.Bisimilarity.html#1415" class="Bound">i</a> <a id="1438" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1440" href="Codata.Conat.Bisimilarity.html#1417" class="Bound">m</a> <a id="1442" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1444" href="Codata.Conat.Bisimilarity.html#1419" class="Bound">n</a>
<a id="1446" href="Codata.Conat.Bisimilarity.html#1406" class="Function">≈⇒≲</a> <a id="1450" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>     <a id="1459" class="Symbol">=</a> <a id="1461" href="Codata.Conat.Bisimilarity.html#1320" class="InductiveConstructor">z≲n</a>
<a id="1465" href="Codata.Conat.Bisimilarity.html#1406" class="Function">≈⇒≲</a> <a id="1469" class="Symbol">(</a><a id="1470" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="1474" href="Codata.Conat.Bisimilarity.html#1474" class="Bound">eq</a><a id="1476" class="Symbol">)</a> <a id="1478" class="Symbol">=</a> <a id="1480" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1484" class="Symbol">λ</a> <a id="1486" class="Keyword">where</a> <a id="1492" class="Symbol">.</a><a id="1493" href="Codata.Thunk.html#544" class="Field">force</a> <a id="1499" class="Symbol">→</a> <a id="1501" href="Codata.Conat.Bisimilarity.html#1406" class="Function">≈⇒≲</a> <a id="1505" class="Symbol">(</a><a id="1506" href="Codata.Conat.Bisimilarity.html#1474" class="Bound">eq</a> <a id="1509" class="Symbol">.</a><a id="1510" href="Codata.Thunk.html#544" class="Field">force</a><a id="1515" class="Symbol">)</a>

<a id="≲-refl"></a><a id="1518" href="Codata.Conat.Bisimilarity.html#1518" class="Function">≲-refl</a> <a id="1525" class="Symbol">:</a> <a id="1527" class="Symbol">∀</a> <a id="1529" class="Symbol">{</a><a id="1530" href="Codata.Conat.Bisimilarity.html#1530" class="Bound">i</a> <a id="1532" href="Codata.Conat.Bisimilarity.html#1532" class="Bound">m</a><a id="1533" class="Symbol">}</a> <a id="1535" class="Symbol">→</a> <a id="1537" href="Codata.Conat.Bisimilarity.html#1530" class="Bound">i</a> <a id="1539" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1541" href="Codata.Conat.Bisimilarity.html#1532" class="Bound">m</a> <a id="1543" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1545" href="Codata.Conat.Bisimilarity.html#1532" class="Bound">m</a>
<a id="1547" href="Codata.Conat.Bisimilarity.html#1518" class="Function">≲-refl</a> <a id="1554" class="Symbol">=</a> <a id="1556" href="Codata.Conat.Bisimilarity.html#1406" class="Function">≈⇒≲</a> <a id="1560" href="Codata.Conat.Bisimilarity.html#564" class="Function">refl</a>

<a id="≲-antisym"></a><a id="1566" href="Codata.Conat.Bisimilarity.html#1566" class="Function">≲-antisym</a> <a id="1576" class="Symbol">:</a> <a id="1578" class="Symbol">∀</a> <a id="1580" class="Symbol">{</a><a id="1581" href="Codata.Conat.Bisimilarity.html#1581" class="Bound">i</a> <a id="1583" href="Codata.Conat.Bisimilarity.html#1583" class="Bound">m</a> <a id="1585" href="Codata.Conat.Bisimilarity.html#1585" class="Bound">n</a><a id="1586" class="Symbol">}</a> <a id="1588" class="Symbol">→</a> <a id="1590" href="Codata.Conat.Bisimilarity.html#1581" class="Bound">i</a> <a id="1592" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1594" href="Codata.Conat.Bisimilarity.html#1583" class="Bound">m</a> <a id="1596" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1598" href="Codata.Conat.Bisimilarity.html#1585" class="Bound">n</a> <a id="1600" class="Symbol">→</a> <a id="1602" href="Codata.Conat.Bisimilarity.html#1581" class="Bound">i</a> <a id="1604" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1606" href="Codata.Conat.Bisimilarity.html#1585" class="Bound">n</a> <a id="1608" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1610" href="Codata.Conat.Bisimilarity.html#1583" class="Bound">m</a> <a id="1612" class="Symbol">→</a> <a id="1614" href="Codata.Conat.Bisimilarity.html#1581" class="Bound">i</a> <a id="1616" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">⊢</a> <a id="1618" href="Codata.Conat.Bisimilarity.html#1583" class="Bound">m</a> <a id="1620" href="Codata.Conat.Bisimilarity.html#441" class="Datatype Operator">≈</a> <a id="1622" href="Codata.Conat.Bisimilarity.html#1585" class="Bound">n</a>
<a id="1624" href="Codata.Conat.Bisimilarity.html#1566" class="Function">≲-antisym</a> <a id="1634" href="Codata.Conat.Bisimilarity.html#1320" class="InductiveConstructor">z≲n</a>      <a id="1643" href="Codata.Conat.Bisimilarity.html#1320" class="InductiveConstructor">z≲n</a>      <a id="1652" class="Symbol">=</a> <a id="1654" href="Codata.Conat.Bisimilarity.html#481" class="InductiveConstructor">zero</a>
<a id="1659" href="Codata.Conat.Bisimilarity.html#1566" class="Function">≲-antisym</a> <a id="1669" class="Symbol">(</a><a id="1670" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1674" href="Codata.Conat.Bisimilarity.html#1674" class="Bound">le</a><a id="1676" class="Symbol">)</a> <a id="1678" class="Symbol">(</a><a id="1679" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1683" href="Codata.Conat.Bisimilarity.html#1683" class="Bound">ge</a><a id="1685" class="Symbol">)</a> <a id="1687" class="Symbol">=</a> <a id="1689" href="Codata.Conat.Bisimilarity.html#506" class="InductiveConstructor">suc</a> <a id="1693" class="Symbol">λ</a> <a id="1695" class="Keyword">where</a> <a id="1701" class="Symbol">.</a><a id="1702" href="Codata.Thunk.html#544" class="Field">force</a> <a id="1708" class="Symbol">→</a> <a id="1710" href="Codata.Conat.Bisimilarity.html#1566" class="Function">≲-antisym</a> <a id="1720" class="Symbol">(</a><a id="1721" href="Codata.Conat.Bisimilarity.html#1674" class="Bound">le</a> <a id="1724" class="Symbol">.</a><a id="1725" href="Codata.Thunk.html#544" class="Field">force</a><a id="1730" class="Symbol">)</a> <a id="1732" class="Symbol">(</a><a id="1733" href="Codata.Conat.Bisimilarity.html#1683" class="Bound">ge</a> <a id="1736" class="Symbol">.</a><a id="1737" href="Codata.Thunk.html#544" class="Field">force</a><a id="1742" class="Symbol">)</a>

<a id="≲-trans"></a><a id="1745" href="Codata.Conat.Bisimilarity.html#1745" class="Function">≲-trans</a> <a id="1753" class="Symbol">:</a> <a id="1755" class="Symbol">∀</a> <a id="1757" class="Symbol">{</a><a id="1758" href="Codata.Conat.Bisimilarity.html#1758" class="Bound">i</a> <a id="1760" href="Codata.Conat.Bisimilarity.html#1760" class="Bound">m</a> <a id="1762" href="Codata.Conat.Bisimilarity.html#1762" class="Bound">n</a> <a id="1764" href="Codata.Conat.Bisimilarity.html#1764" class="Bound">p</a><a id="1765" class="Symbol">}</a> <a id="1767" class="Symbol">→</a> <a id="1769" href="Codata.Conat.Bisimilarity.html#1758" class="Bound">i</a> <a id="1771" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1773" href="Codata.Conat.Bisimilarity.html#1760" class="Bound">m</a> <a id="1775" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1777" href="Codata.Conat.Bisimilarity.html#1762" class="Bound">n</a> <a id="1779" class="Symbol">→</a> <a id="1781" href="Codata.Conat.Bisimilarity.html#1758" class="Bound">i</a> <a id="1783" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1785" href="Codata.Conat.Bisimilarity.html#1762" class="Bound">n</a> <a id="1787" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1789" href="Codata.Conat.Bisimilarity.html#1764" class="Bound">p</a> <a id="1791" class="Symbol">→</a> <a id="1793" href="Codata.Conat.Bisimilarity.html#1758" class="Bound">i</a> <a id="1795" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">⊢</a> <a id="1797" href="Codata.Conat.Bisimilarity.html#1760" class="Bound">m</a> <a id="1799" href="Codata.Conat.Bisimilarity.html#1280" class="Datatype Operator">≲</a> <a id="1801" href="Codata.Conat.Bisimilarity.html#1764" class="Bound">p</a>
<a id="1803" href="Codata.Conat.Bisimilarity.html#1745" class="Function">≲-trans</a> <a id="1811" href="Codata.Conat.Bisimilarity.html#1320" class="InductiveConstructor">z≲n</a>       <a id="1821" class="Symbol">_</a>         <a id="1831" class="Symbol">=</a> <a id="1833" href="Codata.Conat.Bisimilarity.html#1320" class="InductiveConstructor">z≲n</a>
<a id="1837" href="Codata.Conat.Bisimilarity.html#1745" class="Function">≲-trans</a> <a id="1845" class="Symbol">(</a><a id="1846" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1850" href="Codata.Conat.Bisimilarity.html#1850" class="Bound">le₁</a><a id="1853" class="Symbol">)</a> <a id="1855" class="Symbol">(</a><a id="1856" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1860" href="Codata.Conat.Bisimilarity.html#1860" class="Bound">le₂</a><a id="1863" class="Symbol">)</a> <a id="1865" class="Symbol">=</a> <a id="1867" href="Codata.Conat.Bisimilarity.html#1349" class="InductiveConstructor">s≲s</a> <a id="1871" class="Symbol">λ</a> <a id="1873" class="Keyword">where</a> <a id="1879" class="Symbol">.</a><a id="1880" href="Codata.Thunk.html#544" class="Field">force</a> <a id="1886" class="Symbol">→</a> <a id="1888" href="Codata.Conat.Bisimilarity.html#1745" class="Function">≲-trans</a> <a id="1896" class="Symbol">(</a><a id="1897" href="Codata.Conat.Bisimilarity.html#1850" class="Bound">le₁</a> <a id="1901" class="Symbol">.</a><a id="1902" href="Codata.Thunk.html#544" class="Field">force</a><a id="1907" class="Symbol">)</a> <a id="1909" class="Symbol">(</a><a id="1910" href="Codata.Conat.Bisimilarity.html#1860" class="Bound">le₂</a> <a id="1914" class="Symbol">.</a><a id="1915" href="Codata.Thunk.html#544" class="Field">force</a><a id="1920" class="Symbol">)</a>
</pre></body></html>