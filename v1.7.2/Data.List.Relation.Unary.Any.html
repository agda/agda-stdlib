<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.List.Relation.Unary.Any</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Lists where at least one element satisfies a given property</a>
<a id="169" class="Comment">------------------------------------------------------------------------</a>

<a id="243" class="Symbol">{-#</a> <a id="247" class="Keyword">OPTIONS</a> <a id="255" class="Pragma">--cubical-compatible</a> <a id="276" class="Pragma">--safe</a> <a id="283" class="Symbol">#-}</a>

<a id="288" class="Keyword">module</a> <a id="295" href="Data.List.Relation.Unary.Any.html" class="Module">Data.List.Relation.Unary.Any</a> <a id="324" class="Keyword">where</a>

<a id="331" class="Keyword">open</a> <a id="336" class="Keyword">import</a> <a id="343" href="Data.Empty.html" class="Module">Data.Empty</a>
<a id="354" class="Keyword">open</a> <a id="359" class="Keyword">import</a> <a id="366" href="Data.Fin.Base.html" class="Module">Data.Fin.Base</a>
<a id="380" class="Keyword">open</a> <a id="385" class="Keyword">import</a> <a id="392" href="Data.List.Base.html" class="Module">Data.List.Base</a> <a id="407" class="Symbol">as</a> <a id="410" class="Module">List</a> <a id="415" class="Keyword">using</a> <a id="421" class="Symbol">(</a><a id="422" href="Agda.Builtin.List.html#130" class="Datatype">List</a><a id="426" class="Symbol">;</a> <a id="428" href="Data.List.Base.html#10392" class="InductiveConstructor">[]</a><a id="430" class="Symbol">;</a> <a id="432" href="Data.List.Base.html#5034" class="Function Operator">[_]</a><a id="435" class="Symbol">;</a> <a id="437" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">_∷_</a><a id="440" class="Symbol">)</a>
<a id="442" class="Keyword">open</a> <a id="447" class="Keyword">import</a> <a id="454" href="Data.Product.html" class="Module">Data.Product</a> <a id="467" class="Symbol">as</a> <a id="470" class="Module">Prod</a> <a id="475" class="Keyword">using</a> <a id="481" class="Symbol">(</a><a id="482" href="Data.Product.html#1378" class="Function">∃</a><a id="483" class="Symbol">;</a> <a id="485" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">_,_</a><a id="488" class="Symbol">)</a>
<a id="490" class="Keyword">open</a> <a id="495" class="Keyword">import</a> <a id="502" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="516" class="Symbol">as</a> <a id="519" class="Module">Sum</a> <a id="523" class="Keyword">using</a> <a id="529" class="Symbol">(</a><a id="530" href="Data.Sum.Base.html#743" class="Datatype Operator">_⊎_</a><a id="533" class="Symbol">;</a> <a id="535" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a><a id="539" class="Symbol">;</a> <a id="541" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a><a id="545" class="Symbol">)</a>
<a id="547" class="Keyword">open</a> <a id="552" class="Keyword">import</a> <a id="559" href="Level.html" class="Module">Level</a> <a id="565" class="Keyword">using</a> <a id="571" class="Symbol">(</a><a id="572" href="Agda.Primitive.html#591" class="Postulate">Level</a><a id="577" class="Symbol">;</a> <a id="579" href="Agda.Primitive.html#804" class="Primitive Operator">_⊔_</a><a id="582" class="Symbol">)</a>
<a id="584" class="Keyword">open</a> <a id="589" class="Keyword">import</a> <a id="596" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="613" class="Keyword">using</a> <a id="619" class="Symbol">(</a><a id="620" href="Relation.Nullary.html#665" class="Function Operator">¬_</a><a id="622" class="Symbol">;</a> <a id="624" href="Relation.Nullary.html#1657" class="InductiveConstructor">yes</a><a id="627" class="Symbol">;</a> <a id="629" href="Relation.Nullary.html#1694" class="InductiveConstructor">no</a><a id="631" class="Symbol">)</a>
<a id="633" class="Keyword">import</a> <a id="640" href="Relation.Nullary.Decidable.html" class="Module">Relation.Nullary.Decidable</a> <a id="667" class="Symbol">as</a> <a id="670" class="Module">Dec</a>
<a id="674" class="Keyword">open</a> <a id="679" class="Keyword">import</a> <a id="686" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="712" class="Keyword">using</a> <a id="718" class="Symbol">(</a><a id="719" href="Relation.Nullary.Negation.Core.html#787" class="Function">contradiction</a><a id="732" class="Symbol">)</a>
<a id="734" class="Keyword">open</a> <a id="739" class="Keyword">import</a> <a id="746" href="Relation.Nullary.Sum.html" class="Module">Relation.Nullary.Sum</a> <a id="767" class="Keyword">using</a> <a id="773" class="Symbol">(</a><a id="774" href="Relation.Nullary.Sum.html#955" class="Function Operator">_⊎-dec_</a><a id="781" class="Symbol">)</a>
<a id="783" class="Keyword">open</a> <a id="788" class="Keyword">import</a> <a id="795" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="810" class="Keyword">hiding</a> <a id="817" class="Symbol">(</a><a id="818" href="Relation.Unary.html#1532" class="Function Operator">_∈_</a><a id="821" class="Symbol">)</a>

<a id="824" class="Keyword">private</a>
  <a id="834" class="Keyword">variable</a>
    <a id="847" href="Data.List.Relation.Unary.Any.html#847" class="Generalizable">a</a> <a id="849" href="Data.List.Relation.Unary.Any.html#849" class="Generalizable">p</a> <a id="851" href="Data.List.Relation.Unary.Any.html#851" class="Generalizable">q</a> <a id="853" class="Symbol">:</a> <a id="855" href="Agda.Primitive.html#591" class="Postulate">Level</a>
    <a id="865" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a> <a id="867" class="Symbol">:</a> <a id="869" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="873" href="Data.List.Relation.Unary.Any.html#847" class="Generalizable">a</a>
    <a id="879" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="881" href="Data.List.Relation.Unary.Any.html#881" class="Generalizable">Q</a> <a id="883" class="Symbol">:</a> <a id="885" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="890" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a> <a id="892" href="Data.List.Relation.Unary.Any.html#849" class="Generalizable">p</a>
    <a id="898" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="900" class="Symbol">:</a> <a id="902" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a>
    <a id="908" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="911" class="Symbol">:</a> <a id="913" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="918" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a>

<a id="921" class="Comment">------------------------------------------------------------------------</a>
<a id="994" class="Comment">-- Definition</a>

<a id="1009" class="Comment">-- Given a predicate P, then Any P xs means that at least one element</a>
<a id="1079" class="Comment">-- in xs satisfies P. See `Relation.Unary` for an explanation of</a>
<a id="1144" class="Comment">-- predicates.</a>

<a id="1160" class="Keyword">data</a> <a id="Any"></a><a id="1165" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1169" class="Symbol">{</a><a id="1170" href="Data.List.Relation.Unary.Any.html#1170" class="Bound">A</a> <a id="1172" class="Symbol">:</a> <a id="1174" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1178" href="Data.List.Relation.Unary.Any.html#847" class="Generalizable">a</a><a id="1179" class="Symbol">}</a> <a id="1181" class="Symbol">(</a><a id="1182" href="Data.List.Relation.Unary.Any.html#1182" class="Bound">P</a> <a id="1184" class="Symbol">:</a> <a id="1186" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1191" href="Data.List.Relation.Unary.Any.html#1170" class="Bound">A</a> <a id="1193" href="Data.List.Relation.Unary.Any.html#849" class="Generalizable">p</a><a id="1194" class="Symbol">)</a> <a id="1196" class="Symbol">:</a> <a id="1198" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1203" class="Symbol">(</a><a id="1204" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="1209" href="Data.List.Relation.Unary.Any.html#1170" class="Bound">A</a><a id="1210" class="Symbol">)</a> <a id="1212" class="Symbol">(</a><a id="1213" href="Data.List.Relation.Unary.Any.html#1178" class="Bound">a</a> <a id="1215" href="Agda.Primitive.html#804" class="Primitive Operator">⊔</a> <a id="1217" href="Data.List.Relation.Unary.Any.html#1193" class="Bound">p</a><a id="1218" class="Symbol">)</a> <a id="1220" class="Keyword">where</a>
  <a id="Any.here"></a><a id="1228" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a>  <a id="1234" class="Symbol">:</a> <a id="1236" class="Symbol">∀</a> <a id="1238" class="Symbol">{</a><a id="1239" href="Data.List.Relation.Unary.Any.html#1239" class="Bound">x</a> <a id="1241" href="Data.List.Relation.Unary.Any.html#1241" class="Bound">xs</a><a id="1243" class="Symbol">}</a> <a id="1245" class="Symbol">(</a><a id="1246" href="Data.List.Relation.Unary.Any.html#1246" class="Bound">px</a>  <a id="1250" class="Symbol">:</a> <a id="1252" href="Data.List.Relation.Unary.Any.html#1182" class="Bound">P</a> <a id="1254" href="Data.List.Relation.Unary.Any.html#1239" class="Bound">x</a><a id="1255" class="Symbol">)</a>      <a id="1262" class="Symbol">→</a> <a id="1264" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1268" href="Data.List.Relation.Unary.Any.html#1182" class="Bound">P</a> <a id="1270" class="Symbol">(</a><a id="1271" href="Data.List.Relation.Unary.Any.html#1239" class="Bound">x</a> <a id="1273" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1275" href="Data.List.Relation.Unary.Any.html#1241" class="Bound">xs</a><a id="1277" class="Symbol">)</a>
  <a id="Any.there"></a><a id="1281" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="1287" class="Symbol">:</a> <a id="1289" class="Symbol">∀</a> <a id="1291" class="Symbol">{</a><a id="1292" href="Data.List.Relation.Unary.Any.html#1292" class="Bound">x</a> <a id="1294" href="Data.List.Relation.Unary.Any.html#1294" class="Bound">xs</a><a id="1296" class="Symbol">}</a> <a id="1298" class="Symbol">(</a><a id="1299" href="Data.List.Relation.Unary.Any.html#1299" class="Bound">pxs</a> <a id="1303" class="Symbol">:</a> <a id="1305" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1309" href="Data.List.Relation.Unary.Any.html#1182" class="Bound">P</a> <a id="1311" href="Data.List.Relation.Unary.Any.html#1294" class="Bound">xs</a><a id="1313" class="Symbol">)</a> <a id="1315" class="Symbol">→</a> <a id="1317" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1321" href="Data.List.Relation.Unary.Any.html#1182" class="Bound">P</a> <a id="1323" class="Symbol">(</a><a id="1324" href="Data.List.Relation.Unary.Any.html#1292" class="Bound">x</a> <a id="1326" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1328" href="Data.List.Relation.Unary.Any.html#1294" class="Bound">xs</a><a id="1330" class="Symbol">)</a>

<a id="1333" class="Comment">------------------------------------------------------------------------</a>
<a id="1406" class="Comment">-- Operations on Any</a>

<a id="head"></a><a id="1428" href="Data.List.Relation.Unary.Any.html#1428" class="Function">head</a> <a id="1433" class="Symbol">:</a> <a id="1435" href="Relation.Nullary.html#665" class="Function Operator">¬</a> <a id="1437" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1441" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1443" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="1446" class="Symbol">→</a> <a id="1448" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1452" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1454" class="Symbol">(</a><a id="1455" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="1457" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1459" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a><a id="1461" class="Symbol">)</a> <a id="1463" class="Symbol">→</a> <a id="1465" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1467" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a>
<a id="1469" href="Data.List.Relation.Unary.Any.html#1428" class="Function">head</a> <a id="1474" href="Data.List.Relation.Unary.Any.html#1474" class="Bound">¬pxs</a> <a id="1479" class="Symbol">(</a><a id="1480" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="1485" href="Data.List.Relation.Unary.Any.html#1485" class="Bound">px</a><a id="1487" class="Symbol">)</a>   <a id="1491" class="Symbol">=</a> <a id="1493" href="Data.List.Relation.Unary.Any.html#1485" class="Bound">px</a>
<a id="1496" href="Data.List.Relation.Unary.Any.html#1428" class="Function">head</a> <a id="1501" href="Data.List.Relation.Unary.Any.html#1501" class="Bound">¬pxs</a> <a id="1506" class="Symbol">(</a><a id="1507" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="1513" href="Data.List.Relation.Unary.Any.html#1513" class="Bound">pxs</a><a id="1516" class="Symbol">)</a> <a id="1518" class="Symbol">=</a> <a id="1520" href="Relation.Nullary.Negation.Core.html#787" class="Function">contradiction</a> <a id="1534" href="Data.List.Relation.Unary.Any.html#1513" class="Bound">pxs</a> <a id="1538" href="Data.List.Relation.Unary.Any.html#1501" class="Bound">¬pxs</a>

<a id="tail"></a><a id="1544" href="Data.List.Relation.Unary.Any.html#1544" class="Function">tail</a> <a id="1549" class="Symbol">:</a> <a id="1551" href="Relation.Nullary.html#665" class="Function Operator">¬</a> <a id="1553" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1555" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="1557" class="Symbol">→</a> <a id="1559" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1563" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1565" class="Symbol">(</a><a id="1566" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="1568" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="1570" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a><a id="1572" class="Symbol">)</a> <a id="1574" class="Symbol">→</a> <a id="1576" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1580" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1582" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a>
<a id="1585" href="Data.List.Relation.Unary.Any.html#1544" class="Function">tail</a> <a id="1590" href="Data.List.Relation.Unary.Any.html#1590" class="Bound">¬px</a> <a id="1594" class="Symbol">(</a><a id="1595" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a>  <a id="1601" href="Data.List.Relation.Unary.Any.html#1601" class="Bound">px</a><a id="1603" class="Symbol">)</a>  <a id="1606" class="Symbol">=</a> <a id="1608" href="Data.Empty.html#637" class="Function">⊥-elim</a> <a id="1615" class="Symbol">(</a><a id="1616" href="Data.List.Relation.Unary.Any.html#1590" class="Bound">¬px</a> <a id="1620" href="Data.List.Relation.Unary.Any.html#1601" class="Bound">px</a><a id="1622" class="Symbol">)</a>
<a id="1624" href="Data.List.Relation.Unary.Any.html#1544" class="Function">tail</a> <a id="1629" href="Data.List.Relation.Unary.Any.html#1629" class="Bound">¬px</a> <a id="1633" class="Symbol">(</a><a id="1634" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="1640" href="Data.List.Relation.Unary.Any.html#1640" class="Bound">pxs</a><a id="1643" class="Symbol">)</a> <a id="1645" class="Symbol">=</a> <a id="1647" href="Data.List.Relation.Unary.Any.html#1640" class="Bound">pxs</a>

<a id="map"></a><a id="1652" href="Data.List.Relation.Unary.Any.html#1652" class="Function">map</a> <a id="1656" class="Symbol">:</a> <a id="1658" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1660" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="1662" href="Data.List.Relation.Unary.Any.html#881" class="Generalizable">Q</a> <a id="1664" class="Symbol">→</a> <a id="1666" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1670" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1672" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="1674" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1678" href="Data.List.Relation.Unary.Any.html#881" class="Generalizable">Q</a>
<a id="1680" href="Data.List.Relation.Unary.Any.html#1652" class="Function">map</a> <a id="1684" href="Data.List.Relation.Unary.Any.html#1684" class="Bound">g</a> <a id="1686" class="Symbol">(</a><a id="1687" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="1692" href="Data.List.Relation.Unary.Any.html#1692" class="Bound">px</a><a id="1694" class="Symbol">)</a>   <a id="1698" class="Symbol">=</a> <a id="1700" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="1705" class="Symbol">(</a><a id="1706" href="Data.List.Relation.Unary.Any.html#1684" class="Bound">g</a> <a id="1708" href="Data.List.Relation.Unary.Any.html#1692" class="Bound">px</a><a id="1710" class="Symbol">)</a>
<a id="1712" href="Data.List.Relation.Unary.Any.html#1652" class="Function">map</a> <a id="1716" href="Data.List.Relation.Unary.Any.html#1716" class="Bound">g</a> <a id="1718" class="Symbol">(</a><a id="1719" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="1725" href="Data.List.Relation.Unary.Any.html#1725" class="Bound">pxs</a><a id="1728" class="Symbol">)</a> <a id="1730" class="Symbol">=</a> <a id="1732" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="1738" class="Symbol">(</a><a id="1739" href="Data.List.Relation.Unary.Any.html#1652" class="Function">map</a> <a id="1743" href="Data.List.Relation.Unary.Any.html#1716" class="Bound">g</a> <a id="1745" href="Data.List.Relation.Unary.Any.html#1725" class="Bound">pxs</a><a id="1748" class="Symbol">)</a>

<a id="1751" class="Comment">-- `index x∈xs` is the list position (zero-based) which `x∈xs` points to.</a>

<a id="index"></a><a id="1826" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="1832" class="Symbol">:</a> <a id="1834" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1838" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="1840" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="1843" class="Symbol">→</a> <a id="1845" href="Data.Fin.Base.html#1135" class="Datatype">Fin</a> <a id="1849" class="Symbol">(</a><a id="1850" href="Data.List.Base.html#4873" class="Function">List.length</a> <a id="1862" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a><a id="1864" class="Symbol">)</a>
<a id="1866" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="1872" class="Symbol">(</a><a id="1873" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a>  <a id="1879" href="Data.List.Relation.Unary.Any.html#1879" class="Bound">px</a><a id="1881" class="Symbol">)</a>  <a id="1884" class="Symbol">=</a> <a id="1886" href="Data.Fin.Base.html#1157" class="InductiveConstructor">zero</a>
<a id="1891" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="1897" class="Symbol">(</a><a id="1898" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="1904" href="Data.List.Relation.Unary.Any.html#1904" class="Bound">pxs</a><a id="1907" class="Symbol">)</a> <a id="1909" class="Symbol">=</a> <a id="1911" href="Data.Fin.Base.html#1188" class="InductiveConstructor">suc</a> <a id="1915" class="Symbol">(</a><a id="1916" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="1922" href="Data.List.Relation.Unary.Any.html#1904" class="Bound">pxs</a><a id="1925" class="Symbol">)</a>

<a id="lookup"></a><a id="1928" href="Data.List.Relation.Unary.Any.html#1928" class="Function">lookup</a> <a id="1935" class="Symbol">:</a> <a id="1937" class="Symbol">{</a><a id="1938" href="Data.List.Relation.Unary.Any.html#1938" class="Bound">P</a> <a id="1940" class="Symbol">:</a> <a id="1942" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1947" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a> <a id="1949" href="Data.List.Relation.Unary.Any.html#849" class="Generalizable">p</a><a id="1950" class="Symbol">}</a> <a id="1952" class="Symbol">→</a> <a id="1954" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="1958" href="Data.List.Relation.Unary.Any.html#1938" class="Bound">P</a> <a id="1960" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="1963" class="Symbol">→</a> <a id="1965" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a>
<a id="1967" href="Data.List.Relation.Unary.Any.html#1928" class="Function">lookup</a> <a id="1974" class="Symbol">{</a><a id="1975" class="Argument">xs</a> <a id="1978" class="Symbol">=</a> <a id="1980" href="Data.List.Relation.Unary.Any.html#1980" class="Bound">xs</a><a id="1982" class="Symbol">}</a> <a id="1984" href="Data.List.Relation.Unary.Any.html#1984" class="Bound">p</a> <a id="1986" class="Symbol">=</a> <a id="1988" href="Data.List.Base.html#6115" class="Function">List.lookup</a> <a id="2000" href="Data.List.Relation.Unary.Any.html#1980" class="Bound">xs</a> <a id="2003" class="Symbol">(</a><a id="2004" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="2010" href="Data.List.Relation.Unary.Any.html#1984" class="Bound">p</a><a id="2011" class="Symbol">)</a>

<a id="_∷=_"></a><a id="2014" href="Data.List.Relation.Unary.Any.html#2014" class="Function Operator">_∷=_</a> <a id="2019" class="Symbol">:</a> <a id="2021" class="Symbol">{</a><a id="2022" href="Data.List.Relation.Unary.Any.html#2022" class="Bound">P</a> <a id="2024" class="Symbol">:</a> <a id="2026" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2031" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a> <a id="2033" href="Data.List.Relation.Unary.Any.html#849" class="Generalizable">p</a><a id="2034" class="Symbol">}</a> <a id="2036" class="Symbol">→</a> <a id="2038" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2042" href="Data.List.Relation.Unary.Any.html#2022" class="Bound">P</a> <a id="2044" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="2047" class="Symbol">→</a> <a id="2049" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a> <a id="2051" class="Symbol">→</a> <a id="2053" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2058" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a>
<a id="2060" href="Data.List.Relation.Unary.Any.html#2014" class="Function Operator">_∷=_</a> <a id="2065" class="Symbol">{</a><a id="2066" class="Argument">xs</a> <a id="2069" class="Symbol">=</a> <a id="2071" href="Data.List.Relation.Unary.Any.html#2071" class="Bound">xs</a><a id="2073" class="Symbol">}</a> <a id="2075" href="Data.List.Relation.Unary.Any.html#2075" class="Bound">x∈xs</a> <a id="2080" href="Data.List.Relation.Unary.Any.html#2080" class="Bound">v</a> <a id="2082" class="Symbol">=</a> <a id="2084" href="Data.List.Relation.Unary.Any.html#2071" class="Bound">xs</a> <a id="2087" href="Data.List.Base.html#9457" class="Function Operator">List.[</a> <a id="2094" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="2100" href="Data.List.Relation.Unary.Any.html#2075" class="Bound">x∈xs</a> <a id="2105" href="Data.List.Base.html#9457" class="Function Operator">]∷=</a> <a id="2109" href="Data.List.Relation.Unary.Any.html#2080" class="Bound">v</a>

<a id="2112" class="Keyword">infixl</a> <a id="2119" class="Number">4</a> <a id="2121" href="Data.List.Relation.Unary.Any.html#2125" class="Function Operator">_─_</a>
<a id="_─_"></a><a id="2125" href="Data.List.Relation.Unary.Any.html#2125" class="Function Operator">_─_</a> <a id="2129" class="Symbol">:</a> <a id="2131" class="Symbol">{</a><a id="2132" href="Data.List.Relation.Unary.Any.html#2132" class="Bound">P</a> <a id="2134" class="Symbol">:</a> <a id="2136" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2141" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a> <a id="2143" href="Data.List.Relation.Unary.Any.html#849" class="Generalizable">p</a><a id="2144" class="Symbol">}</a> <a id="2146" class="Symbol">→</a> <a id="2148" class="Symbol">∀</a> <a id="2150" href="Data.List.Relation.Unary.Any.html#2150" class="Bound">xs</a> <a id="2153" class="Symbol">→</a> <a id="2155" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2159" href="Data.List.Relation.Unary.Any.html#2132" class="Bound">P</a> <a id="2161" href="Data.List.Relation.Unary.Any.html#2150" class="Bound">xs</a> <a id="2164" class="Symbol">→</a> <a id="2166" href="Agda.Builtin.List.html#130" class="Datatype">List</a> <a id="2171" href="Data.List.Relation.Unary.Any.html#865" class="Generalizable">A</a>
<a id="2173" href="Data.List.Relation.Unary.Any.html#2173" class="Bound">xs</a> <a id="2176" href="Data.List.Relation.Unary.Any.html#2125" class="Function Operator">─</a> <a id="2178" href="Data.List.Relation.Unary.Any.html#2178" class="Bound">x∈xs</a> <a id="2183" class="Symbol">=</a> <a id="2185" href="Data.List.Relation.Unary.Any.html#2173" class="Bound">xs</a> <a id="2188" href="Data.List.Base.html#9547" class="Function Operator">List.─</a> <a id="2195" href="Data.List.Relation.Unary.Any.html#1826" class="Function">index</a> <a id="2201" href="Data.List.Relation.Unary.Any.html#2178" class="Bound">x∈xs</a>

<a id="2207" class="Comment">-- If any element satisfies P, then P is satisfied.</a>

<a id="satisfied"></a><a id="2260" href="Data.List.Relation.Unary.Any.html#2260" class="Function">satisfied</a> <a id="2270" class="Symbol">:</a> <a id="2272" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2276" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2278" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="2281" class="Symbol">→</a> <a id="2283" href="Data.Product.html#1378" class="Function">∃</a> <a id="2285" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a>
<a id="2287" href="Data.List.Relation.Unary.Any.html#2260" class="Function">satisfied</a> <a id="2297" class="Symbol">(</a><a id="2298" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="2303" href="Data.List.Relation.Unary.Any.html#2303" class="Bound">px</a><a id="2305" class="Symbol">)</a>   <a id="2309" class="Symbol">=</a> <a id="2311" class="Symbol">_</a> <a id="2313" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2315" href="Data.List.Relation.Unary.Any.html#2303" class="Bound">px</a>
<a id="2318" href="Data.List.Relation.Unary.Any.html#2260" class="Function">satisfied</a> <a id="2328" class="Symbol">(</a><a id="2329" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="2335" href="Data.List.Relation.Unary.Any.html#2335" class="Bound">pxs</a><a id="2338" class="Symbol">)</a> <a id="2340" class="Symbol">=</a> <a id="2342" href="Data.List.Relation.Unary.Any.html#2260" class="Function">satisfied</a> <a id="2352" href="Data.List.Relation.Unary.Any.html#2335" class="Bound">pxs</a>

<a id="toSum"></a><a id="2357" href="Data.List.Relation.Unary.Any.html#2357" class="Function">toSum</a> <a id="2363" class="Symbol">:</a> <a id="2365" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2369" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2371" class="Symbol">(</a><a id="2372" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="2374" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2376" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a><a id="2378" class="Symbol">)</a> <a id="2380" class="Symbol">→</a> <a id="2382" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2384" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="2386" href="Data.Sum.Base.html#743" class="Datatype Operator">⊎</a> <a id="2388" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2392" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2394" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a>
<a id="2397" href="Data.List.Relation.Unary.Any.html#2357" class="Function">toSum</a> <a id="2403" class="Symbol">(</a><a id="2404" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="2409" href="Data.List.Relation.Unary.Any.html#2409" class="Bound">px</a><a id="2411" class="Symbol">)</a>   <a id="2415" class="Symbol">=</a> <a id="2417" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2422" href="Data.List.Relation.Unary.Any.html#2409" class="Bound">px</a>
<a id="2425" href="Data.List.Relation.Unary.Any.html#2357" class="Function">toSum</a> <a id="2431" class="Symbol">(</a><a id="2432" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="2438" href="Data.List.Relation.Unary.Any.html#2438" class="Bound">pxs</a><a id="2441" class="Symbol">)</a> <a id="2443" class="Symbol">=</a> <a id="2445" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="2450" href="Data.List.Relation.Unary.Any.html#2438" class="Bound">pxs</a>

<a id="fromSum"></a><a id="2455" href="Data.List.Relation.Unary.Any.html#2455" class="Function">fromSum</a> <a id="2463" class="Symbol">:</a> <a id="2465" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2467" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="2469" href="Data.Sum.Base.html#743" class="Datatype Operator">⊎</a> <a id="2471" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2475" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2477" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a> <a id="2480" class="Symbol">→</a> <a id="2482" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2486" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2488" class="Symbol">(</a><a id="2489" href="Data.List.Relation.Unary.Any.html#898" class="Generalizable">x</a> <a id="2491" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2493" href="Data.List.Relation.Unary.Any.html#908" class="Generalizable">xs</a><a id="2495" class="Symbol">)</a>
<a id="2497" href="Data.List.Relation.Unary.Any.html#2455" class="Function">fromSum</a> <a id="2505" class="Symbol">(</a><a id="2506" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2511" href="Data.List.Relation.Unary.Any.html#2511" class="Bound">px</a><a id="2513" class="Symbol">)</a>  <a id="2516" class="Symbol">=</a> <a id="2518" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="2523" href="Data.List.Relation.Unary.Any.html#2511" class="Bound">px</a>
<a id="2526" href="Data.List.Relation.Unary.Any.html#2455" class="Function">fromSum</a> <a id="2534" class="Symbol">(</a><a id="2535" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="2540" href="Data.List.Relation.Unary.Any.html#2540" class="Bound">pxs</a><a id="2543" class="Symbol">)</a> <a id="2545" class="Symbol">=</a> <a id="2547" href="Data.List.Relation.Unary.Any.html#1281" class="InductiveConstructor">there</a> <a id="2553" href="Data.List.Relation.Unary.Any.html#2540" class="Bound">pxs</a>

<a id="2558" class="Comment">------------------------------------------------------------------------</a>
<a id="2631" class="Comment">-- Properties of predicates preserved by Any</a>

<a id="any?"></a><a id="2677" href="Data.List.Relation.Unary.Any.html#2677" class="Function">any?</a> <a id="2682" class="Symbol">:</a> <a id="2684" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2694" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2696" class="Symbol">→</a> <a id="2698" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2708" class="Symbol">(</a><a id="2709" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2713" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a><a id="2714" class="Symbol">)</a>
<a id="2716" href="Data.List.Relation.Unary.Any.html#2677" class="Function">any?</a> <a id="2721" href="Data.List.Relation.Unary.Any.html#2721" class="Bound">P?</a> <a id="2724" href="Agda.Builtin.List.html#167" class="InductiveConstructor">[]</a>       <a id="2733" class="Symbol">=</a> <a id="2735" href="Relation.Nullary.html#1694" class="InductiveConstructor">no</a> <a id="2738" class="Symbol">λ()</a>
<a id="2742" href="Data.List.Relation.Unary.Any.html#2677" class="Function">any?</a> <a id="2747" href="Data.List.Relation.Unary.Any.html#2747" class="Bound">P?</a> <a id="2750" class="Symbol">(</a><a id="2751" href="Data.List.Relation.Unary.Any.html#2751" class="Bound">x</a> <a id="2753" href="Agda.Builtin.List.html#182" class="InductiveConstructor Operator">∷</a> <a id="2755" href="Data.List.Relation.Unary.Any.html#2755" class="Bound">xs</a><a id="2757" class="Symbol">)</a> <a id="2759" class="Symbol">=</a> <a id="2761" href="Relation.Nullary.Decidable.Core.html#3645" class="Function">Dec.map′</a> <a id="2770" href="Data.List.Relation.Unary.Any.html#2455" class="Function">fromSum</a> <a id="2778" href="Data.List.Relation.Unary.Any.html#2357" class="Function">toSum</a> <a id="2784" class="Symbol">(</a><a id="2785" href="Data.List.Relation.Unary.Any.html#2747" class="Bound">P?</a> <a id="2788" href="Data.List.Relation.Unary.Any.html#2751" class="Bound">x</a> <a id="2790" href="Relation.Nullary.Sum.html#955" class="Function Operator">⊎-dec</a> <a id="2796" href="Data.List.Relation.Unary.Any.html#2677" class="Function">any?</a> <a id="2801" href="Data.List.Relation.Unary.Any.html#2747" class="Bound">P?</a> <a id="2804" href="Data.List.Relation.Unary.Any.html#2755" class="Bound">xs</a><a id="2806" class="Symbol">)</a>

<a id="satisfiable"></a><a id="2809" href="Data.List.Relation.Unary.Any.html#2809" class="Function">satisfiable</a> <a id="2821" class="Symbol">:</a> <a id="2823" href="Relation.Unary.html#3094" class="Function">Satisfiable</a> <a id="2835" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a> <a id="2837" class="Symbol">→</a> <a id="2839" href="Relation.Unary.html#3094" class="Function">Satisfiable</a> <a id="2851" class="Symbol">(</a><a id="2852" href="Data.List.Relation.Unary.Any.html#1165" class="Datatype">Any</a> <a id="2856" href="Data.List.Relation.Unary.Any.html#879" class="Generalizable">P</a><a id="2857" class="Symbol">)</a>
<a id="2859" href="Data.List.Relation.Unary.Any.html#2809" class="Function">satisfiable</a> <a id="2871" class="Symbol">(</a><a id="2872" href="Data.List.Relation.Unary.Any.html#2872" class="Bound">x</a> <a id="2874" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2876" href="Data.List.Relation.Unary.Any.html#2876" class="Bound">Px</a><a id="2878" class="Symbol">)</a> <a id="2880" class="Symbol">=</a> <a id="2882" href="Data.List.Base.html#5034" class="Function Operator">[</a> <a id="2884" href="Data.List.Relation.Unary.Any.html#2872" class="Bound">x</a> <a id="2886" href="Data.List.Base.html#5034" class="Function Operator">]</a> <a id="2888" href="Agda.Builtin.Sigma.html#218" class="InductiveConstructor Operator">,</a> <a id="2890" href="Data.List.Relation.Unary.Any.html#1228" class="InductiveConstructor">here</a> <a id="2895" href="Data.List.Relation.Unary.Any.html#2876" class="Bound">Px</a>


<a id="2900" class="Comment">------------------------------------------------------------------------</a>
<a id="2973" class="Comment">-- DEPRECATED</a>
<a id="2987" class="Comment">------------------------------------------------------------------------</a>
<a id="3060" class="Comment">-- Please use the new names as continuing support for the old names is</a>
<a id="3131" class="Comment">-- not guaranteed.</a>

<a id="3151" class="Comment">-- Version 1.4</a>

<a id="any"></a><a id="3167" href="Data.List.Relation.Unary.Any.html#3167" class="Function">any</a> <a id="3171" class="Symbol">=</a> <a id="3173" href="Data.List.Relation.Unary.Any.html#2677" class="Function">any?</a>
<a id="3178" class="Symbol">{-#</a> <a id="3182" class="Keyword">WARNING_ON_USAGE</a> <a id="3199" class="Pragma">any</a>
<a id="3203" class="String">&quot;Warning: any was deprecated in v1.4.
Please use any? instead.&quot;</a>
<a id="3267" class="Symbol">#-}</a>
</pre></body></html>