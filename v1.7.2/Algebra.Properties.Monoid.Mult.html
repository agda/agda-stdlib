<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Properties.Monoid.Mult</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Multiplication over a monoid (i.e. repeated addition)</a>
<a id="163" class="Comment">------------------------------------------------------------------------</a>

<a id="237" class="Symbol">{-#</a> <a id="241" class="Keyword">OPTIONS</a> <a id="249" class="Pragma">--cubical-compatible</a> <a id="270" class="Pragma">--safe</a> <a id="277" class="Symbol">#-}</a>

<a id="282" class="Keyword">open</a> <a id="287" class="Keyword">import</a> <a id="294" href="Algebra.Bundles.html" class="Module">Algebra.Bundles</a> <a id="310" class="Keyword">using</a> <a id="316" class="Symbol">(</a><a id="317" href="Algebra.Bundles.html#4430" class="Record">Monoid</a><a id="323" class="Symbol">)</a>
<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="351" class="Symbol">as</a> <a id="354" class="Module">ℕ</a> <a id="356" class="Keyword">using</a> <a id="362" class="Symbol">(</a><a id="363" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="364" class="Symbol">;</a> <a id="366" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a><a id="370" class="Symbol">;</a> <a id="372" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a><a id="375" class="Symbol">;</a> <a id="377" href="Data.Nat.Base.html#2484" class="Function">NonZero</a><a id="384" class="Symbol">)</a>
<a id="386" class="Keyword">open</a> <a id="391" class="Keyword">import</a> <a id="398" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="414" class="Keyword">open</a> <a id="419" class="Keyword">import</a> <a id="426" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="464" class="Symbol">as</a> <a id="467" class="Module">P</a> <a id="469" class="Keyword">using</a> <a id="475" class="Symbol">(</a><a id="476" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="479" class="Symbol">)</a>

<a id="482" class="Keyword">module</a> <a id="489" href="Algebra.Properties.Monoid.Mult.html" class="Module">Algebra.Properties.Monoid.Mult</a> <a id="520" class="Symbol">{</a><a id="521" href="Algebra.Properties.Monoid.Mult.html#521" class="Bound">a</a> <a id="523" href="Algebra.Properties.Monoid.Mult.html#523" class="Bound">ℓ</a><a id="524" class="Symbol">}</a> <a id="526" class="Symbol">(</a><a id="527" href="Algebra.Properties.Monoid.Mult.html#527" class="Bound">M</a> <a id="529" class="Symbol">:</a> <a id="531" href="Algebra.Bundles.html#4430" class="Record">Monoid</a> <a id="538" href="Algebra.Properties.Monoid.Mult.html#521" class="Bound">a</a> <a id="540" href="Algebra.Properties.Monoid.Mult.html#523" class="Bound">ℓ</a><a id="541" class="Symbol">)</a> <a id="543" class="Keyword">where</a>

<a id="550" class="Comment">-- View of the monoid operator as addition</a>
<a id="593" class="Keyword">open</a> <a id="598" href="Algebra.Bundles.html#4430" class="Module">Monoid</a> <a id="605" href="Algebra.Properties.Monoid.Mult.html#527" class="Bound">M</a>
  <a id="609" class="Keyword">renaming</a>
  <a id="620" class="Symbol">(</a> <a id="622" href="Algebra.Bundles.html#4559" class="Field Operator">_∙_</a>       <a id="632" class="Symbol">to</a> <a id="635" class="Field Operator">_+_</a>
  <a id="641" class="Symbol">;</a> <a id="643" href="Algebra.Structures.html#1219" class="Function">∙-cong</a>    <a id="653" class="Symbol">to</a> <a id="656" class="Function">+-cong</a>
  <a id="665" class="Symbol">;</a> <a id="667" href="Algebra.Structures.html#1430" class="Function">∙-congʳ</a>   <a id="677" class="Symbol">to</a> <a id="680" class="Function">+-congʳ</a>
  <a id="690" class="Symbol">;</a> <a id="692" href="Algebra.Structures.html#1369" class="Function">∙-congˡ</a>   <a id="702" class="Symbol">to</a> <a id="705" class="Function">+-congˡ</a>
  <a id="715" class="Symbol">;</a> <a id="717" href="Algebra.Structures.html#2972" class="Function">identityˡ</a> <a id="727" class="Symbol">to</a> <a id="730" class="Function">+-identityˡ</a>
  <a id="744" class="Symbol">;</a> <a id="746" href="Algebra.Structures.html#3033" class="Function">identityʳ</a> <a id="756" class="Symbol">to</a> <a id="759" class="Function">+-identityʳ</a>
  <a id="773" class="Symbol">;</a> <a id="775" href="Algebra.Structures.html#1876" class="Function">assoc</a>     <a id="785" class="Symbol">to</a> <a id="788" class="Function">+-assoc</a>
  <a id="798" class="Symbol">;</a> <a id="800" href="Algebra.Bundles.html#4586" class="Field">ε</a>         <a id="810" class="Symbol">to</a> <a id="813" class="Field">0#</a>
  <a id="818" class="Symbol">)</a>

<a id="821" class="Keyword">open</a> <a id="826" class="Keyword">import</a> <a id="833" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="866" href="Algebra.Structures.html#1294" class="Function">setoid</a>

<a id="874" class="Keyword">open</a> <a id="879" class="Keyword">import</a> <a id="886" href="Algebra.Definitions.html" class="Module">Algebra.Definitions</a> <a id="906" href="Algebra.Bundles.html#4530" class="Field Operator">_≈_</a>

<a id="911" class="Comment">------------------------------------------------------------------------</a>
<a id="984" class="Comment">-- Definition</a>

<a id="999" class="Keyword">open</a> <a id="1004" class="Keyword">import</a> <a id="1011" href="Algebra.Definitions.RawMonoid.html" class="Module">Algebra.Definitions.RawMonoid</a> <a id="1041" href="Algebra.Bundles.html#4823" class="Function">rawMonoid</a> <a id="1051" class="Keyword">public</a>
  <a id="1060" class="Keyword">using</a> <a id="1066" class="Symbol">(</a><a id="1067" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">_×_</a><a id="1070" class="Symbol">)</a>

<a id="1073" class="Comment">------------------------------------------------------------------------</a>
<a id="1146" class="Comment">-- Properties of _×_</a>

<a id="×-congʳ"></a><a id="1168" href="Algebra.Properties.Monoid.Mult.html#1168" class="Function">×-congʳ</a> <a id="1176" class="Symbol">:</a> <a id="1178" class="Symbol">∀</a> <a id="1180" href="Algebra.Properties.Monoid.Mult.html#1180" class="Bound">n</a> <a id="1182" class="Symbol">→</a> <a id="1184" class="Symbol">(</a><a id="1185" href="Algebra.Properties.Monoid.Mult.html#1180" class="Bound">n</a> <a id="1187" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×_</a><a id="1189" class="Symbol">)</a> <a id="1191" href="Relation.Binary.Core.html#1572" class="Function Operator">Preserves</a> <a id="1201" href="Algebra.Bundles.html#4530" class="Field Operator">_≈_</a> <a id="1205" href="Relation.Binary.Core.html#1572" class="Function Operator">⟶</a> <a id="1207" href="Algebra.Bundles.html#4530" class="Field Operator">_≈_</a>
<a id="1211" href="Algebra.Properties.Monoid.Mult.html#1168" class="Function">×-congʳ</a> <a id="1219" class="Number">0</a>       <a id="1227" href="Algebra.Properties.Monoid.Mult.html#1227" class="Bound">x≈x′</a> <a id="1232" class="Symbol">=</a> <a id="1234" href="Relation.Binary.Structures.html#1577" class="Function">refl</a>
<a id="1239" href="Algebra.Properties.Monoid.Mult.html#1168" class="Function">×-congʳ</a> <a id="1247" class="Symbol">(</a><a id="1248" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1252" href="Algebra.Properties.Monoid.Mult.html#1252" class="Bound">n</a><a id="1253" class="Symbol">)</a> <a id="1255" href="Algebra.Properties.Monoid.Mult.html#1255" class="Bound">x≈x′</a> <a id="1260" class="Symbol">=</a> <a id="1262" href="Algebra.Properties.Monoid.Mult.html#656" class="Function">+-cong</a> <a id="1269" href="Algebra.Properties.Monoid.Mult.html#1255" class="Bound">x≈x′</a> <a id="1274" class="Symbol">(</a><a id="1275" href="Algebra.Properties.Monoid.Mult.html#1168" class="Function">×-congʳ</a> <a id="1283" href="Algebra.Properties.Monoid.Mult.html#1252" class="Bound">n</a> <a id="1285" href="Algebra.Properties.Monoid.Mult.html#1255" class="Bound">x≈x′</a><a id="1289" class="Symbol">)</a>

<a id="×-cong"></a><a id="1292" href="Algebra.Properties.Monoid.Mult.html#1292" class="Function">×-cong</a> <a id="1299" class="Symbol">:</a> <a id="1301" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">_×_</a> <a id="1305" href="Relation.Binary.Core.html#1698" class="Function Operator">Preserves₂</a> <a id="1316" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a> <a id="1320" href="Relation.Binary.Core.html#1698" class="Function Operator">⟶</a> <a id="1322" href="Algebra.Bundles.html#4530" class="Field Operator">_≈_</a> <a id="1326" href="Relation.Binary.Core.html#1698" class="Function Operator">⟶</a> <a id="1328" href="Algebra.Bundles.html#4530" class="Field Operator">_≈_</a>
<a id="1332" href="Algebra.Properties.Monoid.Mult.html#1292" class="Function">×-cong</a> <a id="1339" class="Symbol">{</a><a id="1340" href="Algebra.Properties.Monoid.Mult.html#1340" class="Bound">n</a><a id="1341" class="Symbol">}</a> <a id="1343" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">P.refl</a> <a id="1350" href="Algebra.Properties.Monoid.Mult.html#1350" class="Bound">x≈x′</a> <a id="1355" class="Symbol">=</a> <a id="1357" href="Algebra.Properties.Monoid.Mult.html#1168" class="Function">×-congʳ</a> <a id="1365" href="Algebra.Properties.Monoid.Mult.html#1340" class="Bound">n</a> <a id="1367" href="Algebra.Properties.Monoid.Mult.html#1350" class="Bound">x≈x′</a>

<a id="1373" class="Comment">-- _×_ is homomorphic with respect to _ℕ+_/_+_.</a>

<a id="×-homo-+"></a><a id="1422" href="Algebra.Properties.Monoid.Mult.html#1422" class="Function">×-homo-+</a> <a id="1431" class="Symbol">:</a> <a id="1433" class="Symbol">∀</a> <a id="1435" href="Algebra.Properties.Monoid.Mult.html#1435" class="Bound">x</a> <a id="1437" href="Algebra.Properties.Monoid.Mult.html#1437" class="Bound">m</a> <a id="1439" href="Algebra.Properties.Monoid.Mult.html#1439" class="Bound">n</a> <a id="1441" class="Symbol">→</a> <a id="1443" class="Symbol">(</a><a id="1444" href="Algebra.Properties.Monoid.Mult.html#1437" class="Bound">m</a> <a id="1446" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">ℕ.+</a> <a id="1450" href="Algebra.Properties.Monoid.Mult.html#1439" class="Bound">n</a><a id="1451" class="Symbol">)</a> <a id="1453" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1455" href="Algebra.Properties.Monoid.Mult.html#1435" class="Bound">x</a> <a id="1457" href="Algebra.Bundles.html#4530" class="Field Operator">≈</a> <a id="1459" href="Algebra.Properties.Monoid.Mult.html#1437" class="Bound">m</a> <a id="1461" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1463" href="Algebra.Properties.Monoid.Mult.html#1435" class="Bound">x</a> <a id="1465" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1467" href="Algebra.Properties.Monoid.Mult.html#1439" class="Bound">n</a> <a id="1469" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1471" href="Algebra.Properties.Monoid.Mult.html#1435" class="Bound">x</a>
<a id="1473" href="Algebra.Properties.Monoid.Mult.html#1422" class="Function">×-homo-+</a> <a id="1482" href="Algebra.Properties.Monoid.Mult.html#1482" class="Bound">x</a> <a id="1484" class="Number">0</a>       <a id="1492" href="Algebra.Properties.Monoid.Mult.html#1492" class="Bound">n</a> <a id="1494" class="Symbol">=</a> <a id="1496" href="Relation.Binary.Structures.html#1603" class="Function">sym</a> <a id="1500" class="Symbol">(</a><a id="1501" href="Algebra.Properties.Monoid.Mult.html#730" class="Function">+-identityˡ</a> <a id="1513" class="Symbol">(</a><a id="1514" href="Algebra.Properties.Monoid.Mult.html#1492" class="Bound">n</a> <a id="1516" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1518" href="Algebra.Properties.Monoid.Mult.html#1482" class="Bound">x</a><a id="1519" class="Symbol">))</a>
<a id="1522" href="Algebra.Properties.Monoid.Mult.html#1422" class="Function">×-homo-+</a> <a id="1531" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1533" class="Symbol">(</a><a id="1534" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1538" href="Algebra.Properties.Monoid.Mult.html#1538" class="Bound">m</a><a id="1539" class="Symbol">)</a> <a id="1541" href="Algebra.Properties.Monoid.Mult.html#1541" class="Bound">n</a> <a id="1543" class="Symbol">=</a> <a id="1545" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="1553" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1555" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1557" class="Symbol">(</a><a id="1558" href="Algebra.Properties.Monoid.Mult.html#1538" class="Bound">m</a> <a id="1560" href="Agda.Builtin.Nat.html#319" class="Primitive Operator">ℕ.+</a> <a id="1564" href="Algebra.Properties.Monoid.Mult.html#1541" class="Bound">n</a><a id="1565" class="Symbol">)</a> <a id="1567" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1569" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a>    <a id="1574" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="1577" href="Algebra.Properties.Monoid.Mult.html#656" class="Function">+-cong</a> <a id="1584" href="Relation.Binary.Structures.html#1577" class="Function">refl</a> <a id="1589" class="Symbol">(</a><a id="1590" href="Algebra.Properties.Monoid.Mult.html#1422" class="Function">×-homo-+</a> <a id="1599" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1601" href="Algebra.Properties.Monoid.Mult.html#1538" class="Bound">m</a> <a id="1603" href="Algebra.Properties.Monoid.Mult.html#1541" class="Bound">n</a><a id="1604" class="Symbol">)</a> <a id="1606" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="1610" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1612" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1614" class="Symbol">(</a><a id="1615" href="Algebra.Properties.Monoid.Mult.html#1538" class="Bound">m</a> <a id="1617" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1619" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1621" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1623" href="Algebra.Properties.Monoid.Mult.html#1541" class="Bound">n</a> <a id="1625" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1627" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a><a id="1628" class="Symbol">)</a>  <a id="1631" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="1634" href="Relation.Binary.Structures.html#1603" class="Function">sym</a> <a id="1638" class="Symbol">(</a><a id="1639" href="Algebra.Properties.Monoid.Mult.html#788" class="Function">+-assoc</a> <a id="1647" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1649" class="Symbol">(</a><a id="1650" href="Algebra.Properties.Monoid.Mult.html#1538" class="Bound">m</a> <a id="1652" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1654" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a><a id="1655" class="Symbol">)</a> <a id="1657" class="Symbol">(</a><a id="1658" href="Algebra.Properties.Monoid.Mult.html#1541" class="Bound">n</a> <a id="1660" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1662" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a><a id="1663" class="Symbol">))</a> <a id="1666" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="1670" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1672" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1674" href="Algebra.Properties.Monoid.Mult.html#1538" class="Bound">m</a> <a id="1676" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1678" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a> <a id="1680" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1682" href="Algebra.Properties.Monoid.Mult.html#1541" class="Bound">n</a> <a id="1684" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1686" href="Algebra.Properties.Monoid.Mult.html#1531" class="Bound">x</a>    <a id="1691" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="×-idem"></a><a id="1694" href="Algebra.Properties.Monoid.Mult.html#1694" class="Function">×-idem</a> <a id="1701" class="Symbol">:</a> <a id="1703" class="Symbol">∀</a> <a id="1705" class="Symbol">{</a><a id="1706" href="Algebra.Properties.Monoid.Mult.html#1706" class="Bound">c</a><a id="1707" class="Symbol">}</a> <a id="1709" class="Symbol">→</a> <a id="1711" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">_+_</a> <a id="1715" href="Algebra.Definitions.html#2587" class="Function Operator">IdempotentOn</a> <a id="1728" href="Algebra.Properties.Monoid.Mult.html#1706" class="Bound">c</a> <a id="1730" class="Symbol">→</a>
         <a id="1741" class="Symbol">∀</a> <a id="1743" href="Algebra.Properties.Monoid.Mult.html#1743" class="Bound">n</a> <a id="1745" class="Symbol">→</a> <a id="1747" class="Symbol">.{{</a><a id="1750" href="Algebra.Properties.Monoid.Mult.html#1750" class="Bound">_</a> <a id="1752" class="Symbol">:</a> <a id="1754" href="Data.Nat.Base.html#2484" class="Function">NonZero</a> <a id="1762" href="Algebra.Properties.Monoid.Mult.html#1743" class="Bound">n</a><a id="1763" class="Symbol">}}</a> <a id="1766" class="Symbol">→</a> <a id="1768" href="Algebra.Properties.Monoid.Mult.html#1743" class="Bound">n</a> <a id="1770" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1772" href="Algebra.Properties.Monoid.Mult.html#1706" class="Bound">c</a> <a id="1774" href="Algebra.Bundles.html#4530" class="Field Operator">≈</a> <a id="1776" href="Algebra.Properties.Monoid.Mult.html#1706" class="Bound">c</a>
<a id="1778" href="Algebra.Properties.Monoid.Mult.html#1694" class="Function">×-idem</a> <a id="1785" class="Symbol">{</a><a id="1786" href="Algebra.Properties.Monoid.Mult.html#1786" class="Bound">c</a><a id="1787" class="Symbol">}</a> <a id="1789" href="Algebra.Properties.Monoid.Mult.html#1789" class="Bound">idem</a> <a id="1794" class="Symbol">(</a><a id="1795" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1799" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a><a id="1803" class="Symbol">)</a>    <a id="1808" class="Symbol">=</a> <a id="1810" href="Algebra.Properties.Monoid.Mult.html#759" class="Function">+-identityʳ</a> <a id="1822" href="Algebra.Properties.Monoid.Mult.html#1786" class="Bound">c</a>
<a id="1824" href="Algebra.Properties.Monoid.Mult.html#1694" class="Function">×-idem</a> <a id="1831" class="Symbol">{</a><a id="1832" href="Algebra.Properties.Monoid.Mult.html#1832" class="Bound">c</a><a id="1833" class="Symbol">}</a> <a id="1835" href="Algebra.Properties.Monoid.Mult.html#1835" class="Bound">idem</a> <a id="1840" class="Symbol">(</a><a id="1841" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1845" class="Symbol">(</a><a id="1846" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1850" href="Algebra.Properties.Monoid.Mult.html#1850" class="Bound">n</a><a id="1851" class="Symbol">))</a> <a id="1854" class="Symbol">=</a> <a id="1856" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="1864" href="Algebra.Properties.Monoid.Mult.html#1832" class="Bound">c</a> <a id="1866" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1868" class="Symbol">(</a><a id="1869" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1873" href="Algebra.Properties.Monoid.Mult.html#1850" class="Bound">n</a> <a id="1875" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1877" href="Algebra.Properties.Monoid.Mult.html#1832" class="Bound">c</a><a id="1878" class="Symbol">)</a> <a id="1880" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="1883" href="Algebra.Properties.Monoid.Mult.html#705" class="Function">+-congˡ</a> <a id="1891" class="Symbol">(</a><a id="1892" href="Algebra.Properties.Monoid.Mult.html#1694" class="Function">×-idem</a> <a id="1899" href="Algebra.Properties.Monoid.Mult.html#1835" class="Bound">idem</a> <a id="1904" class="Symbol">(</a><a id="1905" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="1909" href="Algebra.Properties.Monoid.Mult.html#1850" class="Bound">n</a><a id="1910" class="Symbol">)</a> <a id="1912" class="Symbol">)</a> <a id="1914" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="1918" href="Algebra.Properties.Monoid.Mult.html#1832" class="Bound">c</a> <a id="1920" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="1922" href="Algebra.Properties.Monoid.Mult.html#1832" class="Bound">c</a>           <a id="1934" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="1937" href="Algebra.Properties.Monoid.Mult.html#1835" class="Bound">idem</a> <a id="1942" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="1946" href="Algebra.Properties.Monoid.Mult.html#1832" class="Bound">c</a>               <a id="1962" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>

<a id="×-assocˡ"></a><a id="1965" href="Algebra.Properties.Monoid.Mult.html#1965" class="Function">×-assocˡ</a> <a id="1974" class="Symbol">:</a> <a id="1976" class="Symbol">∀</a> <a id="1978" href="Algebra.Properties.Monoid.Mult.html#1978" class="Bound">x</a> <a id="1980" href="Algebra.Properties.Monoid.Mult.html#1980" class="Bound">m</a> <a id="1982" href="Algebra.Properties.Monoid.Mult.html#1982" class="Bound">n</a> <a id="1984" class="Symbol">→</a> <a id="1986" href="Algebra.Properties.Monoid.Mult.html#1980" class="Bound">m</a> <a id="1988" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1990" class="Symbol">(</a><a id="1991" href="Algebra.Properties.Monoid.Mult.html#1982" class="Bound">n</a> <a id="1993" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="1995" href="Algebra.Properties.Monoid.Mult.html#1978" class="Bound">x</a><a id="1996" class="Symbol">)</a> <a id="1998" href="Algebra.Bundles.html#4530" class="Field Operator">≈</a> <a id="2000" class="Symbol">(</a><a id="2001" href="Algebra.Properties.Monoid.Mult.html#1980" class="Bound">m</a> <a id="2003" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">ℕ.*</a> <a id="2007" href="Algebra.Properties.Monoid.Mult.html#1982" class="Bound">n</a><a id="2008" class="Symbol">)</a> <a id="2010" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2012" href="Algebra.Properties.Monoid.Mult.html#1978" class="Bound">x</a>
<a id="2014" href="Algebra.Properties.Monoid.Mult.html#1965" class="Function">×-assocˡ</a> <a id="2023" href="Algebra.Properties.Monoid.Mult.html#2023" class="Bound">x</a> <a id="2025" href="Agda.Builtin.Nat.html#204" class="InductiveConstructor">zero</a>    <a id="2033" href="Algebra.Properties.Monoid.Mult.html#2033" class="Bound">n</a> <a id="2035" class="Symbol">=</a> <a id="2037" href="Relation.Binary.Structures.html#1577" class="Function">refl</a>
<a id="2042" href="Algebra.Properties.Monoid.Mult.html#1965" class="Function">×-assocˡ</a> <a id="2051" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a> <a id="2053" class="Symbol">(</a><a id="2054" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2058" href="Algebra.Properties.Monoid.Mult.html#2058" class="Bound">m</a><a id="2059" class="Symbol">)</a> <a id="2061" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a> <a id="2063" class="Symbol">=</a> <a id="2065" href="Relation.Binary.Reasoning.Base.Single.html#1925" class="Function Operator">begin</a>
  <a id="2073" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a> <a id="2075" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2077" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a> <a id="2079" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="2081" href="Algebra.Properties.Monoid.Mult.html#2058" class="Bound">m</a> <a id="2083" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2085" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a> <a id="2087" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2089" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a>     <a id="2095" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">≈⟨</a> <a id="2098" href="Algebra.Properties.Monoid.Mult.html#705" class="Function">+-congˡ</a> <a id="2106" class="Symbol">(</a><a id="2107" href="Algebra.Properties.Monoid.Mult.html#1965" class="Function">×-assocˡ</a> <a id="2116" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a> <a id="2118" href="Algebra.Properties.Monoid.Mult.html#2058" class="Bound">m</a> <a id="2120" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a><a id="2121" class="Symbol">)</a> <a id="2123" href="Relation.Binary.Reasoning.Setoid.html#1061" class="Function">⟩</a>
  <a id="2127" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a> <a id="2129" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2131" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a> <a id="2133" href="Algebra.Properties.Monoid.Mult.html#635" class="Field Operator">+</a> <a id="2135" class="Symbol">(</a><a id="2136" href="Algebra.Properties.Monoid.Mult.html#2058" class="Bound">m</a> <a id="2138" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">ℕ.*</a> <a id="2142" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a><a id="2143" class="Symbol">)</a> <a id="2145" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2147" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a> <a id="2149" href="Relation.Binary.Reasoning.Setoid.html#1162" class="Function">≈˘⟨</a> <a id="2153" href="Algebra.Properties.Monoid.Mult.html#1422" class="Function">×-homo-+</a> <a id="2162" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a> <a id="2164" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a> <a id="2166" class="Symbol">(</a><a id="2167" href="Algebra.Properties.Monoid.Mult.html#2058" class="Bound">m</a> <a id="2169" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">ℕ.*</a> <a id="2173" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a><a id="2174" class="Symbol">)</a> <a id="2176" href="Relation.Binary.Reasoning.Setoid.html#1162" class="Function">⟩</a>
  <a id="2180" class="Symbol">(</a><a id="2181" href="Agda.Builtin.Nat.html#217" class="InductiveConstructor">suc</a> <a id="2185" href="Algebra.Properties.Monoid.Mult.html#2058" class="Bound">m</a> <a id="2187" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">ℕ.*</a> <a id="2191" href="Algebra.Properties.Monoid.Mult.html#2061" class="Bound">n</a><a id="2192" class="Symbol">)</a> <a id="2194" href="Algebra.Definitions.RawMonoid.html#1089" class="Function Operator">×</a> <a id="2196" href="Algebra.Properties.Monoid.Mult.html#2051" class="Bound">x</a>     <a id="2202" href="Relation.Binary.Reasoning.Base.Single.html#2564" class="Function Operator">∎</a>
</pre></body></html>