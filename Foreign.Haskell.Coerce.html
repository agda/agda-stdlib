<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Foreign.Haskell.Coerce</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Zero-cost coercion to cross the FFI boundary</a>
<a id="154" class="Comment">------------------------------------------------------------------------</a>

<a id="228" class="Symbol">{-#</a> <a id="232" class="Keyword">OPTIONS</a> <a id="240" class="Pragma">--without-K</a> <a id="252" class="Symbol">#-}</a>

<a id="257" class="Keyword">module</a> <a id="264" href="Foreign.Haskell.Coerce.html" class="Module">Foreign.Haskell.Coerce</a> <a id="287" class="Keyword">where</a>

<a id="294" class="Comment">------------------------------------------------------------------------</a>
<a id="367" class="Comment">-- Motivation</a>

<a id="382" class="Comment">-- Problem: No COMPILE directives for common inductive types</a>

<a id="444" class="Comment">-- In order to guarantee that the vast majority of the libary is</a>
<a id="509" class="Comment">-- considered safe by Agda, we had to remove the COMPILE pragmas</a>
<a id="574" class="Comment">-- associated to common inductive types.</a>
<a id="615" class="Comment">-- These directives cannot possibly be checked by the compiler and</a>
<a id="682" class="Comment">-- the user could define unsound mappings (e.g. swapping Bool&#39;s</a>
<a id="746" class="Comment">-- true and false).</a>

<a id="767" class="Comment">-- Solution: Essentially identical definitions + zero-cost coercions</a>

<a id="837" class="Comment">-- To solve this problem, we have introduced a number of essentially</a>
<a id="906" class="Comment">-- identical definitions in the Foreign.Haskell.* modules. However</a>
<a id="973" class="Comment">-- converting back and forth between the FFI-friendly type and its</a>
<a id="1040" class="Comment">-- safe counterpart would take linear time.</a>
<a id="1084" class="Comment">-- This module defines zero cost coercions between these types.</a>

<a id="1149" class="Comment">------------------------------------------------------------------------</a>
<a id="1222" class="Comment">-- Definition</a>

<a id="1237" class="Keyword">open</a> <a id="1242" class="Keyword">import</a> <a id="1249" href="Level.html" class="Module">Level</a> <a id="1255" class="Keyword">using</a> <a id="1261" class="Symbol">(</a><a id="1262" href="Agda.Primitive.html#408" class="Postulate">Level</a><a id="1267" class="Symbol">;</a> <a id="1269" href="Agda.Primitive.html#657" class="Primitive Operator">_⊔_</a><a id="1272" class="Symbol">)</a>
<a id="1274" class="Keyword">open</a> <a id="1279" class="Keyword">import</a> <a id="1286" href="Agda.Builtin.Nat.html" class="Module">Agda.Builtin.Nat</a>
<a id="1303" class="Keyword">open</a> <a id="1308" class="Keyword">import</a> <a id="1315" href="Agda.Builtin.Int.html" class="Module">Agda.Builtin.Int</a>

<a id="1333" class="Keyword">import</a> <a id="1340" href="IO.Primitive.html" class="Module">IO.Primitive</a>    <a id="1356" class="Symbol">as</a> <a id="1359" class="Module">STD</a>
<a id="1363" class="Keyword">import</a> <a id="1370" href="Data.List.Base.html" class="Module">Data.List.Base</a>  <a id="1386" class="Symbol">as</a> <a id="1389" class="Module">STD</a>
<a id="1393" class="Keyword">import</a> <a id="1400" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="1416" class="Symbol">as</a> <a id="1419" class="Module">STD</a>
<a id="1423" class="Keyword">import</a> <a id="1430" href="Data.Product.html" class="Module">Data.Product</a>    <a id="1446" class="Symbol">as</a> <a id="1449" class="Module">STD</a>

<a id="1454" class="Keyword">import</a> <a id="1461" href="Foreign.Haskell.Maybe.html" class="Module">Foreign.Haskell.Maybe</a> <a id="1483" class="Symbol">as</a> <a id="1486" class="Module">FFI</a>
<a id="1490" class="Keyword">import</a> <a id="1497" href="Foreign.Haskell.Pair.html" class="Module">Foreign.Haskell.Pair</a>  <a id="1519" class="Symbol">as</a> <a id="1522" class="Module">FFI</a>

<a id="1527" class="Keyword">private</a>
  <a id="1537" class="Keyword">variable</a>
    <a id="1550" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="1552" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="1554" href="Foreign.Haskell.Coerce.html#1554" class="Generalizable">c</a> <a id="1556" href="Foreign.Haskell.Coerce.html#1556" class="Generalizable">d</a> <a id="1558" href="Foreign.Haskell.Coerce.html#1558" class="Generalizable">e</a> <a id="1560" href="Foreign.Haskell.Coerce.html#1560" class="Generalizable">f</a> <a id="1562" class="Symbol">:</a> <a id="1564" href="Agda.Primitive.html#408" class="Postulate">Level</a>
    <a id="1574" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a> <a id="1576" class="Symbol">:</a> <a id="1578" class="PrimitiveType">Set</a> <a id="1582" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a>
    <a id="1588" href="Foreign.Haskell.Coerce.html#1588" class="Generalizable">B</a> <a id="1590" class="Symbol">:</a> <a id="1592" class="PrimitiveType">Set</a> <a id="1596" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a>
    <a id="1602" href="Foreign.Haskell.Coerce.html#1602" class="Generalizable">C</a> <a id="1604" class="Symbol">:</a> <a id="1606" class="PrimitiveType">Set</a> <a id="1610" href="Foreign.Haskell.Coerce.html#1554" class="Generalizable">c</a>
    <a id="1616" href="Foreign.Haskell.Coerce.html#1616" class="Generalizable">D</a> <a id="1618" class="Symbol">:</a> <a id="1620" class="PrimitiveType">Set</a> <a id="1624" href="Foreign.Haskell.Coerce.html#1556" class="Generalizable">d</a>

<a id="1627" class="Comment">-- We define a simple indexed datatype `Coercible`. A value of `Coercible A B`</a>
<a id="1706" class="Comment">-- is a proof that ̀A` and `B` have the same underlying runtime representation.</a>
<a id="1786" class="Comment">-- The only possible proof is an incantation from the implementer begging to</a>
<a id="1863" class="Comment">-- be trusted.</a>

<a id="1879" class="Comment">-- We need this type to be concrete so that overlapping instances can be checked</a>
<a id="1960" class="Comment">-- for equality: we do not care what proof we get as long as we get one.</a>

<a id="2034" class="Comment">-- We need for it to be a data type rather than a record type so that Agda does</a>
<a id="2114" class="Comment">-- not mistakenly build arbitrary instances by η-expansion.</a>

<a id="2175" class="Keyword">data</a> <a id="Coercible"></a><a id="2180" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="2190" class="Symbol">(</a><a id="2191" href="Foreign.Haskell.Coerce.html#2191" class="Bound">A</a> <a id="2193" class="Symbol">:</a> <a id="2195" class="PrimitiveType">Set</a> <a id="2199" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a><a id="2200" class="Symbol">)</a> <a id="2202" class="Symbol">(</a><a id="2203" href="Foreign.Haskell.Coerce.html#2203" class="Bound">B</a> <a id="2205" class="Symbol">:</a> <a id="2207" class="PrimitiveType">Set</a> <a id="2211" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a><a id="2212" class="Symbol">)</a> <a id="2214" class="Symbol">:</a> <a id="2216" class="PrimitiveType">Set</a> <a id="2220" class="Keyword">where</a>
  <a id="Coercible.TrustMe"></a><a id="2228" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a> <a id="2236" class="Symbol">:</a> <a id="2238" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="2248" href="Foreign.Haskell.Coerce.html#2191" class="Bound">A</a> <a id="2250" href="Foreign.Haskell.Coerce.html#2203" class="Bound">B</a>

<a id="2253" class="Symbol">{-#</a> <a id="2257" class="Keyword">FOREIGN</a> <a id="2265" class="Pragma">GHC</a> <a id="2269" class="Pragma">data</a> <a id="2274" class="Pragma">AgdaCoercible</a> <a id="2288" class="Pragma">l1</a> <a id="2291" class="Pragma">l2</a> <a id="2294" class="Pragma">a</a> <a id="2296" class="Pragma">b</a> <a id="2298" class="Pragma">=</a> <a id="2300" class="Pragma">TrustMe</a> <a id="2308" class="Symbol">#-}</a>
<a id="2312" class="Symbol">{-#</a> <a id="2316" class="Keyword">COMPILE</a> <a id="2324" class="Pragma">GHC</a> <a id="2328" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="2338" class="Pragma">=</a> <a id="2340" class="Pragma">data</a> <a id="2345" class="Pragma">AgdaCoercible</a> <a id="2359" class="Pragma">(TrustMe)</a> <a id="2369" class="Symbol">#-}</a>

<a id="2374" class="Comment">-- Once we get our hands on a proof that `Coercible A B` we postulate that it</a>
<a id="2452" class="Comment">-- is safe to convert an `A` into a `B`. This is done under the hood by using</a>
<a id="2530" class="Comment">-- `unsafeCoerce`.</a>

<a id="2550" class="Keyword">postulate</a> <a id="coerce"></a><a id="2560" href="Foreign.Haskell.Coerce.html#2560" class="Postulate">coerce</a> <a id="2567" class="Symbol">:</a> <a id="2569" class="Symbol">{{</a><a id="2571" href="Foreign.Haskell.Coerce.html#2571" class="Bound">_</a> <a id="2573" class="Symbol">:</a> <a id="2575" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="2585" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a> <a id="2587" href="Foreign.Haskell.Coerce.html#1588" class="Generalizable">B</a><a id="2588" class="Symbol">}}</a> <a id="2591" class="Symbol">→</a> <a id="2593" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a> <a id="2595" class="Symbol">→</a> <a id="2597" href="Foreign.Haskell.Coerce.html#1588" class="Generalizable">B</a>

<a id="2600" class="Symbol">{-#</a> <a id="2604" class="Keyword">FOREIGN</a> <a id="2612" class="Pragma">GHC</a> <a id="2616" class="Pragma">import</a> <a id="2623" class="Pragma">Unsafe.Coerce</a> <a id="2637" class="Symbol">#-}</a>
<a id="2641" class="Symbol">{-#</a> <a id="2645" class="Keyword">COMPILE</a> <a id="2653" class="Pragma">GHC</a> <a id="2657" href="Foreign.Haskell.Coerce.html#2560" class="Postulate">coerce</a> <a id="2664" class="Pragma">=</a> <a id="2666" class="Pragma">\</a> <a id="2668" class="Pragma">_</a> <a id="2670" class="Pragma">_</a> <a id="2672" class="Pragma">_</a> <a id="2674" class="Pragma">_</a> <a id="2676" class="Pragma">_</a> <a id="2678" class="Pragma">-&gt;</a> <a id="2681" class="Pragma">unsafeCoerce</a> <a id="2694" class="Symbol">#-}</a>

<a id="2699" class="Comment">------------------------------------------------------------------------</a>
<a id="2772" class="Comment">-- Unary and binary variants for covariant type constructors</a>

<a id="Coercible₁"></a><a id="2834" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="2845" class="Symbol">:</a> <a id="2847" class="Symbol">∀</a> <a id="2849" href="Foreign.Haskell.Coerce.html#2849" class="Bound">a</a> <a id="2851" href="Foreign.Haskell.Coerce.html#2851" class="Bound">c</a> <a id="2853" class="Symbol">→</a> <a id="2855" class="Symbol">(</a><a id="2856" href="Foreign.Haskell.Coerce.html#2856" class="Bound">T</a> <a id="2858" class="Symbol">:</a> <a id="2860" class="PrimitiveType">Set</a> <a id="2864" href="Foreign.Haskell.Coerce.html#2849" class="Bound">a</a> <a id="2866" class="Symbol">→</a> <a id="2868" class="PrimitiveType">Set</a> <a id="2872" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a><a id="2873" class="Symbol">)</a> <a id="2875" class="Symbol">(</a><a id="2876" href="Foreign.Haskell.Coerce.html#2876" class="Bound">U</a> <a id="2878" class="Symbol">:</a> <a id="2880" class="PrimitiveType">Set</a> <a id="2884" href="Foreign.Haskell.Coerce.html#2851" class="Bound">c</a> <a id="2886" class="Symbol">→</a> <a id="2888" class="PrimitiveType">Set</a> <a id="2892" href="Foreign.Haskell.Coerce.html#1556" class="Generalizable">d</a><a id="2893" class="Symbol">)</a> <a id="2895" class="Symbol">→</a> <a id="2897" class="PrimitiveType">Set</a> <a id="2901" class="Symbol">_</a>
<a id="2903" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="2914" class="Symbol">_</a> <a id="2916" class="Symbol">_</a> <a id="2918" href="Foreign.Haskell.Coerce.html#2918" class="Bound">T</a> <a id="2920" href="Foreign.Haskell.Coerce.html#2920" class="Bound">U</a> <a id="2922" class="Symbol">=</a> <a id="2924" class="Symbol">∀</a> <a id="2926" class="Symbol">{</a><a id="2927" href="Foreign.Haskell.Coerce.html#2927" class="Bound">A</a> <a id="2929" href="Foreign.Haskell.Coerce.html#2929" class="Bound">B</a><a id="2930" class="Symbol">}</a> <a id="2932" class="Symbol">→</a> <a id="2934" class="Symbol">{{</a><a id="2936" href="Foreign.Haskell.Coerce.html#2936" class="Bound">_</a> <a id="2938" class="Symbol">:</a> <a id="2940" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="2950" href="Foreign.Haskell.Coerce.html#2927" class="Bound">A</a> <a id="2952" href="Foreign.Haskell.Coerce.html#2929" class="Bound">B</a><a id="2953" class="Symbol">}}</a> <a id="2956" class="Symbol">→</a> <a id="2958" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="2968" class="Symbol">(</a><a id="2969" href="Foreign.Haskell.Coerce.html#2918" class="Bound">T</a> <a id="2971" href="Foreign.Haskell.Coerce.html#2927" class="Bound">A</a><a id="2972" class="Symbol">)</a> <a id="2974" class="Symbol">(</a><a id="2975" href="Foreign.Haskell.Coerce.html#2920" class="Bound">U</a> <a id="2977" href="Foreign.Haskell.Coerce.html#2929" class="Bound">B</a><a id="2978" class="Symbol">)</a>

<a id="Coercible₂"></a><a id="2981" href="Foreign.Haskell.Coerce.html#2981" class="Function">Coercible₂</a> <a id="2992" class="Symbol">:</a> <a id="2994" class="Symbol">∀</a> <a id="2996" href="Foreign.Haskell.Coerce.html#2996" class="Bound">a</a> <a id="2998" href="Foreign.Haskell.Coerce.html#2998" class="Bound">b</a> <a id="3000" href="Foreign.Haskell.Coerce.html#3000" class="Bound">d</a> <a id="3002" href="Foreign.Haskell.Coerce.html#3002" class="Bound">e</a> <a id="3004" class="Symbol">→</a> <a id="3006" class="Symbol">(</a><a id="3007" href="Foreign.Haskell.Coerce.html#3007" class="Bound">T</a> <a id="3009" class="Symbol">:</a> <a id="3011" class="PrimitiveType">Set</a> <a id="3015" href="Foreign.Haskell.Coerce.html#2996" class="Bound">a</a> <a id="3017" class="Symbol">→</a> <a id="3019" class="PrimitiveType">Set</a> <a id="3023" href="Foreign.Haskell.Coerce.html#2998" class="Bound">b</a> <a id="3025" class="Symbol">→</a> <a id="3027" class="PrimitiveType">Set</a> <a id="3031" href="Foreign.Haskell.Coerce.html#1554" class="Generalizable">c</a><a id="3032" class="Symbol">)</a> <a id="3034" class="Symbol">(</a><a id="3035" href="Foreign.Haskell.Coerce.html#3035" class="Bound">U</a> <a id="3037" class="Symbol">:</a> <a id="3039" class="PrimitiveType">Set</a> <a id="3043" href="Foreign.Haskell.Coerce.html#3000" class="Bound">d</a> <a id="3045" class="Symbol">→</a> <a id="3047" class="PrimitiveType">Set</a> <a id="3051" href="Foreign.Haskell.Coerce.html#3002" class="Bound">e</a> <a id="3053" class="Symbol">→</a> <a id="3055" class="PrimitiveType">Set</a> <a id="3059" href="Foreign.Haskell.Coerce.html#1560" class="Generalizable">f</a><a id="3060" class="Symbol">)</a> <a id="3062" class="Symbol">→</a> <a id="3064" class="PrimitiveType">Set</a> <a id="3068" class="Symbol">_</a>
<a id="3070" href="Foreign.Haskell.Coerce.html#2981" class="Function">Coercible₂</a> <a id="3081" class="Symbol">_</a> <a id="3083" class="Symbol">_</a> <a id="3085" class="Symbol">_</a> <a id="3087" class="Symbol">_</a> <a id="3089" href="Foreign.Haskell.Coerce.html#3089" class="Bound">T</a> <a id="3091" href="Foreign.Haskell.Coerce.html#3091" class="Bound">U</a> <a id="3093" class="Symbol">=</a> <a id="3095" class="Symbol">∀</a> <a id="3097" class="Symbol">{</a><a id="3098" href="Foreign.Haskell.Coerce.html#3098" class="Bound">A</a> <a id="3100" href="Foreign.Haskell.Coerce.html#3100" class="Bound">B</a><a id="3101" class="Symbol">}</a> <a id="3103" class="Symbol">→</a> <a id="3105" class="Symbol">{{</a><a id="3107" href="Foreign.Haskell.Coerce.html#3107" class="Bound">_</a> <a id="3109" class="Symbol">:</a> <a id="3111" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="3121" href="Foreign.Haskell.Coerce.html#3098" class="Bound">A</a> <a id="3123" href="Foreign.Haskell.Coerce.html#3100" class="Bound">B</a><a id="3124" class="Symbol">}}</a> <a id="3127" class="Symbol">→</a> <a id="3129" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="3140" class="Symbol">_</a> <a id="3142" class="Symbol">_</a> <a id="3144" class="Symbol">(</a><a id="3145" href="Foreign.Haskell.Coerce.html#3089" class="Bound">T</a> <a id="3147" href="Foreign.Haskell.Coerce.html#3098" class="Bound">A</a><a id="3148" class="Symbol">)</a> <a id="3150" class="Symbol">(</a><a id="3151" href="Foreign.Haskell.Coerce.html#3091" class="Bound">U</a> <a id="3153" href="Foreign.Haskell.Coerce.html#3100" class="Bound">B</a><a id="3154" class="Symbol">)</a>

<a id="3157" class="Comment">------------------------------------------------------------------------</a>
<a id="3230" class="Comment">-- Instances</a>

<a id="3244" class="Comment">-- Nat</a>

<a id="3252" class="Comment">-- Our first instance reveals one of Agda&#39;s secrets: natural numbers are</a>
<a id="3325" class="Comment">-- represented by (arbitrary precision) integers at runtime! Note that we</a>
<a id="3399" class="Comment">-- may only coerce in one direction: arbitrary integers may actually be</a>
<a id="3471" class="Comment">-- negative and will not do as mere natural numbers.</a>

<a id="3525" class="Keyword">instance</a>

  <a id="nat-toInt"></a><a id="3537" href="Foreign.Haskell.Coerce.html#3537" class="Function">nat-toInt</a> <a id="3547" class="Symbol">:</a> <a id="3549" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="3559" href="Agda.Builtin.Nat.html#165" class="Datatype">Nat</a> <a id="3563" href="Agda.Builtin.Int.html#219" class="Datatype">Int</a>
  <a id="3569" href="Foreign.Haskell.Coerce.html#3537" class="Function">nat-toInt</a> <a id="3579" class="Symbol">=</a> <a id="3581" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

<a id="3590" class="Comment">-- We then proceed to state that data types from the standard library</a>
<a id="3660" class="Comment">-- can be converted to their FFI equivalents which are bound to actual</a>
<a id="3731" class="Comment">-- Haskell types.</a>

<a id="3750" class="Comment">-- Maybe</a>

  <a id="maybe-toFFI"></a><a id="3762" href="Foreign.Haskell.Coerce.html#3762" class="Function">maybe-toFFI</a> <a id="3774" class="Symbol">:</a> <a id="3776" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="3787" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="3789" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="3791" href="Data.Maybe.Base.html#800" class="Datatype">STD.Maybe</a> <a id="3801" href="Foreign.Haskell.Maybe.html#522" class="Datatype">FFI.Maybe</a>
  <a id="3813" href="Foreign.Haskell.Coerce.html#3762" class="Function">maybe-toFFI</a> <a id="3825" class="Symbol">=</a> <a id="3827" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

  <a id="maybe-fromFFI"></a><a id="3838" href="Foreign.Haskell.Coerce.html#3838" class="Function">maybe-fromFFI</a> <a id="3852" class="Symbol">:</a> <a id="3854" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="3865" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="3867" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="3869" href="Foreign.Haskell.Maybe.html#522" class="Datatype">FFI.Maybe</a> <a id="3879" href="Data.Maybe.Base.html#800" class="Datatype">STD.Maybe</a>
  <a id="3891" href="Foreign.Haskell.Coerce.html#3838" class="Function">maybe-fromFFI</a> <a id="3905" class="Symbol">=</a> <a id="3907" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

<a id="3916" class="Comment">-- Product</a>

  <a id="pair-toFFI"></a><a id="3930" href="Foreign.Haskell.Coerce.html#3930" class="Function">pair-toFFI</a> <a id="3941" class="Symbol">:</a> <a id="3943" href="Foreign.Haskell.Coerce.html#2981" class="Function">Coercible₂</a> <a id="3954" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="3956" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="3958" href="Foreign.Haskell.Coerce.html#1554" class="Generalizable">c</a> <a id="3960" href="Foreign.Haskell.Coerce.html#1556" class="Generalizable">d</a> <a id="3962" href="Data.Product.html#1167" class="Function Operator">STD._×_</a> <a id="3970" href="Foreign.Haskell.Pair.html#520" class="Record">FFI.Pair</a>
  <a id="3981" href="Foreign.Haskell.Coerce.html#3930" class="Function">pair-toFFI</a> <a id="3992" class="Symbol">=</a> <a id="3994" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

  <a id="pair-fromFFI"></a><a id="4005" href="Foreign.Haskell.Coerce.html#4005" class="Function">pair-fromFFI</a> <a id="4018" class="Symbol">:</a> <a id="4020" href="Foreign.Haskell.Coerce.html#2981" class="Function">Coercible₂</a> <a id="4031" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="4033" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="4035" href="Foreign.Haskell.Coerce.html#1554" class="Generalizable">c</a> <a id="4037" href="Foreign.Haskell.Coerce.html#1556" class="Generalizable">d</a> <a id="4039" href="Foreign.Haskell.Pair.html#520" class="Record">FFI.Pair</a> <a id="4048" href="Data.Product.html#1167" class="Function Operator">STD._×_</a>
  <a id="4058" href="Foreign.Haskell.Coerce.html#4005" class="Function">pair-fromFFI</a> <a id="4071" class="Symbol">=</a> <a id="4073" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

<a id="4082" class="Comment">-- We follow up with purely structural rules for builtin data types which</a>
<a id="4156" class="Comment">-- already have known low-level representations.</a>

<a id="4206" class="Comment">-- List</a>

  <a id="coerce-list"></a><a id="4217" href="Foreign.Haskell.Coerce.html#4217" class="Function">coerce-list</a> <a id="4229" class="Symbol">:</a> <a id="4231" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="4242" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="4244" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="4246" href="Agda.Builtin.List.html#121" class="Datatype">STD.List</a> <a id="4255" href="Agda.Builtin.List.html#121" class="Datatype">STD.List</a>
  <a id="4266" href="Foreign.Haskell.Coerce.html#4217" class="Function">coerce-list</a> <a id="4278" class="Symbol">=</a> <a id="4280" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

<a id="4289" class="Comment">-- IO</a>

  <a id="coerce-IO"></a><a id="4298" href="Foreign.Haskell.Coerce.html#4298" class="Function">coerce-IO</a> <a id="4308" class="Symbol">:</a> <a id="4310" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="4321" href="Foreign.Haskell.Coerce.html#1550" class="Generalizable">a</a> <a id="4323" href="Foreign.Haskell.Coerce.html#1552" class="Generalizable">b</a> <a id="4325" href="Agda.Builtin.IO.html#111" class="Postulate">STD.IO</a> <a id="4332" href="Agda.Builtin.IO.html#111" class="Postulate">STD.IO</a>
  <a id="4341" href="Foreign.Haskell.Coerce.html#4298" class="Function">coerce-IO</a> <a id="4351" class="Symbol">=</a> <a id="4353" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

<a id="4362" class="Comment">-- Function</a>
<a id="4374" class="Comment">-- Note that functions are contravariant in their domain.</a>

  <a id="coerce-fun"></a><a id="4435" href="Foreign.Haskell.Coerce.html#4435" class="Function">coerce-fun</a> <a id="4446" class="Symbol">:</a> <a id="4448" class="Symbol">{{</a><a id="4450" href="Foreign.Haskell.Coerce.html#4450" class="Bound">_</a> <a id="4452" class="Symbol">:</a> <a id="4454" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="4464" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a> <a id="4466" href="Foreign.Haskell.Coerce.html#1588" class="Generalizable">B</a><a id="4467" class="Symbol">}}</a> <a id="4470" class="Symbol">→</a> <a id="4472" href="Foreign.Haskell.Coerce.html#2834" class="Function">Coercible₁</a> <a id="4483" href="Foreign.Haskell.Coerce.html#1554" class="Generalizable">c</a> <a id="4485" href="Foreign.Haskell.Coerce.html#1556" class="Generalizable">d</a> <a id="4487" class="Symbol">(λ</a> <a id="4490" href="Foreign.Haskell.Coerce.html#4490" class="Bound">C</a> <a id="4492" class="Symbol">→</a> <a id="4494" href="Foreign.Haskell.Coerce.html#1588" class="Generalizable">B</a> <a id="4496" class="Symbol">→</a> <a id="4498" href="Foreign.Haskell.Coerce.html#4490" class="Bound">C</a><a id="4499" class="Symbol">)</a> <a id="4501" class="Symbol">(λ</a> <a id="4504" href="Foreign.Haskell.Coerce.html#4504" class="Bound">D</a> <a id="4506" class="Symbol">→</a> <a id="4508" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a> <a id="4510" class="Symbol">→</a> <a id="4512" href="Foreign.Haskell.Coerce.html#4504" class="Bound">D</a><a id="4513" class="Symbol">)</a>
  <a id="4517" href="Foreign.Haskell.Coerce.html#4435" class="Function">coerce-fun</a> <a id="4528" class="Symbol">=</a> <a id="4530" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>

<a id="4539" class="Comment">-- Finally we add a reflexivity proof to discharge all the dangling constraints</a>
<a id="4619" class="Comment">-- involving type variables and concrete builtin types such as `Bool`.</a>

<a id="4691" class="Comment">-- This rule overlaps with the purely structural ones: when attempting to prove</a>
<a id="4771" class="Comment">-- `Coercible (List A) (List A)`, should Agda use the proof obtained by `coerce-refl`</a>
<a id="4857" class="Comment">-- or the one obtained by `coerce-list coerce-refl`? Because we are using a</a>
<a id="4933" class="Comment">-- datatype with a single constructor these distinctions do not matter: both proofs</a>
<a id="5017" class="Comment">-- are definitionally equal.</a>

  <a id="coerce-refl"></a><a id="5049" href="Foreign.Haskell.Coerce.html#5049" class="Function">coerce-refl</a> <a id="5061" class="Symbol">:</a> <a id="5063" href="Foreign.Haskell.Coerce.html#2180" class="Datatype">Coercible</a> <a id="5073" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a> <a id="5075" href="Foreign.Haskell.Coerce.html#1574" class="Generalizable">A</a>
  <a id="5079" href="Foreign.Haskell.Coerce.html#5049" class="Function">coerce-refl</a> <a id="5091" class="Symbol">=</a> <a id="5093" href="Foreign.Haskell.Coerce.html#2228" class="InductiveConstructor">TrustMe</a>
</pre></body></html>