<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Nat.Divisibility.Core</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Core definition of divisibility</a>
<a id="141" class="Comment">------------------------------------------------------------------------</a>

<a id="215" class="Comment">-- The definition of divisibility is split out from</a>
<a id="267" class="Comment">-- `Data.Nat.Divisibility` to avoid a dependency cycle with</a>
<a id="327" class="Comment">-- `Data.Nat.DivMod`.</a>

<a id="350" class="Symbol">{-#</a> <a id="354" class="Keyword">OPTIONS</a> <a id="362" class="Pragma">--cubical-compatible</a> <a id="383" class="Pragma">--safe</a> <a id="390" class="Symbol">#-}</a>

<a id="395" class="Keyword">module</a> <a id="402" href="Data.Nat.Divisibility.Core.html" class="Module">Data.Nat.Divisibility.Core</a> <a id="429" class="Keyword">where</a>

<a id="436" class="Keyword">open</a> <a id="441" class="Keyword">import</a> <a id="448" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="462" class="Keyword">using</a> <a id="468" class="Symbol">(</a><a id="469" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="470" class="Symbol">;</a> <a id="472" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">_*_</a><a id="475" class="Symbol">)</a>
<a id="477" class="Keyword">open</a> <a id="482" class="Keyword">import</a> <a id="489" href="Data.Nat.Properties.html" class="Module">Data.Nat.Properties</a>
<a id="509" class="Keyword">open</a> <a id="514" class="Keyword">import</a> <a id="521" href="Level.html" class="Module">Level</a> <a id="527" class="Keyword">using</a> <a id="533" class="Symbol">(</a><a id="534" href="Level.html#521" class="Function">0ℓ</a><a id="536" class="Symbol">)</a>
<a id="538" class="Keyword">open</a> <a id="543" class="Keyword">import</a> <a id="550" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="576" class="Keyword">using</a> <a id="582" class="Symbol">(</a><a id="583" href="Relation.Nullary.Negation.Core.html#824" class="Function Operator">¬_</a><a id="585" class="Symbol">)</a>
<a id="587" class="Keyword">open</a> <a id="592" class="Keyword">import</a> <a id="599" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="620" class="Keyword">using</a> <a id="626" class="Symbol">(</a><a id="627" href="Relation.Binary.Core.html#896" class="Function">Rel</a><a id="630" class="Symbol">)</a>
<a id="632" class="Keyword">open</a> <a id="637" class="Keyword">import</a> <a id="644" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>
  <a id="684" class="Keyword">using</a> <a id="690" class="Symbol">(</a><a id="691" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">_≡_</a><a id="694" class="Symbol">;</a> <a id="696" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a><a id="700" class="Symbol">;</a> <a id="702" href="Relation.Binary.PropositionalEquality.Core.html#1712" class="Function">sym</a><a id="705" class="Symbol">;</a> <a id="707" href="Relation.Binary.PropositionalEquality.Core.html#1395" class="Function">cong₂</a><a id="712" class="Symbol">;</a> <a id="714" class="Keyword">module</a> <a id="721" href="Relation.Binary.PropositionalEquality.Core.html#2736" class="Module">≡-Reasoning</a><a id="732" class="Symbol">)</a>

<a id="735" class="Comment">------------------------------------------------------------------------</a>
<a id="808" class="Comment">-- Definition</a>
<a id="822" class="Comment">--</a>
<a id="825" class="Comment">-- m ∣ n is inhabited iff m divides n. Some sources, like Hardy and</a>
<a id="893" class="Comment">-- Wright&#39;s &quot;An Introduction to the Theory of Numbers&quot;, require m to</a>
<a id="962" class="Comment">-- be non-zero. However, some things become a bit nicer if m is</a>
<a id="1026" class="Comment">-- allowed to be zero. For instance, _∣_ becomes a partial order, and</a>
<a id="1096" class="Comment">-- the gcd of 0 and 0 becomes defined.</a>

<a id="1136" class="Keyword">infix</a> <a id="1142" class="Number">4</a> <a id="1144" href="Data.Nat.Divisibility.Core.html#1160" class="Record Operator">_∣_</a> <a id="1148" href="Data.Nat.Divisibility.Core.html#1299" class="Function Operator">_∤_</a>

<a id="1153" class="Keyword">record</a> <a id="_∣_"></a><a id="1160" href="Data.Nat.Divisibility.Core.html#1160" class="Record Operator">_∣_</a> <a id="1164" class="Symbol">(</a><a id="1165" href="Data.Nat.Divisibility.Core.html#1165" class="Bound">m</a> <a id="1167" href="Data.Nat.Divisibility.Core.html#1167" class="Bound">n</a> <a id="1169" class="Symbol">:</a> <a id="1171" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a><a id="1172" class="Symbol">)</a> <a id="1174" class="Symbol">:</a> <a id="1176" href="Agda.Primitive.html#320" class="Primitive">Set</a> <a id="1180" class="Keyword">where</a>
  <a id="1188" class="Keyword">constructor</a> <a id="divides"></a><a id="1200" href="Data.Nat.Divisibility.Core.html#1200" class="InductiveConstructor">divides</a>
  <a id="1210" class="Keyword">field</a> <a id="_∣_.quotient"></a><a id="1216" href="Data.Nat.Divisibility.Core.html#1216" class="Field">quotient</a> <a id="1225" class="Symbol">:</a> <a id="1227" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a>
        <a id="_∣_.equality"></a><a id="1237" href="Data.Nat.Divisibility.Core.html#1237" class="Field">equality</a> <a id="1246" class="Symbol">:</a> <a id="1248" href="Data.Nat.Divisibility.Core.html#1167" class="Bound">n</a> <a id="1250" href="Agda.Builtin.Equality.html#133" class="Datatype Operator">≡</a> <a id="1252" href="Data.Nat.Divisibility.Core.html#1216" class="Field">quotient</a> <a id="1261" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1263" href="Data.Nat.Divisibility.Core.html#1165" class="Bound">m</a>
<a id="1265" class="Keyword">open</a> <a id="1270" href="Data.Nat.Divisibility.Core.html#1160" class="Module Operator">_∣_</a> <a id="1274" class="Keyword">using</a> <a id="1280" class="Symbol">(</a><a id="1281" href="Data.Nat.Divisibility.Core.html#1216" class="Field">quotient</a><a id="1289" class="Symbol">)</a> <a id="1291" class="Keyword">public</a>

<a id="_∤_"></a><a id="1299" href="Data.Nat.Divisibility.Core.html#1299" class="Function Operator">_∤_</a> <a id="1303" class="Symbol">:</a> <a id="1305" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="1309" href="Agda.Builtin.Nat.html#186" class="Datatype">ℕ</a> <a id="1311" href="Level.html#521" class="Function">0ℓ</a>
<a id="1314" href="Data.Nat.Divisibility.Core.html#1314" class="Bound">m</a> <a id="1316" href="Data.Nat.Divisibility.Core.html#1299" class="Function Operator">∤</a> <a id="1318" href="Data.Nat.Divisibility.Core.html#1318" class="Bound">n</a> <a id="1320" class="Symbol">=</a> <a id="1322" href="Relation.Nullary.Negation.Core.html#824" class="Function Operator">¬</a> <a id="1324" class="Symbol">(</a><a id="1325" href="Data.Nat.Divisibility.Core.html#1314" class="Bound">m</a> <a id="1327" href="Data.Nat.Divisibility.Core.html#1160" class="Record Operator">∣</a> <a id="1329" href="Data.Nat.Divisibility.Core.html#1318" class="Bound">n</a><a id="1330" class="Symbol">)</a>

<a id="1333" class="Comment">-- smart constructor</a>

<a id="1355" class="Keyword">pattern</a> <a id="divides-refl"></a><a id="1363" href="Data.Nat.Divisibility.Core.html#1363" class="InductiveConstructor">divides-refl</a> <a id="1376" href="Data.Nat.Divisibility.Core.html#1388" class="Bound">q</a> <a id="1378" class="Symbol">=</a> <a id="1380" href="Data.Nat.Divisibility.Core.html#1200" class="InductiveConstructor">divides</a> <a id="1388" href="Data.Nat.Divisibility.Core.html#1388" class="Bound">q</a> <a id="1390" href="Agda.Builtin.Equality.html#190" class="InductiveConstructor">refl</a>

<a id="1396" class="Comment">------------------------------------------------------------------------</a>
<a id="1469" class="Comment">-- Basic properties</a>

<a id="*-pres-∣"></a><a id="1490" href="Data.Nat.Divisibility.Core.html#1490" class="Function">*-pres-∣</a> <a id="1499" class="Symbol">:</a> <a id="1501" class="Symbol">∀</a> <a id="1503" class="Symbol">{</a><a id="1504" href="Data.Nat.Divisibility.Core.html#1504" class="Bound">m</a> <a id="1506" href="Data.Nat.Divisibility.Core.html#1506" class="Bound">n</a> <a id="1508" href="Data.Nat.Divisibility.Core.html#1508" class="Bound">o</a> <a id="1510" href="Data.Nat.Divisibility.Core.html#1510" class="Bound">p</a><a id="1511" class="Symbol">}</a> <a id="1513" class="Symbol">→</a> <a id="1515" href="Data.Nat.Divisibility.Core.html#1508" class="Bound">o</a> <a id="1517" href="Data.Nat.Divisibility.Core.html#1160" class="Record Operator">∣</a> <a id="1519" href="Data.Nat.Divisibility.Core.html#1504" class="Bound">m</a> <a id="1521" class="Symbol">→</a> <a id="1523" href="Data.Nat.Divisibility.Core.html#1510" class="Bound">p</a> <a id="1525" href="Data.Nat.Divisibility.Core.html#1160" class="Record Operator">∣</a> <a id="1527" href="Data.Nat.Divisibility.Core.html#1506" class="Bound">n</a> <a id="1529" class="Symbol">→</a> <a id="1531" href="Data.Nat.Divisibility.Core.html#1508" class="Bound">o</a> <a id="1533" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1535" href="Data.Nat.Divisibility.Core.html#1510" class="Bound">p</a> <a id="1537" href="Data.Nat.Divisibility.Core.html#1160" class="Record Operator">∣</a> <a id="1539" href="Data.Nat.Divisibility.Core.html#1504" class="Bound">m</a> <a id="1541" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1543" href="Data.Nat.Divisibility.Core.html#1506" class="Bound">n</a>
<a id="1545" href="Data.Nat.Divisibility.Core.html#1490" class="Function">*-pres-∣</a> <a id="1554" class="Symbol">{</a><a id="1555" href="Data.Nat.Divisibility.Core.html#1555" class="Bound">m</a><a id="1556" class="Symbol">}</a> <a id="1558" class="Symbol">{</a><a id="1559" href="Data.Nat.Divisibility.Core.html#1559" class="Bound">n</a><a id="1560" class="Symbol">}</a> <a id="1562" class="Symbol">{</a><a id="1563" href="Data.Nat.Divisibility.Core.html#1563" class="Bound">o</a><a id="1564" class="Symbol">}</a> <a id="1566" class="Symbol">{</a><a id="1567" href="Data.Nat.Divisibility.Core.html#1567" class="Bound">p</a><a id="1568" class="Symbol">}</a> <a id="1570" class="Symbol">(</a><a id="1571" href="Data.Nat.Divisibility.Core.html#1200" class="InductiveConstructor">divides</a> <a id="1579" href="Data.Nat.Divisibility.Core.html#1579" class="Bound">c</a> <a id="1581" href="Data.Nat.Divisibility.Core.html#1581" class="Bound">m≡c*o</a><a id="1586" class="Symbol">)</a> <a id="1588" class="Symbol">(</a><a id="1589" href="Data.Nat.Divisibility.Core.html#1200" class="InductiveConstructor">divides</a> <a id="1597" href="Data.Nat.Divisibility.Core.html#1597" class="Bound">d</a> <a id="1599" href="Data.Nat.Divisibility.Core.html#1599" class="Bound">n≡d*p</a><a id="1604" class="Symbol">)</a> <a id="1606" class="Symbol">=</a>
  <a id="1610" href="Data.Nat.Divisibility.Core.html#1200" class="InductiveConstructor">divides</a> <a id="1618" class="Symbol">(</a><a id="1619" href="Data.Nat.Divisibility.Core.html#1579" class="Bound">c</a> <a id="1621" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1623" href="Data.Nat.Divisibility.Core.html#1597" class="Bound">d</a><a id="1624" class="Symbol">)</a> <a id="1626" class="Symbol">(</a><a id="1627" href="Relation.Binary.PropositionalEquality.Core.html#2834" class="Function Operator">begin</a>
    <a id="1637" href="Data.Nat.Divisibility.Core.html#1555" class="Bound">m</a> <a id="1639" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1641" href="Data.Nat.Divisibility.Core.html#1559" class="Bound">n</a>             <a id="1655" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="1658" href="Relation.Binary.PropositionalEquality.Core.html#1395" class="Function">cong₂</a> <a id="1664" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">_*_</a> <a id="1668" href="Data.Nat.Divisibility.Core.html#1581" class="Bound">m≡c*o</a> <a id="1674" href="Data.Nat.Divisibility.Core.html#1599" class="Bound">n≡d*p</a> <a id="1680" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
    <a id="1686" class="Symbol">(</a><a id="1687" href="Data.Nat.Divisibility.Core.html#1579" class="Bound">c</a> <a id="1689" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1691" href="Data.Nat.Divisibility.Core.html#1563" class="Bound">o</a><a id="1692" class="Symbol">)</a> <a id="1694" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1696" class="Symbol">(</a><a id="1697" href="Data.Nat.Divisibility.Core.html#1597" class="Bound">d</a> <a id="1699" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1701" href="Data.Nat.Divisibility.Core.html#1567" class="Bound">p</a><a id="1702" class="Symbol">)</a> <a id="1704" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">≡⟨</a> <a id="1707" href="Data.Nat.Properties.html#26098" class="Function">[m*n]*[o*p]≡[m*o]*[n*p]</a> <a id="1731" href="Data.Nat.Divisibility.Core.html#1579" class="Bound">c</a> <a id="1733" href="Data.Nat.Divisibility.Core.html#1563" class="Bound">o</a> <a id="1735" href="Data.Nat.Divisibility.Core.html#1597" class="Bound">d</a> <a id="1737" href="Data.Nat.Divisibility.Core.html#1567" class="Bound">p</a> <a id="1739" href="Relation.Binary.PropositionalEquality.Core.html#2951" class="Function">⟩</a>
    <a id="1745" class="Symbol">(</a><a id="1746" href="Data.Nat.Divisibility.Core.html#1579" class="Bound">c</a> <a id="1748" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1750" href="Data.Nat.Divisibility.Core.html#1597" class="Bound">d</a><a id="1751" class="Symbol">)</a> <a id="1753" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1755" class="Symbol">(</a><a id="1756" href="Data.Nat.Divisibility.Core.html#1563" class="Bound">o</a> <a id="1758" href="Agda.Builtin.Nat.html#522" class="Primitive Operator">*</a> <a id="1760" href="Data.Nat.Divisibility.Core.html#1567" class="Bound">p</a><a id="1761" class="Symbol">)</a> <a id="1763" href="Relation.Binary.PropositionalEquality.Core.html#3133" class="Function Operator">∎</a><a id="1764" class="Symbol">)</a>
  <a id="1768" class="Keyword">where</a> <a id="1774" class="Keyword">open</a> <a id="1779" href="Relation.Binary.PropositionalEquality.Core.html#2736" class="Module">≡-Reasoning</a>
</pre></body></html>