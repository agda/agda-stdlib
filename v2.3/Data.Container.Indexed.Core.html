<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Container.Indexed.Core</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Indexed containers core</a>
<a id="133" class="Comment">------------------------------------------------------------------------</a>

<a id="207" class="Symbol">{-#</a> <a id="211" class="Keyword">OPTIONS</a> <a id="219" class="Pragma">--cubical-compatible</a> <a id="240" class="Pragma">--safe</a> <a id="247" class="Symbol">#-}</a>

<a id="252" class="Keyword">module</a> <a id="259" href="Data.Container.Indexed.Core.html" class="Module">Data.Container.Indexed.Core</a> <a id="287" class="Keyword">where</a>

<a id="294" class="Keyword">open</a> <a id="299" class="Keyword">import</a> <a id="306" href="Level.html" class="Module">Level</a> <a id="312" class="Keyword">using</a> <a id="318" class="Symbol">(</a><a id="319" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="324" class="Symbol">;</a> <a id="326" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="329" class="Symbol">;</a> <a id="331" href="Agda.Primitive.html#931" class="Primitive">suc</a><a id="334" class="Symbol">)</a>
<a id="336" class="Keyword">open</a> <a id="341" class="Keyword">import</a> <a id="348" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="366" class="Keyword">using</a> <a id="372" class="Symbol">(</a><a id="373" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a><a id="374" class="Symbol">;</a> <a id="376" href="Data.Product.Base.html#1244" class="Function">Σ-syntax</a><a id="384" class="Symbol">;</a> <a id="386" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="389" class="Symbol">;</a> <a id="391" href="Data.Product.Base.html#852" class="Function">∃</a><a id="392" class="Symbol">)</a>
<a id="394" class="Keyword">open</a> <a id="399" class="Keyword">import</a> <a id="406" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="421" class="Keyword">using</a> <a id="427" class="Symbol">(</a><a id="428" href="Relation.Unary.html#1178" class="Function">Pred</a><a id="432" class="Symbol">)</a>

<a id="435" class="Keyword">private</a> <a id="443" class="Keyword">variable</a>
  <a id="454" href="Data.Container.Indexed.Core.html#454" class="Generalizable">i</a> <a id="456" href="Data.Container.Indexed.Core.html#456" class="Generalizable">o</a> <a id="458" href="Data.Container.Indexed.Core.html#458" class="Generalizable">c</a> <a id="460" href="Data.Container.Indexed.Core.html#460" class="Generalizable">r</a> <a id="462" href="Data.Container.Indexed.Core.html#462" class="Generalizable">ℓ</a> <a id="464" href="Data.Container.Indexed.Core.html#464" class="Generalizable">ℓ′</a> <a id="467" class="Symbol">:</a> <a id="469" href="Agda.Primitive.html#742" class="Postulate">Level</a>
  <a id="477" href="Data.Container.Indexed.Core.html#477" class="Generalizable">I</a> <a id="479" class="Symbol">:</a> <a id="481" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="485" href="Data.Container.Indexed.Core.html#454" class="Generalizable">i</a>
  <a id="489" href="Data.Container.Indexed.Core.html#489" class="Generalizable">O</a> <a id="491" class="Symbol">:</a> <a id="493" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="497" href="Data.Container.Indexed.Core.html#456" class="Generalizable">o</a>

<a id="500" class="Comment">------------------------------------------------------------------------</a>
<a id="573" class="Comment">-- Definition</a>

<a id="588" class="Keyword">infix</a> <a id="594" class="Number">5</a> <a id="596" href="Data.Container.Indexed.Core.html#698" class="InductiveConstructor Operator">_◃_/_</a>

<a id="603" class="Keyword">record</a> <a id="Container"></a><a id="610" href="Data.Container.Indexed.Core.html#610" class="Record">Container</a> <a id="620" class="Symbol">(</a><a id="621" href="Data.Container.Indexed.Core.html#621" class="Bound">I</a> <a id="623" class="Symbol">:</a> <a id="625" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="629" href="Data.Container.Indexed.Core.html#454" class="Generalizable">i</a><a id="630" class="Symbol">)</a> <a id="632" class="Symbol">(</a><a id="633" href="Data.Container.Indexed.Core.html#633" class="Bound">O</a> <a id="635" class="Symbol">:</a> <a id="637" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="641" href="Data.Container.Indexed.Core.html#456" class="Generalizable">o</a><a id="642" class="Symbol">)</a> <a id="644" href="Data.Container.Indexed.Core.html#644" class="Bound">c</a> <a id="646" href="Data.Container.Indexed.Core.html#646" class="Bound">r</a> <a id="648" class="Symbol">:</a> <a id="650" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="654" class="Symbol">(</a><a id="655" href="Data.Container.Indexed.Core.html#629" class="Bound">i</a> <a id="657" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="659" href="Data.Container.Indexed.Core.html#641" class="Bound">o</a> <a id="661" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="663" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="667" href="Data.Container.Indexed.Core.html#644" class="Bound">c</a> <a id="669" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="671" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="675" href="Data.Container.Indexed.Core.html#646" class="Bound">r</a><a id="676" class="Symbol">)</a> <a id="678" class="Keyword">where</a>
  <a id="686" class="Keyword">constructor</a> <a id="_◃_/_"></a><a id="698" href="Data.Container.Indexed.Core.html#698" class="InductiveConstructor Operator">_◃_/_</a>
  <a id="706" class="Keyword">field</a>
    <a id="Container.Command"></a><a id="716" href="Data.Container.Indexed.Core.html#716" class="Field">Command</a>  <a id="725" class="Symbol">:</a> <a id="727" class="Symbol">(</a><a id="728" href="Data.Container.Indexed.Core.html#728" class="Bound">o</a> <a id="730" class="Symbol">:</a> <a id="732" href="Data.Container.Indexed.Core.html#633" class="Bound">O</a><a id="733" class="Symbol">)</a> <a id="735" class="Symbol">→</a> <a id="737" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="741" href="Data.Container.Indexed.Core.html#644" class="Bound">c</a>
    <a id="Container.Response"></a><a id="747" href="Data.Container.Indexed.Core.html#747" class="Field">Response</a> <a id="756" class="Symbol">:</a> <a id="758" class="Symbol">∀</a> <a id="760" class="Symbol">{</a><a id="761" href="Data.Container.Indexed.Core.html#761" class="Bound">o</a><a id="762" class="Symbol">}</a> <a id="764" class="Symbol">→</a> <a id="766" href="Data.Container.Indexed.Core.html#716" class="Field">Command</a> <a id="774" href="Data.Container.Indexed.Core.html#761" class="Bound">o</a> <a id="776" class="Symbol">→</a> <a id="778" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="782" href="Data.Container.Indexed.Core.html#646" class="Bound">r</a>
    <a id="Container.next"></a><a id="788" href="Data.Container.Indexed.Core.html#788" class="Field">next</a>     <a id="797" class="Symbol">:</a> <a id="799" class="Symbol">∀</a> <a id="801" class="Symbol">{</a><a id="802" href="Data.Container.Indexed.Core.html#802" class="Bound">o</a><a id="803" class="Symbol">}</a> <a id="805" class="Symbol">(</a><a id="806" href="Data.Container.Indexed.Core.html#806" class="Bound">c</a> <a id="808" class="Symbol">:</a> <a id="810" href="Data.Container.Indexed.Core.html#716" class="Field">Command</a> <a id="818" href="Data.Container.Indexed.Core.html#802" class="Bound">o</a><a id="819" class="Symbol">)</a> <a id="821" class="Symbol">→</a> <a id="823" href="Data.Container.Indexed.Core.html#747" class="Field">Response</a> <a id="832" href="Data.Container.Indexed.Core.html#806" class="Bound">c</a> <a id="834" class="Symbol">→</a> <a id="836" href="Data.Container.Indexed.Core.html#621" class="Bound">I</a>

<a id="839" class="Comment">------------------------------------------------------------------------</a>
<a id="912" class="Comment">-- The semantics (&quot;extension&quot;) of an indexed container.</a>

<a id="969" class="Keyword">module</a> <a id="976" href="Data.Container.Indexed.Core.html#976" class="Module">_</a> <a id="978" class="Symbol">(</a><a id="979" href="Data.Container.Indexed.Core.html#979" class="Bound">C</a> <a id="981" class="Symbol">:</a> <a id="983" href="Data.Container.Indexed.Core.html#610" class="Record">Container</a> <a id="993" href="Data.Container.Indexed.Core.html#477" class="Generalizable">I</a> <a id="995" href="Data.Container.Indexed.Core.html#489" class="Generalizable">O</a> <a id="997" href="Data.Container.Indexed.Core.html#458" class="Generalizable">c</a> <a id="999" href="Data.Container.Indexed.Core.html#460" class="Generalizable">r</a><a id="1000" class="Symbol">)</a> <a id="1002" class="Symbol">(</a><a id="1003" href="Data.Container.Indexed.Core.html#1003" class="Bound">X</a> <a id="1005" class="Symbol">:</a> <a id="1007" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1012" href="Data.Container.Indexed.Core.html#477" class="Generalizable">I</a> <a id="1014" href="Data.Container.Indexed.Core.html#462" class="Generalizable">ℓ</a><a id="1015" class="Symbol">)</a> <a id="1017" class="Keyword">where</a>
  <a id="1025" class="Keyword">open</a> <a id="1030" href="Data.Container.Indexed.Core.html#610" class="Module">Container</a> <a id="1040" href="Data.Container.Indexed.Core.html#979" class="Bound">C</a>

  <a id="1045" href="Data.Container.Indexed.Core.html#1045" class="Function">Subtrees</a> <a id="1054" class="Symbol">:</a> <a id="1056" class="Symbol">∀</a> <a id="1058" href="Data.Container.Indexed.Core.html#1058" class="Bound">o</a> <a id="1060" class="Symbol">→</a> <a id="1062" href="Data.Container.Indexed.Core.html#716" class="Function">Command</a> <a id="1070" href="Data.Container.Indexed.Core.html#1058" class="Bound">o</a> <a id="1072" class="Symbol">→</a> <a id="1074" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1078" class="Symbol">_</a>
  <a id="1082" href="Data.Container.Indexed.Core.html#1045" class="Function">Subtrees</a> <a id="1091" href="Data.Container.Indexed.Core.html#1091" class="Bound">o</a> <a id="1093" href="Data.Container.Indexed.Core.html#1093" class="Bound">c</a> <a id="1095" class="Symbol">=</a> <a id="1097" class="Symbol">(</a><a id="1098" href="Data.Container.Indexed.Core.html#1098" class="Bound">r</a> <a id="1100" class="Symbol">:</a> <a id="1102" href="Data.Container.Indexed.Core.html#747" class="Function">Response</a> <a id="1111" href="Data.Container.Indexed.Core.html#1093" class="Bound">c</a><a id="1112" class="Symbol">)</a> <a id="1114" class="Symbol">→</a> <a id="1116" href="Data.Container.Indexed.Core.html#1003" class="Bound">X</a> <a id="1118" class="Symbol">(</a><a id="1119" href="Data.Container.Indexed.Core.html#788" class="Function">next</a> <a id="1124" href="Data.Container.Indexed.Core.html#1093" class="Bound">c</a> <a id="1126" href="Data.Container.Indexed.Core.html#1098" class="Bound">r</a><a id="1127" class="Symbol">)</a>

  <a id="1132" href="Data.Container.Indexed.Core.html#1132" class="Function Operator">⟦_⟧</a> <a id="1136" class="Symbol">:</a> <a id="1138" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1143" href="Data.Container.Indexed.Core.html#995" class="Bound">O</a> <a id="1145" class="Symbol">_</a>
  <a id="1149" href="Data.Container.Indexed.Core.html#1132" class="Function Operator">⟦_⟧</a> <a id="1153" href="Data.Container.Indexed.Core.html#1153" class="Bound">o</a> <a id="1155" class="Symbol">=</a> <a id="1157" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="1159" class="Symbol">(</a><a id="1160" href="Data.Container.Indexed.Core.html#716" class="Function">Command</a> <a id="1168" href="Data.Container.Indexed.Core.html#1153" class="Bound">o</a><a id="1169" class="Symbol">)</a> <a id="1171" class="Symbol">(</a><a id="1172" href="Data.Container.Indexed.Core.html#1045" class="Function">Subtrees</a> <a id="1181" href="Data.Container.Indexed.Core.html#1153" class="Bound">o</a><a id="1182" class="Symbol">)</a>

<a id="1185" class="Comment">------------------------------------------------------------------------</a>
<a id="1258" class="Comment">-- All and any</a>

<a id="1274" class="Keyword">module</a> <a id="1281" href="Data.Container.Indexed.Core.html#1281" class="Module">_</a> <a id="1283" class="Symbol">(</a><a id="1284" href="Data.Container.Indexed.Core.html#1284" class="Bound">C</a> <a id="1286" class="Symbol">:</a> <a id="1288" href="Data.Container.Indexed.Core.html#610" class="Record">Container</a> <a id="1298" href="Data.Container.Indexed.Core.html#477" class="Generalizable">I</a> <a id="1300" href="Data.Container.Indexed.Core.html#489" class="Generalizable">O</a> <a id="1302" href="Data.Container.Indexed.Core.html#458" class="Generalizable">c</a> <a id="1304" href="Data.Container.Indexed.Core.html#460" class="Generalizable">r</a><a id="1305" class="Symbol">)</a> <a id="1307" class="Symbol">{</a><a id="1308" href="Data.Container.Indexed.Core.html#1308" class="Bound">X</a> <a id="1310" class="Symbol">:</a> <a id="1312" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1317" href="Data.Container.Indexed.Core.html#477" class="Generalizable">I</a> <a id="1319" href="Data.Container.Indexed.Core.html#462" class="Generalizable">ℓ</a><a id="1320" class="Symbol">}</a> <a id="1322" class="Keyword">where</a>

  <a id="1331" class="Comment">-- All.</a>

  <a id="1342" href="Data.Container.Indexed.Core.html#1342" class="Function">□</a> <a id="1344" class="Symbol">:</a> <a id="1346" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1351" class="Symbol">(</a><a id="1352" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="1354" href="Data.Container.Indexed.Core.html#1298" class="Bound">I</a> <a id="1356" href="Data.Container.Indexed.Core.html#1308" class="Bound">X</a><a id="1357" class="Symbol">)</a> <a id="1359" href="Data.Container.Indexed.Core.html#464" class="Generalizable">ℓ′</a> <a id="1362" class="Symbol">→</a> <a id="1364" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1369" class="Symbol">(</a><a id="1370" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="1372" href="Data.Container.Indexed.Core.html#1300" class="Bound">O</a> <a id="1374" class="Symbol">(</a><a id="1375" href="Data.Container.Indexed.Core.html#1132" class="Function Operator">⟦</a> <a id="1377" href="Data.Container.Indexed.Core.html#1284" class="Bound">C</a> <a id="1379" href="Data.Container.Indexed.Core.html#1132" class="Function Operator">⟧</a> <a id="1381" href="Data.Container.Indexed.Core.html#1308" class="Bound">X</a><a id="1382" class="Symbol">))</a> <a id="1385" class="Symbol">_</a>
  <a id="1389" href="Data.Container.Indexed.Core.html#1342" class="Function">□</a> <a id="1391" href="Data.Container.Indexed.Core.html#1391" class="Bound">P</a> <a id="1393" class="Symbol">(_</a> <a id="1396" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1398" class="Symbol">_</a> <a id="1400" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1402" href="Data.Container.Indexed.Core.html#1402" class="Bound">k</a><a id="1403" class="Symbol">)</a> <a id="1405" class="Symbol">=</a> <a id="1407" class="Symbol">∀</a> <a id="1409" href="Data.Container.Indexed.Core.html#1409" class="Bound">r</a> <a id="1411" class="Symbol">→</a> <a id="1413" href="Data.Container.Indexed.Core.html#1391" class="Bound">P</a> <a id="1415" class="Symbol">(_</a> <a id="1418" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1420" href="Data.Container.Indexed.Core.html#1402" class="Bound">k</a> <a id="1422" href="Data.Container.Indexed.Core.html#1409" class="Bound">r</a><a id="1423" class="Symbol">)</a>

  <a id="1428" class="Comment">-- Any.</a>

  <a id="1439" href="Data.Container.Indexed.Core.html#1439" class="Function">◇</a> <a id="1441" class="Symbol">:</a> <a id="1443" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1448" class="Symbol">(</a><a id="1449" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="1451" href="Data.Container.Indexed.Core.html#1298" class="Bound">I</a> <a id="1453" href="Data.Container.Indexed.Core.html#1308" class="Bound">X</a><a id="1454" class="Symbol">)</a> <a id="1456" href="Data.Container.Indexed.Core.html#464" class="Generalizable">ℓ′</a> <a id="1459" class="Symbol">→</a> <a id="1461" href="Relation.Unary.html#1178" class="Function">Pred</a> <a id="1466" class="Symbol">(</a><a id="1467" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="1469" href="Data.Container.Indexed.Core.html#1300" class="Bound">O</a> <a id="1471" class="Symbol">(</a><a id="1472" href="Data.Container.Indexed.Core.html#1132" class="Function Operator">⟦</a> <a id="1474" href="Data.Container.Indexed.Core.html#1284" class="Bound">C</a> <a id="1476" href="Data.Container.Indexed.Core.html#1132" class="Function Operator">⟧</a> <a id="1478" href="Data.Container.Indexed.Core.html#1308" class="Bound">X</a><a id="1479" class="Symbol">))</a> <a id="1482" class="Symbol">_</a>
  <a id="1486" href="Data.Container.Indexed.Core.html#1439" class="Function">◇</a> <a id="1488" href="Data.Container.Indexed.Core.html#1488" class="Bound">P</a> <a id="1490" class="Symbol">(_</a> <a id="1493" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1495" class="Symbol">_</a> <a id="1497" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1499" href="Data.Container.Indexed.Core.html#1499" class="Bound">k</a><a id="1500" class="Symbol">)</a> <a id="1502" class="Symbol">=</a> <a id="1504" href="Data.Product.Base.html#852" class="Function">∃</a> <a id="1506" class="Symbol">λ</a> <a id="1508" href="Data.Container.Indexed.Core.html#1508" class="Bound">r</a> <a id="1510" class="Symbol">→</a> <a id="1512" href="Data.Container.Indexed.Core.html#1488" class="Bound">P</a> <a id="1514" class="Symbol">(_</a> <a id="1517" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1519" href="Data.Container.Indexed.Core.html#1499" class="Bound">k</a> <a id="1521" href="Data.Container.Indexed.Core.html#1508" class="Bound">r</a><a id="1522" class="Symbol">)</a>
</pre></body></html>