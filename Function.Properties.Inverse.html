<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Function.Properties.Inverse</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Some functional properties are Equivalence Relations</a>
<a id="162" class="Comment">--   This file is meant to be imported qualified.</a>
<a id="212" class="Comment">------------------------------------------------------------------------</a>

<a id="286" class="Symbol">{-#</a> <a id="290" class="Keyword">OPTIONS</a> <a id="298" class="Pragma">--without-K</a> <a id="310" class="Pragma">--safe</a> <a id="317" class="Symbol">#-}</a>

<a id="322" class="Keyword">module</a> <a id="329" href="Function.Properties.Inverse.html" class="Module">Function.Properties.Inverse</a> <a id="357" class="Keyword">where</a>

<a id="364" class="Keyword">open</a> <a id="369" class="Keyword">import</a> <a id="376" href="Data.Product.html" class="Module">Data.Product</a> <a id="389" class="Keyword">using</a> <a id="395" class="Symbol">(</a><a id="396" href="Agda.Builtin.Sigma.html#236" class="InductiveConstructor Operator">_,_</a><a id="399" class="Symbol">;</a> <a id="401" href="Agda.Builtin.Sigma.html#252" class="Field">proj₁</a><a id="406" class="Symbol">;</a> <a id="408" href="Agda.Builtin.Sigma.html#264" class="Field">proj₂</a><a id="413" class="Symbol">)</a>
<a id="415" class="Keyword">open</a> <a id="420" class="Keyword">import</a> <a id="427" href="Function.Bundles.html" class="Module">Function.Bundles</a>
<a id="444" class="Keyword">open</a> <a id="449" class="Keyword">import</a> <a id="456" href="Level.html" class="Module">Level</a> <a id="462" class="Keyword">using</a> <a id="468" class="Symbol">(</a><a id="469" href="Agda.Primitive.html#597" class="Postulate">Level</a><a id="474" class="Symbol">)</a>
<a id="476" class="Keyword">open</a> <a id="481" class="Keyword">import</a> <a id="488" href="Relation.Binary.html" class="Module">Relation.Binary</a> <a id="504" class="Keyword">using</a> <a id="510" class="Symbol">(</a><a id="511" href="Relation.Binary.Bundles.html#1009" class="Record">Setoid</a><a id="517" class="Symbol">;</a> <a id="519" href="Relation.Binary.Structures.html#1522" class="Record">IsEquivalence</a><a id="532" class="Symbol">)</a>
<a id="534" class="Keyword">open</a> <a id="539" class="Keyword">import</a> <a id="546" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="584" class="Symbol">as</a> <a id="587" class="Module">P</a> <a id="589" class="Keyword">using</a> <a id="595" class="Symbol">(</a><a id="596" href="Relation.Binary.PropositionalEquality.Properties.html#5390" class="Function">setoid</a><a id="602" class="Symbol">)</a>
<a id="604" class="Keyword">import</a> <a id="611" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="644" class="Symbol">as</a> <a id="647" class="Module">SetoidReasoning</a>
<a id="663" class="Keyword">open</a> <a id="668" class="Keyword">import</a> <a id="675" href="Function.Consequences.html" class="Module">Function.Consequences</a>

<a id="698" class="Keyword">import</a> <a id="705" href="Function.Construct.Identity.html" class="Module">Function.Construct.Identity</a> <a id="733" class="Symbol">as</a> <a id="736" class="Module">Identity</a>
<a id="745" class="Keyword">import</a> <a id="752" href="Function.Construct.Symmetry.html" class="Module">Function.Construct.Symmetry</a> <a id="780" class="Symbol">as</a> <a id="783" class="Module">Symmetry</a>
<a id="792" class="Keyword">import</a> <a id="799" href="Function.Construct.Composition.html" class="Module">Function.Construct.Composition</a> <a id="830" class="Symbol">as</a> <a id="833" class="Module">Composition</a>

<a id="846" class="Keyword">private</a>
  <a id="856" class="Keyword">variable</a>
    <a id="869" href="Function.Properties.Inverse.html#869" class="Generalizable">a</a> <a id="871" href="Function.Properties.Inverse.html#871" class="Generalizable">b</a> <a id="873" href="Function.Properties.Inverse.html#873" class="Generalizable">ℓ</a> <a id="875" href="Function.Properties.Inverse.html#875" class="Generalizable">ℓ₁</a> <a id="878" href="Function.Properties.Inverse.html#878" class="Generalizable">ℓ₂</a> <a id="881" class="Symbol">:</a> <a id="883" href="Agda.Primitive.html#597" class="Postulate">Level</a>
    <a id="893" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="895" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a> <a id="897" class="Symbol">:</a> <a id="899" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="903" href="Function.Properties.Inverse.html#869" class="Generalizable">a</a>
    <a id="909" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="911" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a> <a id="913" class="Symbol">:</a> <a id="915" href="Relation.Binary.Bundles.html#1009" class="Record">Setoid</a> <a id="922" href="Function.Properties.Inverse.html#869" class="Generalizable">a</a> <a id="924" href="Function.Properties.Inverse.html#873" class="Generalizable">ℓ</a>

<a id="927" class="Comment">------------------------------------------------------------------------</a>
<a id="1000" class="Comment">-- Setoid bundles</a>

<a id="isEquivalence"></a><a id="1019" href="Function.Properties.Inverse.html#1019" class="Function">isEquivalence</a> <a id="1033" class="Symbol">:</a> <a id="1035" href="Relation.Binary.Structures.html#1522" class="Record">IsEquivalence</a> <a id="1049" class="Symbol">(</a><a id="1050" href="Function.Bundles.html#5983" class="Record">Inverse</a> <a id="1058" class="Symbol">{</a><a id="1059" href="Function.Properties.Inverse.html#869" class="Generalizable">a</a><a id="1060" class="Symbol">}</a> <a id="1062" class="Symbol">{</a><a id="1063" href="Function.Properties.Inverse.html#871" class="Generalizable">b</a><a id="1064" class="Symbol">})</a>
<a id="1067" href="Function.Properties.Inverse.html#1019" class="Function">isEquivalence</a> <a id="1081" class="Symbol">=</a> <a id="1083" class="Keyword">record</a>
  <a id="1092" class="Symbol">{</a> <a id="1094" href="Relation.Binary.Structures.html#1568" class="Field">refl</a>  <a id="1100" class="Symbol">=</a> <a id="1102" class="Symbol">λ</a> <a id="1104" class="Symbol">{</a><a id="1105" href="Function.Properties.Inverse.html#1105" class="Bound">x</a><a id="1106" class="Symbol">}</a> <a id="1108" class="Symbol">→</a> <a id="1110" href="Function.Construct.Identity.html#3660" class="Function">Identity.inverse</a> <a id="1127" href="Function.Properties.Inverse.html#1105" class="Bound">x</a>
  <a id="1131" class="Symbol">;</a> <a id="1133" href="Relation.Binary.Structures.html#1594" class="Field">sym</a>   <a id="1139" class="Symbol">=</a> <a id="1141" href="Function.Construct.Symmetry.html#5618" class="Function">Symmetry.inverse</a>
  <a id="1160" class="Symbol">;</a> <a id="1162" href="Relation.Binary.Structures.html#1620" class="Field">trans</a> <a id="1168" class="Symbol">=</a> <a id="1170" href="Function.Construct.Composition.html#8240" class="Function">Composition.inverse</a>
  <a id="1192" class="Symbol">}</a>

<a id="1195" class="Comment">------------------------------------------------------------------------</a>
<a id="1268" class="Comment">-- Propositional bundles</a>

<a id="1294" class="Comment">-- need to η-expand for everything to line up properly</a>
<a id="↔-isEquivalence"></a><a id="1349" href="Function.Properties.Inverse.html#1349" class="Function">↔-isEquivalence</a> <a id="1365" class="Symbol">:</a> <a id="1367" href="Relation.Binary.Structures.html#1522" class="Record">IsEquivalence</a> <a id="1381" class="Symbol">{</a><a id="1382" class="Argument">ℓ</a> <a id="1384" class="Symbol">=</a> <a id="1386" href="Function.Properties.Inverse.html#873" class="Generalizable">ℓ</a><a id="1387" class="Symbol">}</a> <a id="1389" href="Function.Bundles.html#9155" class="Function Operator">_↔_</a>
<a id="1393" href="Function.Properties.Inverse.html#1349" class="Function">↔-isEquivalence</a> <a id="1409" class="Symbol">=</a> <a id="1411" class="Keyword">record</a>
  <a id="1420" class="Symbol">{</a> <a id="1422" href="Relation.Binary.Structures.html#1568" class="Field">refl</a>  <a id="1428" class="Symbol">=</a> <a id="1430" class="Symbol">λ</a> <a id="1432" class="Symbol">{</a><a id="1433" href="Function.Properties.Inverse.html#1433" class="Bound">x</a><a id="1434" class="Symbol">}</a> <a id="1436" class="Symbol">→</a> <a id="1438" href="Function.Construct.Identity.html#3660" class="Function">Identity.inverse</a> <a id="1455" class="Symbol">(</a><a id="1456" href="Relation.Binary.PropositionalEquality.Properties.html#5390" class="Function">P.setoid</a> <a id="1465" href="Function.Properties.Inverse.html#1433" class="Bound">x</a><a id="1466" class="Symbol">)</a>
  <a id="1470" class="Symbol">;</a> <a id="1472" href="Relation.Binary.Structures.html#1594" class="Field">sym</a>   <a id="1478" class="Symbol">=</a> <a id="1480" href="Function.Construct.Symmetry.html#5618" class="Function">Symmetry.inverse</a>
  <a id="1499" class="Symbol">;</a> <a id="1501" href="Relation.Binary.Structures.html#1620" class="Field">trans</a> <a id="1507" class="Symbol">=</a> <a id="1509" href="Function.Construct.Composition.html#8240" class="Function">Composition.inverse</a>
  <a id="1531" class="Symbol">}</a>

<a id="1534" class="Comment">------------------------------------------------------------------------</a>
<a id="1607" class="Comment">-- Conversion functions</a>

<a id="Inverse⇒Injection"></a><a id="1632" href="Function.Properties.Inverse.html#1632" class="Function">Inverse⇒Injection</a> <a id="1650" class="Symbol">:</a> <a id="1652" href="Function.Bundles.html#5983" class="Record">Inverse</a> <a id="1660" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="1662" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a> <a id="1664" class="Symbol">→</a> <a id="1666" href="Function.Bundles.html#2280" class="Record">Injection</a> <a id="1676" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="1678" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a>
<a id="1680" href="Function.Properties.Inverse.html#1632" class="Function">Inverse⇒Injection</a> <a id="1698" class="Symbol">{</a><a id="1699" class="Argument">S</a> <a id="1701" class="Symbol">=</a> <a id="1703" href="Function.Properties.Inverse.html#1703" class="Bound">S</a><a id="1704" class="Symbol">}</a> <a id="1706" href="Function.Properties.Inverse.html#1706" class="Bound">I</a> <a id="1708" class="Symbol">=</a> <a id="1710" class="Keyword">record</a>
  <a id="1719" class="Symbol">{</a> <a id="1721" href="Function.Bundles.html#2336" class="Field">to</a> <a id="1724" class="Symbol">=</a> <a id="1726" href="Function.Bundles.html#6037" class="Field">to</a>
  <a id="1731" class="Symbol">;</a> <a id="1733" href="Function.Bundles.html#2362" class="Field">cong</a> <a id="1738" class="Symbol">=</a> <a id="1740" href="Function.Bundles.html#6085" class="Field">to-cong</a>
  <a id="1750" class="Symbol">;</a> <a id="1752" href="Function.Bundles.html#2407" class="Field">injective</a> <a id="1762" class="Symbol">=</a> <a id="1764" href="Function.Consequences.html#1046" class="Function">inverseʳ⇒injective</a> <a id="1783" href="Function.Properties.Inverse.html#1703" class="Bound">S</a> <a id="1785" class="Symbol">{</a><a id="1786" class="Argument">f⁻¹</a> <a id="1790" class="Symbol">=</a> <a id="1792" href="Function.Bundles.html#6061" class="Field">from</a><a id="1796" class="Symbol">}</a> <a id="1798" href="Function.Bundles.html#6128" class="Field">from-cong</a> <a id="1808" href="Function.Bundles.html#6269" class="Function">inverseʳ</a>
  <a id="1819" class="Symbol">}</a> <a id="1821" class="Keyword">where</a> <a id="1827" class="Keyword">open</a> <a id="1832" href="Function.Bundles.html#5983" class="Module">Inverse</a> <a id="1840" href="Function.Properties.Inverse.html#1706" class="Bound">I</a>

<a id="Inverse⇒Bijection"></a><a id="1843" href="Function.Properties.Inverse.html#1843" class="Function">Inverse⇒Bijection</a> <a id="1861" class="Symbol">:</a> <a id="1863" href="Function.Bundles.html#5983" class="Record">Inverse</a> <a id="1871" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="1873" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a> <a id="1875" class="Symbol">→</a> <a id="1877" href="Function.Bundles.html#3324" class="Record">Bijection</a> <a id="1887" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="1889" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a>
<a id="1891" href="Function.Properties.Inverse.html#1843" class="Function">Inverse⇒Bijection</a> <a id="1909" class="Symbol">{</a><a id="1910" class="Argument">S</a> <a id="1912" class="Symbol">=</a> <a id="1914" href="Function.Properties.Inverse.html#1914" class="Bound">S</a><a id="1915" class="Symbol">}</a> <a id="1917" href="Function.Properties.Inverse.html#1917" class="Bound">I</a> <a id="1919" class="Symbol">=</a> <a id="1921" class="Keyword">record</a>
  <a id="1930" class="Symbol">{</a> <a id="1932" href="Function.Bundles.html#3380" class="Field">to</a>        <a id="1942" class="Symbol">=</a> <a id="1944" href="Function.Bundles.html#6037" class="Field">to</a>
  <a id="1949" class="Symbol">;</a> <a id="1951" href="Function.Bundles.html#3404" class="Field">cong</a>      <a id="1961" class="Symbol">=</a> <a id="1963" href="Function.Bundles.html#6085" class="Field">to-cong</a>
  <a id="1973" class="Symbol">;</a> <a id="1975" href="Function.Bundles.html#3447" class="Field">bijective</a> <a id="1985" class="Symbol">=</a> <a id="1987" href="Function.Consequences.html#1366" class="Function">inverseᵇ⇒bijective</a> <a id="2006" href="Function.Properties.Inverse.html#1914" class="Bound">S</a> <a id="2008" href="Function.Bundles.html#6128" class="Field">from-cong</a> <a id="2018" href="Function.Bundles.html#6173" class="Field">inverse</a>
  <a id="2028" class="Symbol">}</a> <a id="2030" class="Keyword">where</a> <a id="2036" class="Keyword">open</a> <a id="2041" href="Function.Bundles.html#5983" class="Module">Inverse</a> <a id="2049" href="Function.Properties.Inverse.html#1917" class="Bound">I</a>

<a id="Inverse⇒Equivalence"></a><a id="2052" href="Function.Properties.Inverse.html#2052" class="Function">Inverse⇒Equivalence</a> <a id="2072" class="Symbol">:</a> <a id="2074" href="Function.Bundles.html#5983" class="Record">Inverse</a> <a id="2082" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="2084" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a> <a id="2086" class="Symbol">→</a> <a id="2088" href="Function.Bundles.html#4274" class="Record">Equivalence</a> <a id="2100" href="Function.Properties.Inverse.html#909" class="Generalizable">S</a> <a id="2102" href="Function.Properties.Inverse.html#911" class="Generalizable">T</a>
<a id="2104" href="Function.Properties.Inverse.html#2052" class="Function">Inverse⇒Equivalence</a> <a id="2124" href="Function.Properties.Inverse.html#2124" class="Bound">I</a> <a id="2126" class="Symbol">=</a> <a id="2128" class="Keyword">record</a>
  <a id="2137" class="Symbol">{</a> <a id="2139" href="Function.Bundles.html#4332" class="Field">to</a>        <a id="2149" class="Symbol">=</a> <a id="2151" href="Function.Bundles.html#6037" class="Field">to</a>
  <a id="2156" class="Symbol">;</a> <a id="2158" href="Function.Bundles.html#4356" class="Field">from</a>      <a id="2168" class="Symbol">=</a> <a id="2170" href="Function.Bundles.html#6061" class="Field">from</a>
  <a id="2177" class="Symbol">;</a> <a id="2179" href="Function.Bundles.html#4380" class="Field">to-cong</a>   <a id="2189" class="Symbol">=</a> <a id="2191" href="Function.Bundles.html#6085" class="Field">to-cong</a>
  <a id="2201" class="Symbol">;</a> <a id="2203" href="Function.Bundles.html#4423" class="Field">from-cong</a> <a id="2213" class="Symbol">=</a> <a id="2215" href="Function.Bundles.html#6128" class="Field">from-cong</a>
  <a id="2227" class="Symbol">}</a> <a id="2229" class="Keyword">where</a> <a id="2235" class="Keyword">open</a> <a id="2240" href="Function.Bundles.html#5983" class="Module">Inverse</a> <a id="2248" href="Function.Properties.Inverse.html#2124" class="Bound">I</a>

<a id="↔⇒↣"></a><a id="2251" href="Function.Properties.Inverse.html#2251" class="Function">↔⇒↣</a> <a id="2255" class="Symbol">:</a> <a id="2257" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="2259" href="Function.Bundles.html#9155" class="Function Operator">↔</a> <a id="2261" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a> <a id="2263" class="Symbol">→</a> <a id="2265" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="2267" href="Function.Bundles.html#8634" class="Function Operator">↣</a> <a id="2269" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a>
<a id="2271" href="Function.Properties.Inverse.html#2251" class="Function">↔⇒↣</a> <a id="2275" class="Symbol">=</a> <a id="2277" href="Function.Properties.Inverse.html#1632" class="Function">Inverse⇒Injection</a>

<a id="↔⇒⤖"></a><a id="2296" href="Function.Properties.Inverse.html#2296" class="Function">↔⇒⤖</a> <a id="2300" class="Symbol">:</a> <a id="2302" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="2304" href="Function.Bundles.html#9155" class="Function Operator">↔</a> <a id="2306" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a> <a id="2308" class="Symbol">→</a> <a id="2310" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="2312" href="Function.Bundles.html#8781" class="Function Operator">⤖</a> <a id="2314" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a>
<a id="2316" href="Function.Properties.Inverse.html#2296" class="Function">↔⇒⤖</a> <a id="2320" class="Symbol">=</a> <a id="2322" href="Function.Properties.Inverse.html#1843" class="Function">Inverse⇒Bijection</a>

<a id="↔⇒⇔"></a><a id="2341" href="Function.Properties.Inverse.html#2341" class="Function">↔⇒⇔</a> <a id="2345" class="Symbol">:</a> <a id="2347" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="2349" href="Function.Bundles.html#9155" class="Function Operator">↔</a> <a id="2351" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a> <a id="2353" class="Symbol">→</a> <a id="2355" href="Function.Properties.Inverse.html#893" class="Generalizable">A</a> <a id="2357" href="Function.Bundles.html#8854" class="Function Operator">⇔</a> <a id="2359" href="Function.Properties.Inverse.html#895" class="Generalizable">B</a>
<a id="2361" href="Function.Properties.Inverse.html#2341" class="Function">↔⇒⇔</a> <a id="2365" class="Symbol">=</a> <a id="2367" href="Function.Properties.Inverse.html#2052" class="Function">Inverse⇒Equivalence</a>
</pre></body></html>