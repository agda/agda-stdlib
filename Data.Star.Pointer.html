<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Star.Pointer</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Pointers into star-lists</a>
<a id="134" class="Comment">------------------------------------------------------------------------</a>

<a id="208" class="Symbol">{-#</a> <a id="212" class="Keyword">OPTIONS</a> <a id="220" class="Pragma">--with-K</a> <a id="229" class="Pragma">--safe</a> <a id="236" class="Symbol">#-}</a>

<a id="241" class="Keyword">module</a> <a id="248" href="Data.Star.Pointer.html" class="Module">Data.Star.Pointer</a> <a id="266" class="Symbol">{</a><a id="267" href="Data.Star.Pointer.html#267" class="Bound">ℓ</a><a id="268" class="Symbol">}</a> <a id="270" class="Symbol">{</a><a id="271" href="Data.Star.Pointer.html#271" class="Bound">I</a> <a id="273" class="Symbol">:</a> <a id="275" class="PrimitiveType">Set</a> <a id="279" href="Data.Star.Pointer.html#267" class="Bound">ℓ</a><a id="280" class="Symbol">}</a> <a id="282" class="Keyword">where</a>

<a id="289" class="Keyword">open</a> <a id="294" class="Keyword">import</a> <a id="301" href="Data.Maybe.Base.html" class="Module">Data.Maybe.Base</a> <a id="317" class="Keyword">using</a> <a id="323" class="Symbol">(</a><a id="324" href="Data.Maybe.Base.html#800" class="Datatype">Maybe</a><a id="329" class="Symbol">;</a> <a id="331" href="Data.Maybe.Base.html#864" class="InductiveConstructor">nothing</a><a id="338" class="Symbol">;</a> <a id="340" href="Data.Maybe.Base.html#834" class="InductiveConstructor">just</a><a id="344" class="Symbol">)</a>
<a id="346" class="Keyword">open</a> <a id="351" class="Keyword">import</a> <a id="358" href="Data.Star.Decoration.html" class="Module">Data.Star.Decoration</a>
<a id="379" class="Keyword">open</a> <a id="384" class="Keyword">import</a> <a id="391" href="Data.Unit.html" class="Module">Data.Unit</a>
<a id="401" class="Keyword">open</a> <a id="406" class="Keyword">import</a> <a id="413" href="Function.html" class="Module">Function</a>
<a id="422" class="Keyword">open</a> <a id="427" class="Keyword">import</a> <a id="434" href="Level.html" class="Module">Level</a>
<a id="440" class="Keyword">open</a> <a id="445" class="Keyword">import</a> <a id="452" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="468" class="Keyword">open</a> <a id="473" class="Keyword">import</a> <a id="480" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html" class="Module">Relation.Binary.Construct.Closure.ReflexiveTransitive</a>

<a id="535" class="Comment">-- Pointers into star-lists. The edge pointed to is decorated with Q,</a>
<a id="605" class="Comment">-- while other edges are decorated with P.</a>

<a id="649" class="Keyword">data</a> <a id="Pointer"></a><a id="654" href="Data.Star.Pointer.html#654" class="Datatype">Pointer</a> <a id="662" class="Symbol">{</a><a id="663" href="Data.Star.Pointer.html#663" class="Bound">r</a> <a id="665" href="Data.Star.Pointer.html#665" class="Bound">p</a> <a id="667" href="Data.Star.Pointer.html#667" class="Bound">q</a><a id="668" class="Symbol">}</a> <a id="670" class="Symbol">{</a><a id="671" href="Data.Star.Pointer.html#671" class="Bound">T</a> <a id="673" class="Symbol">:</a> <a id="675" href="Relation.Binary.Core.html#1076" class="Function">Rel</a> <a id="679" href="Data.Star.Pointer.html#271" class="Bound">I</a> <a id="681" href="Data.Star.Pointer.html#663" class="Bound">r</a><a id="682" class="Symbol">}</a>
             <a id="697" class="Symbol">(</a><a id="698" href="Data.Star.Pointer.html#698" class="Bound">P</a> <a id="700" class="Symbol">:</a> <a id="702" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="711" href="Data.Star.Pointer.html#665" class="Bound">p</a> <a id="713" href="Data.Star.Pointer.html#671" class="Bound">T</a><a id="714" class="Symbol">)</a> <a id="716" class="Symbol">(</a><a id="717" href="Data.Star.Pointer.html#717" class="Bound">Q</a> <a id="719" class="Symbol">:</a> <a id="721" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="730" href="Data.Star.Pointer.html#667" class="Bound">q</a> <a id="732" href="Data.Star.Pointer.html#671" class="Bound">T</a><a id="733" class="Symbol">)</a>
             <a id="748" class="Symbol">:</a> <a id="750" href="Relation.Binary.Core.html#1076" class="Function">Rel</a> <a id="754" class="Symbol">(</a><a id="755" href="Data.Maybe.Base.html#800" class="Datatype">Maybe</a> <a id="761" class="Symbol">(</a><a id="762" href="Relation.Binary.Core.html#6335" class="Record">NonEmpty</a> <a id="771" class="Symbol">(</a><a id="772" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="777" href="Data.Star.Pointer.html#671" class="Bound">T</a><a id="778" class="Symbol">)))</a> <a id="782" class="Symbol">(</a><a id="783" href="Data.Star.Pointer.html#665" class="Bound">p</a> <a id="785" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="787" href="Data.Star.Pointer.html#667" class="Bound">q</a><a id="788" class="Symbol">)</a> <a id="790" class="Keyword">where</a>
  <a id="Pointer.step"></a><a id="798" href="Data.Star.Pointer.html#798" class="InductiveConstructor">step</a> <a id="803" class="Symbol">:</a> <a id="805" class="Symbol">∀</a> <a id="807" class="Symbol">{</a><a id="808" href="Data.Star.Pointer.html#808" class="Bound">i</a> <a id="810" href="Data.Star.Pointer.html#810" class="Bound">j</a> <a id="812" href="Data.Star.Pointer.html#812" class="Bound">k</a><a id="813" class="Symbol">}</a> <a id="815" class="Symbol">{</a><a id="816" href="Data.Star.Pointer.html#816" class="Bound">x</a> <a id="818" class="Symbol">:</a> <a id="820" href="Data.Star.Pointer.html#671" class="Bound">T</a> <a id="822" href="Data.Star.Pointer.html#808" class="Bound">i</a> <a id="824" href="Data.Star.Pointer.html#810" class="Bound">j</a><a id="825" class="Symbol">}</a> <a id="827" class="Symbol">{</a><a id="828" href="Data.Star.Pointer.html#828" class="Bound">xs</a> <a id="831" class="Symbol">:</a> <a id="833" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="838" href="Data.Star.Pointer.html#671" class="Bound">T</a> <a id="840" href="Data.Star.Pointer.html#810" class="Bound">j</a> <a id="842" href="Data.Star.Pointer.html#812" class="Bound">k</a><a id="843" class="Symbol">}</a>
         <a id="854" class="Symbol">(</a><a id="855" href="Data.Star.Pointer.html#855" class="Bound">p</a> <a id="857" class="Symbol">:</a> <a id="859" href="Data.Star.Pointer.html#698" class="Bound">P</a> <a id="861" href="Data.Star.Pointer.html#816" class="Bound">x</a><a id="862" class="Symbol">)</a> <a id="864" class="Symbol">→</a> <a id="866" href="Data.Star.Pointer.html#654" class="Datatype">Pointer</a> <a id="874" href="Data.Star.Pointer.html#698" class="Bound">P</a> <a id="876" href="Data.Star.Pointer.html#717" class="Bound">Q</a> <a id="878" class="Symbol">(</a><a id="879" href="Data.Maybe.Base.html#834" class="InductiveConstructor">just</a> <a id="884" class="Symbol">(</a><a id="885" href="Relation.Binary.Core.html#6438" class="InductiveConstructor">nonEmpty</a> <a id="894" class="Symbol">(</a><a id="895" href="Data.Star.Pointer.html#816" class="Bound">x</a> <a id="897" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="899" href="Data.Star.Pointer.html#828" class="Bound">xs</a><a id="901" class="Symbol">)))</a>
                                 <a id="938" class="Symbol">(</a><a id="939" href="Data.Maybe.Base.html#834" class="InductiveConstructor">just</a> <a id="944" class="Symbol">(</a><a id="945" href="Relation.Binary.Core.html#6438" class="InductiveConstructor">nonEmpty</a> <a id="954" href="Data.Star.Pointer.html#828" class="Bound">xs</a><a id="956" class="Symbol">))</a>
  <a id="Pointer.done"></a><a id="961" href="Data.Star.Pointer.html#961" class="InductiveConstructor">done</a> <a id="966" class="Symbol">:</a> <a id="968" class="Symbol">∀</a> <a id="970" class="Symbol">{</a><a id="971" href="Data.Star.Pointer.html#971" class="Bound">i</a> <a id="973" href="Data.Star.Pointer.html#973" class="Bound">j</a> <a id="975" href="Data.Star.Pointer.html#975" class="Bound">k</a><a id="976" class="Symbol">}</a> <a id="978" class="Symbol">{</a><a id="979" href="Data.Star.Pointer.html#979" class="Bound">x</a> <a id="981" class="Symbol">:</a> <a id="983" href="Data.Star.Pointer.html#671" class="Bound">T</a> <a id="985" href="Data.Star.Pointer.html#971" class="Bound">i</a> <a id="987" href="Data.Star.Pointer.html#973" class="Bound">j</a><a id="988" class="Symbol">}</a> <a id="990" class="Symbol">{</a><a id="991" href="Data.Star.Pointer.html#991" class="Bound">xs</a> <a id="994" class="Symbol">:</a> <a id="996" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="1001" href="Data.Star.Pointer.html#671" class="Bound">T</a> <a id="1003" href="Data.Star.Pointer.html#973" class="Bound">j</a> <a id="1005" href="Data.Star.Pointer.html#975" class="Bound">k</a><a id="1006" class="Symbol">}</a>
         <a id="1017" class="Symbol">(</a><a id="1018" href="Data.Star.Pointer.html#1018" class="Bound">q</a> <a id="1020" class="Symbol">:</a> <a id="1022" href="Data.Star.Pointer.html#717" class="Bound">Q</a> <a id="1024" href="Data.Star.Pointer.html#979" class="Bound">x</a><a id="1025" class="Symbol">)</a> <a id="1027" class="Symbol">→</a> <a id="1029" href="Data.Star.Pointer.html#654" class="Datatype">Pointer</a> <a id="1037" href="Data.Star.Pointer.html#698" class="Bound">P</a> <a id="1039" href="Data.Star.Pointer.html#717" class="Bound">Q</a> <a id="1041" class="Symbol">(</a><a id="1042" href="Data.Maybe.Base.html#834" class="InductiveConstructor">just</a> <a id="1047" class="Symbol">(</a><a id="1048" href="Relation.Binary.Core.html#6438" class="InductiveConstructor">nonEmpty</a> <a id="1057" class="Symbol">(</a><a id="1058" href="Data.Star.Pointer.html#979" class="Bound">x</a> <a id="1060" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="1062" href="Data.Star.Pointer.html#991" class="Bound">xs</a><a id="1064" class="Symbol">)))</a> <a id="1068" href="Data.Maybe.Base.html#864" class="InductiveConstructor">nothing</a>

<a id="1077" class="Comment">-- Any P Q xs means that some edge in xs satisfies Q, while all</a>
<a id="1141" class="Comment">-- preceding edges satisfy P. A star-list of type Any Always Always xs</a>
<a id="1212" class="Comment">-- is basically a prefix of xs; the existence of such a prefix</a>
<a id="1275" class="Comment">-- guarantees that xs is non-empty.</a>

<a id="Any"></a><a id="1312" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="1316" class="Symbol">:</a> <a id="1318" class="Symbol">∀</a> <a id="1320" class="Symbol">{</a><a id="1321" href="Data.Star.Pointer.html#1321" class="Bound">r</a> <a id="1323" href="Data.Star.Pointer.html#1323" class="Bound">p</a> <a id="1325" href="Data.Star.Pointer.html#1325" class="Bound">q</a><a id="1326" class="Symbol">}</a> <a id="1328" class="Symbol">{</a><a id="1329" href="Data.Star.Pointer.html#1329" class="Bound">T</a> <a id="1331" class="Symbol">:</a> <a id="1333" href="Relation.Binary.Core.html#1076" class="Function">Rel</a> <a id="1337" href="Data.Star.Pointer.html#271" class="Bound">I</a> <a id="1339" href="Data.Star.Pointer.html#1321" class="Bound">r</a><a id="1340" class="Symbol">}</a> <a id="1342" class="Symbol">(</a><a id="1343" href="Data.Star.Pointer.html#1343" class="Bound">P</a> <a id="1345" class="Symbol">:</a> <a id="1347" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1356" href="Data.Star.Pointer.html#1323" class="Bound">p</a> <a id="1358" href="Data.Star.Pointer.html#1329" class="Bound">T</a><a id="1359" class="Symbol">)</a> <a id="1361" class="Symbol">(</a><a id="1362" href="Data.Star.Pointer.html#1362" class="Bound">Q</a> <a id="1364" class="Symbol">:</a> <a id="1366" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1375" href="Data.Star.Pointer.html#1325" class="Bound">q</a> <a id="1377" href="Data.Star.Pointer.html#1329" class="Bound">T</a><a id="1378" class="Symbol">)</a> <a id="1380" class="Symbol">→</a>
      <a id="1388" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1397" class="Symbol">(</a><a id="1398" href="Data.Star.Pointer.html#267" class="Bound">ℓ</a> <a id="1400" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1402" class="Symbol">(</a><a id="1403" href="Data.Star.Pointer.html#1321" class="Bound">r</a> <a id="1405" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1407" class="Symbol">(</a><a id="1408" href="Data.Star.Pointer.html#1323" class="Bound">p</a> <a id="1410" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1412" href="Data.Star.Pointer.html#1325" class="Bound">q</a><a id="1413" class="Symbol">)))</a> <a id="1417" class="Symbol">(</a><a id="1418" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="1423" href="Data.Star.Pointer.html#1329" class="Bound">T</a><a id="1424" class="Symbol">)</a>
<a id="1426" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="1430" href="Data.Star.Pointer.html#1430" class="Bound">P</a> <a id="1432" href="Data.Star.Pointer.html#1432" class="Bound">Q</a> <a id="1434" href="Data.Star.Pointer.html#1434" class="Bound">xs</a> <a id="1437" class="Symbol">=</a> <a id="1439" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="1444" class="Symbol">(</a><a id="1445" href="Data.Star.Pointer.html#654" class="Datatype">Pointer</a> <a id="1453" href="Data.Star.Pointer.html#1430" class="Bound">P</a> <a id="1455" href="Data.Star.Pointer.html#1432" class="Bound">Q</a><a id="1456" class="Symbol">)</a> <a id="1458" class="Symbol">(</a><a id="1459" href="Data.Maybe.Base.html#834" class="InductiveConstructor">just</a> <a id="1464" class="Symbol">(</a><a id="1465" href="Relation.Binary.Core.html#6438" class="InductiveConstructor">nonEmpty</a> <a id="1474" href="Data.Star.Pointer.html#1434" class="Bound">xs</a><a id="1476" class="Symbol">))</a> <a id="1479" href="Data.Maybe.Base.html#864" class="InductiveConstructor">nothing</a>

<a id="1488" class="Keyword">module</a> <a id="1495" href="Data.Star.Pointer.html#1495" class="Module">_</a> <a id="1497" class="Symbol">{</a><a id="1498" href="Data.Star.Pointer.html#1498" class="Bound">r</a> <a id="1500" href="Data.Star.Pointer.html#1500" class="Bound">p</a> <a id="1502" href="Data.Star.Pointer.html#1502" class="Bound">q</a><a id="1503" class="Symbol">}</a> <a id="1505" class="Symbol">{</a><a id="1506" href="Data.Star.Pointer.html#1506" class="Bound">T</a> <a id="1508" class="Symbol">:</a> <a id="1510" href="Relation.Binary.Core.html#1076" class="Function">Rel</a> <a id="1514" href="Data.Star.Pointer.html#271" class="Bound">I</a> <a id="1516" href="Data.Star.Pointer.html#1498" class="Bound">r</a><a id="1517" class="Symbol">}</a> <a id="1519" class="Symbol">{</a><a id="1520" href="Data.Star.Pointer.html#1520" class="Bound">P</a> <a id="1522" class="Symbol">:</a> <a id="1524" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1533" href="Data.Star.Pointer.html#1500" class="Bound">p</a> <a id="1535" href="Data.Star.Pointer.html#1506" class="Bound">T</a><a id="1536" class="Symbol">}</a> <a id="1538" class="Symbol">{</a><a id="1539" href="Data.Star.Pointer.html#1539" class="Bound">Q</a> <a id="1541" class="Symbol">:</a> <a id="1543" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1552" href="Data.Star.Pointer.html#1502" class="Bound">q</a> <a id="1554" href="Data.Star.Pointer.html#1506" class="Bound">T</a><a id="1555" class="Symbol">}</a> <a id="1557" class="Keyword">where</a>

  <a id="1566" href="Data.Star.Pointer.html#1566" class="Function">this</a> <a id="1571" class="Symbol">:</a> <a id="1573" class="Symbol">∀</a> <a id="1575" class="Symbol">{</a><a id="1576" href="Data.Star.Pointer.html#1576" class="Bound">i</a> <a id="1578" href="Data.Star.Pointer.html#1578" class="Bound">j</a> <a id="1580" href="Data.Star.Pointer.html#1580" class="Bound">k</a><a id="1581" class="Symbol">}</a> <a id="1583" class="Symbol">{</a><a id="1584" href="Data.Star.Pointer.html#1584" class="Bound">x</a> <a id="1586" class="Symbol">:</a> <a id="1588" href="Data.Star.Pointer.html#1506" class="Bound">T</a> <a id="1590" href="Data.Star.Pointer.html#1576" class="Bound">i</a> <a id="1592" href="Data.Star.Pointer.html#1578" class="Bound">j</a><a id="1593" class="Symbol">}</a> <a id="1595" class="Symbol">{</a><a id="1596" href="Data.Star.Pointer.html#1596" class="Bound">xs</a> <a id="1599" class="Symbol">:</a> <a id="1601" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="1606" href="Data.Star.Pointer.html#1506" class="Bound">T</a> <a id="1608" href="Data.Star.Pointer.html#1578" class="Bound">j</a> <a id="1610" href="Data.Star.Pointer.html#1580" class="Bound">k</a><a id="1611" class="Symbol">}</a> <a id="1613" class="Symbol">→</a>
         <a id="1624" href="Data.Star.Pointer.html#1539" class="Bound">Q</a> <a id="1626" href="Data.Star.Pointer.html#1584" class="Bound">x</a> <a id="1628" class="Symbol">→</a> <a id="1630" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="1634" href="Data.Star.Pointer.html#1520" class="Bound">P</a> <a id="1636" href="Data.Star.Pointer.html#1539" class="Bound">Q</a> <a id="1638" class="Symbol">(</a><a id="1639" href="Data.Star.Pointer.html#1584" class="Bound">x</a> <a id="1641" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="1643" href="Data.Star.Pointer.html#1596" class="Bound">xs</a><a id="1645" class="Symbol">)</a>
  <a id="1649" href="Data.Star.Pointer.html#1566" class="Function">this</a> <a id="1654" href="Data.Star.Pointer.html#1654" class="Bound">q</a> <a id="1656" class="Symbol">=</a> <a id="1658" href="Data.Star.Pointer.html#961" class="InductiveConstructor">done</a> <a id="1663" href="Data.Star.Pointer.html#1654" class="Bound">q</a> <a id="1665" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="1667" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#546" class="InductiveConstructor">ε</a>

  <a id="1672" href="Data.Star.Pointer.html#1672" class="Function">that</a> <a id="1677" class="Symbol">:</a> <a id="1679" class="Symbol">∀</a> <a id="1681" class="Symbol">{</a><a id="1682" href="Data.Star.Pointer.html#1682" class="Bound">i</a> <a id="1684" href="Data.Star.Pointer.html#1684" class="Bound">j</a> <a id="1686" href="Data.Star.Pointer.html#1686" class="Bound">k</a><a id="1687" class="Symbol">}</a> <a id="1689" class="Symbol">{</a><a id="1690" href="Data.Star.Pointer.html#1690" class="Bound">x</a> <a id="1692" class="Symbol">:</a> <a id="1694" href="Data.Star.Pointer.html#1506" class="Bound">T</a> <a id="1696" href="Data.Star.Pointer.html#1682" class="Bound">i</a> <a id="1698" href="Data.Star.Pointer.html#1684" class="Bound">j</a><a id="1699" class="Symbol">}</a> <a id="1701" class="Symbol">{</a><a id="1702" href="Data.Star.Pointer.html#1702" class="Bound">xs</a> <a id="1705" class="Symbol">:</a> <a id="1707" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="1712" href="Data.Star.Pointer.html#1506" class="Bound">T</a> <a id="1714" href="Data.Star.Pointer.html#1684" class="Bound">j</a> <a id="1716" href="Data.Star.Pointer.html#1686" class="Bound">k</a><a id="1717" class="Symbol">}</a> <a id="1719" class="Symbol">→</a>
         <a id="1730" href="Data.Star.Pointer.html#1520" class="Bound">P</a> <a id="1732" href="Data.Star.Pointer.html#1690" class="Bound">x</a> <a id="1734" class="Symbol">→</a> <a id="1736" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="1740" href="Data.Star.Pointer.html#1520" class="Bound">P</a> <a id="1742" href="Data.Star.Pointer.html#1539" class="Bound">Q</a> <a id="1744" href="Data.Star.Pointer.html#1702" class="Bound">xs</a> <a id="1747" class="Symbol">→</a> <a id="1749" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="1753" href="Data.Star.Pointer.html#1520" class="Bound">P</a> <a id="1755" href="Data.Star.Pointer.html#1539" class="Bound">Q</a> <a id="1757" class="Symbol">(</a><a id="1758" href="Data.Star.Pointer.html#1690" class="Bound">x</a> <a id="1760" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="1762" href="Data.Star.Pointer.html#1702" class="Bound">xs</a><a id="1764" class="Symbol">)</a>
  <a id="1768" href="Data.Star.Pointer.html#1672" class="Function">that</a> <a id="1773" href="Data.Star.Pointer.html#1773" class="Bound">p</a> <a id="1775" class="Symbol">=</a> <a id="1777" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">_◅_</a> <a id="1781" class="Symbol">(</a><a id="1782" href="Data.Star.Pointer.html#798" class="InductiveConstructor">step</a> <a id="1787" href="Data.Star.Pointer.html#1773" class="Bound">p</a><a id="1788" class="Symbol">)</a>

<a id="1791" class="Comment">-- Safe lookup.</a>

<a id="1808" class="Keyword">data</a> <a id="Result"></a><a id="1813" href="Data.Star.Pointer.html#1813" class="Datatype">Result</a> <a id="1820" class="Symbol">{</a><a id="1821" href="Data.Star.Pointer.html#1821" class="Bound">r</a> <a id="1823" href="Data.Star.Pointer.html#1823" class="Bound">p</a> <a id="1825" href="Data.Star.Pointer.html#1825" class="Bound">q</a><a id="1826" class="Symbol">}</a> <a id="1828" class="Symbol">(</a><a id="1829" href="Data.Star.Pointer.html#1829" class="Bound">T</a> <a id="1831" class="Symbol">:</a> <a id="1833" href="Relation.Binary.Core.html#1076" class="Function">Rel</a> <a id="1837" href="Data.Star.Pointer.html#271" class="Bound">I</a> <a id="1839" href="Data.Star.Pointer.html#1821" class="Bound">r</a><a id="1840" class="Symbol">)</a>
            <a id="1854" class="Symbol">(</a><a id="1855" href="Data.Star.Pointer.html#1855" class="Bound">P</a> <a id="1857" class="Symbol">:</a> <a id="1859" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1868" href="Data.Star.Pointer.html#1823" class="Bound">p</a> <a id="1870" href="Data.Star.Pointer.html#1829" class="Bound">T</a><a id="1871" class="Symbol">)</a> <a id="1873" class="Symbol">(</a><a id="1874" href="Data.Star.Pointer.html#1874" class="Bound">Q</a> <a id="1876" class="Symbol">:</a> <a id="1878" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="1887" href="Data.Star.Pointer.html#1825" class="Bound">q</a> <a id="1889" href="Data.Star.Pointer.html#1829" class="Bound">T</a><a id="1890" class="Symbol">)</a> <a id="1892" class="Symbol">:</a> <a id="1894" class="PrimitiveType">Set</a> <a id="1898" class="Symbol">(</a><a id="1899" href="Data.Star.Pointer.html#267" class="Bound">ℓ</a> <a id="1901" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1903" href="Data.Star.Pointer.html#1821" class="Bound">r</a> <a id="1905" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1907" href="Data.Star.Pointer.html#1823" class="Bound">p</a> <a id="1909" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="1911" href="Data.Star.Pointer.html#1825" class="Bound">q</a><a id="1912" class="Symbol">)</a> <a id="1914" class="Keyword">where</a>
  <a id="Result.result"></a><a id="1922" href="Data.Star.Pointer.html#1922" class="InductiveConstructor">result</a> <a id="1929" class="Symbol">:</a> <a id="1931" class="Symbol">∀</a> <a id="1933" class="Symbol">{</a><a id="1934" href="Data.Star.Pointer.html#1934" class="Bound">i</a> <a id="1936" href="Data.Star.Pointer.html#1936" class="Bound">j</a><a id="1937" class="Symbol">}</a> <a id="1939" class="Symbol">{</a><a id="1940" href="Data.Star.Pointer.html#1940" class="Bound">x</a> <a id="1942" class="Symbol">:</a> <a id="1944" href="Data.Star.Pointer.html#1829" class="Bound">T</a> <a id="1946" href="Data.Star.Pointer.html#1934" class="Bound">i</a> <a id="1948" href="Data.Star.Pointer.html#1936" class="Bound">j</a><a id="1949" class="Symbol">}</a> <a id="1951" class="Symbol">(</a><a id="1952" href="Data.Star.Pointer.html#1952" class="Bound">p</a> <a id="1954" class="Symbol">:</a> <a id="1956" href="Data.Star.Pointer.html#1855" class="Bound">P</a> <a id="1958" href="Data.Star.Pointer.html#1940" class="Bound">x</a><a id="1959" class="Symbol">)</a> <a id="1961" class="Symbol">(</a><a id="1962" href="Data.Star.Pointer.html#1962" class="Bound">q</a> <a id="1964" class="Symbol">:</a> <a id="1966" href="Data.Star.Pointer.html#1874" class="Bound">Q</a> <a id="1968" href="Data.Star.Pointer.html#1940" class="Bound">x</a><a id="1969" class="Symbol">)</a> <a id="1971" class="Symbol">→</a> <a id="1973" href="Data.Star.Pointer.html#1813" class="Datatype">Result</a> <a id="1980" href="Data.Star.Pointer.html#1829" class="Bound">T</a> <a id="1982" href="Data.Star.Pointer.html#1855" class="Bound">P</a> <a id="1984" href="Data.Star.Pointer.html#1874" class="Bound">Q</a>

<a id="1987" class="Comment">-- The first argument points out which edge to extract. The edge is</a>
<a id="2055" class="Comment">-- returned, together with proofs that it satisfies Q and R.</a>

<a id="2117" class="Keyword">module</a> <a id="2124" href="Data.Star.Pointer.html#2124" class="Module">_</a> <a id="2126" class="Symbol">{</a><a id="2127" href="Data.Star.Pointer.html#2127" class="Bound">t</a> <a id="2129" href="Data.Star.Pointer.html#2129" class="Bound">p</a> <a id="2131" href="Data.Star.Pointer.html#2131" class="Bound">q</a><a id="2132" class="Symbol">}</a> <a id="2134" class="Symbol">{</a><a id="2135" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2137" class="Symbol">:</a> <a id="2139" href="Relation.Binary.Core.html#1076" class="Function">Rel</a> <a id="2143" href="Data.Star.Pointer.html#271" class="Bound">I</a> <a id="2145" href="Data.Star.Pointer.html#2127" class="Bound">t</a><a id="2146" class="Symbol">}</a> <a id="2148" class="Symbol">{</a><a id="2149" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="2151" class="Symbol">:</a> <a id="2153" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="2162" href="Data.Star.Pointer.html#2129" class="Bound">p</a> <a id="2164" href="Data.Star.Pointer.html#2135" class="Bound">T</a><a id="2165" class="Symbol">}</a> <a id="2167" class="Symbol">{</a><a id="2168" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="2170" class="Symbol">:</a> <a id="2172" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="2181" href="Data.Star.Pointer.html#2131" class="Bound">q</a> <a id="2183" href="Data.Star.Pointer.html#2135" class="Bound">T</a><a id="2184" class="Symbol">}</a> <a id="2186" class="Keyword">where</a>

  <a id="2195" href="Data.Star.Pointer.html#2195" class="Function">lookup</a> <a id="2202" class="Symbol">:</a> <a id="2204" class="Symbol">∀</a> <a id="2206" class="Symbol">{</a><a id="2207" href="Data.Star.Pointer.html#2207" class="Bound">r</a><a id="2208" class="Symbol">}</a> <a id="2210" class="Symbol">{</a><a id="2211" href="Data.Star.Pointer.html#2211" class="Bound">R</a> <a id="2213" class="Symbol">:</a> <a id="2215" href="Data.Star.Decoration.html#500" class="Function">EdgePred</a> <a id="2224" href="Data.Star.Pointer.html#2207" class="Bound">r</a> <a id="2226" href="Data.Star.Pointer.html#2135" class="Bound">T</a><a id="2227" class="Symbol">}</a> <a id="2229" class="Symbol">{</a><a id="2230" href="Data.Star.Pointer.html#2230" class="Bound">i</a> <a id="2232" href="Data.Star.Pointer.html#2232" class="Bound">j</a><a id="2233" class="Symbol">}</a> <a id="2235" class="Symbol">{</a><a id="2236" href="Data.Star.Pointer.html#2236" class="Bound">xs</a> <a id="2239" class="Symbol">:</a> <a id="2241" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="2246" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2248" href="Data.Star.Pointer.html#2230" class="Bound">i</a> <a id="2250" href="Data.Star.Pointer.html#2232" class="Bound">j</a><a id="2251" class="Symbol">}</a> <a id="2253" class="Symbol">→</a>
           <a id="2266" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="2270" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="2272" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="2274" href="Data.Star.Pointer.html#2236" class="Bound">xs</a> <a id="2277" class="Symbol">→</a> <a id="2279" href="Data.Star.Decoration.html#1528" class="Function">All</a> <a id="2283" href="Data.Star.Pointer.html#2211" class="Bound">R</a> <a id="2285" href="Data.Star.Pointer.html#2236" class="Bound">xs</a> <a id="2288" class="Symbol">→</a> <a id="2290" href="Data.Star.Pointer.html#1813" class="Datatype">Result</a> <a id="2297" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2299" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="2301" href="Data.Star.Pointer.html#2211" class="Bound">R</a>
  <a id="2305" href="Data.Star.Pointer.html#2195" class="Function">lookup</a> <a id="2312" class="Symbol">(</a><a id="2313" href="Data.Star.Pointer.html#961" class="InductiveConstructor">done</a> <a id="2318" href="Data.Star.Pointer.html#2318" class="Bound">q</a> <a id="2320" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2322" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#546" class="InductiveConstructor">ε</a><a id="2323" class="Symbol">)</a>      <a id="2330" class="Symbol">(</a><a id="2331" href="Data.Star.Decoration.html#1016" class="InductiveConstructor">↦</a> <a id="2333" href="Data.Star.Pointer.html#2333" class="Bound">r</a> <a id="2335" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2337" class="Symbol">_)</a>  <a id="2341" class="Symbol">=</a> <a id="2343" href="Data.Star.Pointer.html#1922" class="InductiveConstructor">result</a> <a id="2350" href="Data.Star.Pointer.html#2318" class="Bound">q</a> <a id="2352" href="Data.Star.Pointer.html#2333" class="Bound">r</a>
  <a id="2356" href="Data.Star.Pointer.html#2195" class="Function">lookup</a> <a id="2363" class="Symbol">(</a><a id="2364" href="Data.Star.Pointer.html#798" class="InductiveConstructor">step</a> <a id="2369" href="Data.Star.Pointer.html#2369" class="Bound">p</a> <a id="2371" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2373" href="Data.Star.Pointer.html#2373" class="Bound">ps</a><a id="2375" class="Symbol">)</a>     <a id="2381" class="Symbol">(</a><a id="2382" href="Data.Star.Decoration.html#1016" class="InductiveConstructor">↦</a> <a id="2384" href="Data.Star.Pointer.html#2384" class="Bound">r</a> <a id="2386" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2388" href="Data.Star.Pointer.html#2388" class="Bound">rs</a><a id="2390" class="Symbol">)</a> <a id="2392" class="Symbol">=</a> <a id="2394" href="Data.Star.Pointer.html#2195" class="Function">lookup</a> <a id="2401" href="Data.Star.Pointer.html#2373" class="Bound">ps</a> <a id="2404" href="Data.Star.Pointer.html#2388" class="Bound">rs</a>

<a id="2408" class="Comment">-- We can define something resembling init.</a>

  <a id="2455" href="Data.Star.Pointer.html#2455" class="Function">prefixIndex</a> <a id="2467" class="Symbol">:</a> <a id="2469" class="Symbol">∀</a> <a id="2471" class="Symbol">{</a><a id="2472" href="Data.Star.Pointer.html#2472" class="Bound">i</a> <a id="2474" href="Data.Star.Pointer.html#2474" class="Bound">j</a><a id="2475" class="Symbol">}</a> <a id="2477" class="Symbol">{</a><a id="2478" href="Data.Star.Pointer.html#2478" class="Bound">xs</a> <a id="2481" class="Symbol">:</a> <a id="2483" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="2488" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2490" href="Data.Star.Pointer.html#2472" class="Bound">i</a> <a id="2492" href="Data.Star.Pointer.html#2474" class="Bound">j</a><a id="2493" class="Symbol">}</a> <a id="2495" class="Symbol">→</a> <a id="2497" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="2501" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="2503" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="2505" href="Data.Star.Pointer.html#2478" class="Bound">xs</a> <a id="2508" class="Symbol">→</a> <a id="2510" href="Data.Star.Pointer.html#271" class="Bound">I</a>
  <a id="2514" href="Data.Star.Pointer.html#2455" class="Function">prefixIndex</a> <a id="2526" class="Symbol">(</a><a id="2527" href="Data.Star.Pointer.html#961" class="InductiveConstructor">done</a> <a id="2532" class="Symbol">{</a><a id="2533" class="Argument">i</a> <a id="2535" class="Symbol">=</a> <a id="2537" href="Data.Star.Pointer.html#2537" class="Bound">i</a><a id="2538" class="Symbol">}</a> <a id="2540" href="Data.Star.Pointer.html#2540" class="Bound">q</a> <a id="2542" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2544" class="Symbol">_)</a>  <a id="2548" class="Symbol">=</a> <a id="2550" href="Data.Star.Pointer.html#2537" class="Bound">i</a>
  <a id="2554" href="Data.Star.Pointer.html#2455" class="Function">prefixIndex</a> <a id="2566" class="Symbol">(</a><a id="2567" href="Data.Star.Pointer.html#798" class="InductiveConstructor">step</a> <a id="2572" href="Data.Star.Pointer.html#2572" class="Bound">p</a>         <a id="2582" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2584" href="Data.Star.Pointer.html#2584" class="Bound">ps</a><a id="2586" class="Symbol">)</a> <a id="2588" class="Symbol">=</a> <a id="2590" href="Data.Star.Pointer.html#2455" class="Function">prefixIndex</a> <a id="2602" href="Data.Star.Pointer.html#2584" class="Bound">ps</a>

  <a id="2608" href="Data.Star.Pointer.html#2608" class="Function">prefix</a> <a id="2615" class="Symbol">:</a> <a id="2617" class="Symbol">∀</a> <a id="2619" class="Symbol">{</a><a id="2620" href="Data.Star.Pointer.html#2620" class="Bound">i</a> <a id="2622" href="Data.Star.Pointer.html#2622" class="Bound">j</a><a id="2623" class="Symbol">}</a> <a id="2625" class="Symbol">{</a><a id="2626" href="Data.Star.Pointer.html#2626" class="Bound">xs</a> <a id="2629" class="Symbol">:</a> <a id="2631" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="2636" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2638" href="Data.Star.Pointer.html#2620" class="Bound">i</a> <a id="2640" href="Data.Star.Pointer.html#2622" class="Bound">j</a><a id="2641" class="Symbol">}</a> <a id="2643" class="Symbol">→</a>
           <a id="2656" class="Symbol">(</a><a id="2657" href="Data.Star.Pointer.html#2657" class="Bound">ps</a> <a id="2660" class="Symbol">:</a> <a id="2662" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="2666" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="2668" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="2670" href="Data.Star.Pointer.html#2626" class="Bound">xs</a><a id="2672" class="Symbol">)</a> <a id="2674" class="Symbol">→</a> <a id="2676" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="2681" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2683" href="Data.Star.Pointer.html#2620" class="Bound">i</a> <a id="2685" class="Symbol">(</a><a id="2686" href="Data.Star.Pointer.html#2455" class="Function">prefixIndex</a> <a id="2698" href="Data.Star.Pointer.html#2657" class="Bound">ps</a><a id="2700" class="Symbol">)</a>
  <a id="2704" href="Data.Star.Pointer.html#2608" class="Function">prefix</a> <a id="2711" class="Symbol">(</a><a id="2712" href="Data.Star.Pointer.html#961" class="InductiveConstructor">done</a> <a id="2717" href="Data.Star.Pointer.html#2717" class="Bound">q</a>         <a id="2727" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2729" class="Symbol">_)</a>  <a id="2733" class="Symbol">=</a> <a id="2735" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#546" class="InductiveConstructor">ε</a>
  <a id="2739" href="Data.Star.Pointer.html#2608" class="Function">prefix</a> <a id="2746" class="Symbol">(</a><a id="2747" href="Data.Star.Pointer.html#798" class="InductiveConstructor">step</a> <a id="2752" class="Symbol">{</a><a id="2753" class="Argument">x</a> <a id="2755" class="Symbol">=</a> <a id="2757" href="Data.Star.Pointer.html#2757" class="Bound">x</a><a id="2758" class="Symbol">}</a> <a id="2760" href="Data.Star.Pointer.html#2760" class="Bound">p</a> <a id="2762" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2764" href="Data.Star.Pointer.html#2764" class="Bound">ps</a><a id="2766" class="Symbol">)</a> <a id="2768" class="Symbol">=</a> <a id="2770" href="Data.Star.Pointer.html#2757" class="Bound">x</a> <a id="2772" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2774" href="Data.Star.Pointer.html#2608" class="Function">prefix</a> <a id="2781" href="Data.Star.Pointer.html#2764" class="Bound">ps</a>

<a id="2785" class="Comment">-- Here we are taking the initial segment of ps (all elements but the</a>
<a id="2855" class="Comment">-- last, i.e. all edges satisfying P).</a>

  <a id="2897" href="Data.Star.Pointer.html#2897" class="Function">init</a> <a id="2902" class="Symbol">:</a> <a id="2904" class="Symbol">∀</a> <a id="2906" class="Symbol">{</a><a id="2907" href="Data.Star.Pointer.html#2907" class="Bound">i</a> <a id="2909" href="Data.Star.Pointer.html#2909" class="Bound">j</a><a id="2910" class="Symbol">}</a> <a id="2912" class="Symbol">{</a><a id="2913" href="Data.Star.Pointer.html#2913" class="Bound">xs</a> <a id="2916" class="Symbol">:</a> <a id="2918" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="2923" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="2925" href="Data.Star.Pointer.html#2907" class="Bound">i</a> <a id="2927" href="Data.Star.Pointer.html#2909" class="Bound">j</a><a id="2928" class="Symbol">}</a> <a id="2930" class="Symbol">→</a>
        <a id="2940" class="Symbol">(</a><a id="2941" href="Data.Star.Pointer.html#2941" class="Bound">ps</a> <a id="2944" class="Symbol">:</a> <a id="2946" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="2950" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="2952" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="2954" href="Data.Star.Pointer.html#2913" class="Bound">xs</a><a id="2956" class="Symbol">)</a> <a id="2958" class="Symbol">→</a> <a id="2960" href="Data.Star.Decoration.html#1528" class="Function">All</a> <a id="2964" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="2966" class="Symbol">(</a><a id="2967" href="Data.Star.Pointer.html#2608" class="Function">prefix</a> <a id="2974" href="Data.Star.Pointer.html#2941" class="Bound">ps</a><a id="2976" class="Symbol">)</a>
  <a id="2980" href="Data.Star.Pointer.html#2897" class="Function">init</a> <a id="2985" class="Symbol">(</a><a id="2986" href="Data.Star.Pointer.html#961" class="InductiveConstructor">done</a> <a id="2991" href="Data.Star.Pointer.html#2991" class="Bound">q</a> <a id="2993" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="2995" class="Symbol">_)</a>  <a id="2999" class="Symbol">=</a> <a id="3001" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#546" class="InductiveConstructor">ε</a>
  <a id="3005" href="Data.Star.Pointer.html#2897" class="Function">init</a> <a id="3010" class="Symbol">(</a><a id="3011" href="Data.Star.Pointer.html#798" class="InductiveConstructor">step</a> <a id="3016" href="Data.Star.Pointer.html#3016" class="Bound">p</a> <a id="3018" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="3020" href="Data.Star.Pointer.html#3020" class="Bound">ps</a><a id="3022" class="Symbol">)</a> <a id="3024" class="Symbol">=</a> <a id="3026" href="Data.Star.Decoration.html#1016" class="InductiveConstructor">↦</a> <a id="3028" href="Data.Star.Pointer.html#3016" class="Bound">p</a> <a id="3030" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#573" class="InductiveConstructor Operator">◅</a> <a id="3032" href="Data.Star.Pointer.html#2897" class="Function">init</a> <a id="3037" href="Data.Star.Pointer.html#3020" class="Bound">ps</a>

<a id="3041" class="Comment">-- One can simplify the implementation by not carrying around the</a>
<a id="3107" class="Comment">-- indices in the type:</a>

  <a id="3134" href="Data.Star.Pointer.html#3134" class="Function">last</a> <a id="3139" class="Symbol">:</a> <a id="3141" class="Symbol">∀</a> <a id="3143" class="Symbol">{</a><a id="3144" href="Data.Star.Pointer.html#3144" class="Bound">i</a> <a id="3146" href="Data.Star.Pointer.html#3146" class="Bound">j</a><a id="3147" class="Symbol">}</a> <a id="3149" class="Symbol">{</a><a id="3150" href="Data.Star.Pointer.html#3150" class="Bound">xs</a> <a id="3153" class="Symbol">:</a> <a id="3155" href="Relation.Binary.Construct.Closure.ReflexiveTransitive.html#485" class="Datatype">Star</a> <a id="3160" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="3162" href="Data.Star.Pointer.html#3144" class="Bound">i</a> <a id="3164" href="Data.Star.Pointer.html#3146" class="Bound">j</a><a id="3165" class="Symbol">}</a> <a id="3167" class="Symbol">→</a>
         <a id="3178" href="Data.Star.Pointer.html#1312" class="Function">Any</a> <a id="3182" href="Data.Star.Pointer.html#2149" class="Bound">P</a> <a id="3184" href="Data.Star.Pointer.html#2168" class="Bound">Q</a> <a id="3186" href="Data.Star.Pointer.html#3150" class="Bound">xs</a> <a id="3189" class="Symbol">→</a> <a id="3191" href="Data.Star.Decoration.html#627" class="Datatype">NonEmptyEdgePred</a> <a id="3208" href="Data.Star.Pointer.html#2135" class="Bound">T</a> <a id="3210" href="Data.Star.Pointer.html#2168" class="Bound">Q</a>
  <a id="3214" href="Data.Star.Pointer.html#3134" class="Function">last</a> <a id="3219" href="Data.Star.Pointer.html#3219" class="Bound">ps</a> <a id="3222" class="Keyword">with</a> <a id="3227" href="Data.Star.Pointer.html#2195" class="Function">lookup</a> <a id="3234" class="Symbol">{</a><a id="3235" class="Argument">r</a> <a id="3237" class="Symbol">=</a> <a id="3239" href="Data.Star.Pointer.html#2129" class="Bound">p</a><a id="3240" class="Symbol">}</a> <a id="3242" href="Data.Star.Pointer.html#3219" class="Bound">ps</a> <a id="3245" class="Symbol">(</a><a id="3246" href="Data.Star.Decoration.html#2633" class="Function">decorate</a> <a id="3255" class="Symbol">(</a><a id="3256" href="Function.Core.html#808" class="Function">const</a> <a id="3262" class="Symbol">(</a><a id="3263" href="Level.html#457" class="InductiveConstructor">lift</a> <a id="3268" href="Agda.Builtin.Unit.html#174" class="InductiveConstructor">tt</a><a id="3270" class="Symbol">))</a> <a id="3273" class="Symbol">_)</a>
  <a id="3278" class="Symbol">...</a> <a id="3282" class="Symbol">|</a> <a id="3284" href="Data.Star.Pointer.html#1922" class="InductiveConstructor">result</a> <a id="3291" href="Data.Star.Pointer.html#3291" class="Bound">q</a> <a id="3293" class="Symbol">_</a> <a id="3295" class="Symbol">=</a> <a id="3297" href="Data.Star.Decoration.html#753" class="InductiveConstructor">nonEmptyEdgePred</a> <a id="3314" href="Data.Star.Pointer.html#3291" class="Bound">q</a>
</pre></body></html>