<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Unary.Properties</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Properties of constructions over unary relations</a>
<a id="158" class="Comment">------------------------------------------------------------------------</a>

<a id="232" class="Symbol">{-#</a> <a id="236" class="Keyword">OPTIONS</a> <a id="244" class="Pragma">--cubical-compatible</a> <a id="265" class="Pragma">--safe</a> <a id="272" class="Symbol">#-}</a>

<a id="277" class="Keyword">module</a> <a id="284" href="Relation.Unary.Properties.html" class="Module">Relation.Unary.Properties</a> <a id="310" class="Keyword">where</a>

<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="Data.Product.html" class="Module">Data.Product</a> <a id="342" class="Keyword">using</a> <a id="348" class="Symbol">(</a><a id="349" href="Data.Product.html#1176" class="Function Operator">_×_</a><a id="352" class="Symbol">;</a> <a id="354" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="357" class="Symbol">;</a> <a id="359" href="Data.Product.html#5326" class="Function">swap</a><a id="363" class="Symbol">;</a> <a id="365" href="Agda.Builtin.Sigma.html#251" class="Field">proj₁</a><a id="370" class="Symbol">)</a>
<a id="372" class="Keyword">open</a> <a id="377" class="Keyword">import</a> <a id="384" href="Data.Sum.Base.html" class="Module">Data.Sum.Base</a> <a id="398" class="Keyword">using</a> <a id="404" class="Symbol">(</a><a id="405" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a><a id="409" class="Symbol">;</a> <a id="411" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a><a id="415" class="Symbol">)</a>
<a id="417" class="Keyword">open</a> <a id="422" class="Keyword">import</a> <a id="429" href="Data.Unit.Base.html" class="Module">Data.Unit.Base</a> <a id="444" class="Keyword">using</a> <a id="450" class="Symbol">(</a><a id="451" href="Agda.Builtin.Unit.html#212" class="InductiveConstructor">tt</a><a id="453" class="Symbol">)</a>
<a id="455" class="Keyword">open</a> <a id="460" class="Keyword">import</a> <a id="467" href="Level.html" class="Module">Level</a>
<a id="473" class="Keyword">open</a> <a id="478" class="Keyword">import</a> <a id="485" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a>
<a id="506" class="Keyword">open</a> <a id="511" class="Keyword">import</a> <a id="518" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a> <a id="546" class="Keyword">hiding</a> <a id="553" class="Symbol">(</a><a id="554" href="Relation.Binary.Definitions.html#4584" class="Function">Decidable</a><a id="563" class="Symbol">;</a> <a id="565" href="Relation.Binary.Definitions.html#5365" class="Function">Universal</a><a id="574" class="Symbol">)</a>
<a id="576" class="Keyword">open</a> <a id="581" class="Keyword">import</a> <a id="588" href="Relation.Unary.html" class="Module">Relation.Unary</a>
<a id="603" class="Keyword">open</a> <a id="608" class="Keyword">import</a> <a id="615" href="Relation.Nullary.html" class="Module">Relation.Nullary</a> <a id="632" class="Keyword">using</a> <a id="638" class="Symbol">(</a><a id="639" href="Relation.Nullary.html#1657" class="InductiveConstructor">yes</a><a id="642" class="Symbol">;</a> <a id="644" href="Relation.Nullary.html#1694" class="InductiveConstructor">no</a><a id="646" class="Symbol">)</a>
<a id="648" class="Keyword">open</a> <a id="653" class="Keyword">import</a> <a id="660" href="Relation.Nullary.Product.html" class="Module">Relation.Nullary.Product</a> <a id="685" class="Keyword">using</a> <a id="691" class="Symbol">(</a><a id="692" href="Relation.Nullary.Product.html#930" class="Function Operator">_×-dec_</a><a id="699" class="Symbol">)</a>
<a id="701" class="Keyword">open</a> <a id="706" class="Keyword">import</a> <a id="713" href="Relation.Nullary.Sum.html" class="Module">Relation.Nullary.Sum</a> <a id="734" class="Keyword">using</a> <a id="740" class="Symbol">(</a><a id="741" href="Relation.Nullary.Sum.html#955" class="Function Operator">_⊎-dec_</a><a id="748" class="Symbol">)</a>
<a id="750" class="Keyword">open</a> <a id="755" class="Keyword">import</a> <a id="762" href="Relation.Nullary.Negation.Core.html" class="Module">Relation.Nullary.Negation.Core</a> <a id="793" class="Keyword">using</a> <a id="799" class="Symbol">(</a><a id="800" href="Relation.Nullary.Negation.Core.html#1364" class="Function">¬?</a><a id="802" class="Symbol">)</a>
<a id="804" class="Keyword">open</a> <a id="809" class="Keyword">import</a> <a id="816" href="Function.Base.html" class="Module">Function.Base</a> <a id="830" class="Keyword">using</a> <a id="836" class="Symbol">(</a><a id="837" href="Function.Base.html#1928" class="Function Operator">_$_</a><a id="840" class="Symbol">;</a> <a id="842" href="Function.Base.html#1040" class="Function Operator">_∘_</a><a id="845" class="Symbol">)</a>

<a id="848" class="Keyword">private</a>
  <a id="858" class="Keyword">variable</a>
    <a id="871" href="Relation.Unary.Properties.html#871" class="Generalizable">a</a> <a id="873" href="Relation.Unary.Properties.html#873" class="Generalizable">b</a> <a id="875" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a> <a id="877" href="Relation.Unary.Properties.html#877" class="Generalizable">ℓ₁</a> <a id="880" href="Relation.Unary.Properties.html#880" class="Generalizable">ℓ₂</a> <a id="883" class="Symbol">:</a> <a id="885" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="895" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="897" class="Symbol">:</a> <a id="899" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="903" href="Relation.Unary.Properties.html#871" class="Generalizable">a</a>
    <a id="909" href="Relation.Unary.Properties.html#909" class="Generalizable">B</a> <a id="911" class="Symbol">:</a> <a id="913" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="917" href="Relation.Unary.Properties.html#873" class="Generalizable">b</a>

<a id="920" class="Comment">----------------------------------------------------------------------</a>
<a id="991" class="Comment">-- The empty set</a>

<a id="∅?"></a><a id="1009" href="Relation.Unary.Properties.html#1009" class="Function">∅?</a> <a id="1012" class="Symbol">:</a> <a id="1014" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="1024" class="Symbol">{</a><a id="1025" class="Argument">A</a> <a id="1027" class="Symbol">=</a> <a id="1029" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1030" class="Symbol">}</a> <a id="1032" href="Relation.Unary.html#1294" class="Function">∅</a>
<a id="1034" href="Relation.Unary.Properties.html#1009" class="Function">∅?</a> <a id="1037" class="Symbol">_</a> <a id="1039" class="Symbol">=</a> <a id="1041" href="Relation.Nullary.html#1694" class="InductiveConstructor">no</a> <a id="1044" class="Symbol">λ()</a>

<a id="∅-Empty"></a><a id="1049" href="Relation.Unary.Properties.html#1049" class="Function">∅-Empty</a> <a id="1057" class="Symbol">:</a> <a id="1059" href="Relation.Unary.html#2994" class="Function">Empty</a> <a id="1065" class="Symbol">{</a><a id="1066" class="Argument">A</a> <a id="1068" class="Symbol">=</a> <a id="1070" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1071" class="Symbol">}</a> <a id="1073" href="Relation.Unary.html#1294" class="Function">∅</a>
<a id="1075" href="Relation.Unary.Properties.html#1049" class="Function">∅-Empty</a> <a id="1083" href="Relation.Unary.Properties.html#1083" class="Bound">x</a> <a id="1085" class="Symbol">()</a>

<a id="∁∅-Universal"></a><a id="1089" href="Relation.Unary.Properties.html#1089" class="Function">∁∅-Universal</a> <a id="1102" class="Symbol">:</a> <a id="1104" href="Relation.Unary.html#3230" class="Function">Universal</a> <a id="1114" class="Symbol">{</a><a id="1115" class="Argument">A</a> <a id="1117" class="Symbol">=</a> <a id="1119" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1120" class="Symbol">}</a> <a id="1122" class="Symbol">(</a><a id="1123" href="Relation.Unary.html#4287" class="Function">∁</a> <a id="1125" href="Relation.Unary.html#1294" class="Function">∅</a><a id="1126" class="Symbol">)</a>
<a id="1128" href="Relation.Unary.Properties.html#1089" class="Function">∁∅-Universal</a> <a id="1141" class="Symbol">=</a> <a id="1143" class="Symbol">λ</a> <a id="1145" href="Relation.Unary.Properties.html#1145" class="Bound">x</a> <a id="1147" href="Relation.Unary.Properties.html#1147" class="Bound">x∈∅</a> <a id="1151" class="Symbol">→</a> <a id="1153" href="Relation.Unary.Properties.html#1147" class="Bound">x∈∅</a>

<a id="1158" class="Comment">----------------------------------------------------------------------</a>
<a id="1229" class="Comment">-- The universe</a>

<a id="U?"></a><a id="1246" href="Relation.Unary.Properties.html#1246" class="Function">U?</a> <a id="1249" class="Symbol">:</a> <a id="1251" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="1261" class="Symbol">{</a><a id="1262" class="Argument">A</a> <a id="1264" class="Symbol">=</a> <a id="1266" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1267" class="Symbol">}</a> <a id="1269" href="Relation.Unary.html#1400" class="Function">U</a>
<a id="1271" href="Relation.Unary.Properties.html#1246" class="Function">U?</a> <a id="1274" class="Symbol">_</a> <a id="1276" class="Symbol">=</a> <a id="1278" href="Relation.Nullary.html#1657" class="InductiveConstructor">yes</a> <a id="1282" href="Agda.Builtin.Unit.html#212" class="InductiveConstructor">tt</a>

<a id="U-Universal"></a><a id="1286" href="Relation.Unary.Properties.html#1286" class="Function">U-Universal</a> <a id="1298" class="Symbol">:</a> <a id="1300" href="Relation.Unary.html#3230" class="Function">Universal</a> <a id="1310" class="Symbol">{</a><a id="1311" class="Argument">A</a> <a id="1313" class="Symbol">=</a> <a id="1315" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1316" class="Symbol">}</a> <a id="1318" href="Relation.Unary.html#1400" class="Function">U</a>
<a id="1320" href="Relation.Unary.Properties.html#1286" class="Function">U-Universal</a> <a id="1332" class="Symbol">=</a> <a id="1334" class="Symbol">λ</a> <a id="1336" href="Relation.Unary.Properties.html#1336" class="Bound">_</a> <a id="1338" class="Symbol">→</a> <a id="1340" class="Symbol">_</a>

<a id="∁U-Empty"></a><a id="1343" href="Relation.Unary.Properties.html#1343" class="Function">∁U-Empty</a> <a id="1352" class="Symbol">:</a> <a id="1354" href="Relation.Unary.html#2994" class="Function">Empty</a> <a id="1360" class="Symbol">{</a><a id="1361" class="Argument">A</a> <a id="1363" class="Symbol">=</a> <a id="1365" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1366" class="Symbol">}</a> <a id="1368" class="Symbol">(</a><a id="1369" href="Relation.Unary.html#4287" class="Function">∁</a> <a id="1371" href="Relation.Unary.html#1400" class="Function">U</a><a id="1372" class="Symbol">)</a>
<a id="1374" href="Relation.Unary.Properties.html#1343" class="Function">∁U-Empty</a> <a id="1383" class="Symbol">=</a> <a id="1385" class="Symbol">λ</a> <a id="1387" href="Relation.Unary.Properties.html#1387" class="Bound">x</a> <a id="1389" href="Relation.Unary.Properties.html#1389" class="Bound">x∈∁U</a> <a id="1394" class="Symbol">→</a> <a id="1396" href="Relation.Unary.Properties.html#1389" class="Bound">x∈∁U</a> <a id="1401" class="Symbol">_</a>

<a id="1404" class="Comment">----------------------------------------------------------------------</a>
<a id="1475" class="Comment">-- Subset properties</a>

<a id="∅-⊆"></a><a id="1497" href="Relation.Unary.Properties.html#1497" class="Function">∅-⊆</a> <a id="1501" class="Symbol">:</a> <a id="1503" class="Symbol">(</a><a id="1504" href="Relation.Unary.Properties.html#1504" class="Bound">P</a> <a id="1506" class="Symbol">:</a> <a id="1508" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1513" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="1515" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="1516" class="Symbol">)</a> <a id="1518" class="Symbol">→</a> <a id="1520" href="Relation.Unary.html#1294" class="Function">∅</a> <a id="1522" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="1524" href="Relation.Unary.Properties.html#1504" class="Bound">P</a>
<a id="1526" href="Relation.Unary.Properties.html#1497" class="Function">∅-⊆</a> <a id="1530" href="Relation.Unary.Properties.html#1530" class="Bound">P</a> <a id="1532" class="Symbol">()</a>

<a id="⊆-U"></a><a id="1536" href="Relation.Unary.Properties.html#1536" class="Function">⊆-U</a> <a id="1540" class="Symbol">:</a> <a id="1542" class="Symbol">(</a><a id="1543" href="Relation.Unary.Properties.html#1543" class="Bound">P</a> <a id="1545" class="Symbol">:</a> <a id="1547" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1552" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="1554" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="1555" class="Symbol">)</a> <a id="1557" class="Symbol">→</a> <a id="1559" href="Relation.Unary.Properties.html#1543" class="Bound">P</a> <a id="1561" href="Relation.Unary.html#1751" class="Function Operator">⊆</a> <a id="1563" href="Relation.Unary.html#1400" class="Function">U</a>
<a id="1565" href="Relation.Unary.Properties.html#1536" class="Function">⊆-U</a> <a id="1569" href="Relation.Unary.Properties.html#1569" class="Bound">P</a> <a id="1571" class="Symbol">_</a> <a id="1573" class="Symbol">=</a> <a id="1575" class="Symbol">_</a>

<a id="⊆-refl"></a><a id="1578" href="Relation.Unary.Properties.html#1578" class="Function">⊆-refl</a> <a id="1585" class="Symbol">:</a> <a id="1587" href="Relation.Binary.Definitions.html#1348" class="Function">Reflexive</a> <a id="1597" class="Symbol">(</a><a id="1598" href="Relation.Unary.html#1751" class="Function Operator">_⊆_</a> <a id="1602" class="Symbol">{</a><a id="1603" class="Argument">A</a> <a id="1605" class="Symbol">=</a> <a id="1607" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1608" class="Symbol">}</a> <a id="1610" class="Symbol">{</a><a id="1611" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="1612" class="Symbol">})</a>
<a id="1615" href="Relation.Unary.Properties.html#1578" class="Function">⊆-refl</a> <a id="1622" href="Relation.Unary.Properties.html#1622" class="Bound">x∈P</a> <a id="1626" class="Symbol">=</a> <a id="1628" href="Relation.Unary.Properties.html#1622" class="Bound">x∈P</a>

<a id="⊆-trans"></a><a id="1633" href="Relation.Unary.Properties.html#1633" class="Function">⊆-trans</a> <a id="1641" class="Symbol">:</a> <a id="1643" href="Relation.Binary.Definitions.html#1875" class="Function">Transitive</a> <a id="1654" class="Symbol">(</a><a id="1655" href="Relation.Unary.html#1751" class="Function Operator">_⊆_</a> <a id="1659" class="Symbol">{</a><a id="1660" class="Argument">A</a> <a id="1662" class="Symbol">=</a> <a id="1664" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1665" class="Symbol">}</a> <a id="1667" class="Symbol">{</a><a id="1668" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="1669" class="Symbol">})</a>
<a id="1672" href="Relation.Unary.Properties.html#1633" class="Function">⊆-trans</a> <a id="1680" href="Relation.Unary.Properties.html#1680" class="Bound">P⊆Q</a> <a id="1684" href="Relation.Unary.Properties.html#1684" class="Bound">Q⊆R</a> <a id="1688" href="Relation.Unary.Properties.html#1688" class="Bound">x∈P</a> <a id="1692" class="Symbol">=</a> <a id="1694" href="Relation.Unary.Properties.html#1684" class="Bound">Q⊆R</a> <a id="1698" class="Symbol">(</a><a id="1699" href="Relation.Unary.Properties.html#1680" class="Bound">P⊆Q</a> <a id="1703" href="Relation.Unary.Properties.html#1688" class="Bound">x∈P</a><a id="1706" class="Symbol">)</a>

<a id="⊂-asym"></a><a id="1709" href="Relation.Unary.Properties.html#1709" class="Function">⊂-asym</a> <a id="1716" class="Symbol">:</a> <a id="1718" href="Relation.Binary.Definitions.html#2362" class="Function">Asymmetric</a> <a id="1729" class="Symbol">(</a><a id="1730" href="Relation.Unary.html#1979" class="Function Operator">_⊂_</a> <a id="1734" class="Symbol">{</a><a id="1735" class="Argument">A</a> <a id="1737" class="Symbol">=</a> <a id="1739" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a><a id="1740" class="Symbol">}</a> <a id="1742" class="Symbol">{</a><a id="1743" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="1744" class="Symbol">})</a>
<a id="1747" href="Relation.Unary.Properties.html#1709" class="Function">⊂-asym</a> <a id="1754" class="Symbol">(_</a> <a id="1757" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1759" href="Relation.Unary.Properties.html#1759" class="Bound">Q⊈P</a><a id="1762" class="Symbol">)</a> <a id="1764" class="Symbol">=</a> <a id="1766" href="Relation.Unary.Properties.html#1759" class="Bound">Q⊈P</a> <a id="1770" href="Function.Base.html#1040" class="Function Operator">∘</a> <a id="1772" href="Agda.Builtin.Sigma.html#251" class="Field">proj₁</a>

<a id="1779" class="Comment">----------------------------------------------------------------------</a>
<a id="1850" class="Comment">-- Decidability properties</a>

<a id="∁?"></a><a id="1878" href="Relation.Unary.Properties.html#1878" class="Function">∁?</a> <a id="1881" class="Symbol">:</a> <a id="1883" class="Symbol">{</a><a id="1884" href="Relation.Unary.Properties.html#1884" class="Bound">P</a> <a id="1886" class="Symbol">:</a> <a id="1888" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1893" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="1895" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="1896" class="Symbol">}</a> <a id="1898" class="Symbol">→</a> <a id="1900" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="1910" href="Relation.Unary.Properties.html#1884" class="Bound">P</a> <a id="1912" class="Symbol">→</a> <a id="1914" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="1924" class="Symbol">(</a><a id="1925" href="Relation.Unary.html#4287" class="Function">∁</a> <a id="1927" href="Relation.Unary.Properties.html#1884" class="Bound">P</a><a id="1928" class="Symbol">)</a>
<a id="1930" href="Relation.Unary.Properties.html#1878" class="Function">∁?</a> <a id="1933" href="Relation.Unary.Properties.html#1933" class="Bound">P?</a> <a id="1936" href="Relation.Unary.Properties.html#1936" class="Bound">x</a> <a id="1938" class="Symbol">=</a> <a id="1940" href="Relation.Nullary.Negation.Core.html#1364" class="Function">¬?</a> <a id="1943" class="Symbol">(</a><a id="1944" href="Relation.Unary.Properties.html#1933" class="Bound">P?</a> <a id="1947" href="Relation.Unary.Properties.html#1936" class="Bound">x</a><a id="1948" class="Symbol">)</a>

<a id="_∪?_"></a><a id="1951" href="Relation.Unary.Properties.html#1951" class="Function Operator">_∪?_</a> <a id="1956" class="Symbol">:</a> <a id="1958" class="Symbol">{</a><a id="1959" href="Relation.Unary.Properties.html#1959" class="Bound">P</a> <a id="1961" class="Symbol">:</a> <a id="1963" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1968" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="1970" href="Relation.Unary.Properties.html#877" class="Generalizable">ℓ₁</a><a id="1972" class="Symbol">}</a> <a id="1974" class="Symbol">{</a><a id="1975" href="Relation.Unary.Properties.html#1975" class="Bound">Q</a> <a id="1977" class="Symbol">:</a> <a id="1979" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="1984" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="1986" href="Relation.Unary.Properties.html#880" class="Generalizable">ℓ₂</a><a id="1988" class="Symbol">}</a> <a id="1990" class="Symbol">→</a>
       <a id="1999" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2009" href="Relation.Unary.Properties.html#1959" class="Bound">P</a> <a id="2011" class="Symbol">→</a> <a id="2013" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2023" href="Relation.Unary.Properties.html#1975" class="Bound">Q</a> <a id="2025" class="Symbol">→</a> <a id="2027" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2037" class="Symbol">(</a><a id="2038" href="Relation.Unary.Properties.html#1959" class="Bound">P</a> <a id="2040" href="Relation.Unary.html#4426" class="Function Operator">∪</a> <a id="2042" href="Relation.Unary.Properties.html#1975" class="Bound">Q</a><a id="2043" class="Symbol">)</a>
<a id="2045" href="Relation.Unary.Properties.html#1951" class="Function Operator">_∪?_</a> <a id="2050" href="Relation.Unary.Properties.html#2050" class="Bound">P?</a> <a id="2053" href="Relation.Unary.Properties.html#2053" class="Bound">Q?</a> <a id="2056" href="Relation.Unary.Properties.html#2056" class="Bound">x</a> <a id="2058" class="Symbol">=</a> <a id="2060" class="Symbol">(</a><a id="2061" href="Relation.Unary.Properties.html#2050" class="Bound">P?</a> <a id="2064" href="Relation.Unary.Properties.html#2056" class="Bound">x</a><a id="2065" class="Symbol">)</a> <a id="2067" href="Relation.Nullary.Sum.html#955" class="Function Operator">⊎-dec</a> <a id="2073" class="Symbol">(</a><a id="2074" href="Relation.Unary.Properties.html#2053" class="Bound">Q?</a> <a id="2077" href="Relation.Unary.Properties.html#2056" class="Bound">x</a><a id="2078" class="Symbol">)</a>

<a id="_∩?_"></a><a id="2081" href="Relation.Unary.Properties.html#2081" class="Function Operator">_∩?_</a> <a id="2086" class="Symbol">:</a> <a id="2088" class="Symbol">{</a><a id="2089" href="Relation.Unary.Properties.html#2089" class="Bound">P</a> <a id="2091" class="Symbol">:</a> <a id="2093" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2098" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="2100" href="Relation.Unary.Properties.html#877" class="Generalizable">ℓ₁</a><a id="2102" class="Symbol">}</a> <a id="2104" class="Symbol">{</a><a id="2105" href="Relation.Unary.Properties.html#2105" class="Bound">Q</a> <a id="2107" class="Symbol">:</a> <a id="2109" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2114" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="2116" href="Relation.Unary.Properties.html#880" class="Generalizable">ℓ₂</a><a id="2118" class="Symbol">}</a> <a id="2120" class="Symbol">→</a>
       <a id="2129" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2139" href="Relation.Unary.Properties.html#2089" class="Bound">P</a> <a id="2141" class="Symbol">→</a> <a id="2143" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2153" href="Relation.Unary.Properties.html#2105" class="Bound">Q</a> <a id="2155" class="Symbol">→</a> <a id="2157" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2167" class="Symbol">(</a><a id="2168" href="Relation.Unary.Properties.html#2089" class="Bound">P</a> <a id="2170" href="Relation.Unary.html#4512" class="Function Operator">∩</a> <a id="2172" href="Relation.Unary.Properties.html#2105" class="Bound">Q</a><a id="2173" class="Symbol">)</a>
<a id="2175" href="Relation.Unary.Properties.html#2081" class="Function Operator">_∩?_</a> <a id="2180" href="Relation.Unary.Properties.html#2180" class="Bound">P?</a> <a id="2183" href="Relation.Unary.Properties.html#2183" class="Bound">Q?</a> <a id="2186" href="Relation.Unary.Properties.html#2186" class="Bound">x</a> <a id="2188" class="Symbol">=</a> <a id="2190" class="Symbol">(</a><a id="2191" href="Relation.Unary.Properties.html#2180" class="Bound">P?</a> <a id="2194" href="Relation.Unary.Properties.html#2186" class="Bound">x</a><a id="2195" class="Symbol">)</a> <a id="2197" href="Relation.Nullary.Product.html#930" class="Function Operator">×-dec</a> <a id="2203" class="Symbol">(</a><a id="2204" href="Relation.Unary.Properties.html#2183" class="Bound">Q?</a> <a id="2207" href="Relation.Unary.Properties.html#2186" class="Bound">x</a><a id="2208" class="Symbol">)</a>

<a id="_×?_"></a><a id="2211" href="Relation.Unary.Properties.html#2211" class="Function Operator">_×?_</a> <a id="2216" class="Symbol">:</a> <a id="2218" class="Symbol">{</a><a id="2219" href="Relation.Unary.Properties.html#2219" class="Bound">P</a> <a id="2221" class="Symbol">:</a> <a id="2223" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2228" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="2230" href="Relation.Unary.Properties.html#877" class="Generalizable">ℓ₁</a><a id="2232" class="Symbol">}</a> <a id="2234" class="Symbol">{</a><a id="2235" href="Relation.Unary.Properties.html#2235" class="Bound">Q</a> <a id="2237" class="Symbol">:</a> <a id="2239" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2244" href="Relation.Unary.Properties.html#909" class="Generalizable">B</a> <a id="2246" href="Relation.Unary.Properties.html#880" class="Generalizable">ℓ₂</a><a id="2248" class="Symbol">}</a> <a id="2250" class="Symbol">→</a>
       <a id="2259" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2269" href="Relation.Unary.Properties.html#2219" class="Bound">P</a> <a id="2271" class="Symbol">→</a> <a id="2273" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2283" href="Relation.Unary.Properties.html#2235" class="Bound">Q</a> <a id="2285" class="Symbol">→</a> <a id="2287" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2297" class="Symbol">(</a><a id="2298" href="Relation.Unary.Properties.html#2219" class="Bound">P</a> <a id="2300" href="Relation.Unary.html#5475" class="Function Operator">⟨×⟩</a> <a id="2304" href="Relation.Unary.Properties.html#2235" class="Bound">Q</a><a id="2305" class="Symbol">)</a>
<a id="2307" href="Relation.Unary.Properties.html#2211" class="Function Operator">_×?_</a> <a id="2312" href="Relation.Unary.Properties.html#2312" class="Bound">P?</a> <a id="2315" href="Relation.Unary.Properties.html#2315" class="Bound">Q?</a> <a id="2318" class="Symbol">(</a><a id="2319" href="Relation.Unary.Properties.html#2319" class="Bound">a</a> <a id="2321" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2323" href="Relation.Unary.Properties.html#2323" class="Bound">b</a><a id="2324" class="Symbol">)</a> <a id="2326" class="Symbol">=</a> <a id="2328" class="Symbol">(</a><a id="2329" href="Relation.Unary.Properties.html#2312" class="Bound">P?</a> <a id="2332" href="Relation.Unary.Properties.html#2319" class="Bound">a</a><a id="2333" class="Symbol">)</a> <a id="2335" href="Relation.Nullary.Product.html#930" class="Function Operator">×-dec</a> <a id="2341" class="Symbol">(</a><a id="2342" href="Relation.Unary.Properties.html#2315" class="Bound">Q?</a> <a id="2345" href="Relation.Unary.Properties.html#2323" class="Bound">b</a><a id="2346" class="Symbol">)</a>

<a id="_⊙?_"></a><a id="2349" href="Relation.Unary.Properties.html#2349" class="Function Operator">_⊙?_</a> <a id="2354" class="Symbol">:</a> <a id="2356" class="Symbol">{</a><a id="2357" href="Relation.Unary.Properties.html#2357" class="Bound">P</a> <a id="2359" class="Symbol">:</a> <a id="2361" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2366" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="2368" href="Relation.Unary.Properties.html#877" class="Generalizable">ℓ₁</a><a id="2370" class="Symbol">}</a> <a id="2372" class="Symbol">{</a><a id="2373" href="Relation.Unary.Properties.html#2373" class="Bound">Q</a> <a id="2375" class="Symbol">:</a> <a id="2377" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2382" href="Relation.Unary.Properties.html#909" class="Generalizable">B</a> <a id="2384" href="Relation.Unary.Properties.html#880" class="Generalizable">ℓ₂</a><a id="2386" class="Symbol">}</a> <a id="2388" class="Symbol">→</a>
       <a id="2397" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2407" href="Relation.Unary.Properties.html#2357" class="Bound">P</a> <a id="2409" class="Symbol">→</a> <a id="2411" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2421" href="Relation.Unary.Properties.html#2373" class="Bound">Q</a> <a id="2423" class="Symbol">→</a> <a id="2425" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2435" class="Symbol">(</a><a id="2436" href="Relation.Unary.Properties.html#2357" class="Bound">P</a> <a id="2438" href="Relation.Unary.html#5676" class="Function Operator">⟨⊙⟩</a> <a id="2442" href="Relation.Unary.Properties.html#2373" class="Bound">Q</a><a id="2443" class="Symbol">)</a>
<a id="2445" href="Relation.Unary.Properties.html#2349" class="Function Operator">_⊙?_</a> <a id="2450" href="Relation.Unary.Properties.html#2450" class="Bound">P?</a> <a id="2453" href="Relation.Unary.Properties.html#2453" class="Bound">Q?</a> <a id="2456" class="Symbol">(</a><a id="2457" href="Relation.Unary.Properties.html#2457" class="Bound">a</a> <a id="2459" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2461" href="Relation.Unary.Properties.html#2461" class="Bound">b</a><a id="2462" class="Symbol">)</a> <a id="2464" class="Symbol">=</a> <a id="2466" class="Symbol">(</a><a id="2467" href="Relation.Unary.Properties.html#2450" class="Bound">P?</a> <a id="2470" href="Relation.Unary.Properties.html#2457" class="Bound">a</a><a id="2471" class="Symbol">)</a> <a id="2473" href="Relation.Nullary.Sum.html#955" class="Function Operator">⊎-dec</a> <a id="2479" class="Symbol">(</a><a id="2480" href="Relation.Unary.Properties.html#2453" class="Bound">Q?</a> <a id="2483" href="Relation.Unary.Properties.html#2461" class="Bound">b</a><a id="2484" class="Symbol">)</a>

<a id="_⊎?_"></a><a id="2487" href="Relation.Unary.Properties.html#2487" class="Function Operator">_⊎?_</a> <a id="2492" class="Symbol">:</a> <a id="2494" class="Symbol">{</a><a id="2495" href="Relation.Unary.Properties.html#2495" class="Bound">P</a> <a id="2497" class="Symbol">:</a> <a id="2499" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2504" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="2506" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="2507" class="Symbol">}</a> <a id="2509" class="Symbol">{</a><a id="2510" href="Relation.Unary.Properties.html#2510" class="Bound">Q</a> <a id="2512" class="Symbol">:</a> <a id="2514" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2519" href="Relation.Unary.Properties.html#909" class="Generalizable">B</a> <a id="2521" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="2522" class="Symbol">}</a> <a id="2524" class="Symbol">→</a>
       <a id="2533" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2543" href="Relation.Unary.Properties.html#2495" class="Bound">P</a> <a id="2545" class="Symbol">→</a> <a id="2547" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2557" href="Relation.Unary.Properties.html#2510" class="Bound">Q</a> <a id="2559" class="Symbol">→</a> <a id="2561" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2571" class="Symbol">(</a><a id="2572" href="Relation.Unary.Properties.html#2495" class="Bound">P</a> <a id="2574" href="Relation.Unary.html#5583" class="Function Operator">⟨⊎⟩</a> <a id="2578" href="Relation.Unary.Properties.html#2510" class="Bound">Q</a><a id="2579" class="Symbol">)</a>
<a id="2581" href="Relation.Unary.Properties.html#2487" class="Function Operator">_⊎?_</a> <a id="2586" href="Relation.Unary.Properties.html#2586" class="Bound">P?</a> <a id="2589" href="Relation.Unary.Properties.html#2589" class="Bound">Q?</a> <a id="2592" class="Symbol">(</a><a id="2593" href="Data.Sum.Base.html#793" class="InductiveConstructor">inj₁</a> <a id="2598" href="Relation.Unary.Properties.html#2598" class="Bound">a</a><a id="2599" class="Symbol">)</a> <a id="2601" class="Symbol">=</a> <a id="2603" href="Relation.Unary.Properties.html#2586" class="Bound">P?</a> <a id="2606" href="Relation.Unary.Properties.html#2598" class="Bound">a</a>
<a id="2608" href="Relation.Unary.Properties.html#2487" class="Function Operator">_⊎?_</a> <a id="2613" href="Relation.Unary.Properties.html#2613" class="Bound">P?</a> <a id="2616" href="Relation.Unary.Properties.html#2616" class="Bound">Q?</a> <a id="2619" class="Symbol">(</a><a id="2620" href="Data.Sum.Base.html#818" class="InductiveConstructor">inj₂</a> <a id="2625" href="Relation.Unary.Properties.html#2625" class="Bound">b</a><a id="2626" class="Symbol">)</a> <a id="2628" class="Symbol">=</a> <a id="2630" href="Relation.Unary.Properties.html#2616" class="Bound">Q?</a> <a id="2633" href="Relation.Unary.Properties.html#2625" class="Bound">b</a>

<a id="_~?"></a><a id="2636" href="Relation.Unary.Properties.html#2636" class="Function Operator">_~?</a> <a id="2640" class="Symbol">:</a> <a id="2642" class="Symbol">{</a><a id="2643" href="Relation.Unary.Properties.html#2643" class="Bound">P</a> <a id="2645" class="Symbol">:</a> <a id="2647" href="Relation.Unary.html#1110" class="Function">Pred</a> <a id="2652" class="Symbol">(</a><a id="2653" href="Relation.Unary.Properties.html#895" class="Generalizable">A</a> <a id="2655" href="Data.Product.html#1176" class="Function Operator">×</a> <a id="2657" href="Relation.Unary.Properties.html#909" class="Generalizable">B</a><a id="2658" class="Symbol">)</a> <a id="2660" href="Relation.Unary.Properties.html#875" class="Generalizable">ℓ</a><a id="2661" class="Symbol">}</a> <a id="2663" class="Symbol">→</a> <a id="2665" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2675" href="Relation.Unary.Properties.html#2643" class="Bound">P</a> <a id="2677" class="Symbol">→</a> <a id="2679" href="Relation.Unary.html#3545" class="Function">Decidable</a> <a id="2689" class="Symbol">(</a><a id="2690" href="Relation.Unary.Properties.html#2643" class="Bound">P</a> <a id="2692" href="Relation.Unary.html#5992" class="Function Operator">~</a><a id="2693" class="Symbol">)</a>
<a id="2695" href="Relation.Unary.Properties.html#2636" class="Function Operator">_~?</a> <a id="2699" href="Relation.Unary.Properties.html#2699" class="Bound">P?</a> <a id="2702" class="Symbol">=</a> <a id="2704" href="Relation.Unary.Properties.html#2699" class="Bound">P?</a> <a id="2707" href="Function.Base.html#1040" class="Function Operator">∘</a> <a id="2709" href="Data.Product.html#5326" class="Function">swap</a>
</pre></body></html>