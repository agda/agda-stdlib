<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Induction.Lexicographic</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Lexicographic induction</a>
<a id="133" class="Comment">------------------------------------------------------------------------</a>

<a id="207" class="Symbol">{-#</a> <a id="211" class="Keyword">OPTIONS</a> <a id="219" class="Pragma">--cubical-compatible</a> <a id="240" class="Pragma">--safe</a> <a id="247" class="Symbol">#-}</a>

<a id="252" class="Keyword">module</a> <a id="259" href="Induction.Lexicographic.html" class="Module">Induction.Lexicographic</a> <a id="283" class="Keyword">where</a>

<a id="290" class="Keyword">open</a> <a id="295" class="Keyword">import</a> <a id="302" href="Data.Product.html" class="Module">Data.Product</a>
<a id="315" class="Keyword">open</a> <a id="320" class="Keyword">import</a> <a id="327" href="Induction.html" class="Module">Induction</a>
<a id="337" class="Keyword">open</a> <a id="342" class="Keyword">import</a> <a id="349" href="Level.html" class="Module">Level</a>

<a id="356" class="Comment">-- The structure of lexicographic induction.</a>

<a id="Σ-Rec"></a><a id="402" href="Induction.Lexicographic.html#402" class="Function">Σ-Rec</a> <a id="408" class="Symbol">:</a> <a id="410" class="Symbol">∀</a> <a id="412" class="Symbol">{</a><a id="413" href="Induction.Lexicographic.html#413" class="Bound">a</a> <a id="415" href="Induction.Lexicographic.html#415" class="Bound">b</a> <a id="417" href="Induction.Lexicographic.html#417" class="Bound">ℓ₁</a> <a id="420" href="Induction.Lexicographic.html#420" class="Bound">ℓ₂</a> <a id="423" href="Induction.Lexicographic.html#423" class="Bound">ℓ₃</a><a id="425" class="Symbol">}</a> <a id="427" class="Symbol">{</a><a id="428" href="Induction.Lexicographic.html#428" class="Bound">A</a> <a id="430" class="Symbol">:</a> <a id="432" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="436" href="Induction.Lexicographic.html#413" class="Bound">a</a><a id="437" class="Symbol">}</a> <a id="439" class="Symbol">{</a><a id="440" href="Induction.Lexicographic.html#440" class="Bound">B</a> <a id="442" class="Symbol">:</a> <a id="444" href="Induction.Lexicographic.html#428" class="Bound">A</a> <a id="446" class="Symbol">→</a> <a id="448" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="452" href="Induction.Lexicographic.html#415" class="Bound">b</a><a id="453" class="Symbol">}</a> <a id="455" class="Symbol">→</a>
        <a id="465" href="Induction.html#769" class="Function">RecStruct</a> <a id="475" href="Induction.Lexicographic.html#428" class="Bound">A</a> <a id="477" class="Symbol">(</a><a id="478" href="Induction.Lexicographic.html#417" class="Bound">ℓ₁</a> <a id="481" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="483" href="Induction.Lexicographic.html#415" class="Bound">b</a><a id="484" class="Symbol">)</a> <a id="486" href="Induction.Lexicographic.html#420" class="Bound">ℓ₂</a> <a id="489" class="Symbol">→</a> <a id="491" class="Symbol">(∀</a> <a id="494" href="Induction.Lexicographic.html#494" class="Bound">x</a> <a id="496" class="Symbol">→</a> <a id="498" href="Induction.html#769" class="Function">RecStruct</a> <a id="508" class="Symbol">(</a><a id="509" href="Induction.Lexicographic.html#440" class="Bound">B</a> <a id="511" href="Induction.Lexicographic.html#494" class="Bound">x</a><a id="512" class="Symbol">)</a> <a id="514" href="Induction.Lexicographic.html#417" class="Bound">ℓ₁</a> <a id="517" href="Induction.Lexicographic.html#423" class="Bound">ℓ₃</a><a id="519" class="Symbol">)</a> <a id="521" class="Symbol">→</a>
        <a id="531" href="Induction.html#769" class="Function">RecStruct</a> <a id="541" class="Symbol">(</a><a id="542" href="Agda.Builtin.Sigma.html#165" class="Record">Σ</a> <a id="544" href="Induction.Lexicographic.html#428" class="Bound">A</a> <a id="546" href="Induction.Lexicographic.html#440" class="Bound">B</a><a id="547" class="Symbol">)</a> <a id="549" class="Symbol">_</a> <a id="551" class="Symbol">_</a>
<a id="553" href="Induction.Lexicographic.html#402" class="Function">Σ-Rec</a> <a id="559" href="Induction.Lexicographic.html#559" class="Bound">RecA</a> <a id="564" href="Induction.Lexicographic.html#564" class="Bound">RecB</a> <a id="569" href="Induction.Lexicographic.html#569" class="Bound">P</a> <a id="571" class="Symbol">(</a><a id="572" href="Induction.Lexicographic.html#572" class="Bound">x</a> <a id="574" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="576" href="Induction.Lexicographic.html#576" class="Bound">y</a><a id="577" class="Symbol">)</a> <a id="579" class="Symbol">=</a>
  <a id="583" class="Comment">-- Either x is constant and y is &quot;smaller&quot;, ...</a>
  <a id="633" href="Induction.Lexicographic.html#564" class="Bound">RecB</a> <a id="638" href="Induction.Lexicographic.html#572" class="Bound">x</a> <a id="640" class="Symbol">(λ</a> <a id="643" href="Induction.Lexicographic.html#643" class="Bound">y′</a> <a id="646" class="Symbol">→</a> <a id="648" href="Induction.Lexicographic.html#569" class="Bound">P</a> <a id="650" class="Symbol">(</a><a id="651" href="Induction.Lexicographic.html#572" class="Bound">x</a> <a id="653" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="655" href="Induction.Lexicographic.html#643" class="Bound">y′</a><a id="657" class="Symbol">))</a> <a id="660" href="Induction.Lexicographic.html#576" class="Bound">y</a>
    <a id="666" href="Data.Product.html#1176" class="Function Operator">×</a>
  <a id="670" class="Comment">-- ...or x is &quot;smaller&quot; and y is arbitrary.</a>
  <a id="716" href="Induction.Lexicographic.html#559" class="Bound">RecA</a> <a id="721" class="Symbol">(λ</a> <a id="724" href="Induction.Lexicographic.html#724" class="Bound">x′</a> <a id="727" class="Symbol">→</a> <a id="729" class="Symbol">∀</a> <a id="731" href="Induction.Lexicographic.html#731" class="Bound">y′</a> <a id="734" class="Symbol">→</a> <a id="736" href="Induction.Lexicographic.html#569" class="Bound">P</a> <a id="738" class="Symbol">(</a><a id="739" href="Induction.Lexicographic.html#724" class="Bound">x′</a> <a id="742" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="744" href="Induction.Lexicographic.html#731" class="Bound">y′</a><a id="746" class="Symbol">))</a> <a id="749" href="Induction.Lexicographic.html#572" class="Bound">x</a>

<a id="752" class="Keyword">infixr</a> <a id="759" class="Number">2</a> <a id="761" href="Induction.Lexicographic.html#766" class="Function Operator">_⊗_</a>

<a id="_⊗_"></a><a id="766" href="Induction.Lexicographic.html#766" class="Function Operator">_⊗_</a> <a id="770" class="Symbol">:</a> <a id="772" class="Symbol">∀</a> <a id="774" class="Symbol">{</a><a id="775" href="Induction.Lexicographic.html#775" class="Bound">a</a> <a id="777" href="Induction.Lexicographic.html#777" class="Bound">b</a> <a id="779" href="Induction.Lexicographic.html#779" class="Bound">ℓ₁</a> <a id="782" href="Induction.Lexicographic.html#782" class="Bound">ℓ₂</a> <a id="785" href="Induction.Lexicographic.html#785" class="Bound">ℓ₃</a><a id="787" class="Symbol">}</a> <a id="789" class="Symbol">{</a><a id="790" href="Induction.Lexicographic.html#790" class="Bound">A</a> <a id="792" class="Symbol">:</a> <a id="794" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="798" href="Induction.Lexicographic.html#775" class="Bound">a</a><a id="799" class="Symbol">}</a> <a id="801" class="Symbol">{</a><a id="802" href="Induction.Lexicographic.html#802" class="Bound">B</a> <a id="804" class="Symbol">:</a> <a id="806" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="810" href="Induction.Lexicographic.html#777" class="Bound">b</a><a id="811" class="Symbol">}</a> <a id="813" class="Symbol">→</a>
      <a id="821" href="Induction.html#769" class="Function">RecStruct</a> <a id="831" href="Induction.Lexicographic.html#790" class="Bound">A</a> <a id="833" class="Symbol">(</a><a id="834" href="Induction.Lexicographic.html#779" class="Bound">ℓ₁</a> <a id="837" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="839" href="Induction.Lexicographic.html#777" class="Bound">b</a><a id="840" class="Symbol">)</a> <a id="842" href="Induction.Lexicographic.html#782" class="Bound">ℓ₂</a> <a id="845" class="Symbol">→</a> <a id="847" href="Induction.html#769" class="Function">RecStruct</a> <a id="857" href="Induction.Lexicographic.html#802" class="Bound">B</a> <a id="859" href="Induction.Lexicographic.html#779" class="Bound">ℓ₁</a> <a id="862" href="Induction.Lexicographic.html#785" class="Bound">ℓ₃</a> <a id="865" class="Symbol">→</a>
      <a id="873" href="Induction.html#769" class="Function">RecStruct</a> <a id="883" class="Symbol">(</a><a id="884" href="Induction.Lexicographic.html#790" class="Bound">A</a> <a id="886" href="Data.Product.html#1176" class="Function Operator">×</a> <a id="888" href="Induction.Lexicographic.html#802" class="Bound">B</a><a id="889" class="Symbol">)</a> <a id="891" class="Symbol">_</a> <a id="893" class="Symbol">_</a>
<a id="895" href="Induction.Lexicographic.html#895" class="Bound">RecA</a> <a id="900" href="Induction.Lexicographic.html#766" class="Function Operator">⊗</a> <a id="902" href="Induction.Lexicographic.html#902" class="Bound">RecB</a> <a id="907" class="Symbol">=</a> <a id="909" href="Induction.Lexicographic.html#402" class="Function">Σ-Rec</a> <a id="915" href="Induction.Lexicographic.html#895" class="Bound">RecA</a> <a id="920" class="Symbol">(λ</a> <a id="923" href="Induction.Lexicographic.html#923" class="Bound">_</a> <a id="925" class="Symbol">→</a> <a id="927" href="Induction.Lexicographic.html#902" class="Bound">RecB</a><a id="931" class="Symbol">)</a>

<a id="934" class="Comment">-- Constructs a recursor builder for lexicographic induction.</a>

<a id="Σ-rec-builder"></a><a id="997" href="Induction.Lexicographic.html#997" class="Function">Σ-rec-builder</a> <a id="1011" class="Symbol">:</a>
  <a id="1015" class="Symbol">∀</a> <a id="1017" class="Symbol">{</a><a id="1018" href="Induction.Lexicographic.html#1018" class="Bound">a</a> <a id="1020" href="Induction.Lexicographic.html#1020" class="Bound">b</a> <a id="1022" href="Induction.Lexicographic.html#1022" class="Bound">ℓ₁</a> <a id="1025" href="Induction.Lexicographic.html#1025" class="Bound">ℓ₂</a> <a id="1028" href="Induction.Lexicographic.html#1028" class="Bound">ℓ₃</a><a id="1030" class="Symbol">}</a> <a id="1032" class="Symbol">{</a><a id="1033" href="Induction.Lexicographic.html#1033" class="Bound">A</a> <a id="1035" class="Symbol">:</a> <a id="1037" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1041" href="Induction.Lexicographic.html#1018" class="Bound">a</a><a id="1042" class="Symbol">}</a> <a id="1044" class="Symbol">{</a><a id="1045" href="Induction.Lexicographic.html#1045" class="Bound">B</a> <a id="1047" class="Symbol">:</a> <a id="1049" href="Induction.Lexicographic.html#1033" class="Bound">A</a> <a id="1051" class="Symbol">→</a> <a id="1053" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1057" href="Induction.Lexicographic.html#1020" class="Bound">b</a><a id="1058" class="Symbol">}</a>
    <a id="1064" class="Symbol">{</a><a id="1065" href="Induction.Lexicographic.html#1065" class="Bound">RecA</a> <a id="1070" class="Symbol">:</a> <a id="1072" href="Induction.html#769" class="Function">RecStruct</a> <a id="1082" href="Induction.Lexicographic.html#1033" class="Bound">A</a> <a id="1084" class="Symbol">(</a><a id="1085" href="Induction.Lexicographic.html#1022" class="Bound">ℓ₁</a> <a id="1088" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1090" href="Induction.Lexicographic.html#1020" class="Bound">b</a><a id="1091" class="Symbol">)</a> <a id="1093" href="Induction.Lexicographic.html#1025" class="Bound">ℓ₂</a><a id="1095" class="Symbol">}</a>
    <a id="1101" class="Symbol">{</a><a id="1102" href="Induction.Lexicographic.html#1102" class="Bound">RecB</a> <a id="1107" class="Symbol">:</a> <a id="1109" class="Symbol">∀</a> <a id="1111" href="Induction.Lexicographic.html#1111" class="Bound">x</a> <a id="1113" class="Symbol">→</a> <a id="1115" href="Induction.html#769" class="Function">RecStruct</a> <a id="1125" class="Symbol">(</a><a id="1126" href="Induction.Lexicographic.html#1045" class="Bound">B</a> <a id="1128" href="Induction.Lexicographic.html#1111" class="Bound">x</a><a id="1129" class="Symbol">)</a> <a id="1131" href="Induction.Lexicographic.html#1022" class="Bound">ℓ₁</a> <a id="1134" href="Induction.Lexicographic.html#1028" class="Bound">ℓ₃</a><a id="1136" class="Symbol">}</a> <a id="1138" class="Symbol">→</a>
  <a id="1142" href="Induction.html#957" class="Function">RecursorBuilder</a> <a id="1158" href="Induction.Lexicographic.html#1065" class="Bound">RecA</a> <a id="1163" class="Symbol">→</a> <a id="1165" class="Symbol">(∀</a> <a id="1168" href="Induction.Lexicographic.html#1168" class="Bound">x</a> <a id="1170" class="Symbol">→</a> <a id="1172" href="Induction.html#957" class="Function">RecursorBuilder</a> <a id="1188" class="Symbol">(</a><a id="1189" href="Induction.Lexicographic.html#1102" class="Bound">RecB</a> <a id="1194" href="Induction.Lexicographic.html#1168" class="Bound">x</a><a id="1195" class="Symbol">))</a> <a id="1198" class="Symbol">→</a>
  <a id="1202" href="Induction.html#957" class="Function">RecursorBuilder</a> <a id="1218" class="Symbol">(</a><a id="1219" href="Induction.Lexicographic.html#402" class="Function">Σ-Rec</a> <a id="1225" href="Induction.Lexicographic.html#1065" class="Bound">RecA</a> <a id="1230" href="Induction.Lexicographic.html#1102" class="Bound">RecB</a><a id="1234" class="Symbol">)</a>
<a id="1236" href="Induction.Lexicographic.html#997" class="Function">Σ-rec-builder</a> <a id="1250" class="Symbol">{</a><a id="1251" class="Argument">RecA</a> <a id="1256" class="Symbol">=</a> <a id="1258" href="Induction.Lexicographic.html#1258" class="Bound">RecA</a><a id="1262" class="Symbol">}</a> <a id="1264" class="Symbol">{</a><a id="1265" class="Argument">RecB</a> <a id="1270" class="Symbol">=</a> <a id="1272" href="Induction.Lexicographic.html#1272" class="Bound">RecB</a><a id="1276" class="Symbol">}</a> <a id="1278" href="Induction.Lexicographic.html#1278" class="Bound">recA</a> <a id="1283" href="Induction.Lexicographic.html#1283" class="Bound">recB</a> <a id="1288" href="Induction.Lexicographic.html#1288" class="Bound">P</a> <a id="1290" href="Induction.Lexicographic.html#1290" class="Bound">f</a> <a id="1292" class="Symbol">(</a><a id="1293" href="Induction.Lexicographic.html#1293" class="Bound">x</a> <a id="1295" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1297" href="Induction.Lexicographic.html#1297" class="Bound">y</a><a id="1298" class="Symbol">)</a> <a id="1300" class="Symbol">=</a>
  <a id="1304" class="Symbol">(</a><a id="1305" href="Induction.Lexicographic.html#1333" class="Function">p₁</a> <a id="1308" href="Induction.Lexicographic.html#1293" class="Bound">x</a> <a id="1310" href="Induction.Lexicographic.html#1297" class="Bound">y</a> <a id="1312" href="Induction.Lexicographic.html#1727" class="Function">p₂x</a> <a id="1316" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1318" href="Induction.Lexicographic.html#1727" class="Function">p₂x</a><a id="1321" class="Symbol">)</a>
  <a id="1325" class="Keyword">where</a>
  <a id="1333" href="Induction.Lexicographic.html#1333" class="Function">p₁</a> <a id="1336" class="Symbol">:</a> <a id="1338" class="Symbol">∀</a> <a id="1340" href="Induction.Lexicographic.html#1340" class="Bound">x</a> <a id="1342" href="Induction.Lexicographic.html#1342" class="Bound">y</a> <a id="1344" class="Symbol">→</a>
       <a id="1353" href="Induction.Lexicographic.html#1258" class="Bound">RecA</a> <a id="1358" class="Symbol">(λ</a> <a id="1361" href="Induction.Lexicographic.html#1361" class="Bound">x′</a> <a id="1364" class="Symbol">→</a> <a id="1366" class="Symbol">∀</a> <a id="1368" href="Induction.Lexicographic.html#1368" class="Bound">y′</a> <a id="1371" class="Symbol">→</a> <a id="1373" href="Induction.Lexicographic.html#1288" class="Bound">P</a> <a id="1375" class="Symbol">(</a><a id="1376" href="Induction.Lexicographic.html#1361" class="Bound">x′</a> <a id="1379" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1381" href="Induction.Lexicographic.html#1368" class="Bound">y′</a><a id="1383" class="Symbol">))</a> <a id="1386" href="Induction.Lexicographic.html#1340" class="Bound">x</a> <a id="1388" class="Symbol">→</a>
       <a id="1397" href="Induction.Lexicographic.html#1272" class="Bound">RecB</a> <a id="1402" href="Induction.Lexicographic.html#1340" class="Bound">x</a> <a id="1404" class="Symbol">(λ</a> <a id="1407" href="Induction.Lexicographic.html#1407" class="Bound">y′</a> <a id="1410" class="Symbol">→</a> <a id="1412" href="Induction.Lexicographic.html#1288" class="Bound">P</a> <a id="1414" class="Symbol">(</a><a id="1415" href="Induction.Lexicographic.html#1340" class="Bound">x</a> <a id="1417" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1419" href="Induction.Lexicographic.html#1407" class="Bound">y′</a><a id="1421" class="Symbol">))</a> <a id="1424" href="Induction.Lexicographic.html#1342" class="Bound">y</a>
  <a id="1428" href="Induction.Lexicographic.html#1333" class="Function">p₁</a> <a id="1431" href="Induction.Lexicographic.html#1431" class="Bound">x</a> <a id="1433" href="Induction.Lexicographic.html#1433" class="Bound">y</a> <a id="1435" href="Induction.Lexicographic.html#1435" class="Bound">x-rec</a> <a id="1441" class="Symbol">=</a> <a id="1443" href="Induction.Lexicographic.html#1283" class="Bound">recB</a> <a id="1448" href="Induction.Lexicographic.html#1431" class="Bound">x</a>
                      <a id="1472" class="Symbol">(λ</a> <a id="1475" href="Induction.Lexicographic.html#1475" class="Bound">y′</a> <a id="1478" class="Symbol">→</a> <a id="1480" href="Induction.Lexicographic.html#1288" class="Bound">P</a> <a id="1482" class="Symbol">(</a><a id="1483" href="Induction.Lexicographic.html#1431" class="Bound">x</a> <a id="1485" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1487" href="Induction.Lexicographic.html#1475" class="Bound">y′</a><a id="1489" class="Symbol">))</a>
                      <a id="1514" class="Symbol">(λ</a> <a id="1517" href="Induction.Lexicographic.html#1517" class="Bound">y</a> <a id="1519" href="Induction.Lexicographic.html#1519" class="Bound">y-rec</a> <a id="1525" class="Symbol">→</a> <a id="1527" href="Induction.Lexicographic.html#1290" class="Bound">f</a> <a id="1529" class="Symbol">(</a><a id="1530" href="Induction.Lexicographic.html#1431" class="Bound">x</a> <a id="1532" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1534" href="Induction.Lexicographic.html#1517" class="Bound">y</a><a id="1535" class="Symbol">)</a> <a id="1537" class="Symbol">(</a><a id="1538" href="Induction.Lexicographic.html#1519" class="Bound">y-rec</a> <a id="1544" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1546" href="Induction.Lexicographic.html#1435" class="Bound">x-rec</a><a id="1551" class="Symbol">))</a>
                      <a id="1576" href="Induction.Lexicographic.html#1433" class="Bound">y</a>

  <a id="1581" href="Induction.Lexicographic.html#1581" class="Function">p₂</a> <a id="1584" class="Symbol">:</a> <a id="1586" class="Symbol">∀</a> <a id="1588" href="Induction.Lexicographic.html#1588" class="Bound">x</a> <a id="1590" class="Symbol">→</a> <a id="1592" href="Induction.Lexicographic.html#1258" class="Bound">RecA</a> <a id="1597" class="Symbol">(λ</a> <a id="1600" href="Induction.Lexicographic.html#1600" class="Bound">x′</a> <a id="1603" class="Symbol">→</a> <a id="1605" class="Symbol">∀</a> <a id="1607" href="Induction.Lexicographic.html#1607" class="Bound">y′</a> <a id="1610" class="Symbol">→</a> <a id="1612" href="Induction.Lexicographic.html#1288" class="Bound">P</a> <a id="1614" class="Symbol">(</a><a id="1615" href="Induction.Lexicographic.html#1600" class="Bound">x′</a> <a id="1618" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1620" href="Induction.Lexicographic.html#1607" class="Bound">y′</a><a id="1622" class="Symbol">))</a> <a id="1625" href="Induction.Lexicographic.html#1588" class="Bound">x</a>
  <a id="1629" href="Induction.Lexicographic.html#1581" class="Function">p₂</a> <a id="1632" class="Symbol">=</a> <a id="1634" href="Induction.Lexicographic.html#1278" class="Bound">recA</a> <a id="1639" class="Symbol">(λ</a> <a id="1642" href="Induction.Lexicographic.html#1642" class="Bound">x</a> <a id="1644" class="Symbol">→</a> <a id="1646" class="Symbol">∀</a> <a id="1648" href="Induction.Lexicographic.html#1648" class="Bound">y</a> <a id="1650" class="Symbol">→</a> <a id="1652" href="Induction.Lexicographic.html#1288" class="Bound">P</a> <a id="1654" class="Symbol">(</a><a id="1655" href="Induction.Lexicographic.html#1642" class="Bound">x</a> <a id="1657" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1659" href="Induction.Lexicographic.html#1648" class="Bound">y</a><a id="1660" class="Symbol">))</a>
            <a id="1675" class="Symbol">(λ</a> <a id="1678" href="Induction.Lexicographic.html#1678" class="Bound">x</a> <a id="1680" href="Induction.Lexicographic.html#1680" class="Bound">x-rec</a> <a id="1686" href="Induction.Lexicographic.html#1686" class="Bound">y</a> <a id="1688" class="Symbol">→</a> <a id="1690" href="Induction.Lexicographic.html#1290" class="Bound">f</a> <a id="1692" class="Symbol">(</a><a id="1693" href="Induction.Lexicographic.html#1678" class="Bound">x</a> <a id="1695" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1697" href="Induction.Lexicographic.html#1686" class="Bound">y</a><a id="1698" class="Symbol">)</a> <a id="1700" class="Symbol">(</a><a id="1701" href="Induction.Lexicographic.html#1333" class="Function">p₁</a> <a id="1704" href="Induction.Lexicographic.html#1678" class="Bound">x</a> <a id="1706" href="Induction.Lexicographic.html#1686" class="Bound">y</a> <a id="1708" href="Induction.Lexicographic.html#1680" class="Bound">x-rec</a> <a id="1714" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1716" href="Induction.Lexicographic.html#1680" class="Bound">x-rec</a><a id="1721" class="Symbol">))</a>

  <a id="1727" href="Induction.Lexicographic.html#1727" class="Function">p₂x</a> <a id="1731" class="Symbol">=</a> <a id="1733" href="Induction.Lexicographic.html#1581" class="Function">p₂</a> <a id="1736" href="Induction.Lexicographic.html#1293" class="Bound">x</a>

<a id="[_⊗_]"></a><a id="1739" href="Induction.Lexicographic.html#1739" class="Function Operator">[_⊗_]</a> <a id="1745" class="Symbol">:</a> <a id="1747" class="Symbol">∀</a> <a id="1749" class="Symbol">{</a><a id="1750" href="Induction.Lexicographic.html#1750" class="Bound">a</a> <a id="1752" href="Induction.Lexicographic.html#1752" class="Bound">b</a> <a id="1754" href="Induction.Lexicographic.html#1754" class="Bound">ℓ₁</a> <a id="1757" href="Induction.Lexicographic.html#1757" class="Bound">ℓ₂</a> <a id="1760" href="Induction.Lexicographic.html#1760" class="Bound">ℓ₃</a><a id="1762" class="Symbol">}</a> <a id="1764" class="Symbol">{</a><a id="1765" href="Induction.Lexicographic.html#1765" class="Bound">A</a> <a id="1767" class="Symbol">:</a> <a id="1769" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1773" href="Induction.Lexicographic.html#1750" class="Bound">a</a><a id="1774" class="Symbol">}</a> <a id="1776" class="Symbol">{</a><a id="1777" href="Induction.Lexicographic.html#1777" class="Bound">B</a> <a id="1779" class="Symbol">:</a> <a id="1781" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1785" href="Induction.Lexicographic.html#1752" class="Bound">b</a><a id="1786" class="Symbol">}</a>
          <a id="1798" class="Symbol">{</a><a id="1799" href="Induction.Lexicographic.html#1799" class="Bound">RecA</a> <a id="1804" class="Symbol">:</a> <a id="1806" href="Induction.html#769" class="Function">RecStruct</a> <a id="1816" href="Induction.Lexicographic.html#1765" class="Bound">A</a> <a id="1818" class="Symbol">(</a><a id="1819" href="Induction.Lexicographic.html#1754" class="Bound">ℓ₁</a> <a id="1822" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1824" href="Induction.Lexicographic.html#1752" class="Bound">b</a><a id="1825" class="Symbol">)</a> <a id="1827" href="Induction.Lexicographic.html#1757" class="Bound">ℓ₂</a><a id="1829" class="Symbol">}</a> <a id="1831" class="Symbol">{</a><a id="1832" href="Induction.Lexicographic.html#1832" class="Bound">RecB</a> <a id="1837" class="Symbol">:</a> <a id="1839" href="Induction.html#769" class="Function">RecStruct</a> <a id="1849" href="Induction.Lexicographic.html#1777" class="Bound">B</a> <a id="1851" href="Induction.Lexicographic.html#1754" class="Bound">ℓ₁</a> <a id="1854" href="Induction.Lexicographic.html#1760" class="Bound">ℓ₃</a><a id="1856" class="Symbol">}</a> <a id="1858" class="Symbol">→</a>
        <a id="1868" href="Induction.html#957" class="Function">RecursorBuilder</a> <a id="1884" href="Induction.Lexicographic.html#1799" class="Bound">RecA</a> <a id="1889" class="Symbol">→</a> <a id="1891" href="Induction.html#957" class="Function">RecursorBuilder</a> <a id="1907" href="Induction.Lexicographic.html#1832" class="Bound">RecB</a> <a id="1912" class="Symbol">→</a>
        <a id="1922" href="Induction.html#957" class="Function">RecursorBuilder</a> <a id="1938" class="Symbol">(</a><a id="1939" href="Induction.Lexicographic.html#1799" class="Bound">RecA</a> <a id="1944" href="Induction.Lexicographic.html#766" class="Function Operator">⊗</a> <a id="1946" href="Induction.Lexicographic.html#1832" class="Bound">RecB</a><a id="1950" class="Symbol">)</a>
<a id="1952" href="Induction.Lexicographic.html#1739" class="Function Operator">[</a> <a id="1954" href="Induction.Lexicographic.html#1954" class="Bound">recA</a> <a id="1959" href="Induction.Lexicographic.html#1739" class="Function Operator">⊗</a> <a id="1961" href="Induction.Lexicographic.html#1961" class="Bound">recB</a> <a id="1966" href="Induction.Lexicographic.html#1739" class="Function Operator">]</a> <a id="1968" class="Symbol">=</a> <a id="1970" href="Induction.Lexicographic.html#997" class="Function">Σ-rec-builder</a> <a id="1984" href="Induction.Lexicographic.html#1954" class="Bound">recA</a> <a id="1989" class="Symbol">(λ</a> <a id="1992" href="Induction.Lexicographic.html#1992" class="Bound">_</a> <a id="1994" class="Symbol">→</a> <a id="1996" href="Induction.Lexicographic.html#1961" class="Bound">recB</a><a id="2000" class="Symbol">)</a>

<a id="2003" class="Comment">------------------------------------------------------------------------</a>
<a id="2076" class="Comment">-- Example</a>

<a id="2088" class="Keyword">private</a>

  <a id="2099" class="Keyword">open</a> <a id="2104" class="Keyword">import</a> <a id="2111" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a>
  <a id="2127" class="Keyword">open</a> <a id="2132" class="Keyword">import</a> <a id="2139" href="Data.Nat.Induction.html" class="Module">Data.Nat.Induction</a> <a id="2158" class="Symbol">as</a> <a id="2161" class="Module">N</a>

  <a id="2166" class="Comment">-- The Ackermann function à la Rózsa Péter.</a>

  <a id="ackermann"></a><a id="2213" href="Induction.Lexicographic.html#2213" class="Function">ackermann</a> <a id="2223" class="Symbol">:</a> <a id="2225" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2227" class="Symbol">→</a> <a id="2229" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a> <a id="2231" class="Symbol">→</a> <a id="2233" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a>
  <a id="2237" href="Induction.Lexicographic.html#2213" class="Function">ackermann</a> <a id="2247" href="Induction.Lexicographic.html#2247" class="Bound">m</a> <a id="2249" href="Induction.Lexicographic.html#2249" class="Bound">n</a> <a id="2251" class="Symbol">=</a>
    <a id="2257" href="Induction.html#1329" class="Function">build</a> <a id="2263" href="Induction.Lexicographic.html#1739" class="Function Operator">[</a> <a id="2265" href="Data.Nat.Induction.html#973" class="Function">N.recBuilder</a> <a id="2278" href="Induction.Lexicographic.html#1739" class="Function Operator">⊗</a> <a id="2280" href="Data.Nat.Induction.html#973" class="Function">N.recBuilder</a> <a id="2293" href="Induction.Lexicographic.html#1739" class="Function Operator">]</a>
          <a id="2305" class="Symbol">(λ</a> <a id="2308" href="Induction.Lexicographic.html#2308" class="Bound">_</a> <a id="2310" class="Symbol">→</a> <a id="2312" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="2313" class="Symbol">)</a>
          <a id="2325" class="Symbol">(λ</a> <a id="2328" class="Symbol">{</a> <a id="2330" class="Symbol">(</a><a id="2331" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>  <a id="2337" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2339" href="Induction.Lexicographic.html#2339" class="Bound">n</a><a id="2340" class="Symbol">)</a>     <a id="2346" class="Symbol">_</a>                   <a id="2366" class="Symbol">→</a> <a id="2368" class="Number">1</a> <a id="2370" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">+</a> <a id="2372" href="Induction.Lexicographic.html#2339" class="Bound">n</a>
             <a id="2387" class="Symbol">;</a> <a id="2389" class="Symbol">(</a><a id="2390" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2394" href="Induction.Lexicographic.html#2394" class="Bound">m</a> <a id="2396" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2398" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a><a id="2402" class="Symbol">)</a>  <a id="2405" class="Symbol">(_</a>         <a id="2416" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2418" href="Induction.Lexicographic.html#2418" class="Bound">ackm•</a><a id="2423" class="Symbol">)</a> <a id="2425" class="Symbol">→</a> <a id="2427" href="Induction.Lexicographic.html#2418" class="Bound">ackm•</a> <a id="2433" class="Number">1</a>
             <a id="2448" class="Symbol">;</a> <a id="2450" class="Symbol">(</a><a id="2451" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2455" href="Induction.Lexicographic.html#2455" class="Bound">m</a> <a id="2457" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2459" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2463" href="Induction.Lexicographic.html#2463" class="Bound">n</a><a id="2464" class="Symbol">)</a> <a id="2466" class="Symbol">(</a><a id="2467" href="Induction.Lexicographic.html#2467" class="Bound">ack[1+m]n</a> <a id="2477" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2479" href="Induction.Lexicographic.html#2479" class="Bound">ackm•</a><a id="2484" class="Symbol">)</a> <a id="2486" class="Symbol">→</a> <a id="2488" href="Induction.Lexicographic.html#2479" class="Bound">ackm•</a> <a id="2494" href="Induction.Lexicographic.html#2467" class="Bound">ack[1+m]n</a>
             <a id="2517" class="Symbol">})</a>
          <a id="2530" class="Symbol">(</a><a id="2531" href="Induction.Lexicographic.html#2247" class="Bound">m</a> <a id="2533" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2535" href="Induction.Lexicographic.html#2249" class="Bound">n</a><a id="2536" class="Symbol">)</a>
</pre></body></html>