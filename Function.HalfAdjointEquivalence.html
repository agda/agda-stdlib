<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Function.HalfAdjointEquivalence</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Half adjoint equivalences</a>
<a id="135" class="Comment">------------------------------------------------------------------------</a>

<a id="209" class="Symbol">{-#</a> <a id="213" class="Keyword">OPTIONS</a> <a id="221" class="Pragma">--without-K</a> <a id="233" class="Pragma">--safe</a> <a id="240" class="Symbol">#-}</a>

<a id="245" class="Keyword">module</a> <a id="252" href="Function.HalfAdjointEquivalence.html" class="Module">Function.HalfAdjointEquivalence</a> <a id="284" class="Keyword">where</a>

<a id="291" class="Keyword">open</a> <a id="296" class="Keyword">import</a> <a id="303" href="Function.Base.html" class="Module">Function.Base</a>
<a id="317" class="Keyword">open</a> <a id="322" class="Keyword">import</a> <a id="329" href="Function.Equality.html" class="Module">Function.Equality</a> <a id="347" class="Keyword">using</a> <a id="353" class="Symbol">(</a><a id="354" href="Function.Equality.html#1064" class="Field Operator">_⟨$⟩_</a><a id="359" class="Symbol">)</a>
<a id="361" class="Keyword">open</a> <a id="366" class="Keyword">import</a> <a id="373" href="Function.Inverse.html" class="Module">Function.Inverse</a> <a id="390" class="Symbol">as</a> <a id="393" class="Module">Inv</a> <a id="397" class="Keyword">using</a> <a id="403" class="Symbol">(</a><a id="404" href="Function.Inverse.html#2457" class="Function Operator">_↔_</a><a id="407" class="Symbol">;</a> <a id="409" class="Keyword">module</a> <a id="416" href="Function.Inverse.html#1385" class="Module">Inverse</a><a id="423" class="Symbol">)</a>
<a id="425" class="Keyword">open</a> <a id="430" class="Keyword">import</a> <a id="437" href="Level.html" class="Module">Level</a>
<a id="443" class="Keyword">open</a> <a id="448" class="Keyword">import</a> <a id="455" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a>

<a id="494" class="Comment">-- Half adjoint equivalences (see the HoTT book).</a>

<a id="545" class="Keyword">record</a> <a id="_≃_"></a><a id="552" href="Function.HalfAdjointEquivalence.html#552" class="Record Operator">_≃_</a> <a id="556" class="Symbol">{</a><a id="557" href="Function.HalfAdjointEquivalence.html#557" class="Bound">a</a> <a id="559" href="Function.HalfAdjointEquivalence.html#559" class="Bound">b</a><a id="560" class="Symbol">}</a> <a id="562" class="Symbol">(</a><a id="563" href="Function.HalfAdjointEquivalence.html#563" class="Bound">A</a> <a id="565" class="Symbol">:</a> <a id="567" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="571" href="Function.HalfAdjointEquivalence.html#557" class="Bound">a</a><a id="572" class="Symbol">)</a> <a id="574" class="Symbol">(</a><a id="575" href="Function.HalfAdjointEquivalence.html#575" class="Bound">B</a> <a id="577" class="Symbol">:</a> <a id="579" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="583" href="Function.HalfAdjointEquivalence.html#559" class="Bound">b</a><a id="584" class="Symbol">)</a> <a id="586" class="Symbol">:</a> <a id="588" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="592" class="Symbol">(</a><a id="593" href="Function.HalfAdjointEquivalence.html#557" class="Bound">a</a> <a id="595" href="Agda.Primitive.html#810" class="Primitive Operator">⊔</a> <a id="597" href="Function.HalfAdjointEquivalence.html#559" class="Bound">b</a><a id="598" class="Symbol">)</a> <a id="600" class="Keyword">where</a>
  <a id="608" class="Keyword">field</a>
    <a id="_≃_.to"></a><a id="618" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a>               <a id="635" class="Symbol">:</a> <a id="637" href="Function.HalfAdjointEquivalence.html#563" class="Bound">A</a> <a id="639" class="Symbol">→</a> <a id="641" href="Function.HalfAdjointEquivalence.html#575" class="Bound">B</a>
    <a id="_≃_.from"></a><a id="647" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a>             <a id="664" class="Symbol">:</a> <a id="666" href="Function.HalfAdjointEquivalence.html#575" class="Bound">B</a> <a id="668" class="Symbol">→</a> <a id="670" href="Function.HalfAdjointEquivalence.html#563" class="Bound">A</a>
    <a id="_≃_.left-inverse-of"></a><a id="676" href="Function.HalfAdjointEquivalence.html#676" class="Field">left-inverse-of</a>  <a id="693" class="Symbol">:</a> <a id="695" class="Symbol">∀</a> <a id="697" href="Function.HalfAdjointEquivalence.html#697" class="Bound">x</a> <a id="699" class="Symbol">→</a> <a id="701" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a> <a id="706" class="Symbol">(</a><a id="707" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="710" href="Function.HalfAdjointEquivalence.html#697" class="Bound">x</a><a id="711" class="Symbol">)</a> <a id="713" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="715" href="Function.HalfAdjointEquivalence.html#697" class="Bound">x</a>
    <a id="_≃_.right-inverse-of"></a><a id="721" href="Function.HalfAdjointEquivalence.html#721" class="Field">right-inverse-of</a> <a id="738" class="Symbol">:</a> <a id="740" class="Symbol">∀</a> <a id="742" href="Function.HalfAdjointEquivalence.html#742" class="Bound">x</a> <a id="744" class="Symbol">→</a> <a id="746" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="749" class="Symbol">(</a><a id="750" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a> <a id="755" href="Function.HalfAdjointEquivalence.html#742" class="Bound">x</a><a id="756" class="Symbol">)</a> <a id="758" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="760" href="Function.HalfAdjointEquivalence.html#742" class="Bound">x</a>
    <a id="_≃_.left-right"></a><a id="766" href="Function.HalfAdjointEquivalence.html#766" class="Field">left-right</a>       <a id="783" class="Symbol">:</a>
      <a id="791" class="Symbol">∀</a> <a id="793" href="Function.HalfAdjointEquivalence.html#793" class="Bound">x</a> <a id="795" class="Symbol">→</a> <a id="797" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="802" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="805" class="Symbol">(</a><a id="806" href="Function.HalfAdjointEquivalence.html#676" class="Field">left-inverse-of</a> <a id="822" href="Function.HalfAdjointEquivalence.html#793" class="Bound">x</a><a id="823" class="Symbol">)</a> <a id="825" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="827" href="Function.HalfAdjointEquivalence.html#721" class="Field">right-inverse-of</a> <a id="844" class="Symbol">(</a><a id="845" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="848" href="Function.HalfAdjointEquivalence.html#793" class="Bound">x</a><a id="849" class="Symbol">)</a>

  <a id="854" class="Comment">-- Half adjoint equivalences can be turned into inverses.</a>

  <a id="_≃_.inverse"></a><a id="915" href="Function.HalfAdjointEquivalence.html#915" class="Function">inverse</a> <a id="923" class="Symbol">:</a> <a id="925" href="Function.HalfAdjointEquivalence.html#563" class="Bound">A</a> <a id="927" href="Function.Inverse.html#2457" class="Function Operator">↔</a> <a id="929" href="Function.HalfAdjointEquivalence.html#575" class="Bound">B</a>
  <a id="933" href="Function.HalfAdjointEquivalence.html#915" class="Function">inverse</a> <a id="941" class="Symbol">=</a> <a id="943" href="Function.Inverse.html#2641" class="Function">Inv.inverse</a> <a id="955" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="958" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a> <a id="963" href="Function.HalfAdjointEquivalence.html#676" class="Field">left-inverse-of</a> <a id="979" href="Function.HalfAdjointEquivalence.html#721" class="Field">right-inverse-of</a>

  <a id="999" class="Comment">-- The forward direction of a half adjoint equivalence is injective.</a>

  <a id="_≃_.injective"></a><a id="1071" href="Function.HalfAdjointEquivalence.html#1071" class="Function">injective</a> <a id="1081" class="Symbol">:</a> <a id="1083" class="Symbol">∀</a> <a id="1085" class="Symbol">{</a><a id="1086" href="Function.HalfAdjointEquivalence.html#1086" class="Bound">x</a> <a id="1088" href="Function.HalfAdjointEquivalence.html#1088" class="Bound">y</a><a id="1089" class="Symbol">}</a> <a id="1091" class="Symbol">→</a> <a id="1093" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="1096" href="Function.HalfAdjointEquivalence.html#1086" class="Bound">x</a> <a id="1098" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="1100" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="1103" href="Function.HalfAdjointEquivalence.html#1088" class="Bound">y</a> <a id="1105" class="Symbol">→</a> <a id="1107" href="Function.HalfAdjointEquivalence.html#1086" class="Bound">x</a> <a id="1109" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="1111" href="Function.HalfAdjointEquivalence.html#1088" class="Bound">y</a>
  <a id="1115" href="Function.HalfAdjointEquivalence.html#1071" class="Function">injective</a> <a id="1125" class="Symbol">{</a><a id="1126" href="Function.HalfAdjointEquivalence.html#1126" class="Bound">x</a><a id="1127" class="Symbol">}</a> <a id="1129" class="Symbol">{</a><a id="1130" href="Function.HalfAdjointEquivalence.html#1130" class="Bound">y</a><a id="1131" class="Symbol">}</a> <a id="1133" href="Function.HalfAdjointEquivalence.html#1133" class="Bound">to-x≡to-y</a> <a id="1143" class="Symbol">=</a>
    <a id="1149" href="Function.HalfAdjointEquivalence.html#1126" class="Bound">x</a>            <a id="1162" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="1165" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="1169" class="Symbol">(</a><a id="1170" href="Function.HalfAdjointEquivalence.html#676" class="Field">left-inverse-of</a> <a id="1186" class="Symbol">_)</a> <a id="1189" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="1195" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a> <a id="1200" class="Symbol">(</a><a id="1201" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="1204" href="Function.HalfAdjointEquivalence.html#1126" class="Bound">x</a><a id="1205" class="Symbol">)</a>  <a id="1208" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="1211" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="1216" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a> <a id="1221" href="Function.HalfAdjointEquivalence.html#1133" class="Bound">to-x≡to-y</a> <a id="1231" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="1237" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a> <a id="1242" class="Symbol">(</a><a id="1243" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a> <a id="1246" href="Function.HalfAdjointEquivalence.html#1130" class="Bound">y</a><a id="1247" class="Symbol">)</a>  <a id="1250" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="1253" href="Function.HalfAdjointEquivalence.html#676" class="Field">left-inverse-of</a> <a id="1269" class="Symbol">_</a> <a id="1271" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="1277" href="Function.HalfAdjointEquivalence.html#1130" class="Bound">y</a>            <a id="1290" href="Relation.Binary.PropositionalEquality.Core.html#3105" class="Function Operator">∎</a>
    <a id="1296" class="Keyword">where</a>
    <a id="1306" class="Keyword">open</a> <a id="1311" href="Relation.Binary.PropositionalEquality.Core.html#2708" class="Module">≡-Reasoning</a>

<a id="1324" class="Comment">-- Inverses can be turned into half adjoint equivalences.</a>
<a id="1382" class="Comment">--</a>
<a id="1385" class="Comment">-- (This proof is based on one in the HoTT book.)</a>

<a id="↔→≃"></a><a id="1436" href="Function.HalfAdjointEquivalence.html#1436" class="Function">↔→≃</a> <a id="1440" class="Symbol">:</a> <a id="1442" class="Symbol">∀</a> <a id="1444" class="Symbol">{</a><a id="1445" href="Function.HalfAdjointEquivalence.html#1445" class="Bound">a</a> <a id="1447" href="Function.HalfAdjointEquivalence.html#1447" class="Bound">b</a><a id="1448" class="Symbol">}</a> <a id="1450" class="Symbol">{</a><a id="1451" href="Function.HalfAdjointEquivalence.html#1451" class="Bound">A</a> <a id="1453" class="Symbol">:</a> <a id="1455" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1459" href="Function.HalfAdjointEquivalence.html#1445" class="Bound">a</a><a id="1460" class="Symbol">}</a> <a id="1462" class="Symbol">{</a><a id="1463" href="Function.HalfAdjointEquivalence.html#1463" class="Bound">B</a> <a id="1465" class="Symbol">:</a> <a id="1467" href="Agda.Primitive.html#326" class="Primitive">Set</a> <a id="1471" href="Function.HalfAdjointEquivalence.html#1447" class="Bound">b</a><a id="1472" class="Symbol">}</a> <a id="1474" class="Symbol">→</a> <a id="1476" href="Function.HalfAdjointEquivalence.html#1451" class="Bound">A</a> <a id="1478" href="Function.Inverse.html#2457" class="Function Operator">↔</a> <a id="1480" href="Function.HalfAdjointEquivalence.html#1463" class="Bound">B</a> <a id="1482" class="Symbol">→</a> <a id="1484" href="Function.HalfAdjointEquivalence.html#1451" class="Bound">A</a> <a id="1486" href="Function.HalfAdjointEquivalence.html#552" class="Record Operator">≃</a> <a id="1488" href="Function.HalfAdjointEquivalence.html#1463" class="Bound">B</a>
<a id="1490" href="Function.HalfAdjointEquivalence.html#1436" class="Function">↔→≃</a> <a id="1494" href="Function.HalfAdjointEquivalence.html#1494" class="Bound">A↔B</a> <a id="1498" class="Symbol">=</a> <a id="1500" class="Keyword">record</a>
  <a id="1509" class="Symbol">{</a> <a id="1511" href="Function.HalfAdjointEquivalence.html#618" class="Field">to</a>               <a id="1528" class="Symbol">=</a> <a id="1530" href="Function.Inverse.html#1523" class="Field">to</a>   <a id="1535" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a>
  <a id="1542" class="Symbol">;</a> <a id="1544" href="Function.HalfAdjointEquivalence.html#647" class="Field">from</a>             <a id="1561" class="Symbol">=</a> <a id="1563" href="Function.Inverse.html#1550" class="Field">from</a> <a id="1568" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a>
  <a id="1575" class="Symbol">;</a> <a id="1577" href="Function.HalfAdjointEquivalence.html#676" class="Field">left-inverse-of</a>  <a id="1594" class="Symbol">=</a> <a id="1596" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a>
  <a id="1614" class="Symbol">;</a> <a id="1616" href="Function.HalfAdjointEquivalence.html#721" class="Field">right-inverse-of</a> <a id="1633" class="Symbol">=</a> <a id="1635" href="Function.HalfAdjointEquivalence.html#1786" class="Function">right-inverse-of</a>
  <a id="1654" class="Symbol">;</a> <a id="1656" href="Function.HalfAdjointEquivalence.html#766" class="Field">left-right</a>       <a id="1673" class="Symbol">=</a> <a id="1675" href="Function.HalfAdjointEquivalence.html#2145" class="Function">left-right</a>
  <a id="1688" class="Symbol">}</a>
  <a id="1692" class="Keyword">where</a>
  <a id="1700" class="Keyword">open</a> <a id="1705" href="Relation.Binary.PropositionalEquality.Core.html#2708" class="Module">≡-Reasoning</a>
  <a id="1719" class="Keyword">open</a> <a id="1724" class="Keyword">module</a> <a id="1731" href="Function.HalfAdjointEquivalence.html#1731" class="Module">A↔B</a> <a id="1735" class="Symbol">=</a> <a id="1737" href="Function.Inverse.html#1385" class="Module">Inverse</a> <a id="1745" href="Function.HalfAdjointEquivalence.html#1494" class="Bound">A↔B</a> <a id="1749" class="Keyword">using</a> <a id="1755" class="Symbol">(</a><a id="1756" href="Function.Inverse.html#1523" class="Field">to</a><a id="1758" class="Symbol">;</a> <a id="1760" href="Function.Inverse.html#1550" class="Field">from</a><a id="1764" class="Symbol">;</a> <a id="1766" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a><a id="1781" class="Symbol">)</a>

  <a id="1786" href="Function.HalfAdjointEquivalence.html#1786" class="Function">right-inverse-of</a> <a id="1803" class="Symbol">:</a> <a id="1805" class="Symbol">∀</a> <a id="1807" href="Function.HalfAdjointEquivalence.html#1807" class="Bound">x</a> <a id="1809" class="Symbol">→</a> <a id="1811" href="Function.Inverse.html#1523" class="Field">to</a> <a id="1814" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1818" class="Symbol">(</a><a id="1819" href="Function.Inverse.html#1550" class="Field">from</a> <a id="1824" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1828" href="Function.HalfAdjointEquivalence.html#1807" class="Bound">x</a><a id="1829" class="Symbol">)</a> <a id="1831" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="1833" href="Function.HalfAdjointEquivalence.html#1807" class="Bound">x</a>
  <a id="1837" href="Function.HalfAdjointEquivalence.html#1786" class="Function">right-inverse-of</a> <a id="1854" href="Function.HalfAdjointEquivalence.html#1854" class="Bound">x</a> <a id="1856" class="Symbol">=</a>
    <a id="1862" href="Function.Inverse.html#1523" class="Field">to</a> <a id="1865" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1869" class="Symbol">(</a><a id="1870" href="Function.Inverse.html#1550" class="Field">from</a> <a id="1875" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1879" href="Function.HalfAdjointEquivalence.html#1854" class="Bound">x</a><a id="1880" class="Symbol">)</a>                      <a id="1903" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="1906" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="1910" class="Symbol">(</a><a id="1911" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="1932" class="Symbol">_)</a> <a id="1935" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="1941" href="Function.Inverse.html#1523" class="Field">to</a> <a id="1944" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1948" class="Symbol">(</a><a id="1949" href="Function.Inverse.html#1550" class="Field">from</a> <a id="1954" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1958" class="Symbol">(</a><a id="1959" href="Function.Inverse.html#1523" class="Field">to</a> <a id="1962" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1966" class="Symbol">(</a><a id="1967" href="Function.Inverse.html#1550" class="Field">from</a> <a id="1972" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="1976" href="Function.HalfAdjointEquivalence.html#1854" class="Bound">x</a><a id="1977" class="Symbol">)))</a>  <a id="1982" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="1985" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="1990" class="Symbol">(</a><a id="1991" href="Function.Inverse.html#1523" class="Field">to</a> <a id="1994" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="1998" class="Symbol">)</a> <a id="2000" class="Symbol">(</a><a id="2001" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2017" class="Symbol">_)</a> <a id="2020" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="2026" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2029" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="2033" class="Symbol">(</a><a id="2034" href="Function.Inverse.html#1550" class="Field">from</a> <a id="2039" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="2043" href="Function.HalfAdjointEquivalence.html#1854" class="Bound">x</a><a id="2044" class="Symbol">)</a>                      <a id="2067" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="2070" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2091" class="Symbol">_</a> <a id="2093" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="2099" href="Function.HalfAdjointEquivalence.html#1854" class="Bound">x</a>                                        <a id="2140" href="Relation.Binary.PropositionalEquality.Core.html#3105" class="Function Operator">∎</a>

  <a id="2145" href="Function.HalfAdjointEquivalence.html#2145" class="Function">left-right</a> <a id="2156" class="Symbol">:</a>
    <a id="2162" class="Symbol">∀</a> <a id="2164" href="Function.HalfAdjointEquivalence.html#2164" class="Bound">x</a> <a id="2166" class="Symbol">→</a>
    <a id="2172" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2177" class="Symbol">(</a><a id="2178" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2181" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2185" class="Symbol">)</a> <a id="2187" class="Symbol">(</a><a id="2188" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2204" href="Function.HalfAdjointEquivalence.html#2164" class="Bound">x</a><a id="2205" class="Symbol">)</a> <a id="2207" href="Agda.Builtin.Equality.html#151" class="Datatype Operator">≡</a> <a id="2209" href="Function.HalfAdjointEquivalence.html#1786" class="Function">right-inverse-of</a> <a id="2226" class="Symbol">(</a><a id="2227" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2230" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="2234" href="Function.HalfAdjointEquivalence.html#2164" class="Bound">x</a><a id="2235" class="Symbol">)</a>
  <a id="2239" href="Function.HalfAdjointEquivalence.html#2145" class="Function">left-right</a> <a id="2250" href="Function.HalfAdjointEquivalence.html#2250" class="Bound">x</a> <a id="2252" class="Symbol">=</a>
    <a id="2258" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2263" class="Symbol">(</a><a id="2264" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2267" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2271" class="Symbol">)</a> <a id="2273" class="Symbol">(</a><a id="2274" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2290" href="Function.HalfAdjointEquivalence.html#2250" class="Bound">x</a><a id="2291" class="Symbol">)</a>               <a id="2307" href="Relation.Binary.PropositionalEquality.Core.html#2864" class="Function Operator">≡⟨⟩</a>

    <a id="2316" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2322" href="Agda.Builtin.Equality.html#208" class="InductiveConstructor">refl</a> <a id="2327" class="Symbol">(</a><a id="2328" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2333" class="Symbol">(</a><a id="2334" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2337" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2341" class="Symbol">)</a> <a id="2343" class="Symbol">(</a><a id="2344" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2360" class="Symbol">_))</a>  <a id="2365" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="2368" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2373" class="Symbol">(λ</a> <a id="2376" href="Function.HalfAdjointEquivalence.html#2376" class="Bound">p</a> <a id="2378" class="Symbol">→</a> <a id="2380" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2386" href="Function.HalfAdjointEquivalence.html#2376" class="Bound">p</a> <a id="2388" class="Symbol">(</a><a id="2389" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2394" class="Symbol">(</a><a id="2395" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2398" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2402" class="Symbol">)</a> <a id="2404" class="Symbol">_))</a>
                                                          <a id="2466" class="Symbol">(</a><a id="2467" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="2471" class="Symbol">(</a><a id="2472" href="Relation.Binary.PropositionalEquality.Properties.html#1062" class="Function">trans-symˡ</a> <a id="2483" class="Symbol">(</a><a id="2484" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2505" class="Symbol">_)))</a> <a id="2510" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
    <a id="2516" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2522" class="Symbol">(</a><a id="2523" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2529" class="Symbol">(</a><a id="2530" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="2534" class="Symbol">(</a><a id="2535" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2556" class="Symbol">_))</a>
               <a id="2575" class="Symbol">(</a><a id="2576" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2597" class="Symbol">_))</a>
      <a id="2607" class="Symbol">(</a><a id="2608" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2613" class="Symbol">(</a><a id="2614" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2617" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2621" class="Symbol">)</a> <a id="2623" class="Symbol">(</a><a id="2624" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2640" class="Symbol">_))</a>           <a id="2654" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="2657" href="Relation.Binary.PropositionalEquality.Properties.html#925" class="Function">trans-assoc</a> <a id="2669" class="Symbol">(</a><a id="2670" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="2674" class="Symbol">(</a><a id="2675" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2696" class="Symbol">_))</a> <a id="2700" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>

    <a id="2707" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2713" class="Symbol">(</a><a id="2714" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="2718" class="Symbol">(</a><a id="2719" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2740" class="Symbol">_))</a>
      <a id="2750" class="Symbol">(</a><a id="2751" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2757" class="Symbol">(</a><a id="2758" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2779" class="Symbol">_)</a>
         <a id="2791" class="Symbol">(</a><a id="2792" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2797" class="Symbol">(</a><a id="2798" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2801" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2805" class="Symbol">)</a> <a id="2807" class="Symbol">(</a><a id="2808" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2824" class="Symbol">_)))</a>       <a id="2835" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="2838" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2843" class="Symbol">(</a><a id="2844" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2850" class="Symbol">(</a><a id="2851" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="2855" class="Symbol">(</a><a id="2856" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2877" class="Symbol">_)))</a> <a id="2882" href="Function.HalfAdjointEquivalence.html#3109" class="Function">lemma</a> <a id="2888" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>

    <a id="2895" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2901" class="Symbol">(</a><a id="2902" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="2906" class="Symbol">(</a><a id="2907" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="2928" class="Symbol">_))</a>
      <a id="2938" class="Symbol">(</a><a id="2939" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2945" class="Symbol">(</a><a id="2946" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="2951" class="Symbol">(</a><a id="2952" href="Function.Inverse.html#1523" class="Field">to</a> <a id="2955" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="2959" class="Symbol">)</a> <a id="2961" class="Symbol">(</a><a id="2962" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="2978" class="Symbol">_))</a>
         <a id="2991" class="Symbol">(</a><a id="2992" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="2998" class="Symbol">(</a><a id="2999" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3020" class="Symbol">_)</a> <a id="3023" href="Agda.Builtin.Equality.html#208" class="InductiveConstructor">refl</a><a id="3027" class="Symbol">))</a>      <a id="3035" href="Relation.Binary.PropositionalEquality.Core.html#2864" class="Function Operator">≡⟨⟩</a>

    <a id="3044" href="Function.HalfAdjointEquivalence.html#1786" class="Function">right-inverse-of</a> <a id="3061" class="Symbol">(</a><a id="3062" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3065" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩</a> <a id="3069" href="Function.HalfAdjointEquivalence.html#2250" class="Bound">x</a><a id="3070" class="Symbol">)</a>                      <a id="3093" href="Relation.Binary.PropositionalEquality.Core.html#3105" class="Function Operator">∎</a>
    <a id="3099" class="Keyword">where</a>
    <a id="3109" href="Function.HalfAdjointEquivalence.html#3109" class="Function">lemma</a> <a id="3115" class="Symbol">=</a>
      <a id="3123" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3129" class="Symbol">(</a><a id="3130" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3151" class="Symbol">_)</a>
        <a id="3162" class="Symbol">(</a><a id="3163" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3168" class="Symbol">(</a><a id="3169" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3172" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3176" class="Symbol">)</a> <a id="3178" class="Symbol">(</a><a id="3179" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="3195" class="Symbol">_))</a>             <a id="3211" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="3214" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3219" class="Symbol">(</a><a id="3220" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3226" class="Symbol">(</a><a id="3227" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3248" class="Symbol">_))</a> <a id="3252" class="Symbol">(</a><a id="3253" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="3257" class="Symbol">(</a><a id="3258" href="Relation.Binary.PropositionalEquality.Properties.html#1565" class="Function">cong-id</a> <a id="3266" class="Symbol">_))</a> <a id="3270" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>

      <a id="3279" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3285" class="Symbol">(</a><a id="3286" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3307" class="Symbol">_)</a>
        <a id="3318" class="Symbol">(</a><a id="3319" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3324" href="Function.Base.html#615" class="Function">id</a> <a id="3327" class="Symbol">(</a><a id="3328" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3333" class="Symbol">(</a><a id="3334" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3337" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3341" class="Symbol">)</a> <a id="3343" class="Symbol">(</a><a id="3344" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="3360" class="Symbol">_)))</a>   <a id="3367" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="3370" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="3374" class="Symbol">(</a><a id="3375" href="Relation.Binary.PropositionalEquality.html#2850" class="Function">naturality</a> <a id="3386" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a><a id="3406" class="Symbol">)</a> <a id="3408" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>

      <a id="3417" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3423" class="Symbol">(</a><a id="3424" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3429" class="Symbol">((</a><a id="3431" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3434" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3438" class="Symbol">)</a> <a id="3440" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="3442" class="Symbol">(</a><a id="3443" href="Function.Inverse.html#1550" class="Field">from</a> <a id="3448" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3452" class="Symbol">))</a>
                 <a id="3472" class="Symbol">(</a><a id="3473" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3478" class="Symbol">(</a><a id="3479" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3482" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3486" class="Symbol">)</a> <a id="3488" class="Symbol">(</a><a id="3489" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="3505" class="Symbol">_)))</a>
        <a id="3518" class="Symbol">(</a><a id="3519" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3540" class="Symbol">_)</a>                         <a id="3567" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="3570" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3575" class="Symbol">(λ</a> <a id="3578" href="Function.HalfAdjointEquivalence.html#3578" class="Bound">p</a> <a id="3580" class="Symbol">→</a> <a id="3582" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3588" href="Function.HalfAdjointEquivalence.html#3578" class="Bound">p</a> <a id="3590" class="Symbol">(</a><a id="3591" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3612" class="Symbol">_))</a>
                                                              <a id="3678" class="Symbol">(</a><a id="3679" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="3683" class="Symbol">(</a><a id="3684" href="Relation.Binary.PropositionalEquality.Properties.html#1636" class="Function">cong-∘</a> <a id="3691" class="Symbol">_))</a> <a id="3695" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
      <a id="3703" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3709" class="Symbol">(</a><a id="3710" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3715" class="Symbol">((</a><a id="3717" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3720" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3724" class="Symbol">)</a> <a id="3726" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="3728" class="Symbol">(</a><a id="3729" href="Function.Inverse.html#1550" class="Field">from</a> <a id="3734" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3738" class="Symbol">)</a> <a id="3740" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="3742" class="Symbol">(</a><a id="3743" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3746" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="3750" class="Symbol">))</a>
                      <a id="3775" class="Symbol">(</a><a id="3776" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="3792" class="Symbol">_))</a>
        <a id="3804" class="Symbol">(</a><a id="3805" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3826" class="Symbol">_)</a>                         <a id="3853" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="3856" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3861" class="Symbol">(λ</a> <a id="3864" href="Function.HalfAdjointEquivalence.html#3864" class="Bound">p</a> <a id="3866" class="Symbol">→</a> <a id="3868" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3874" href="Function.HalfAdjointEquivalence.html#3864" class="Bound">p</a> <a id="3876" class="Symbol">(</a><a id="3877" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="3898" class="Symbol">_))</a>
                                                              <a id="3964" class="Symbol">(</a><a id="3965" href="Relation.Binary.PropositionalEquality.Properties.html#1636" class="Function">cong-∘</a> <a id="3972" class="Symbol">_)</a> <a id="3975" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
      <a id="3983" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="3989" class="Symbol">(</a><a id="3990" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="3995" class="Symbol">(</a><a id="3996" href="Function.Inverse.html#1523" class="Field">to</a> <a id="3999" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4003" class="Symbol">)</a>
                 <a id="4022" class="Symbol">(</a><a id="4023" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4028" class="Symbol">((</a><a id="4030" href="Function.Inverse.html#1550" class="Field">from</a> <a id="4035" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4039" class="Symbol">)</a> <a id="4041" href="Function.Base.html#1031" class="Function Operator">∘</a> <a id="4043" class="Symbol">(</a><a id="4044" href="Function.Inverse.html#1523" class="Field">to</a> <a id="4047" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4051" class="Symbol">))</a>
                    <a id="4074" class="Symbol">(</a><a id="4075" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="4091" class="Symbol">_)))</a>
        <a id="4104" class="Symbol">(</a><a id="4105" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="4126" class="Symbol">_)</a>                         <a id="4153" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="4156" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4161" class="Symbol">(λ</a> <a id="4164" href="Function.HalfAdjointEquivalence.html#4164" class="Bound">p</a> <a id="4166" class="Symbol">→</a> <a id="4168" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="4174" class="Symbol">(</a><a id="4175" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4180" class="Symbol">(</a><a id="4181" href="Function.Inverse.html#1523" class="Field">to</a> <a id="4184" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4188" class="Symbol">)</a> <a id="4190" href="Function.HalfAdjointEquivalence.html#4164" class="Bound">p</a><a id="4191" class="Symbol">)</a> <a id="4193" class="Symbol">_)</a>
                                                              <a id="4258" class="Symbol">(</a><a id="4259" href="Relation.Binary.PropositionalEquality.html#3214" class="Function">cong-≡id</a> <a id="4268" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a><a id="4283" class="Symbol">)</a> <a id="4285" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
      <a id="4293" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="4299" class="Symbol">(</a><a id="4300" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4305" class="Symbol">(</a><a id="4306" href="Function.Inverse.html#1523" class="Field">to</a> <a id="4309" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4313" class="Symbol">)</a> <a id="4315" class="Symbol">(</a><a id="4316" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="4332" class="Symbol">_))</a>
        <a id="4344" class="Symbol">(</a><a id="4345" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="4366" class="Symbol">_)</a>                         <a id="4393" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">≡⟨</a> <a id="4396" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4401" class="Symbol">(</a><a id="4402" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="4408" class="Symbol">(</a><a id="4409" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4414" class="Symbol">(</a><a id="4415" href="Function.Inverse.html#1523" class="Field">to</a> <a id="4418" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4422" class="Symbol">)</a> <a id="4424" class="Symbol">(</a><a id="4425" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="4441" class="Symbol">_)))</a>
                                                              <a id="4508" class="Symbol">(</a><a id="4509" href="Relation.Binary.PropositionalEquality.Core.html#1684" class="Function">sym</a> <a id="4513" class="Symbol">(</a><a id="4514" href="Relation.Binary.PropositionalEquality.Properties.html#843" class="Function">trans-reflʳ</a> <a id="4526" class="Symbol">_))</a> <a id="4530" href="Relation.Binary.PropositionalEquality.Core.html#2923" class="Function">⟩</a>
      <a id="4538" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="4544" class="Symbol">(</a><a id="4545" href="Relation.Binary.PropositionalEquality.Core.html#1130" class="Function">cong</a> <a id="4550" class="Symbol">(</a><a id="4551" href="Function.Inverse.html#1523" class="Field">to</a> <a id="4554" href="Function.Equality.html#1064" class="Field Operator">⟨$⟩_</a><a id="4558" class="Symbol">)</a> <a id="4560" class="Symbol">(</a><a id="4561" href="Function.Inverse.html#1168" class="Function">left-inverse-of</a> <a id="4577" class="Symbol">_))</a>
        <a id="4589" class="Symbol">(</a><a id="4590" href="Relation.Binary.PropositionalEquality.Core.html#1729" class="Function">trans</a> <a id="4596" class="Symbol">(</a><a id="4597" href="Function.Inverse.html#1214" class="Function">A↔B.right-inverse-of</a> <a id="4618" class="Symbol">_)</a> <a id="4621" href="Agda.Builtin.Equality.html#208" class="InductiveConstructor">refl</a><a id="4625" class="Symbol">)</a>            <a id="4638" href="Relation.Binary.PropositionalEquality.Core.html#3105" class="Function Operator">∎</a>
</pre></body></html>