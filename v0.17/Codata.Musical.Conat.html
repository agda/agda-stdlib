<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Musical.Conat</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Coinductive &quot;natural&quot; numbers</a>
<a id="139" class="Comment">------------------------------------------------------------------------</a>

<a id="213" class="Keyword">module</a> <a id="220" href="Codata.Musical.Conat.html" class="Module">Codata.Musical.Conat</a> <a id="241" class="Keyword">where</a>

<a id="248" class="Keyword">open</a> <a id="253" class="Keyword">import</a> <a id="260" href="Codata.Musical.Notation.html" class="Module">Codata.Musical.Notation</a>
<a id="284" class="Keyword">open</a> <a id="289" class="Keyword">import</a> <a id="296" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="310" class="Keyword">using</a> <a id="316" class="Symbol">(</a><a id="317" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a><a id="318" class="Symbol">;</a> <a id="320" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="324" class="Symbol">;</a> <a id="326" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a><a id="329" class="Symbol">)</a>
<a id="331" class="Keyword">open</a> <a id="336" class="Keyword">import</a> <a id="343" href="Function.html" class="Module">Function</a>
<a id="352" class="Keyword">open</a> <a id="357" class="Keyword">import</a> <a id="364" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="380" class="Keyword">open</a> <a id="385" class="Keyword">import</a> <a id="392" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="430" class="Symbol">as</a> <a id="433" class="Module">P</a> <a id="435" class="Keyword">using</a> <a id="441" class="Symbol">(</a><a id="442" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a><a id="445" class="Symbol">)</a>

<a id="448" class="Comment">------------------------------------------------------------------------</a>
<a id="521" class="Comment">-- The type</a>

<a id="534" class="Keyword">data</a> <a id="Coℕ"></a><a id="539" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="543" class="Symbol">:</a> <a id="545" class="PrimitiveType">Set</a> <a id="549" class="Keyword">where</a>
  <a id="Coℕ.zero"></a><a id="557" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a> <a id="562" class="Symbol">:</a> <a id="564" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>
  <a id="Coℕ.suc"></a><a id="570" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a>  <a id="575" class="Symbol">:</a> <a id="577" class="Symbol">(</a><a id="578" href="Codata.Musical.Conat.html#578" class="Bound">n</a> <a id="580" class="Symbol">:</a> <a id="582" href="Agda.Builtin.Coinduction.html#105" class="Record">∞</a> <a id="584" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a><a id="587" class="Symbol">)</a> <a id="589" class="Symbol">→</a> <a id="591" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>

<a id="596" class="Keyword">module</a> <a id="Coℕ-injective"></a><a id="603" href="Codata.Musical.Conat.html#603" class="Module">Coℕ-injective</a> <a id="617" class="Keyword">where</a>

 <a id="Coℕ-injective.suc-injective"></a><a id="625" href="Codata.Musical.Conat.html#625" class="Function">suc-injective</a> <a id="639" class="Symbol">:</a> <a id="641" class="Symbol">∀</a> <a id="643" class="Symbol">{</a><a id="644" href="Codata.Musical.Conat.html#644" class="Bound">m</a> <a id="646" href="Codata.Musical.Conat.html#646" class="Bound">n</a><a id="647" class="Symbol">}</a> <a id="649" class="Symbol">→</a> <a id="651" class="Symbol">(</a><a id="652" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="656" href="Function.html#3061" class="Function Operator">∋</a> <a id="658" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="662" href="Codata.Musical.Conat.html#644" class="Bound">m</a><a id="663" class="Symbol">)</a> <a id="665" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="667" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="671" href="Codata.Musical.Conat.html#646" class="Bound">n</a> <a id="673" class="Symbol">→</a> <a id="675" href="Codata.Musical.Conat.html#644" class="Bound">m</a> <a id="677" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="679" href="Codata.Musical.Conat.html#646" class="Bound">n</a>
 <a id="682" href="Codata.Musical.Conat.html#625" class="Function">suc-injective</a> <a id="696" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a> <a id="703" class="Symbol">=</a> <a id="705" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a>

<a id="713" class="Comment">------------------------------------------------------------------------</a>
<a id="786" class="Comment">-- Some operations</a>

<a id="pred"></a><a id="806" href="Codata.Musical.Conat.html#806" class="Function">pred</a> <a id="811" class="Symbol">:</a> <a id="813" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="817" class="Symbol">→</a> <a id="819" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>
<a id="823" href="Codata.Musical.Conat.html#806" class="Function">pred</a> <a id="828" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a>    <a id="836" class="Symbol">=</a> <a id="838" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a>
<a id="843" href="Codata.Musical.Conat.html#806" class="Function">pred</a> <a id="848" class="Symbol">(</a><a id="849" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="853" href="Codata.Musical.Conat.html#853" class="Bound">n</a><a id="854" class="Symbol">)</a> <a id="856" class="Symbol">=</a> <a id="858" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="860" href="Codata.Musical.Conat.html#853" class="Bound">n</a>

<a id="fromℕ"></a><a id="863" href="Codata.Musical.Conat.html#863" class="Function">fromℕ</a> <a id="869" class="Symbol">:</a> <a id="871" href="Agda.Builtin.Nat.html#97" class="Datatype">ℕ</a> <a id="873" class="Symbol">→</a> <a id="875" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>
<a id="879" href="Codata.Musical.Conat.html#863" class="Function">fromℕ</a> <a id="885" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a>    <a id="893" class="Symbol">=</a> <a id="895" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a>
<a id="900" href="Codata.Musical.Conat.html#863" class="Function">fromℕ</a> <a id="906" class="Symbol">(</a><a id="907" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="911" href="Codata.Musical.Conat.html#911" class="Bound">n</a><a id="912" class="Symbol">)</a> <a id="914" class="Symbol">=</a> <a id="916" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="920" class="Symbol">(</a><a id="921" href="Agda.Builtin.Coinduction.html#133" class="CoinductiveConstructor Operator">♯</a> <a id="923" href="Codata.Musical.Conat.html#863" class="Function">fromℕ</a> <a id="929" href="Codata.Musical.Conat.html#911" class="Bound">n</a><a id="930" class="Symbol">)</a>

<a id="fromℕ-injective"></a><a id="933" href="Codata.Musical.Conat.html#933" class="Function">fromℕ-injective</a> <a id="949" class="Symbol">:</a> <a id="951" class="Symbol">∀</a> <a id="953" class="Symbol">{</a><a id="954" href="Codata.Musical.Conat.html#954" class="Bound">m</a> <a id="956" href="Codata.Musical.Conat.html#956" class="Bound">n</a><a id="957" class="Symbol">}</a> <a id="959" class="Symbol">→</a> <a id="961" href="Codata.Musical.Conat.html#863" class="Function">fromℕ</a> <a id="967" href="Codata.Musical.Conat.html#954" class="Bound">m</a> <a id="969" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="971" href="Codata.Musical.Conat.html#863" class="Function">fromℕ</a> <a id="977" href="Codata.Musical.Conat.html#956" class="Bound">n</a> <a id="979" class="Symbol">→</a> <a id="981" href="Codata.Musical.Conat.html#954" class="Bound">m</a> <a id="983" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="985" href="Codata.Musical.Conat.html#956" class="Bound">n</a>
<a id="987" href="Codata.Musical.Conat.html#933" class="Function">fromℕ-injective</a> <a id="1003" class="Symbol">{</a><a id="1004" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="1008" class="Symbol">}</a>  <a id="1011" class="Symbol">{</a><a id="1012" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="1016" class="Symbol">}</a>  <a id="1019" href="Codata.Musical.Conat.html#1019" class="Bound">eq</a> <a id="1022" class="Symbol">=</a> <a id="1024" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a>
<a id="1031" href="Codata.Musical.Conat.html#933" class="Function">fromℕ-injective</a> <a id="1047" class="Symbol">{</a><a id="1048" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="1052" class="Symbol">}</a>  <a id="1055" class="Symbol">{</a><a id="1056" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1060" href="Codata.Musical.Conat.html#1060" class="Bound">n</a><a id="1061" class="Symbol">}</a> <a id="1063" class="Symbol">()</a>
<a id="1066" href="Codata.Musical.Conat.html#933" class="Function">fromℕ-injective</a> <a id="1082" class="Symbol">{</a><a id="1083" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1087" href="Codata.Musical.Conat.html#1087" class="Bound">m</a><a id="1088" class="Symbol">}</a> <a id="1090" class="Symbol">{</a><a id="1091" href="Agda.Builtin.Nat.html#115" class="InductiveConstructor">zero</a><a id="1095" class="Symbol">}</a>  <a id="1098" class="Symbol">()</a>
<a id="1101" href="Codata.Musical.Conat.html#933" class="Function">fromℕ-injective</a> <a id="1117" class="Symbol">{</a><a id="1118" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1122" href="Codata.Musical.Conat.html#1122" class="Bound">m</a><a id="1123" class="Symbol">}</a> <a id="1125" class="Symbol">{</a><a id="1126" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1130" href="Codata.Musical.Conat.html#1130" class="Bound">n</a><a id="1131" class="Symbol">}</a> <a id="1133" href="Codata.Musical.Conat.html#1133" class="Bound">eq</a> <a id="1136" class="Symbol">=</a> <a id="1138" href="Relation.Binary.PropositionalEquality.html#1170" class="Function">P.cong</a> <a id="1145" href="Agda.Builtin.Nat.html#128" class="InductiveConstructor">suc</a> <a id="1149" class="Symbol">(</a><a id="1150" href="Codata.Musical.Conat.html#933" class="Function">fromℕ-injective</a> <a id="1166" class="Symbol">(</a><a id="1167" href="Relation.Binary.PropositionalEquality.html#1170" class="Function">P.cong</a> <a id="1174" href="Codata.Musical.Conat.html#806" class="Function">pred</a> <a id="1179" href="Codata.Musical.Conat.html#1133" class="Bound">eq</a><a id="1181" class="Symbol">))</a>

<a id="∞ℕ"></a><a id="1185" href="Codata.Musical.Conat.html#1185" class="Function">∞ℕ</a> <a id="1188" class="Symbol">:</a> <a id="1190" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>
<a id="1194" href="Codata.Musical.Conat.html#1185" class="Function">∞ℕ</a> <a id="1197" class="Symbol">=</a> <a id="1199" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1203" class="Symbol">(</a><a id="1204" href="Agda.Builtin.Coinduction.html#133" class="CoinductiveConstructor Operator">♯</a> <a id="1206" href="Codata.Musical.Conat.html#1185" class="Function">∞ℕ</a><a id="1208" class="Symbol">)</a>

<a id="1211" class="Keyword">infixl</a> <a id="1218" class="Number">6</a> <a id="1220" href="Codata.Musical.Conat.html#1225" class="Function Operator">_+_</a>

<a id="_+_"></a><a id="1225" href="Codata.Musical.Conat.html#1225" class="Function Operator">_+_</a> <a id="1229" class="Symbol">:</a> <a id="1231" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="1235" class="Symbol">→</a> <a id="1237" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="1241" class="Symbol">→</a> <a id="1243" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>
<a id="1247" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a>  <a id="1253" href="Codata.Musical.Conat.html#1225" class="Function Operator">+</a> <a id="1255" href="Codata.Musical.Conat.html#1255" class="Bound">n</a> <a id="1257" class="Symbol">=</a> <a id="1259" href="Codata.Musical.Conat.html#1255" class="Bound">n</a>
<a id="1261" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1265" href="Codata.Musical.Conat.html#1265" class="Bound">m</a> <a id="1267" href="Codata.Musical.Conat.html#1225" class="Function Operator">+</a> <a id="1269" href="Codata.Musical.Conat.html#1269" class="Bound">n</a> <a id="1271" class="Symbol">=</a> <a id="1273" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1277" class="Symbol">(</a><a id="1278" href="Agda.Builtin.Coinduction.html#133" class="CoinductiveConstructor Operator">♯</a> <a id="1280" class="Symbol">(</a><a id="1281" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="1283" href="Codata.Musical.Conat.html#1265" class="Bound">m</a> <a id="1285" href="Codata.Musical.Conat.html#1225" class="Function Operator">+</a> <a id="1287" href="Codata.Musical.Conat.html#1269" class="Bound">n</a><a id="1288" class="Symbol">))</a>

<a id="1292" class="Comment">------------------------------------------------------------------------</a>
<a id="1365" class="Comment">-- Equality</a>

<a id="1378" class="Keyword">data</a> <a id="_≈_"></a><a id="1383" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">_≈_</a> <a id="1387" class="Symbol">:</a> <a id="1389" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="1393" class="Symbol">→</a> <a id="1395" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a> <a id="1399" class="Symbol">→</a> <a id="1401" class="PrimitiveType">Set</a> <a id="1405" class="Keyword">where</a>
  <a id="_≈_.zero"></a><a id="1413" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a> <a id="1418" class="Symbol">:</a>                                 <a id="1452" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a>  <a id="1458" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">≈</a> <a id="1460" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a>
  <a id="_≈_.suc"></a><a id="1467" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a>  <a id="1472" class="Symbol">:</a> <a id="1474" class="Symbol">∀</a> <a id="1476" class="Symbol">{</a><a id="1477" href="Codata.Musical.Conat.html#1477" class="Bound">m</a> <a id="1479" href="Codata.Musical.Conat.html#1479" class="Bound">n</a><a id="1480" class="Symbol">}</a> <a id="1482" class="Symbol">(</a><a id="1483" href="Codata.Musical.Conat.html#1483" class="Bound">m≈n</a> <a id="1487" class="Symbol">:</a> <a id="1489" href="Agda.Builtin.Coinduction.html#105" class="Record">∞</a> <a id="1491" class="Symbol">(</a><a id="1492" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="1494" href="Codata.Musical.Conat.html#1477" class="Bound">m</a> <a id="1496" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">≈</a> <a id="1498" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="1500" href="Codata.Musical.Conat.html#1479" class="Bound">n</a><a id="1501" class="Symbol">))</a> <a id="1504" class="Symbol">→</a> <a id="1506" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1510" href="Codata.Musical.Conat.html#1477" class="Bound">m</a> <a id="1512" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">≈</a> <a id="1514" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1518" href="Codata.Musical.Conat.html#1479" class="Bound">n</a>

<a id="1521" class="Keyword">module</a> <a id="≈-injective"></a><a id="1528" href="Codata.Musical.Conat.html#1528" class="Module">≈-injective</a> <a id="1540" class="Keyword">where</a>

 <a id="≈-injective.suc-injective"></a><a id="1548" href="Codata.Musical.Conat.html#1548" class="Function">suc-injective</a> <a id="1562" class="Symbol">:</a> <a id="1564" class="Symbol">∀</a> <a id="1566" class="Symbol">{</a><a id="1567" href="Codata.Musical.Conat.html#1567" class="Bound">m</a> <a id="1569" href="Codata.Musical.Conat.html#1569" class="Bound">n</a> <a id="1571" href="Codata.Musical.Conat.html#1571" class="Bound">p</a> <a id="1573" href="Codata.Musical.Conat.html#1573" class="Bound">q</a><a id="1574" class="Symbol">}</a> <a id="1576" class="Symbol">→</a> <a id="1578" class="Symbol">(</a><a id="1579" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1583" href="Codata.Musical.Conat.html#1567" class="Bound">m</a> <a id="1585" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">≈</a> <a id="1587" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1591" href="Codata.Musical.Conat.html#1569" class="Bound">n</a> <a id="1593" href="Function.html#3061" class="Function Operator">∋</a> <a id="1595" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="1599" href="Codata.Musical.Conat.html#1571" class="Bound">p</a><a id="1600" class="Symbol">)</a> <a id="1602" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1604" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="1608" href="Codata.Musical.Conat.html#1573" class="Bound">q</a> <a id="1610" class="Symbol">→</a> <a id="1612" href="Codata.Musical.Conat.html#1571" class="Bound">p</a> <a id="1614" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1616" href="Codata.Musical.Conat.html#1573" class="Bound">q</a>
 <a id="1619" href="Codata.Musical.Conat.html#1548" class="Function">suc-injective</a> <a id="1633" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a> <a id="1640" class="Symbol">=</a> <a id="1642" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">P.refl</a>

<a id="setoid"></a><a id="1650" href="Codata.Musical.Conat.html#1650" class="Function">setoid</a> <a id="1657" class="Symbol">:</a> <a id="1659" href="Relation.Binary.html#1894" class="Record">Setoid</a> <a id="1666" class="Symbol">_</a> <a id="1668" class="Symbol">_</a>
<a id="1670" href="Codata.Musical.Conat.html#1650" class="Function">setoid</a> <a id="1677" class="Symbol">=</a> <a id="1679" class="Keyword">record</a>
  <a id="1688" class="Symbol">{</a> <a id="1690" href="Relation.Binary.html#1957" class="Field">Carrier</a>       <a id="1704" class="Symbol">=</a> <a id="1706" href="Codata.Musical.Conat.html#539" class="Datatype">Coℕ</a>
  <a id="1712" class="Symbol">;</a> <a id="1714" href="Relation.Binary.html#1983" class="Field Operator">_≈_</a>           <a id="1728" class="Symbol">=</a> <a id="1730" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">_≈_</a>
  <a id="1736" class="Symbol">;</a> <a id="1738" href="Relation.Binary.html#2017" class="Field">isEquivalence</a> <a id="1752" class="Symbol">=</a> <a id="1754" class="Keyword">record</a>
    <a id="1765" class="Symbol">{</a> <a id="1767" href="Relation.Binary.Core.html#5287" class="Field">refl</a>  <a id="1773" class="Symbol">=</a> <a id="1775" href="Codata.Musical.Conat.html#1838" class="Function">refl</a>
    <a id="1784" class="Symbol">;</a> <a id="1786" href="Relation.Binary.Core.html#5313" class="Field">sym</a>   <a id="1792" class="Symbol">=</a> <a id="1794" href="Codata.Musical.Conat.html#1914" class="Function">sym</a>
    <a id="1802" class="Symbol">;</a> <a id="1804" href="Relation.Binary.Core.html#5339" class="Field">trans</a> <a id="1810" class="Symbol">=</a> <a id="1812" href="Codata.Musical.Conat.html#1998" class="Function">trans</a>
    <a id="1822" class="Symbol">}</a>
  <a id="1826" class="Symbol">}</a>
  <a id="1830" class="Keyword">where</a>
  <a id="1838" href="Codata.Musical.Conat.html#1838" class="Function">refl</a> <a id="1843" class="Symbol">:</a> <a id="1845" href="Relation.Binary.Core.html#2025" class="Function">Reflexive</a> <a id="1855" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">_≈_</a>
  <a id="1861" href="Codata.Musical.Conat.html#1838" class="Function">refl</a> <a id="1866" class="Symbol">{</a><a id="1867" href="Codata.Musical.Conat.html#557" class="InductiveConstructor">zero</a><a id="1871" class="Symbol">}</a>  <a id="1874" class="Symbol">=</a> <a id="1876" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a>
  <a id="1883" href="Codata.Musical.Conat.html#1838" class="Function">refl</a> <a id="1888" class="Symbol">{</a><a id="1889" href="Codata.Musical.Conat.html#570" class="InductiveConstructor">suc</a> <a id="1893" href="Codata.Musical.Conat.html#1893" class="Bound">n</a><a id="1894" class="Symbol">}</a> <a id="1896" class="Symbol">=</a> <a id="1898" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="1902" class="Symbol">(</a><a id="1903" href="Agda.Builtin.Coinduction.html#133" class="CoinductiveConstructor Operator">♯</a> <a id="1905" href="Codata.Musical.Conat.html#1838" class="Function">refl</a><a id="1909" class="Symbol">)</a>

  <a id="1914" href="Codata.Musical.Conat.html#1914" class="Function">sym</a> <a id="1918" class="Symbol">:</a> <a id="1920" href="Relation.Binary.Core.html#2448" class="Function">Symmetric</a> <a id="1930" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">_≈_</a>
  <a id="1936" href="Codata.Musical.Conat.html#1914" class="Function">sym</a> <a id="1940" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a>      <a id="1950" class="Symbol">=</a> <a id="1952" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a>
  <a id="1959" href="Codata.Musical.Conat.html#1914" class="Function">sym</a> <a id="1963" class="Symbol">(</a><a id="1964" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="1968" href="Codata.Musical.Conat.html#1968" class="Bound">m≈n</a><a id="1971" class="Symbol">)</a> <a id="1973" class="Symbol">=</a> <a id="1975" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="1979" class="Symbol">(</a><a id="1980" href="Agda.Builtin.Coinduction.html#133" class="CoinductiveConstructor Operator">♯</a> <a id="1982" href="Codata.Musical.Conat.html#1914" class="Function">sym</a> <a id="1986" class="Symbol">(</a><a id="1987" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="1989" href="Codata.Musical.Conat.html#1968" class="Bound">m≈n</a><a id="1992" class="Symbol">))</a>

  <a id="1998" href="Codata.Musical.Conat.html#1998" class="Function">trans</a> <a id="2004" class="Symbol">:</a> <a id="2006" href="Relation.Binary.Core.html#2927" class="Function">Transitive</a> <a id="2017" href="Codata.Musical.Conat.html#1383" class="Datatype Operator">_≈_</a>
  <a id="2023" href="Codata.Musical.Conat.html#1998" class="Function">trans</a> <a id="2029" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a>      <a id="2039" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a>      <a id="2049" class="Symbol">=</a> <a id="2051" href="Codata.Musical.Conat.html#1413" class="InductiveConstructor">zero</a>
  <a id="2058" href="Codata.Musical.Conat.html#1998" class="Function">trans</a> <a id="2064" class="Symbol">(</a><a id="2065" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="2069" href="Codata.Musical.Conat.html#2069" class="Bound">m≈n</a><a id="2072" class="Symbol">)</a> <a id="2074" class="Symbol">(</a><a id="2075" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="2079" href="Codata.Musical.Conat.html#2079" class="Bound">n≈k</a><a id="2082" class="Symbol">)</a> <a id="2084" class="Symbol">=</a> <a id="2086" href="Codata.Musical.Conat.html#1467" class="InductiveConstructor">suc</a> <a id="2090" class="Symbol">(</a><a id="2091" href="Agda.Builtin.Coinduction.html#133" class="CoinductiveConstructor Operator">♯</a> <a id="2093" href="Codata.Musical.Conat.html#1998" class="Function">trans</a> <a id="2099" class="Symbol">(</a><a id="2100" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="2102" href="Codata.Musical.Conat.html#2069" class="Bound">m≈n</a><a id="2105" class="Symbol">)</a> <a id="2107" class="Symbol">(</a><a id="2108" href="Agda.Builtin.Coinduction.html#161" class="Field">♭</a> <a id="2110" href="Codata.Musical.Conat.html#2079" class="Bound">n≈k</a><a id="2113" class="Symbol">))</a>
</pre></body></html>