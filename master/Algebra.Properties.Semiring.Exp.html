<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Properties.Semiring.Exp</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Exponentiation defined over a semiring as repeated multiplication</a>
<a id="175" class="Comment">------------------------------------------------------------------------</a>

<a id="249" class="Symbol">{-#</a> <a id="253" class="Keyword">OPTIONS</a> <a id="261" class="Pragma">--cubical-compatible</a> <a id="282" class="Pragma">--safe</a> <a id="289" class="Symbol">#-}</a>

<a id="294" class="Keyword">open</a> <a id="299" class="Keyword">import</a> <a id="306" href="Algebra.Bundles.html" class="Module">Algebra.Bundles</a> <a id="322" class="Keyword">using</a> <a id="328" class="Symbol">(</a><a id="329" href="Algebra.Bundles.html#18455" class="Record">Semiring</a><a id="337" class="Symbol">)</a>

<a id="340" class="Keyword">module</a> <a id="347" href="Algebra.Properties.Semiring.Exp.html" class="Module">Algebra.Properties.Semiring.Exp</a>
  <a id="381" class="Symbol">{</a><a id="382" href="Algebra.Properties.Semiring.Exp.html#382" class="Bound">a</a> <a id="384" href="Algebra.Properties.Semiring.Exp.html#384" class="Bound">ℓ</a><a id="385" class="Symbol">}</a> <a id="387" class="Symbol">(</a><a id="388" href="Algebra.Properties.Semiring.Exp.html#388" class="Bound">S</a> <a id="390" class="Symbol">:</a> <a id="392" href="Algebra.Bundles.html#18455" class="Record">Semiring</a> <a id="401" href="Algebra.Properties.Semiring.Exp.html#382" class="Bound">a</a> <a id="403" href="Algebra.Properties.Semiring.Exp.html#384" class="Bound">ℓ</a><a id="404" class="Symbol">)</a> <a id="406" class="Keyword">where</a>

<a id="413" class="Keyword">open</a> <a id="418" class="Keyword">import</a> <a id="425" href="Data.Nat.Base.html" class="Module">Data.Nat.Base</a> <a id="439" class="Symbol">as</a> <a id="442" class="Module">ℕ</a> <a id="444" class="Keyword">using</a> <a id="450" class="Symbol">(</a><a id="451" href="Agda.Builtin.Nat.html#203" class="Datatype">ℕ</a><a id="452" class="Symbol">;</a> <a id="454" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a><a id="458" class="Symbol">;</a> <a id="460" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a><a id="463" class="Symbol">)</a>
<a id="465" class="Keyword">open</a> <a id="470" class="Keyword">import</a> <a id="477" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="498" class="Keyword">using</a> <a id="504" class="Symbol">(</a><a id="505" href="Relation.Binary.Core.html#1577" class="Function Operator">_Preserves_⟶_</a><a id="518" class="Symbol">;</a> <a id="520" href="Relation.Binary.Core.html#1703" class="Function Operator">_Preserves₂_⟶_⟶_</a><a id="536" class="Symbol">)</a>
<a id="538" class="Keyword">open</a> <a id="543" class="Keyword">import</a> <a id="550" href="Relation.Binary.PropositionalEquality.Core.html" class="Module">Relation.Binary.PropositionalEquality.Core</a> <a id="593" class="Symbol">as</a> <a id="596" class="Module">≡</a> <a id="598" class="Keyword">using</a> <a id="604" class="Symbol">(</a><a id="605" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a><a id="608" class="Symbol">)</a>
<a id="610" class="Keyword">import</a> <a id="617" href="Data.Nat.Properties.html" class="Module">Data.Nat.Properties</a> <a id="637" class="Symbol">as</a> <a id="640" class="Module">ℕ</a> <a id="642" class="Keyword">using</a> <a id="648" class="Symbol">(</a><a id="649" href="Data.Nat.Properties.html#22333" class="Function">*-comm</a><a id="655" class="Symbol">)</a>

<a id="658" class="Comment">-- Need to see almost everything, including setoid</a>
<a id="709" class="Keyword">open</a> <a id="714" href="Algebra.Bundles.html#18455" class="Module">Semiring</a> <a id="723" href="Algebra.Properties.Semiring.Exp.html#388" class="Bound">S</a>

<a id="726" class="Keyword">open</a> <a id="731" class="Keyword">import</a> <a id="738" href="Relation.Binary.Reasoning.Setoid.html" class="Module">Relation.Binary.Reasoning.Setoid</a> <a id="771" href="Algebra.Structures.html#1873" class="Function">setoid</a>
<a id="778" class="Keyword">import</a> <a id="785" href="Algebra.Properties.Monoid.Mult.html" class="Module">Algebra.Properties.Monoid.Mult</a> <a id="816" href="Algebra.Bundles.html#18200" class="Function">*-monoid</a> <a id="825" class="Symbol">as</a> <a id="Mult"></a><a id="828" href="Algebra.Properties.Semiring.Exp.html#828" class="Module">Mult</a>

<a id="834" class="Comment">------------------------------------------------------------------------</a>
<a id="907" class="Comment">-- Definition</a>

<a id="922" class="Keyword">open</a> <a id="927" class="Keyword">import</a> <a id="934" href="Algebra.Definitions.RawSemiring.html" class="Module">Algebra.Definitions.RawSemiring</a> <a id="966" href="Algebra.Bundles.html#17431" class="Function">rawSemiring</a> <a id="978" class="Keyword">public</a>
  <a id="987" class="Keyword">using</a> <a id="993" class="Symbol">(</a><a id="994" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">_^_</a><a id="997" class="Symbol">)</a>

<a id="1000" class="Comment">------------------------------------------------------------------------</a>
<a id="1073" class="Comment">-- Properties</a>

<a id="^-congˡ"></a><a id="1088" href="Algebra.Properties.Semiring.Exp.html#1088" class="Function">^-congˡ</a> <a id="1096" class="Symbol">:</a> <a id="1098" class="Symbol">∀</a> <a id="1100" href="Algebra.Properties.Semiring.Exp.html#1100" class="Bound">n</a> <a id="1102" class="Symbol">→</a> <a id="1104" class="Symbol">(</a><a id="1105" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">_^</a> <a id="1108" href="Algebra.Properties.Semiring.Exp.html#1100" class="Bound">n</a><a id="1109" class="Symbol">)</a> <a id="1111" href="Relation.Binary.Core.html#1577" class="Function Operator">Preserves</a> <a id="1121" href="Algebra.Bundles.html#18574" class="Field Operator">_≈_</a> <a id="1125" href="Relation.Binary.Core.html#1577" class="Function Operator">⟶</a> <a id="1127" href="Algebra.Bundles.html#18574" class="Field Operator">_≈_</a>
<a id="1131" href="Algebra.Properties.Semiring.Exp.html#1088" class="Function">^-congˡ</a> <a id="1139" class="Symbol">=</a> <a id="1141" href="Algebra.Properties.Monoid.Mult.html#1218" class="Function">Mult.×-congʳ</a>

<a id="^-cong"></a><a id="1155" href="Algebra.Properties.Semiring.Exp.html#1155" class="Function">^-cong</a> <a id="1162" class="Symbol">:</a> <a id="1164" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">_^_</a> <a id="1168" href="Relation.Binary.Core.html#1703" class="Function Operator">Preserves₂</a> <a id="1179" href="Algebra.Bundles.html#18574" class="Field Operator">_≈_</a> <a id="1183" href="Relation.Binary.Core.html#1703" class="Function Operator">⟶</a> <a id="1185" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a> <a id="1189" href="Relation.Binary.Core.html#1703" class="Function Operator">⟶</a> <a id="1191" href="Algebra.Bundles.html#18574" class="Field Operator">_≈_</a>
<a id="1195" href="Algebra.Properties.Semiring.Exp.html#1155" class="Function">^-cong</a> <a id="1202" href="Algebra.Properties.Semiring.Exp.html#1202" class="Bound">x≈y</a> <a id="1206" href="Algebra.Properties.Semiring.Exp.html#1206" class="Bound">u≡v</a> <a id="1210" class="Symbol">=</a> <a id="1212" href="Algebra.Properties.Monoid.Mult.html#1342" class="Function">Mult.×-cong</a> <a id="1224" href="Algebra.Properties.Semiring.Exp.html#1206" class="Bound">u≡v</a> <a id="1228" href="Algebra.Properties.Semiring.Exp.html#1202" class="Bound">x≈y</a>

<a id="^-congʳ"></a><a id="1233" href="Algebra.Properties.Semiring.Exp.html#1233" class="Function">^-congʳ</a> <a id="1241" class="Symbol">:</a> <a id="1243" class="Symbol">∀</a> <a id="1245" href="Algebra.Properties.Semiring.Exp.html#1245" class="Bound">x</a> <a id="1247" class="Symbol">→</a> <a id="1249" class="Symbol">(</a><a id="1250" href="Algebra.Properties.Semiring.Exp.html#1245" class="Bound">x</a> <a id="1252" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^_</a><a id="1254" class="Symbol">)</a> <a id="1256" href="Relation.Binary.Core.html#1577" class="Function Operator">Preserves</a> <a id="1266" href="Agda.Builtin.Equality.html#150" class="Datatype Operator">_≡_</a> <a id="1270" href="Relation.Binary.Core.html#1577" class="Function Operator">⟶</a> <a id="1272" href="Algebra.Bundles.html#18574" class="Field Operator">_≈_</a>
<a id="1276" href="Algebra.Properties.Semiring.Exp.html#1233" class="Function">^-congʳ</a> <a id="1284" href="Algebra.Properties.Semiring.Exp.html#1284" class="Bound">x</a> <a id="1286" class="Symbol">=</a> <a id="1288" href="Algebra.Properties.Monoid.Mult.html#1423" class="Function">Mult.×-congˡ</a>

<a id="1302" class="Comment">-- xᵐ⁺ⁿ ≈ xᵐxⁿ</a>
<a id="^-homo-*"></a><a id="1317" href="Algebra.Properties.Semiring.Exp.html#1317" class="Function">^-homo-*</a> <a id="1326" class="Symbol">:</a> <a id="1328" class="Symbol">∀</a> <a id="1330" href="Algebra.Properties.Semiring.Exp.html#1330" class="Bound">x</a> <a id="1332" href="Algebra.Properties.Semiring.Exp.html#1332" class="Bound">m</a> <a id="1334" href="Algebra.Properties.Semiring.Exp.html#1334" class="Bound">n</a> <a id="1336" class="Symbol">→</a> <a id="1338" href="Algebra.Properties.Semiring.Exp.html#1330" class="Bound">x</a> <a id="1340" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1342" class="Symbol">(</a><a id="1343" href="Algebra.Properties.Semiring.Exp.html#1332" class="Bound">m</a> <a id="1345" href="Agda.Builtin.Nat.html#336" class="Primitive Operator">ℕ.+</a> <a id="1349" href="Algebra.Properties.Semiring.Exp.html#1334" class="Bound">n</a><a id="1350" class="Symbol">)</a> <a id="1352" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1354" class="Symbol">(</a><a id="1355" href="Algebra.Properties.Semiring.Exp.html#1330" class="Bound">x</a> <a id="1357" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1359" href="Algebra.Properties.Semiring.Exp.html#1332" class="Bound">m</a><a id="1360" class="Symbol">)</a> <a id="1362" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1364" class="Symbol">(</a><a id="1365" href="Algebra.Properties.Semiring.Exp.html#1330" class="Bound">x</a> <a id="1367" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1369" href="Algebra.Properties.Semiring.Exp.html#1334" class="Bound">n</a><a id="1370" class="Symbol">)</a>
<a id="1372" href="Algebra.Properties.Semiring.Exp.html#1317" class="Function">^-homo-*</a> <a id="1381" class="Symbol">=</a> <a id="1383" href="Algebra.Properties.Monoid.Mult.html#1646" class="Function">Mult.×-homo-+</a>

<a id="1398" class="Comment">-- (xᵐ)ⁿ≈xᵐ*ⁿ</a>
<a id="^-assocʳ"></a><a id="1412" href="Algebra.Properties.Semiring.Exp.html#1412" class="Function">^-assocʳ</a> <a id="1421" class="Symbol">:</a> <a id="1423" class="Symbol">∀</a> <a id="1425" href="Algebra.Properties.Semiring.Exp.html#1425" class="Bound">x</a> <a id="1427" href="Algebra.Properties.Semiring.Exp.html#1427" class="Bound">m</a> <a id="1429" href="Algebra.Properties.Semiring.Exp.html#1429" class="Bound">n</a> <a id="1431" class="Symbol">→</a> <a id="1433" class="Symbol">(</a><a id="1434" href="Algebra.Properties.Semiring.Exp.html#1425" class="Bound">x</a> <a id="1436" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1438" href="Algebra.Properties.Semiring.Exp.html#1427" class="Bound">m</a><a id="1439" class="Symbol">)</a> <a id="1441" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1443" href="Algebra.Properties.Semiring.Exp.html#1429" class="Bound">n</a> <a id="1445" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1447" href="Algebra.Properties.Semiring.Exp.html#1425" class="Bound">x</a> <a id="1449" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1451" class="Symbol">(</a><a id="1452" href="Algebra.Properties.Semiring.Exp.html#1427" class="Bound">m</a> <a id="1454" href="Agda.Builtin.Nat.html#539" class="Primitive Operator">ℕ.*</a> <a id="1458" href="Algebra.Properties.Semiring.Exp.html#1429" class="Bound">n</a><a id="1459" class="Symbol">)</a>
<a id="1461" href="Algebra.Properties.Semiring.Exp.html#1412" class="Function">^-assocʳ</a> <a id="1470" href="Algebra.Properties.Semiring.Exp.html#1470" class="Bound">x</a> <a id="1472" href="Algebra.Properties.Semiring.Exp.html#1472" class="Bound">m</a> <a id="1474" href="Algebra.Properties.Semiring.Exp.html#1474" class="Bound">n</a> <a id="1476" class="Keyword">rewrite</a> <a id="1484" href="Data.Nat.Properties.html#22333" class="Function">ℕ.*-comm</a> <a id="1493" href="Algebra.Properties.Semiring.Exp.html#1472" class="Bound">m</a> <a id="1495" href="Algebra.Properties.Semiring.Exp.html#1474" class="Bound">n</a> <a id="1497" class="Symbol">=</a> <a id="1499" href="Algebra.Properties.Monoid.Mult.html#2173" class="Function">Mult.×-assocˡ</a> <a id="1513" href="Algebra.Properties.Semiring.Exp.html#1470" class="Bound">x</a> <a id="1515" href="Algebra.Properties.Semiring.Exp.html#1474" class="Bound">n</a> <a id="1517" href="Algebra.Properties.Semiring.Exp.html#1472" class="Bound">m</a>

<a id="1520" class="Comment">------------------------------------------------------------------------</a>
<a id="1593" class="Comment">-- A lemma using commutativity, needed for the Binomial Theorem</a>

<a id="y*x^m*y^n≈x^m*y^[n+1]"></a><a id="1658" href="Algebra.Properties.Semiring.Exp.html#1658" class="Function">y*x^m*y^n≈x^m*y^[n+1]</a> <a id="1680" class="Symbol">:</a> <a id="1682" class="Symbol">∀</a> <a id="1684" class="Symbol">{</a><a id="1685" href="Algebra.Properties.Semiring.Exp.html#1685" class="Bound">x</a><a id="1686" class="Symbol">}</a> <a id="1688" class="Symbol">{</a><a id="1689" href="Algebra.Properties.Semiring.Exp.html#1689" class="Bound">y</a><a id="1690" class="Symbol">}</a> <a id="1692" class="Symbol">(</a><a id="1693" href="Algebra.Properties.Semiring.Exp.html#1693" class="Bound">x*y≈y*x</a> <a id="1701" class="Symbol">:</a> <a id="1703" href="Algebra.Properties.Semiring.Exp.html#1685" class="Bound">x</a> <a id="1705" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1707" href="Algebra.Properties.Semiring.Exp.html#1689" class="Bound">y</a> <a id="1709" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1711" href="Algebra.Properties.Semiring.Exp.html#1689" class="Bound">y</a> <a id="1713" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1715" href="Algebra.Properties.Semiring.Exp.html#1685" class="Bound">x</a><a id="1716" class="Symbol">)</a> <a id="1718" class="Symbol">→</a>
                        <a id="1744" class="Symbol">∀</a> <a id="1746" href="Algebra.Properties.Semiring.Exp.html#1746" class="Bound">m</a> <a id="1748" href="Algebra.Properties.Semiring.Exp.html#1748" class="Bound">n</a> <a id="1750" class="Symbol">→</a> <a id="1752" href="Algebra.Properties.Semiring.Exp.html#1689" class="Bound">y</a> <a id="1754" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1756" class="Symbol">(</a><a id="1757" href="Algebra.Properties.Semiring.Exp.html#1685" class="Bound">x</a> <a id="1759" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1761" href="Algebra.Properties.Semiring.Exp.html#1746" class="Bound">m</a> <a id="1763" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1765" href="Algebra.Properties.Semiring.Exp.html#1689" class="Bound">y</a> <a id="1767" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1769" href="Algebra.Properties.Semiring.Exp.html#1748" class="Bound">n</a><a id="1770" class="Symbol">)</a> <a id="1772" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1774" href="Algebra.Properties.Semiring.Exp.html#1685" class="Bound">x</a> <a id="1776" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1778" href="Algebra.Properties.Semiring.Exp.html#1746" class="Bound">m</a> <a id="1780" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1782" href="Algebra.Properties.Semiring.Exp.html#1689" class="Bound">y</a> <a id="1784" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1786" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1790" href="Algebra.Properties.Semiring.Exp.html#1748" class="Bound">n</a>
<a id="1792" href="Algebra.Properties.Semiring.Exp.html#1658" class="Function">y*x^m*y^n≈x^m*y^[n+1]</a> <a id="1814" class="Symbol">{</a><a id="1815" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a><a id="1816" class="Symbol">}</a> <a id="1818" class="Symbol">{</a><a id="1819" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a><a id="1820" class="Symbol">}</a> <a id="1822" href="Algebra.Properties.Semiring.Exp.html#1822" class="Bound">x*y≈y*x</a> <a id="1830" class="Symbol">=</a> <a id="1832" href="Algebra.Properties.Semiring.Exp.html#1851" class="Function">helper</a>
  <a id="1841" class="Keyword">where</a>
    <a id="1851" href="Algebra.Properties.Semiring.Exp.html#1851" class="Function">helper</a> <a id="1858" class="Symbol">:</a> <a id="1860" class="Symbol">∀</a> <a id="1862" href="Algebra.Properties.Semiring.Exp.html#1862" class="Bound">m</a> <a id="1864" href="Algebra.Properties.Semiring.Exp.html#1864" class="Bound">n</a> <a id="1866" class="Symbol">→</a> <a id="1868" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1870" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1872" class="Symbol">(</a><a id="1873" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="1875" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1877" href="Algebra.Properties.Semiring.Exp.html#1862" class="Bound">m</a> <a id="1879" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1881" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1883" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1885" href="Algebra.Properties.Semiring.Exp.html#1864" class="Bound">n</a><a id="1886" class="Symbol">)</a> <a id="1888" href="Algebra.Bundles.html#18574" class="Field Operator">≈</a> <a id="1890" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="1892" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1894" href="Algebra.Properties.Semiring.Exp.html#1862" class="Bound">m</a> <a id="1896" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1898" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1900" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1902" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="1906" href="Algebra.Properties.Semiring.Exp.html#1864" class="Bound">n</a>
    <a id="1912" href="Algebra.Properties.Semiring.Exp.html#1851" class="Function">helper</a> <a id="1919" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">zero</a>    <a id="1927" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a> <a id="1929" class="Symbol">=</a> <a id="1931" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
      <a id="1943" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1945" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1947" class="Symbol">(</a><a id="1948" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="1950" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1952" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">ℕ.zero</a> <a id="1959" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1961" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1963" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1965" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="1966" class="Symbol">)</a>  <a id="1969" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
      <a id="1979" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1981" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1983" class="Symbol">(</a><a id="1984" href="Algebra.Bundles.html#18688" class="Field">1#</a> <a id="1987" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="1989" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="1991" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="1993" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="1994" class="Symbol">)</a>          <a id="2005" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2008" href="Algebra.Structures.html#14625" class="Function">*-congˡ</a> <a id="2016" class="Symbol">(</a><a id="2017" href="Algebra.Structures.html#14683" class="Function">*-identityˡ</a> <a id="2029" class="Symbol">(</a><a id="2030" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2032" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2034" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="2035" class="Symbol">))</a> <a id="2038" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2046" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2048" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2050" class="Symbol">(</a><a id="2051" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2053" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2055" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="2056" class="Symbol">)</a>               <a id="2072" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
      <a id="2082" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2084" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2086" class="Symbol">(</a><a id="2087" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2091" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="2092" class="Symbol">)</a>               <a id="2108" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">≈⟨</a> <a id="2111" href="Algebra.Structures.html#14683" class="Function">*-identityˡ</a> <a id="2123" class="Symbol">(</a><a id="2124" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2126" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2128" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2132" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="2133" class="Symbol">)</a> <a id="2135" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">⟨</a>
      <a id="2143" href="Algebra.Bundles.html#18688" class="Field">1#</a> <a id="2146" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2148" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2150" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2152" class="Symbol">(</a><a id="2153" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2157" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="2158" class="Symbol">)</a>          <a id="2169" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
      <a id="2179" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2181" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2183" href="Agda.Builtin.Nat.html#221" class="InductiveConstructor">ℕ.zero</a> <a id="2190" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2192" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2194" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2196" class="Symbol">(</a><a id="2197" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2201" href="Algebra.Properties.Semiring.Exp.html#1927" class="Bound">n</a><a id="2202" class="Symbol">)</a>  <a id="2205" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
    <a id="2211" href="Algebra.Properties.Semiring.Exp.html#1851" class="Function">helper</a> <a id="2218" class="Symbol">(</a><a id="2219" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2223" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a><a id="2224" class="Symbol">)</a> <a id="2226" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a> <a id="2228" class="Symbol">=</a> <a id="2230" href="Relation.Binary.Reasoning.Syntax.html#1572" class="Function Operator">begin</a>
      <a id="2242" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2244" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2246" class="Symbol">(</a><a id="2247" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2249" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2251" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2255" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2257" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2259" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2261" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2263" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2264" class="Symbol">)</a>    <a id="2269" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
      <a id="2279" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2281" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2283" class="Symbol">((</a><a id="2285" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2287" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2289" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2291" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2293" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a><a id="2294" class="Symbol">)</a> <a id="2296" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2298" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2300" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2302" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2303" class="Symbol">)</a>  <a id="2306" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2309" href="Algebra.Structures.html#14625" class="Function">*-congˡ</a> <a id="2317" class="Symbol">(</a><a id="2318" href="Algebra.Structures.html#13243" class="Function">*-assoc</a> <a id="2326" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2328" class="Symbol">(</a><a id="2329" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2331" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2333" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a><a id="2334" class="Symbol">)</a> <a id="2336" class="Symbol">(</a><a id="2337" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2339" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2341" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2342" class="Symbol">))</a> <a id="2345" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2353" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2355" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2357" class="Symbol">(</a><a id="2358" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2360" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2362" class="Symbol">(</a><a id="2363" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2365" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2367" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2369" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2371" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2373" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2375" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2376" class="Symbol">))</a>  <a id="2380" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">≈⟨</a> <a id="2383" href="Algebra.Structures.html#13243" class="Function">*-assoc</a> <a id="2391" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2393" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2395" class="Symbol">(</a><a id="2396" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2398" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2400" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2402" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2404" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2406" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2408" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2409" class="Symbol">)</a> <a id="2411" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">⟨</a>
      <a id="2419" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2421" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2423" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2425" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2427" class="Symbol">(</a><a id="2428" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2430" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2432" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2434" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2436" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2438" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2440" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2441" class="Symbol">)</a>    <a id="2446" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">≈⟨</a> <a id="2449" href="Algebra.Structures.html#14654" class="Function">*-congʳ</a> <a id="2457" href="Algebra.Properties.Semiring.Exp.html#1822" class="Bound">x*y≈y*x</a> <a id="2465" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">⟨</a>
      <a id="2473" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2475" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2477" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2479" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2481" class="Symbol">(</a><a id="2482" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2484" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2486" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2488" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2490" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2492" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2494" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2495" class="Symbol">)</a>    <a id="2500" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2503" href="Algebra.Structures.html#13243" class="Function">*-assoc</a> <a id="2511" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2513" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2515" class="Symbol">_</a> <a id="2517" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2525" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2527" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2529" class="Symbol">(</a><a id="2530" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2532" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2534" class="Symbol">(</a><a id="2535" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2537" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2539" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2541" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2543" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2545" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2547" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2548" class="Symbol">))</a>  <a id="2552" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">≈⟨</a> <a id="2555" href="Algebra.Structures.html#14625" class="Function">*-congˡ</a> <a id="2563" class="Symbol">(</a><a id="2564" href="Algebra.Properties.Semiring.Exp.html#1851" class="Function">helper</a> <a id="2571" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2573" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2574" class="Symbol">)</a> <a id="2576" href="Relation.Binary.Reasoning.Syntax.html#7111" class="Function">⟩</a>
      <a id="2584" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2586" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2588" class="Symbol">(</a><a id="2589" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2591" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2593" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2595" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2597" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2599" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2601" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2605" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2606" class="Symbol">)</a>    <a id="2611" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">≈⟨</a> <a id="2614" href="Algebra.Structures.html#13243" class="Function">*-assoc</a> <a id="2622" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2624" class="Symbol">(</a><a id="2625" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2627" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2629" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a><a id="2630" class="Symbol">)</a> <a id="2632" class="Symbol">(</a><a id="2633" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2635" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2637" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2641" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a><a id="2642" class="Symbol">)</a> <a id="2644" href="Relation.Binary.Reasoning.Syntax.html#7136" class="Function">⟨</a>
      <a id="2652" class="Symbol">(</a><a id="2653" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2655" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2657" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2659" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2661" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a><a id="2662" class="Symbol">)</a> <a id="2664" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2666" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2668" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2670" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2674" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a>    <a id="2679" href="Relation.Binary.Reasoning.Syntax.html#11079" class="Function">≡⟨⟩</a>
      <a id="2689" href="Algebra.Properties.Semiring.Exp.html#1815" class="Bound">x</a> <a id="2691" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2693" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2697" href="Algebra.Properties.Semiring.Exp.html#2223" class="Bound">m</a> <a id="2699" href="Algebra.Bundles.html#18634" class="Field Operator">*</a> <a id="2701" href="Algebra.Properties.Semiring.Exp.html#1819" class="Bound">y</a> <a id="2703" href="Algebra.Definitions.RawSemiring.html#1256" class="Function Operator">^</a> <a id="2705" href="Agda.Builtin.Nat.html#234" class="InductiveConstructor">suc</a> <a id="2709" href="Algebra.Properties.Semiring.Exp.html#2226" class="Bound">n</a>      <a id="2716" href="Relation.Binary.Reasoning.Syntax.html#12345" class="Function Operator">∎</a>
</pre></body></html>