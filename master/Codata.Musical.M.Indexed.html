<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Codata.Musical.M.Indexed</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Indexed M-types (the dual of indexed W-types aka Petersson-Synek</a>
<a id="174" class="Comment">-- trees).</a>
<a id="185" class="Comment">------------------------------------------------------------------------</a>

<a id="259" class="Symbol">{-#</a> <a id="263" class="Keyword">OPTIONS</a> <a id="271" class="Pragma">--cubical-compatible</a> <a id="292" class="Pragma">--safe</a> <a id="299" class="Pragma">--guardedness</a> <a id="313" class="Symbol">#-}</a>

<a id="318" class="Keyword">module</a> <a id="325" href="Codata.Musical.M.Indexed.html" class="Module">Codata.Musical.M.Indexed</a> <a id="350" class="Keyword">where</a>

<a id="357" class="Keyword">open</a> <a id="362" class="Keyword">import</a> <a id="369" href="Level.html" class="Module">Level</a>
<a id="375" class="Keyword">open</a> <a id="380" class="Keyword">import</a> <a id="387" href="Codata.Musical.Notation.html" class="Module">Codata.Musical.Notation</a>
<a id="411" class="Keyword">open</a> <a id="416" class="Keyword">import</a> <a id="423" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="441" class="Keyword">using</a> <a id="447" class="Symbol">(</a><a id="448" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="451" class="Symbol">;</a> <a id="453" href="Data.Product.Base.html#636" class="Field">proj₁</a><a id="458" class="Symbol">;</a> <a id="460" href="Data.Product.Base.html#650" class="Field">proj₂</a><a id="465" class="Symbol">)</a>
<a id="467" class="Keyword">open</a> <a id="472" class="Keyword">import</a> <a id="479" href="Data.Container.Indexed.Core.html" class="Module">Data.Container.Indexed.Core</a>
<a id="507" class="Keyword">open</a> <a id="512" class="Keyword">import</a> <a id="519" href="Function.Base.html" class="Module">Function.Base</a> <a id="533" class="Keyword">using</a> <a id="539" class="Symbol">(</a><a id="540" href="Function.Base.html#1115" class="Function Operator">_∘_</a><a id="543" class="Symbol">)</a>
<a id="545" class="Keyword">open</a> <a id="550" class="Keyword">import</a> <a id="557" href="Relation.Unary.html" class="Module">Relation.Unary</a>

<a id="573" class="Comment">-- The family of indexed M-types.</a>

<a id="608" class="Keyword">module</a> <a id="615" href="Codata.Musical.M.Indexed.html#615" class="Module">_</a> <a id="617" class="Symbol">{</a><a id="618" href="Codata.Musical.M.Indexed.html#618" class="Bound">ℓ</a> <a id="620" href="Codata.Musical.M.Indexed.html#620" class="Bound">c</a> <a id="622" href="Codata.Musical.M.Indexed.html#622" class="Bound">r</a><a id="623" class="Symbol">}</a> <a id="625" class="Symbol">{</a><a id="626" href="Codata.Musical.M.Indexed.html#626" class="Bound">O</a> <a id="628" class="Symbol">:</a> <a id="630" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="634" href="Codata.Musical.M.Indexed.html#618" class="Bound">ℓ</a><a id="635" class="Symbol">}</a> <a id="637" class="Symbol">(</a><a id="638" href="Codata.Musical.M.Indexed.html#638" class="Bound">C</a> <a id="640" class="Symbol">:</a> <a id="642" href="Data.Container.Indexed.Core.html#573" class="Record">Container</a> <a id="652" href="Codata.Musical.M.Indexed.html#626" class="Bound">O</a> <a id="654" href="Codata.Musical.M.Indexed.html#626" class="Bound">O</a> <a id="656" href="Codata.Musical.M.Indexed.html#620" class="Bound">c</a> <a id="658" href="Codata.Musical.M.Indexed.html#622" class="Bound">r</a><a id="659" class="Symbol">)</a> <a id="661" class="Keyword">where</a>

  <a id="670" class="Keyword">data</a> <a id="675" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a> <a id="677" class="Symbol">(</a><a id="678" href="Codata.Musical.M.Indexed.html#678" class="Bound">o</a> <a id="680" class="Symbol">:</a> <a id="682" href="Codata.Musical.M.Indexed.html#626" class="Bound">O</a><a id="683" class="Symbol">)</a> <a id="685" class="Symbol">:</a> <a id="687" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="691" class="Symbol">(</a><a id="692" href="Codata.Musical.M.Indexed.html#618" class="Bound">ℓ</a> <a id="694" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="696" href="Codata.Musical.M.Indexed.html#620" class="Bound">c</a> <a id="698" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="700" href="Codata.Musical.M.Indexed.html#622" class="Bound">r</a><a id="701" class="Symbol">)</a> <a id="703" class="Keyword">where</a>
    <a id="713" href="Codata.Musical.M.Indexed.html#713" class="InductiveConstructor">inf</a> <a id="717" class="Symbol">:</a> <a id="719" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟦</a> <a id="721" href="Codata.Musical.M.Indexed.html#638" class="Bound">C</a> <a id="723" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟧</a> <a id="725" class="Symbol">(</a><a id="726" href="Agda.Builtin.Coinduction.html#196" class="Record">∞</a> <a id="728" href="Function.Base.html#1115" class="Function Operator">∘</a> <a id="730" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a><a id="731" class="Symbol">)</a> <a id="733" href="Codata.Musical.M.Indexed.html#678" class="Bound">o</a> <a id="735" class="Symbol">→</a> <a id="737" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a> <a id="739" href="Codata.Musical.M.Indexed.html#678" class="Bound">o</a>

  <a id="744" class="Keyword">open</a> <a id="749" href="Data.Container.Indexed.Core.html#573" class="Module">Container</a> <a id="759" href="Codata.Musical.M.Indexed.html#638" class="Bound">C</a>

  <a id="764" class="Comment">-- Projections.</a>

  <a id="783" href="Codata.Musical.M.Indexed.html#783" class="Function">head</a> <a id="788" class="Symbol">:</a> <a id="790" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a> <a id="792" href="Relation.Unary.html#2097" class="Function Operator">⊆</a> <a id="794" href="Data.Container.Indexed.Core.html#679" class="Field">Command</a>
  <a id="804" href="Codata.Musical.M.Indexed.html#783" class="Function">head</a> <a id="809" class="Symbol">(</a><a id="810" href="Codata.Musical.M.Indexed.html#713" class="InductiveConstructor">inf</a> <a id="814" class="Symbol">(</a><a id="815" href="Codata.Musical.M.Indexed.html#815" class="Bound">c</a> <a id="817" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="819" class="Symbol">_))</a> <a id="823" class="Symbol">=</a> <a id="825" href="Codata.Musical.M.Indexed.html#815" class="Bound">c</a>

  <a id="830" href="Codata.Musical.M.Indexed.html#830" class="Function">tail</a> <a id="835" class="Symbol">:</a> <a id="837" class="Symbol">∀</a> <a id="839" class="Symbol">{</a><a id="840" href="Codata.Musical.M.Indexed.html#840" class="Bound">o</a><a id="841" class="Symbol">}</a> <a id="843" class="Symbol">(</a><a id="844" href="Codata.Musical.M.Indexed.html#844" class="Bound">m</a> <a id="846" class="Symbol">:</a> <a id="848" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a> <a id="850" href="Codata.Musical.M.Indexed.html#840" class="Bound">o</a><a id="851" class="Symbol">)</a> <a id="853" class="Symbol">(</a><a id="854" href="Codata.Musical.M.Indexed.html#854" class="Bound">r</a> <a id="856" class="Symbol">:</a> <a id="858" href="Data.Container.Indexed.Core.html#710" class="Field">Response</a> <a id="867" class="Symbol">(</a><a id="868" href="Codata.Musical.M.Indexed.html#783" class="Function">head</a> <a id="873" href="Codata.Musical.M.Indexed.html#844" class="Bound">m</a><a id="874" class="Symbol">))</a> <a id="877" class="Symbol">→</a> <a id="879" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a> <a id="881" class="Symbol">(</a><a id="882" href="Data.Container.Indexed.Core.html#751" class="Field">next</a> <a id="887" class="Symbol">(</a><a id="888" href="Codata.Musical.M.Indexed.html#783" class="Function">head</a> <a id="893" href="Codata.Musical.M.Indexed.html#844" class="Bound">m</a><a id="894" class="Symbol">)</a> <a id="896" href="Codata.Musical.M.Indexed.html#854" class="Bound">r</a><a id="897" class="Symbol">)</a>
  <a id="901" href="Codata.Musical.M.Indexed.html#830" class="Function">tail</a> <a id="906" class="Symbol">(</a><a id="907" href="Codata.Musical.M.Indexed.html#713" class="InductiveConstructor">inf</a> <a id="911" class="Symbol">(_</a> <a id="914" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="916" href="Codata.Musical.M.Indexed.html#916" class="Bound">k</a><a id="917" class="Symbol">))</a> <a id="920" href="Codata.Musical.M.Indexed.html#920" class="Bound">r</a> <a id="922" class="Symbol">=</a> <a id="924" href="Agda.Builtin.Coinduction.html#264" class="Field">♭</a> <a id="926" class="Symbol">(</a><a id="927" href="Codata.Musical.M.Indexed.html#916" class="Bound">k</a> <a id="929" href="Codata.Musical.M.Indexed.html#920" class="Bound">r</a><a id="930" class="Symbol">)</a>

  <a id="935" href="Codata.Musical.M.Indexed.html#935" class="Function">force</a> <a id="941" class="Symbol">:</a> <a id="943" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a> <a id="945" href="Relation.Unary.html#2097" class="Function Operator">⊆</a> <a id="947" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟦</a> <a id="949" href="Codata.Musical.M.Indexed.html#638" class="Bound">C</a> <a id="951" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟧</a> <a id="953" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a>
  <a id="957" href="Codata.Musical.M.Indexed.html#935" class="Function">force</a> <a id="963" class="Symbol">(</a><a id="964" href="Codata.Musical.M.Indexed.html#713" class="InductiveConstructor">inf</a> <a id="968" class="Symbol">(</a><a id="969" href="Codata.Musical.M.Indexed.html#969" class="Bound">c</a> <a id="971" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="973" href="Codata.Musical.M.Indexed.html#973" class="Bound">k</a><a id="974" class="Symbol">))</a> <a id="977" class="Symbol">=</a> <a id="979" href="Codata.Musical.M.Indexed.html#969" class="Bound">c</a> <a id="981" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="983" class="Symbol">λ</a> <a id="985" href="Codata.Musical.M.Indexed.html#985" class="Bound">r</a> <a id="987" class="Symbol">→</a> <a id="989" href="Agda.Builtin.Coinduction.html#264" class="Field">♭</a> <a id="991" class="Symbol">(</a><a id="992" href="Codata.Musical.M.Indexed.html#973" class="Bound">k</a> <a id="994" href="Codata.Musical.M.Indexed.html#985" class="Bound">r</a><a id="995" class="Symbol">)</a>

  <a id="1000" class="Comment">-- Coiteration.</a>

  <a id="1019" href="Codata.Musical.M.Indexed.html#1019" class="Function">coit</a> <a id="1024" class="Symbol">:</a> <a id="1026" class="Symbol">∀</a> <a id="1028" class="Symbol">{</a><a id="1029" href="Codata.Musical.M.Indexed.html#1029" class="Bound">ℓ</a><a id="1030" class="Symbol">}</a> <a id="1032" class="Symbol">{</a><a id="1033" href="Codata.Musical.M.Indexed.html#1033" class="Bound">X</a> <a id="1035" class="Symbol">:</a> <a id="1037" href="Relation.Unary.html#1229" class="Function">Pred</a> <a id="1042" href="Codata.Musical.M.Indexed.html#626" class="Bound">O</a> <a id="1044" href="Codata.Musical.M.Indexed.html#1029" class="Bound">ℓ</a><a id="1045" class="Symbol">}</a> <a id="1047" class="Symbol">→</a> <a id="1049" href="Codata.Musical.M.Indexed.html#1033" class="Bound">X</a> <a id="1051" href="Relation.Unary.html#2097" class="Function Operator">⊆</a> <a id="1053" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟦</a> <a id="1055" href="Codata.Musical.M.Indexed.html#638" class="Bound">C</a> <a id="1057" href="Data.Container.Indexed.Core.html#1095" class="Function Operator">⟧</a> <a id="1059" href="Codata.Musical.M.Indexed.html#1033" class="Bound">X</a> <a id="1061" class="Symbol">→</a> <a id="1063" href="Codata.Musical.M.Indexed.html#1033" class="Bound">X</a> <a id="1065" href="Relation.Unary.html#2097" class="Function Operator">⊆</a> <a id="1067" href="Codata.Musical.M.Indexed.html#675" class="Datatype">M</a>
  <a id="1071" href="Codata.Musical.M.Indexed.html#1019" class="Function">coit</a> <a id="1076" href="Codata.Musical.M.Indexed.html#1076" class="Bound">ψ</a> <a id="1078" href="Codata.Musical.M.Indexed.html#1078" class="Bound">x</a> <a id="1080" class="Symbol">=</a> <a id="1082" href="Codata.Musical.M.Indexed.html#713" class="InductiveConstructor">inf</a> <a id="1086" class="Symbol">(</a><a id="1087" href="Data.Product.Base.html#636" class="Field">proj₁</a> <a id="1093" href="Codata.Musical.M.Indexed.html#1141" class="Function">cs</a> <a id="1096" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="1098" class="Symbol">λ</a> <a id="1100" href="Codata.Musical.M.Indexed.html#1100" class="Bound">r</a> <a id="1102" class="Symbol">→</a> <a id="1104" href="Agda.Builtin.Coinduction.html#229" class="CoinductiveConstructor Operator">♯</a> <a id="1106" href="Codata.Musical.M.Indexed.html#1019" class="Function">coit</a> <a id="1111" href="Codata.Musical.M.Indexed.html#1076" class="Bound">ψ</a> <a id="1113" class="Symbol">(</a><a id="1114" href="Data.Product.Base.html#650" class="Field">proj₂</a> <a id="1120" href="Codata.Musical.M.Indexed.html#1141" class="Function">cs</a> <a id="1123" href="Codata.Musical.M.Indexed.html#1100" class="Bound">r</a><a id="1124" class="Symbol">))</a>
    <a id="1131" class="Keyword">where</a>
    <a id="1141" href="Codata.Musical.M.Indexed.html#1141" class="Function">cs</a> <a id="1144" class="Symbol">=</a> <a id="1146" href="Codata.Musical.M.Indexed.html#1076" class="Bound">ψ</a> <a id="1148" href="Codata.Musical.M.Indexed.html#1078" class="Bound">x</a>
</pre></body></html>