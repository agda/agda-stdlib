<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Data.Refinement</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Refinement type: a value together with a proof irrelevant witness.</a>
<a id="176" class="Comment">------------------------------------------------------------------------</a>

<a id="250" class="Symbol">{-#</a> <a id="254" class="Keyword">OPTIONS</a> <a id="262" class="Pragma">--cubical-compatible</a> <a id="283" class="Pragma">--safe</a> <a id="290" class="Symbol">#-}</a>

<a id="295" class="Keyword">module</a> <a id="302" href="Data.Refinement.html" class="Module">Data.Refinement</a> <a id="318" class="Keyword">where</a>

<a id="325" class="Keyword">open</a> <a id="330" class="Keyword">import</a> <a id="337" href="Level.html" class="Module">Level</a>
<a id="343" class="Keyword">open</a> <a id="348" class="Keyword">import</a> <a id="355" href="Data.Irrelevant.html" class="Module">Data.Irrelevant</a> <a id="371" class="Symbol">as</a> <a id="374" class="Module">Irrelevant</a> <a id="385" class="Keyword">using</a> <a id="391" class="Symbol">(</a><a id="392" href="Data.Irrelevant.html#725" class="Record">Irrelevant</a><a id="402" class="Symbol">)</a>
<a id="404" class="Keyword">open</a> <a id="409" class="Keyword">import</a> <a id="416" href="Function.Base.html" class="Module">Function.Base</a>
<a id="430" class="Keyword">open</a> <a id="435" class="Keyword">import</a> <a id="442" href="Relation.Unary.html" class="Module">Relation.Unary</a> <a id="457" class="Keyword">using</a> <a id="463" class="Symbol">(</a><a id="464" href="Relation.Unary.html#3844" class="Function">IUniversal</a><a id="474" class="Symbol">;</a> <a id="476" href="Relation.Unary.html#5090" class="Function Operator">_⇒_</a><a id="479" class="Symbol">;</a> <a id="481" href="Relation.Unary.html#5835" class="Function Operator">_⊢_</a><a id="484" class="Symbol">)</a>

<a id="487" class="Keyword">private</a>
  <a id="497" class="Keyword">variable</a>
    <a id="510" href="Data.Refinement.html#510" class="Generalizable">a</a> <a id="512" href="Data.Refinement.html#512" class="Generalizable">b</a> <a id="514" href="Data.Refinement.html#514" class="Generalizable">p</a> <a id="516" href="Data.Refinement.html#516" class="Generalizable">q</a> <a id="518" class="Symbol">:</a> <a id="520" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="530" href="Data.Refinement.html#530" class="Generalizable">A</a> <a id="532" class="Symbol">:</a> <a id="534" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="538" href="Data.Refinement.html#510" class="Generalizable">a</a>
    <a id="544" href="Data.Refinement.html#544" class="Generalizable">B</a> <a id="546" class="Symbol">:</a> <a id="548" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="552" href="Data.Refinement.html#512" class="Generalizable">b</a>

<a id="555" class="Keyword">record</a> <a id="Refinement"></a><a id="562" href="Data.Refinement.html#562" class="Record">Refinement</a> <a id="573" class="Symbol">{</a><a id="574" href="Data.Refinement.html#574" class="Bound">a</a> <a id="576" href="Data.Refinement.html#576" class="Bound">p</a><a id="577" class="Symbol">}</a> <a id="579" class="Symbol">(</a><a id="580" href="Data.Refinement.html#580" class="Bound">A</a> <a id="582" class="Symbol">:</a> <a id="584" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="588" href="Data.Refinement.html#574" class="Bound">a</a><a id="589" class="Symbol">)</a> <a id="591" class="Symbol">(</a><a id="592" href="Data.Refinement.html#592" class="Bound">P</a> <a id="594" class="Symbol">:</a> <a id="596" href="Data.Refinement.html#580" class="Bound">A</a> <a id="598" class="Symbol">→</a> <a id="600" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="604" href="Data.Refinement.html#576" class="Bound">p</a><a id="605" class="Symbol">)</a> <a id="607" class="Symbol">:</a> <a id="609" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="613" class="Symbol">(</a><a id="614" href="Data.Refinement.html#574" class="Bound">a</a> <a id="616" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="618" href="Data.Refinement.html#576" class="Bound">p</a><a id="619" class="Symbol">)</a> <a id="621" class="Keyword">where</a>
  <a id="629" class="Keyword">constructor</a> <a id="_,_"></a><a id="641" href="Data.Refinement.html#641" class="InductiveConstructor Operator">_,_</a>
  <a id="647" class="Keyword">field</a> <a id="Refinement.value"></a><a id="653" href="Data.Refinement.html#653" class="Field">value</a> <a id="659" class="Symbol">:</a> <a id="661" href="Data.Refinement.html#580" class="Bound">A</a>
        <a id="Refinement.proof"></a><a id="671" href="Data.Refinement.html#671" class="Field">proof</a> <a id="677" class="Symbol">:</a> <a id="679" href="Data.Irrelevant.html#725" class="Record">Irrelevant</a> <a id="690" class="Symbol">(</a><a id="691" href="Data.Refinement.html#592" class="Bound">P</a> <a id="693" href="Data.Refinement.html#653" class="Field">value</a><a id="698" class="Symbol">)</a>
<a id="700" class="Keyword">infixr</a> <a id="707" class="Number">4</a> <a id="709" href="Data.Refinement.html#641" class="InductiveConstructor Operator">_,_</a>
<a id="713" class="Keyword">open</a> <a id="718" href="Data.Refinement.html#562" class="Module">Refinement</a> <a id="729" class="Keyword">public</a>

<a id="737" class="Comment">-- The syntax declaration below is meant to mimick set comprehension.</a>
<a id="807" class="Comment">-- It is attached to Refinement-syntax, to make it easy to import</a>
<a id="873" class="Comment">-- Data.Refinement without the special syntax.</a>

<a id="921" class="Keyword">infix</a> <a id="927" class="Number">2</a> <a id="929" href="Data.Refinement.html#947" class="Function">Refinement-syntax</a>
<a id="Refinement-syntax"></a><a id="947" href="Data.Refinement.html#947" class="Function">Refinement-syntax</a> <a id="965" class="Symbol">=</a> <a id="967" href="Data.Refinement.html#562" class="Record">Refinement</a>
<a id="978" class="Keyword">syntax</a> <a id="985" href="Data.Refinement.html#947" class="Function">Refinement-syntax</a> <a id="1003" class="Bound">A</a> <a id="1005" class="Symbol">(λ</a> <a id="1008" class="Bound">x</a> <a id="1010" class="Symbol">→</a> <a id="1012" class="Bound">P</a><a id="1013" class="Symbol">)</a> <a id="1015" class="Symbol">=</a> <a id="1017" class="Function">[</a> <a id="1019" class="Bound">x</a> <a id="1021" class="Function">∈</a> <a id="1023" class="Bound">A</a> <a id="1025" class="Function">∣</a> <a id="1027" class="Bound">P</a> <a id="1029" class="Function">]</a>

<a id="1032" class="Keyword">module</a> <a id="1039" href="Data.Refinement.html#1039" class="Module">_</a> <a id="1041" class="Symbol">{</a><a id="1042" href="Data.Refinement.html#1042" class="Bound">P</a> <a id="1044" class="Symbol">:</a> <a id="1046" href="Data.Refinement.html#530" class="Generalizable">A</a> <a id="1048" class="Symbol">→</a> <a id="1050" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1054" href="Data.Refinement.html#514" class="Generalizable">p</a><a id="1055" class="Symbol">}</a> <a id="1057" class="Symbol">{</a><a id="1058" href="Data.Refinement.html#1058" class="Bound">Q</a> <a id="1060" class="Symbol">:</a> <a id="1062" href="Data.Refinement.html#544" class="Generalizable">B</a> <a id="1064" class="Symbol">→</a> <a id="1066" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1070" href="Data.Refinement.html#516" class="Generalizable">q</a><a id="1071" class="Symbol">}</a> <a id="1073" class="Keyword">where</a>

  <a id="1082" href="Data.Refinement.html#1082" class="Function">map</a> <a id="1086" class="Symbol">:</a> <a id="1088" class="Symbol">(</a><a id="1089" href="Data.Refinement.html#1089" class="Bound">f</a> <a id="1091" class="Symbol">:</a> <a id="1093" href="Data.Refinement.html#1046" class="Bound">A</a> <a id="1095" class="Symbol">→</a> <a id="1097" href="Data.Refinement.html#1062" class="Bound">B</a><a id="1098" class="Symbol">)</a> <a id="1100" class="Symbol">→</a> <a id="1102" href="Relation.Unary.html#3844" class="Function">∀[</a> <a id="1105" href="Data.Refinement.html#1042" class="Bound">P</a> <a id="1107" href="Relation.Unary.html#5090" class="Function Operator">⇒</a> <a id="1109" href="Data.Refinement.html#1089" class="Bound">f</a> <a id="1111" href="Relation.Unary.html#5835" class="Function Operator">⊢</a> <a id="1113" href="Data.Refinement.html#1058" class="Bound">Q</a> <a id="1115" href="Relation.Unary.html#3844" class="Function">]</a> <a id="1117" class="Symbol">→</a>
        <a id="1127" href="Data.Refinement.html#947" class="Function">[</a> <a id="1129" href="Data.Refinement.html#1129" class="Bound">a</a> <a id="1131" href="Data.Refinement.html#947" class="Function">∈</a> <a id="1133" href="Data.Refinement.html#1046" class="Bound">A</a> <a id="1135" href="Data.Refinement.html#947" class="Function">∣</a> <a id="1137" href="Data.Refinement.html#1042" class="Bound">P</a> <a id="1139" href="Data.Refinement.html#1129" class="Bound">a</a> <a id="1141" href="Data.Refinement.html#947" class="Function">]</a> <a id="1143" class="Symbol">→</a> <a id="1145" href="Data.Refinement.html#947" class="Function">[</a> <a id="1147" href="Data.Refinement.html#1147" class="Bound">b</a> <a id="1149" href="Data.Refinement.html#947" class="Function">∈</a> <a id="1151" href="Data.Refinement.html#1062" class="Bound">B</a> <a id="1153" href="Data.Refinement.html#947" class="Function">∣</a> <a id="1155" href="Data.Refinement.html#1058" class="Bound">Q</a> <a id="1157" href="Data.Refinement.html#1147" class="Bound">b</a> <a id="1159" href="Data.Refinement.html#947" class="Function">]</a>
  <a id="1163" href="Data.Refinement.html#1082" class="Function">map</a> <a id="1167" href="Data.Refinement.html#1167" class="Bound">f</a> <a id="1169" href="Data.Refinement.html#1169" class="Bound">prf</a> <a id="1173" class="Symbol">(</a><a id="1174" href="Data.Refinement.html#1174" class="Bound">a</a> <a id="1176" href="Data.Refinement.html#641" class="InductiveConstructor Operator">,</a> <a id="1178" href="Data.Refinement.html#1178" class="Bound">p</a><a id="1179" class="Symbol">)</a> <a id="1181" class="Symbol">=</a> <a id="1183" href="Data.Refinement.html#1167" class="Bound">f</a> <a id="1185" href="Data.Refinement.html#1174" class="Bound">a</a> <a id="1187" href="Data.Refinement.html#641" class="InductiveConstructor Operator">,</a> <a id="1189" href="Data.Irrelevant.html#963" class="Function">Irrelevant.map</a> <a id="1204" href="Data.Refinement.html#1169" class="Bound">prf</a> <a id="1208" href="Data.Refinement.html#1178" class="Bound">p</a>

<a id="1211" class="Keyword">module</a> <a id="1218" href="Data.Refinement.html#1218" class="Module">_</a> <a id="1220" class="Symbol">{</a><a id="1221" href="Data.Refinement.html#1221" class="Bound">P</a> <a id="1223" class="Symbol">:</a> <a id="1225" href="Data.Refinement.html#530" class="Generalizable">A</a> <a id="1227" class="Symbol">→</a> <a id="1229" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1233" href="Data.Refinement.html#514" class="Generalizable">p</a><a id="1234" class="Symbol">}</a> <a id="1236" class="Symbol">{</a><a id="1237" href="Data.Refinement.html#1237" class="Bound">Q</a> <a id="1239" class="Symbol">:</a> <a id="1241" href="Data.Refinement.html#530" class="Generalizable">A</a> <a id="1243" class="Symbol">→</a> <a id="1245" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1249" href="Data.Refinement.html#516" class="Generalizable">q</a><a id="1250" class="Symbol">}</a> <a id="1252" class="Keyword">where</a>

  <a id="1261" href="Data.Refinement.html#1261" class="Function">refine</a> <a id="1268" class="Symbol">:</a> <a id="1270" href="Relation.Unary.html#3844" class="Function">∀[</a> <a id="1273" href="Data.Refinement.html#1221" class="Bound">P</a> <a id="1275" href="Relation.Unary.html#5090" class="Function Operator">⇒</a> <a id="1277" href="Data.Refinement.html#1237" class="Bound">Q</a> <a id="1279" href="Relation.Unary.html#3844" class="Function">]</a> <a id="1281" class="Symbol">→</a> <a id="1283" href="Data.Refinement.html#947" class="Function">[</a> <a id="1285" href="Data.Refinement.html#1285" class="Bound">a</a> <a id="1287" href="Data.Refinement.html#947" class="Function">∈</a> <a id="1289" href="Data.Refinement.html#1225" class="Bound">A</a> <a id="1291" href="Data.Refinement.html#947" class="Function">∣</a> <a id="1293" href="Data.Refinement.html#1221" class="Bound">P</a> <a id="1295" href="Data.Refinement.html#1285" class="Bound">a</a> <a id="1297" href="Data.Refinement.html#947" class="Function">]</a> <a id="1299" class="Symbol">→</a> <a id="1301" href="Data.Refinement.html#947" class="Function">[</a> <a id="1303" href="Data.Refinement.html#1303" class="Bound">a</a> <a id="1305" href="Data.Refinement.html#947" class="Function">∈</a> <a id="1307" href="Data.Refinement.html#1225" class="Bound">A</a> <a id="1309" href="Data.Refinement.html#947" class="Function">∣</a> <a id="1311" href="Data.Refinement.html#1237" class="Bound">Q</a> <a id="1313" href="Data.Refinement.html#1303" class="Bound">a</a> <a id="1315" href="Data.Refinement.html#947" class="Function">]</a>
  <a id="1319" href="Data.Refinement.html#1261" class="Function">refine</a> <a id="1326" class="Symbol">=</a> <a id="1328" href="Data.Refinement.html#1082" class="Function">map</a> <a id="1332" href="Function.Base.html#704" class="Function">id</a>
</pre></body></html>