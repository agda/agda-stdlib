<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Indexed.Homogeneous.Bundles</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Homogeneously-indexed binary relations</a>
<a id="148" class="Comment">------------------------------------------------------------------------</a>

<a id="222" class="Comment">-- The contents of this module should be accessed via</a>
<a id="276" class="Comment">-- `Relation.Binary.Indexed.Homogeneous`.</a>

<a id="319" class="Symbol">{-#</a> <a id="323" class="Keyword">OPTIONS</a> <a id="331" class="Pragma">--cubical-compatible</a> <a id="352" class="Pragma">--safe</a> <a id="359" class="Symbol">#-}</a>

<a id="364" class="Keyword">module</a> <a id="371" href="Relation.Binary.Indexed.Homogeneous.Bundles.html" class="Module">Relation.Binary.Indexed.Homogeneous.Bundles</a> <a id="415" class="Keyword">where</a>

<a id="422" class="Keyword">open</a> <a id="427" class="Keyword">import</a> <a id="434" href="Level.html" class="Module">Level</a> <a id="440" class="Keyword">using</a> <a id="446" class="Symbol">(</a><a id="447" href="Agda.Primitive.html#931" class="Primitive">suc</a><a id="450" class="Symbol">;</a> <a id="452" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="455" class="Symbol">)</a>
<a id="457" class="Keyword">open</a> <a id="462" class="Keyword">import</a> <a id="469" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a> <a id="490" class="Keyword">using</a> <a id="496" class="Symbol">(</a><a id="497" href="Relation.Binary.Core.html#896" class="Function">Rel</a><a id="500" class="Symbol">)</a>
<a id="502" class="Keyword">open</a> <a id="507" class="Keyword">import</a> <a id="514" href="Relation.Binary.Bundles.html" class="Module">Relation.Binary.Bundles</a> <a id="538" class="Symbol">as</a> <a id="541" class="Module">B</a>
<a id="543" class="Keyword">open</a> <a id="548" class="Keyword">import</a> <a id="555" href="Relation.Nullary.Negation.html" class="Module">Relation.Nullary.Negation</a> <a id="581" class="Keyword">using</a> <a id="587" class="Symbol">(</a><a id="588" href="Relation.Nullary.Negation.Core.html#698" class="Function Operator">¬_</a><a id="590" class="Symbol">)</a>
<a id="592" class="Keyword">open</a> <a id="597" class="Keyword">import</a> <a id="604" href="Relation.Binary.Indexed.Homogeneous.Core.html" class="Module">Relation.Binary.Indexed.Homogeneous.Core</a>
<a id="645" class="Keyword">open</a> <a id="650" class="Keyword">import</a> <a id="657" href="Relation.Binary.Indexed.Homogeneous.Structures.html" class="Module">Relation.Binary.Indexed.Homogeneous.Structures</a>

<a id="705" class="Comment">-- Indexed structures are laid out in a similar manner as to those</a>
<a id="772" class="Comment">-- in Relation.Binary. The main difference is each structure also</a>
<a id="838" class="Comment">-- contains proofs for the lifted version of the relation.</a>

<a id="898" class="Comment">------------------------------------------------------------------------</a>
<a id="971" class="Comment">-- Equivalences</a>

<a id="988" class="Keyword">record</a> <a id="IndexedSetoid"></a><a id="995" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#995" class="Record">IndexedSetoid</a> <a id="1009" class="Symbol">{</a><a id="1010" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1010" class="Bound">i</a><a id="1011" class="Symbol">}</a> <a id="1013" class="Symbol">(</a><a id="1014" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1014" class="Bound">I</a> <a id="1016" class="Symbol">:</a> <a id="1018" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1022" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1010" class="Bound">i</a><a id="1023" class="Symbol">)</a> <a id="1025" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1025" class="Bound">c</a> <a id="1027" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1027" class="Bound">ℓ</a> <a id="1029" class="Symbol">:</a> <a id="1031" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1035" class="Symbol">(</a><a id="1036" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="1040" class="Symbol">(</a><a id="1041" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1010" class="Bound">i</a> <a id="1043" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1045" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1025" class="Bound">c</a> <a id="1047" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1049" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1027" class="Bound">ℓ</a><a id="1050" class="Symbol">))</a> <a id="1053" class="Keyword">where</a>
  <a id="1061" class="Keyword">infix</a> <a id="1067" class="Number">4</a> <a id="1069" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1121" class="Field Operator">_≈ᵢ_</a> <a id="1074" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1327" class="Function Operator">_≈_</a>
  <a id="1080" class="Keyword">field</a>
    <a id="IndexedSetoid.Carrierᵢ"></a><a id="1090" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1090" class="Field">Carrierᵢ</a>       <a id="1105" class="Symbol">:</a> <a id="1107" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1014" class="Bound">I</a> <a id="1109" class="Symbol">→</a> <a id="1111" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1115" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1025" class="Bound">c</a>
    <a id="IndexedSetoid._≈ᵢ_"></a><a id="1121" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1121" class="Field Operator">_≈ᵢ_</a>           <a id="1136" class="Symbol">:</a> <a id="1138" href="Relation.Binary.Indexed.Homogeneous.Core.html#1037" class="Function">IRel</a> <a id="1143" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1090" class="Field">Carrierᵢ</a> <a id="1152" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1027" class="Bound">ℓ</a>
    <a id="IndexedSetoid.isEquivalenceᵢ"></a><a id="1158" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1158" class="Field">isEquivalenceᵢ</a> <a id="1173" class="Symbol">:</a> <a id="1175" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1289" class="Record">IsIndexedEquivalence</a> <a id="1196" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1090" class="Field">Carrierᵢ</a> <a id="1205" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1121" class="Field Operator">_≈ᵢ_</a>

  <a id="1213" class="Keyword">open</a> <a id="1218" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1289" class="Module">IsIndexedEquivalence</a> <a id="1239" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1158" class="Field">isEquivalenceᵢ</a> <a id="1254" class="Keyword">public</a>

  <a id="IndexedSetoid.Carrier"></a><a id="1264" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1264" class="Function">Carrier</a> <a id="1272" class="Symbol">:</a> <a id="1274" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1278" class="Symbol">_</a>
  <a id="1282" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1264" class="Function">Carrier</a> <a id="1290" class="Symbol">=</a> <a id="1292" class="Symbol">∀</a> <a id="1294" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1294" class="Bound">i</a> <a id="1296" class="Symbol">→</a> <a id="1298" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1090" class="Field">Carrierᵢ</a> <a id="1307" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1294" class="Bound">i</a>

  <a id="1312" class="Keyword">infix</a> <a id="1318" class="Number">4</a> <a id="1320" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1377" class="Function Operator">_≉_</a>

  <a id="IndexedSetoid._≈_"></a><a id="1327" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1327" class="Function Operator">_≈_</a> <a id="1331" class="Symbol">:</a> <a id="1333" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="1337" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1264" class="Function">Carrier</a> <a id="1345" class="Symbol">_</a>
  <a id="1349" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1327" class="Function Operator">_≈_</a> <a id="1353" class="Symbol">=</a> <a id="1355" href="Relation.Binary.Indexed.Homogeneous.Core.html#1343" class="Function">Lift</a> <a id="1360" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1090" class="Field">Carrierᵢ</a> <a id="1369" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1121" class="Field Operator">_≈ᵢ_</a>

  <a id="IndexedSetoid._≉_"></a><a id="1377" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1377" class="Function Operator">_≉_</a> <a id="1381" class="Symbol">:</a> <a id="1383" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="1387" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1264" class="Function">Carrier</a> <a id="1395" class="Symbol">_</a>
  <a id="1399" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1399" class="Bound">x</a> <a id="1401" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1377" class="Function Operator">≉</a> <a id="1403" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1403" class="Bound">y</a> <a id="1405" class="Symbol">=</a> <a id="1407" href="Relation.Nullary.Negation.Core.html#698" class="Function Operator">¬</a> <a id="1409" class="Symbol">(</a><a id="1410" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1399" class="Bound">x</a> <a id="1412" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1327" class="Function Operator">≈</a> <a id="1414" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1403" class="Bound">y</a><a id="1415" class="Symbol">)</a>

  <a id="IndexedSetoid.setoid"></a><a id="1420" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1420" class="Function">setoid</a> <a id="1427" class="Symbol">:</a> <a id="1429" href="Relation.Binary.Bundles.html#1095" class="Record">B.Setoid</a> <a id="1438" class="Symbol">_</a> <a id="1440" class="Symbol">_</a>
  <a id="1444" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1420" class="Function">setoid</a> <a id="1451" class="Symbol">=</a> <a id="1453" class="Keyword">record</a>
    <a id="1464" class="Symbol">{</a> <a id="1466" href="Relation.Binary.Bundles.html#1218" class="Field">isEquivalence</a> <a id="1480" class="Symbol">=</a> <a id="1482" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1793" class="Function">isEquivalence</a>
    <a id="1500" class="Symbol">}</a>


<a id="1504" class="Keyword">record</a> <a id="IndexedDecSetoid"></a><a id="1511" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1511" class="Record">IndexedDecSetoid</a> <a id="1528" class="Symbol">{</a><a id="1529" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1529" class="Bound">i</a><a id="1530" class="Symbol">}</a> <a id="1532" class="Symbol">(</a><a id="1533" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1533" class="Bound">I</a> <a id="1535" class="Symbol">:</a> <a id="1537" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1541" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1529" class="Bound">i</a><a id="1542" class="Symbol">)</a> <a id="1544" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1544" class="Bound">c</a> <a id="1546" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1546" class="Bound">ℓ</a> <a id="1548" class="Symbol">:</a> <a id="1550" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1554" class="Symbol">(</a><a id="1555" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="1559" class="Symbol">(</a><a id="1560" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1529" class="Bound">i</a> <a id="1562" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1564" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1544" class="Bound">c</a> <a id="1566" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="1568" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1546" class="Bound">ℓ</a><a id="1569" class="Symbol">))</a> <a id="1572" class="Keyword">where</a>
  <a id="1580" class="Keyword">infix</a> <a id="1586" class="Number">4</a> <a id="1588" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1639" class="Field Operator">_≈ᵢ_</a>
  <a id="1595" class="Keyword">field</a>
    <a id="IndexedDecSetoid.Carrierᵢ"></a><a id="1605" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1605" class="Field">Carrierᵢ</a>          <a id="1623" class="Symbol">:</a> <a id="1625" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1533" class="Bound">I</a> <a id="1627" class="Symbol">→</a> <a id="1629" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="1633" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1544" class="Bound">c</a>
    <a id="IndexedDecSetoid._≈ᵢ_"></a><a id="1639" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1639" class="Field Operator">_≈ᵢ_</a>              <a id="1657" class="Symbol">:</a> <a id="1659" href="Relation.Binary.Indexed.Homogeneous.Core.html#1037" class="Function">IRel</a> <a id="1664" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1605" class="Field">Carrierᵢ</a> <a id="1673" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1546" class="Bound">ℓ</a>
    <a id="IndexedDecSetoid.isDecEquivalenceᵢ"></a><a id="1679" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1679" class="Field">isDecEquivalenceᵢ</a> <a id="1697" class="Symbol">:</a> <a id="1699" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1936" class="Record">IsIndexedDecEquivalence</a> <a id="1723" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1605" class="Field">Carrierᵢ</a> <a id="1732" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1639" class="Field Operator">_≈ᵢ_</a>

  <a id="1740" class="Keyword">open</a> <a id="1745" href="Relation.Binary.Indexed.Homogeneous.Structures.html#1936" class="Module">IsIndexedDecEquivalence</a> <a id="1769" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1679" class="Field">isDecEquivalenceᵢ</a> <a id="1787" class="Keyword">public</a>

  <a id="IndexedDecSetoid.indexedSetoid"></a><a id="1797" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1797" class="Function">indexedSetoid</a> <a id="1811" class="Symbol">:</a> <a id="1813" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#995" class="Record">IndexedSetoid</a> <a id="1827" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1533" class="Bound">I</a> <a id="1829" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1544" class="Bound">c</a> <a id="1831" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1546" class="Bound">ℓ</a>
  <a id="1835" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1797" class="Function">indexedSetoid</a> <a id="1849" class="Symbol">=</a> <a id="1851" class="Keyword">record</a>
    <a id="1862" class="Symbol">{</a> <a id="1864" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1158" class="Field">isEquivalenceᵢ</a> <a id="1879" class="Symbol">=</a> <a id="1881" href="Relation.Binary.Indexed.Homogeneous.Structures.html#2049" class="Function">isEquivalenceᵢ</a>
    <a id="1900" class="Symbol">}</a>

  <a id="1905" class="Keyword">open</a> <a id="1910" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#995" class="Module">IndexedSetoid</a> <a id="1924" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1797" class="Function">indexedSetoid</a> <a id="1938" class="Keyword">public</a>
    <a id="1949" class="Keyword">using</a> <a id="1955" class="Symbol">(</a><a id="1956" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1264" class="Function">Carrier</a><a id="1963" class="Symbol">;</a> <a id="1965" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1327" class="Function Operator">_≈_</a><a id="1968" class="Symbol">;</a> <a id="1970" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1377" class="Function Operator">_≉_</a><a id="1973" class="Symbol">;</a> <a id="1975" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#1420" class="Function">setoid</a><a id="1981" class="Symbol">)</a>

<a id="1984" class="Comment">------------------------------------------------------------------------</a>
<a id="2057" class="Comment">-- Preorders</a>

<a id="2071" class="Keyword">record</a> <a id="IndexedPreorder"></a><a id="2078" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2078" class="Record">IndexedPreorder</a> <a id="2094" class="Symbol">{</a><a id="2095" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2095" class="Bound">i</a><a id="2096" class="Symbol">}</a> <a id="2098" class="Symbol">(</a><a id="2099" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2099" class="Bound">I</a> <a id="2101" class="Symbol">:</a> <a id="2103" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2107" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2095" class="Bound">i</a><a id="2108" class="Symbol">)</a> <a id="2110" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2110" class="Bound">c</a> <a id="2112" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2112" class="Bound">ℓ₁</a> <a id="2115" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2115" class="Bound">ℓ₂</a> <a id="2118" class="Symbol">:</a>
                       <a id="2143" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2147" class="Symbol">(</a><a id="2148" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="2152" class="Symbol">(</a><a id="2153" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2095" class="Bound">i</a> <a id="2155" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2157" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2110" class="Bound">c</a> <a id="2159" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2161" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2112" class="Bound">ℓ₁</a> <a id="2164" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2166" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2115" class="Bound">ℓ₂</a><a id="2168" class="Symbol">))</a> <a id="2171" class="Keyword">where</a>

  <a id="2180" class="Keyword">infix</a> <a id="2186" class="Number">4</a> <a id="2188" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2247" class="Field Operator">_≈ᵢ_</a> <a id="2193" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2282" class="Field Operator">_∼ᵢ_</a> <a id="2198" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2464" class="Function Operator">_≈_</a> <a id="2202" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2514" class="Function Operator">_∼_</a>

  <a id="2209" class="Keyword">field</a>
    <a id="IndexedPreorder.Carrierᵢ"></a><a id="2219" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2219" class="Field">Carrierᵢ</a>    <a id="2231" class="Symbol">:</a> <a id="2233" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2099" class="Bound">I</a> <a id="2235" class="Symbol">→</a> <a id="2237" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2241" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2110" class="Bound">c</a>
    <a id="IndexedPreorder._≈ᵢ_"></a><a id="2247" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2247" class="Field Operator">_≈ᵢ_</a>        <a id="2259" class="Symbol">:</a> <a id="2261" href="Relation.Binary.Indexed.Homogeneous.Core.html#1037" class="Function">IRel</a> <a id="2266" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2219" class="Field">Carrierᵢ</a> <a id="2275" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2112" class="Bound">ℓ₁</a>
    <a id="IndexedPreorder._∼ᵢ_"></a><a id="2282" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2282" class="Field Operator">_∼ᵢ_</a>        <a id="2294" class="Symbol">:</a> <a id="2296" href="Relation.Binary.Indexed.Homogeneous.Core.html#1037" class="Function">IRel</a> <a id="2301" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2219" class="Field">Carrierᵢ</a> <a id="2310" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2115" class="Bound">ℓ₂</a>
    <a id="IndexedPreorder.isPreorderᵢ"></a><a id="2317" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2317" class="Field">isPreorderᵢ</a> <a id="2329" class="Symbol">:</a> <a id="2331" href="Relation.Binary.Indexed.Homogeneous.Structures.html#2234" class="Record">IsIndexedPreorder</a> <a id="2349" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2219" class="Field">Carrierᵢ</a> <a id="2358" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2247" class="Field Operator">_≈ᵢ_</a> <a id="2363" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2282" class="Field Operator">_∼ᵢ_</a>

  <a id="2371" class="Keyword">open</a> <a id="2376" href="Relation.Binary.Indexed.Homogeneous.Structures.html#2234" class="Module">IsIndexedPreorder</a> <a id="2394" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2317" class="Field">isPreorderᵢ</a> <a id="2406" class="Keyword">public</a>

  <a id="IndexedPreorder.Carrier"></a><a id="2416" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Function">Carrier</a> <a id="2424" class="Symbol">:</a> <a id="2426" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2430" class="Symbol">_</a>
  <a id="2434" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Function">Carrier</a> <a id="2442" class="Symbol">=</a> <a id="2444" class="Symbol">∀</a> <a id="2446" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2446" class="Bound">i</a> <a id="2448" class="Symbol">→</a> <a id="2450" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2219" class="Field">Carrierᵢ</a> <a id="2459" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2446" class="Bound">i</a>

  <a id="IndexedPreorder._≈_"></a><a id="2464" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2464" class="Function Operator">_≈_</a> <a id="2468" class="Symbol">:</a> <a id="2470" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="2474" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Function">Carrier</a> <a id="2482" class="Symbol">_</a>
  <a id="2486" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2486" class="Bound">x</a> <a id="2488" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2464" class="Function Operator">≈</a> <a id="2490" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2490" class="Bound">y</a> <a id="2492" class="Symbol">=</a> <a id="2494" class="Symbol">∀</a> <a id="2496" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2496" class="Bound">i</a> <a id="2498" class="Symbol">→</a> <a id="2500" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2486" class="Bound">x</a> <a id="2502" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2496" class="Bound">i</a> <a id="2504" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2247" class="Field Operator">≈ᵢ</a> <a id="2507" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2490" class="Bound">y</a> <a id="2509" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2496" class="Bound">i</a>

  <a id="IndexedPreorder._∼_"></a><a id="2514" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2514" class="Function Operator">_∼_</a> <a id="2518" class="Symbol">:</a> <a id="2520" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="2524" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Function">Carrier</a> <a id="2532" class="Symbol">_</a>
  <a id="2536" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2536" class="Bound">x</a> <a id="2538" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2514" class="Function Operator">∼</a> <a id="2540" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2540" class="Bound">y</a> <a id="2542" class="Symbol">=</a> <a id="2544" class="Symbol">∀</a> <a id="2546" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2546" class="Bound">i</a> <a id="2548" class="Symbol">→</a> <a id="2550" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2536" class="Bound">x</a> <a id="2552" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2546" class="Bound">i</a> <a id="2554" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2282" class="Field Operator">∼ᵢ</a> <a id="2557" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2540" class="Bound">y</a> <a id="2559" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2546" class="Bound">i</a>

  <a id="IndexedPreorder.preorder"></a><a id="2564" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2564" class="Function">preorder</a> <a id="2573" class="Symbol">:</a> <a id="2575" href="Relation.Binary.Bundles.html#2136" class="Record">B.Preorder</a> <a id="2586" class="Symbol">_</a> <a id="2588" class="Symbol">_</a> <a id="2590" class="Symbol">_</a>
  <a id="2594" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2564" class="Function">preorder</a> <a id="2603" class="Symbol">=</a> <a id="2605" class="Keyword">record</a>
    <a id="2616" class="Symbol">{</a> <a id="2618" href="Relation.Binary.Bundles.html#2349" class="Field">isPreorder</a> <a id="2629" class="Symbol">=</a> <a id="2631" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3379" class="Function">isPreorder</a>
    <a id="2646" class="Symbol">}</a>

<a id="2649" class="Comment">------------------------------------------------------------------------</a>
<a id="2722" class="Comment">-- Partial orders</a>

<a id="2741" class="Keyword">record</a> <a id="IndexedPoset"></a><a id="2748" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2748" class="Record">IndexedPoset</a> <a id="2761" class="Symbol">{</a><a id="2762" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2762" class="Bound">i</a><a id="2763" class="Symbol">}</a> <a id="2765" class="Symbol">(</a><a id="2766" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2766" class="Bound">I</a> <a id="2768" class="Symbol">:</a> <a id="2770" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2774" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2762" class="Bound">i</a><a id="2775" class="Symbol">)</a> <a id="2777" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2777" class="Bound">c</a> <a id="2779" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2779" class="Bound">ℓ₁</a> <a id="2782" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2782" class="Bound">ℓ₂</a> <a id="2785" class="Symbol">:</a>
                    <a id="2807" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2811" class="Symbol">(</a><a id="2812" href="Agda.Primitive.html#931" class="Primitive">suc</a> <a id="2816" class="Symbol">(</a><a id="2817" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2762" class="Bound">i</a> <a id="2819" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2821" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2777" class="Bound">c</a> <a id="2823" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2825" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2779" class="Bound">ℓ₁</a> <a id="2828" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2830" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2782" class="Bound">ℓ₂</a><a id="2832" class="Symbol">))</a> <a id="2835" class="Keyword">where</a>
  <a id="2843" class="Keyword">infix</a> <a id="2849" class="Number">4</a> <a id="2851" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2905" class="Field Operator">_≈ᵢ_</a> <a id="2856" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2944" class="Field Operator">_≤ᵢ_</a>
  <a id="2863" class="Keyword">field</a>
    <a id="IndexedPoset.Carrierᵢ"></a><a id="2873" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2873" class="Field">Carrierᵢ</a>        <a id="2889" class="Symbol">:</a> <a id="2891" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2766" class="Bound">I</a> <a id="2893" class="Symbol">→</a> <a id="2895" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="2899" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2777" class="Bound">c</a>
    <a id="IndexedPoset._≈ᵢ_"></a><a id="2905" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2905" class="Field Operator">_≈ᵢ_</a>            <a id="2921" class="Symbol">:</a> <a id="2923" href="Relation.Binary.Indexed.Homogeneous.Core.html#1037" class="Function">IRel</a> <a id="2928" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2873" class="Field">Carrierᵢ</a> <a id="2937" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2779" class="Bound">ℓ₁</a>
    <a id="IndexedPoset._≤ᵢ_"></a><a id="2944" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2944" class="Field Operator">_≤ᵢ_</a>            <a id="2960" class="Symbol">:</a> <a id="2962" href="Relation.Binary.Indexed.Homogeneous.Core.html#1037" class="Function">IRel</a> <a id="2967" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2873" class="Field">Carrierᵢ</a> <a id="2976" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2782" class="Bound">ℓ₂</a>
    <a id="IndexedPoset.isPartialOrderᵢ"></a><a id="2983" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2983" class="Field">isPartialOrderᵢ</a> <a id="2999" class="Symbol">:</a> <a id="3001" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3661" class="Record">IsIndexedPartialOrder</a> <a id="3023" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2873" class="Field">Carrierᵢ</a> <a id="3032" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2905" class="Field Operator">_≈ᵢ_</a> <a id="3037" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2944" class="Field Operator">_≤ᵢ_</a>

  <a id="3045" class="Keyword">open</a> <a id="3050" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3661" class="Module">IsIndexedPartialOrder</a> <a id="3072" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2983" class="Field">isPartialOrderᵢ</a> <a id="3088" class="Keyword">public</a>

  <a id="IndexedPoset.preorderᵢ"></a><a id="3098" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3098" class="Function">preorderᵢ</a> <a id="3108" class="Symbol">:</a> <a id="3110" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2078" class="Record">IndexedPreorder</a> <a id="3126" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2766" class="Bound">I</a> <a id="3128" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2777" class="Bound">c</a> <a id="3130" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2779" class="Bound">ℓ₁</a> <a id="3133" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2782" class="Bound">ℓ₂</a>
  <a id="3138" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3098" class="Function">preorderᵢ</a> <a id="3148" class="Symbol">=</a> <a id="3150" class="Keyword">record</a>
    <a id="3161" class="Symbol">{</a> <a id="3163" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2317" class="Field">isPreorderᵢ</a> <a id="3175" class="Symbol">=</a> <a id="3177" href="Relation.Binary.Indexed.Homogeneous.Structures.html#3775" class="Function">isPreorderᵢ</a>
    <a id="3193" class="Symbol">}</a>

  <a id="3198" class="Keyword">open</a> <a id="3203" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2078" class="Module">IndexedPreorder</a> <a id="3219" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3098" class="Function">preorderᵢ</a> <a id="3229" class="Keyword">public</a>
    <a id="3240" class="Keyword">using</a> <a id="3246" class="Symbol">(</a><a id="3247" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2416" class="Function">Carrier</a><a id="3254" class="Symbol">;</a> <a id="3256" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2464" class="Function Operator">_≈_</a><a id="3259" class="Symbol">;</a> <a id="3261" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2564" class="Function">preorder</a><a id="3269" class="Symbol">)</a> <a id="3271" class="Keyword">renaming</a> <a id="3280" class="Symbol">(</a><a id="3281" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#2514" class="Function Operator">_∼_</a> <a id="3285" class="Symbol">to</a> <a id="3288" class="Function Operator">_≤_</a><a id="3291" class="Symbol">)</a>

  <a id="IndexedPoset.poset"></a><a id="3296" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3296" class="Function">poset</a> <a id="3302" class="Symbol">:</a> <a id="3304" href="Relation.Binary.Bundles.html#3545" class="Record">B.Poset</a> <a id="3312" class="Symbol">_</a> <a id="3314" class="Symbol">_</a> <a id="3316" class="Symbol">_</a>
  <a id="3320" href="Relation.Binary.Indexed.Homogeneous.Bundles.html#3296" class="Function">poset</a> <a id="3326" class="Symbol">=</a> <a id="3328" class="Keyword">record</a>
    <a id="3339" class="Symbol">{</a> <a id="3341" href="Relation.Binary.Bundles.html#3720" class="Field">isPartialOrder</a> <a id="3356" class="Symbol">=</a> <a id="3358" href="Relation.Binary.Indexed.Homogeneous.Structures.html#4217" class="Function">isPartialOrder</a>
    <a id="3377" class="Symbol">}</a>
</pre></body></html>