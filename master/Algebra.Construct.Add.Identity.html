<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Algebra.Construct.Add.Identity</title><link rel="stylesheet" href="Agda.css"></head><body><pre class="Agda"><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Definition of algebraic structures we get from freely adding an</a>
<a id="173" class="Comment">-- identity element</a>
<a id="193" class="Comment">------------------------------------------------------------------------</a>

<a id="267" class="Symbol">{-#</a> <a id="271" class="Keyword">OPTIONS</a> <a id="279" class="Pragma">--cubical-compatible</a> <a id="300" class="Pragma">--safe</a> <a id="307" class="Symbol">#-}</a>

<a id="312" class="Keyword">module</a> <a id="319" href="Algebra.Construct.Add.Identity.html" class="Module">Algebra.Construct.Add.Identity</a> <a id="350" class="Keyword">where</a>

<a id="357" class="Keyword">open</a> <a id="362" class="Keyword">import</a> <a id="369" href="Algebra.Bundles.html" class="Module">Algebra.Bundles</a>
<a id="385" class="Keyword">open</a> <a id="390" class="Keyword">import</a> <a id="397" href="Algebra.Core.html" class="Module">Algebra.Core</a> <a id="410" class="Keyword">using</a> <a id="416" class="Symbol">(</a><a id="417" href="Algebra.Core.html#527" class="Function">Op₂</a><a id="420" class="Symbol">)</a>
<a id="422" class="Keyword">open</a> <a id="427" class="Keyword">import</a> <a id="434" href="Algebra.Definitions.html" class="Module">Algebra.Definitions</a>
<a id="454" class="Keyword">open</a> <a id="459" class="Keyword">import</a> <a id="466" href="Algebra.Structures.html" class="Module">Algebra.Structures</a>
<a id="485" class="Keyword">open</a> <a id="490" class="Keyword">import</a> <a id="497" href="Relation.Binary.Construct.Add.Point.Equality.html" class="Module">Relation.Binary.Construct.Add.Point.Equality</a> <a id="542" class="Keyword">renaming</a> <a id="551" class="Symbol">(</a><a id="552" href="Relation.Binary.Construct.Add.Point.Equality.html#1082" class="Datatype Operator">_≈∙_</a> <a id="557" class="Symbol">to</a> <a id="560" class="Datatype Operator">lift≈</a><a id="565" class="Symbol">)</a>
<a id="567" class="Keyword">open</a> <a id="572" class="Keyword">import</a> <a id="579" href="Data.Product.Base.html" class="Module">Data.Product.Base</a> <a id="597" class="Keyword">using</a> <a id="603" class="Symbol">(</a><a id="604" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">_,_</a><a id="607" class="Symbol">)</a>
<a id="609" class="Keyword">open</a> <a id="614" class="Keyword">import</a> <a id="621" href="Level.html" class="Module">Level</a> <a id="627" class="Keyword">using</a> <a id="633" class="Symbol">(</a><a id="634" href="Agda.Primitive.html#742" class="Postulate">Level</a><a id="639" class="Symbol">;</a> <a id="641" href="Agda.Primitive.html#961" class="Primitive Operator">_⊔_</a><a id="644" class="Symbol">)</a>
<a id="646" class="Keyword">open</a> <a id="651" class="Keyword">import</a> <a id="658" href="Relation.Binary.Core.html" class="Module">Relation.Binary.Core</a>
<a id="679" class="Keyword">open</a> <a id="684" class="Keyword">import</a> <a id="691" href="Relation.Binary.Definitions.html" class="Module">Relation.Binary.Definitions</a>
<a id="719" class="Keyword">open</a> <a id="724" class="Keyword">import</a> <a id="731" href="Relation.Binary.Structures.html" class="Module">Relation.Binary.Structures</a>
<a id="758" class="Keyword">open</a> <a id="763" class="Keyword">import</a> <a id="770" href="Relation.Nullary.Construct.Add.Point.html" class="Module">Relation.Nullary.Construct.Add.Point</a>

<a id="808" class="Keyword">private</a>
  <a id="818" class="Keyword">variable</a>
    <a id="831" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="833" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a> <a id="835" class="Symbol">:</a> <a id="837" href="Agda.Primitive.html#742" class="Postulate">Level</a>
    <a id="847" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a> <a id="849" class="Symbol">:</a> <a id="851" href="Agda.Primitive.html#388" class="Primitive">Set</a> <a id="855" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a>

<a id="liftOp"></a><a id="858" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="865" class="Symbol">:</a> <a id="867" href="Algebra.Core.html#527" class="Function">Op₂</a> <a id="871" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a> <a id="873" class="Symbol">→</a> <a id="875" href="Algebra.Core.html#527" class="Function">Op₂</a> <a id="879" class="Symbol">(</a><a id="880" href="Agda.Builtin.Maybe.html#135" class="Datatype">Pointed</a> <a id="888" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a><a id="889" class="Symbol">)</a>
<a id="891" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="898" href="Algebra.Construct.Add.Identity.html#898" class="Bound">op</a> <a id="901" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="903" href="Algebra.Construct.Add.Identity.html#903" class="Bound">p</a> <a id="905" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="907" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="909" href="Algebra.Construct.Add.Identity.html#909" class="Bound">q</a> <a id="911" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="913" class="Symbol">=</a> <a id="915" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="917" href="Algebra.Construct.Add.Identity.html#898" class="Bound">op</a> <a id="920" href="Algebra.Construct.Add.Identity.html#903" class="Bound">p</a> <a id="922" href="Algebra.Construct.Add.Identity.html#909" class="Bound">q</a> <a id="924" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a>
<a id="926" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="933" class="Symbol">_</a>  <a id="936" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="938" href="Algebra.Construct.Add.Identity.html#938" class="Bound">p</a> <a id="940" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="942" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="948" class="Symbol">=</a> <a id="950" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="952" href="Algebra.Construct.Add.Identity.html#938" class="Bound">p</a> <a id="954" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a>
<a id="956" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="963" class="Symbol">_</a>  <a id="966" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="972" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="974" href="Algebra.Construct.Add.Identity.html#974" class="Bound">q</a> <a id="976" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="978" class="Symbol">=</a> <a id="980" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="982" href="Algebra.Construct.Add.Identity.html#974" class="Bound">q</a> <a id="984" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a>
<a id="986" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="993" class="Symbol">_</a>  <a id="996" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1002" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1008" class="Symbol">=</a> <a id="1010" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>

<a id="1013" class="Keyword">module</a> <a id="1020" href="Algebra.Construct.Add.Identity.html#1020" class="Module">_</a> <a id="1022" class="Symbol">{</a><a id="1023" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1027" class="Symbol">:</a> <a id="1029" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="1033" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a> <a id="1035" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a><a id="1036" class="Symbol">}</a> <a id="1038" class="Symbol">{</a><a id="1039" href="Algebra.Construct.Add.Identity.html#1039" class="Bound">op</a> <a id="1042" class="Symbol">:</a> <a id="1044" href="Algebra.Core.html#527" class="Function">Op₂</a> <a id="1048" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a><a id="1049" class="Symbol">}</a> <a id="1051" class="Symbol">(</a><a id="1052" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a> <a id="1059" class="Symbol">:</a> <a id="1061" href="Relation.Binary.Definitions.html#1407" class="Function">Reflexive</a> <a id="1071" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a><a id="1074" class="Symbol">)</a> <a id="1076" class="Keyword">where</a>
  <a id="1084" class="Keyword">private</a>
    <a id="1096" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">_≈∙_</a> <a id="1101" class="Symbol">=</a> <a id="1103" href="Algebra.Construct.Add.Identity.html#560" class="Datatype">lift≈</a> <a id="1109" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a>
    <a id="1117" href="Algebra.Construct.Add.Identity.html#1117" class="Function">op∙</a> <a id="1121" class="Symbol">=</a> <a id="1123" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="1130" href="Algebra.Construct.Add.Identity.html#1039" class="Bound">op</a>

    <a id="1138" href="Algebra.Construct.Add.Identity.html#1138" class="Function">lift-≈</a> <a id="1145" class="Symbol">:</a> <a id="1147" class="Symbol">∀</a> <a id="1149" class="Symbol">{</a><a id="1150" href="Algebra.Construct.Add.Identity.html#1150" class="Bound">x</a> <a id="1152" href="Algebra.Construct.Add.Identity.html#1152" class="Bound">y</a> <a id="1154" class="Symbol">:</a> <a id="1156" href="Algebra.Construct.Add.Identity.html#1033" class="Bound">A</a><a id="1157" class="Symbol">}</a> <a id="1159" class="Symbol">→</a> <a id="1161" href="Algebra.Construct.Add.Identity.html#1150" class="Bound">x</a> <a id="1163" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">≈</a> <a id="1165" href="Algebra.Construct.Add.Identity.html#1152" class="Bound">y</a> <a id="1167" class="Symbol">→</a> <a id="1169" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1171" href="Algebra.Construct.Add.Identity.html#1150" class="Bound">x</a> <a id="1173" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1175" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">≈∙</a> <a id="1178" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1180" href="Algebra.Construct.Add.Identity.html#1152" class="Bound">y</a> <a id="1182" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a>
    <a id="1188" href="Algebra.Construct.Add.Identity.html#1138" class="Function">lift-≈</a> <a id="1195" class="Symbol">=</a> <a id="1197" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">[_]</a>

  <a id="1204" href="Algebra.Construct.Add.Identity.html#1204" class="Function">cong₂</a> <a id="1210" class="Symbol">:</a> <a id="1212" href="Algebra.Definitions.html#1302" class="Function">Congruent₂</a> <a id="1223" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1227" href="Algebra.Construct.Add.Identity.html#1039" class="Bound">op</a> <a id="1230" class="Symbol">→</a> <a id="1232" href="Algebra.Definitions.html#1302" class="Function">Congruent₂</a> <a id="1243" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">_≈∙_</a> <a id="1248" class="Symbol">(</a><a id="1249" href="Algebra.Construct.Add.Identity.html#1117" class="Function">op∙</a><a id="1252" class="Symbol">)</a>
  <a id="1256" href="Algebra.Construct.Add.Identity.html#1204" class="Function">cong₂</a> <a id="1262" href="Algebra.Construct.Add.Identity.html#1262" class="Bound">R-cong</a> <a id="1269" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">[</a> <a id="1271" href="Algebra.Construct.Add.Identity.html#1271" class="Bound">eq-l</a> <a id="1276" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">]</a> <a id="1278" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">[</a> <a id="1280" href="Algebra.Construct.Add.Identity.html#1280" class="Bound">eq-r</a> <a id="1285" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">]</a> <a id="1287" class="Symbol">=</a> <a id="1289" href="Algebra.Construct.Add.Identity.html#1138" class="Function">lift-≈</a> <a id="1296" class="Symbol">(</a><a id="1297" href="Algebra.Construct.Add.Identity.html#1262" class="Bound">R-cong</a> <a id="1304" href="Algebra.Construct.Add.Identity.html#1271" class="Bound">eq-l</a> <a id="1309" href="Algebra.Construct.Add.Identity.html#1280" class="Bound">eq-r</a><a id="1313" class="Symbol">)</a>
  <a id="1317" href="Algebra.Construct.Add.Identity.html#1204" class="Function">cong₂</a> <a id="1323" href="Algebra.Construct.Add.Identity.html#1323" class="Bound">R-cong</a> <a id="1330" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">[</a> <a id="1332" href="Algebra.Construct.Add.Identity.html#1332" class="Bound">eq</a>   <a id="1337" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">]</a> <a id="1339" href="Relation.Binary.Construct.Add.Point.Equality.html#1121" class="InductiveConstructor">∙≈∙</a>      <a id="1348" class="Symbol">=</a> <a id="1350" href="Algebra.Construct.Add.Identity.html#1138" class="Function">lift-≈</a> <a id="1357" href="Algebra.Construct.Add.Identity.html#1332" class="Bound">eq</a>
  <a id="1362" href="Algebra.Construct.Add.Identity.html#1204" class="Function">cong₂</a> <a id="1368" href="Algebra.Construct.Add.Identity.html#1368" class="Bound">R-cong</a> <a id="1375" href="Relation.Binary.Construct.Add.Point.Equality.html#1121" class="InductiveConstructor">∙≈∙</a>      <a id="1384" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">[</a> <a id="1386" href="Algebra.Construct.Add.Identity.html#1386" class="Bound">eq</a>   <a id="1391" href="Relation.Binary.Construct.Add.Point.Equality.html#1160" class="InductiveConstructor Operator">]</a> <a id="1393" class="Symbol">=</a> <a id="1395" href="Algebra.Construct.Add.Identity.html#1138" class="Function">lift-≈</a> <a id="1402" href="Algebra.Construct.Add.Identity.html#1386" class="Bound">eq</a>
  <a id="1407" href="Algebra.Construct.Add.Identity.html#1204" class="Function">cong₂</a> <a id="1413" href="Algebra.Construct.Add.Identity.html#1413" class="Bound">R-cong</a> <a id="1420" href="Relation.Binary.Construct.Add.Point.Equality.html#1121" class="InductiveConstructor">∙≈∙</a>      <a id="1429" href="Relation.Binary.Construct.Add.Point.Equality.html#1121" class="InductiveConstructor">∙≈∙</a>      <a id="1438" class="Symbol">=</a> <a id="1440" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1448" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1452" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>

  <a id="1462" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1468" class="Symbol">:</a> <a id="1470" href="Algebra.Definitions.html#1548" class="Function">Associative</a> <a id="1482" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1486" href="Algebra.Construct.Add.Identity.html#1039" class="Bound">op</a> <a id="1489" class="Symbol">→</a> <a id="1491" href="Algebra.Definitions.html#1548" class="Function">Associative</a> <a id="1503" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">_≈∙_</a> <a id="1508" class="Symbol">(</a><a id="1509" href="Algebra.Construct.Add.Identity.html#1117" class="Function">op∙</a><a id="1512" class="Symbol">)</a>
  <a id="1516" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1522" href="Algebra.Construct.Add.Identity.html#1522" class="Bound">assoc</a> <a id="1528" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1530" href="Algebra.Construct.Add.Identity.html#1530" class="Bound">p</a> <a id="1532" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1534" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1536" href="Algebra.Construct.Add.Identity.html#1536" class="Bound">q</a> <a id="1538" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1540" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1542" href="Algebra.Construct.Add.Identity.html#1542" class="Bound">r</a> <a id="1544" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1546" class="Symbol">=</a> <a id="1548" href="Algebra.Construct.Add.Identity.html#1138" class="Function">lift-≈</a> <a id="1555" class="Symbol">(</a><a id="1556" href="Algebra.Construct.Add.Identity.html#1522" class="Bound">assoc</a> <a id="1562" href="Algebra.Construct.Add.Identity.html#1530" class="Bound">p</a> <a id="1564" href="Algebra.Construct.Add.Identity.html#1536" class="Bound">q</a> <a id="1566" href="Algebra.Construct.Add.Identity.html#1542" class="Bound">r</a><a id="1567" class="Symbol">)</a>
  <a id="1571" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1577" class="Symbol">_</a>     <a id="1583" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1585" href="Algebra.Construct.Add.Identity.html#1585" class="Bound">p</a> <a id="1587" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1589" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1591" href="Algebra.Construct.Add.Identity.html#1591" class="Bound">q</a> <a id="1593" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1595" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1601" class="Symbol">=</a> <a id="1603" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1611" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1615" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="1624" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1630" class="Symbol">_</a>     <a id="1636" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1638" href="Algebra.Construct.Add.Identity.html#1638" class="Bound">p</a> <a id="1640" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1642" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1648" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1650" href="Algebra.Construct.Add.Identity.html#1650" class="Bound">r</a> <a id="1652" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1654" class="Symbol">=</a> <a id="1656" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1664" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1668" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="1677" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1683" class="Symbol">_</a>     <a id="1689" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1691" href="Algebra.Construct.Add.Identity.html#1691" class="Bound">p</a> <a id="1693" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1695" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1701" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1707" class="Symbol">=</a> <a id="1709" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1717" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1721" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="1730" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1736" class="Symbol">_</a>     <a id="1742" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1748" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1750" href="Algebra.Construct.Add.Identity.html#1750" class="Bound">r</a> <a id="1752" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1754" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1756" href="Algebra.Construct.Add.Identity.html#1756" class="Bound">q</a> <a id="1758" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1760" class="Symbol">=</a> <a id="1762" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1770" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1774" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="1783" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1789" class="Symbol">_</a>     <a id="1795" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1801" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1803" href="Algebra.Construct.Add.Identity.html#1803" class="Bound">q</a> <a id="1805" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1807" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1813" class="Symbol">=</a> <a id="1815" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1823" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1827" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="1836" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1842" class="Symbol">_</a>     <a id="1848" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1854" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1860" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1862" href="Algebra.Construct.Add.Identity.html#1862" class="Bound">r</a> <a id="1864" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1866" class="Symbol">=</a> <a id="1868" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1876" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1880" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="1889" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="1895" class="Symbol">_</a>     <a id="1901" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1907" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1913" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="1919" class="Symbol">=</a> <a id="1921" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="1929" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="1933" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>

  <a id="1943" href="Algebra.Construct.Add.Identity.html#1943" class="Function">identityˡ</a> <a id="1953" class="Symbol">:</a> <a id="1955" href="Algebra.Definitions.html#1708" class="Function">LeftIdentity</a> <a id="1968" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">_≈∙_</a> <a id="1973" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a> <a id="1975" class="Symbol">(</a><a id="1976" href="Algebra.Construct.Add.Identity.html#1117" class="Function">op∙</a><a id="1979" class="Symbol">)</a>
  <a id="1983" href="Algebra.Construct.Add.Identity.html#1943" class="Function">identityˡ</a> <a id="1993" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="1995" href="Algebra.Construct.Add.Identity.html#1995" class="Bound">p</a> <a id="1997" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="1999" class="Symbol">=</a> <a id="2001" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="2009" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="2013" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="2022" href="Algebra.Construct.Add.Identity.html#1943" class="Function">identityˡ</a> <a id="2032" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="2038" class="Symbol">=</a> <a id="2040" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="2048" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="2052" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>

  <a id="2062" href="Algebra.Construct.Add.Identity.html#2062" class="Function">identityʳ</a> <a id="2072" class="Symbol">:</a> <a id="2074" href="Algebra.Definitions.html#1781" class="Function">RightIdentity</a> <a id="2088" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">_≈∙_</a> <a id="2093" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a> <a id="2095" class="Symbol">(</a><a id="2096" href="Algebra.Construct.Add.Identity.html#1117" class="Function">op∙</a><a id="2099" class="Symbol">)</a>
  <a id="2103" href="Algebra.Construct.Add.Identity.html#2062" class="Function">identityʳ</a> <a id="2113" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">[</a> <a id="2115" href="Algebra.Construct.Add.Identity.html#2115" class="Bound">p</a> <a id="2117" href="Agda.Builtin.Maybe.html#173" class="InductiveConstructor Operator">]</a> <a id="2119" class="Symbol">=</a> <a id="2121" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="2129" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="2133" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>
  <a id="2142" href="Algebra.Construct.Add.Identity.html#2062" class="Function">identityʳ</a> <a id="2152" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>     <a id="2158" class="Symbol">=</a> <a id="2160" href="Relation.Binary.Construct.Add.Point.Equality.html#1379" class="Function">≈∙-refl</a> <a id="2168" href="Algebra.Construct.Add.Identity.html#1023" class="Bound Operator">_≈_</a> <a id="2172" href="Algebra.Construct.Add.Identity.html#1052" class="Bound">refl-≈</a>

  <a id="2182" href="Algebra.Construct.Add.Identity.html#2182" class="Function">identity</a> <a id="2191" class="Symbol">:</a> <a id="2193" href="Algebra.Definitions.html#1856" class="Function">Identity</a> <a id="2202" href="Algebra.Construct.Add.Identity.html#1096" class="Function Operator">_≈∙_</a> <a id="2207" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a> <a id="2209" class="Symbol">(</a><a id="2210" href="Algebra.Construct.Add.Identity.html#1117" class="Function">op∙</a><a id="2213" class="Symbol">)</a>
  <a id="2217" href="Algebra.Construct.Add.Identity.html#2182" class="Function">identity</a> <a id="2226" class="Symbol">=</a> <a id="2228" href="Algebra.Construct.Add.Identity.html#1943" class="Function">identityˡ</a> <a id="2238" href="Agda.Builtin.Sigma.html#235" class="InductiveConstructor Operator">,</a> <a id="2240" href="Algebra.Construct.Add.Identity.html#2062" class="Function">identityʳ</a>

<a id="2251" class="Keyword">module</a> <a id="2258" href="Algebra.Construct.Add.Identity.html#2258" class="Module">_</a> <a id="2260" class="Symbol">{</a><a id="2261" href="Algebra.Construct.Add.Identity.html#2261" class="Bound Operator">_≈_</a> <a id="2265" class="Symbol">:</a> <a id="2267" href="Relation.Binary.Core.html#896" class="Function">Rel</a> <a id="2271" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a> <a id="2273" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a><a id="2274" class="Symbol">}</a> <a id="2276" class="Symbol">{</a><a id="2277" href="Algebra.Construct.Add.Identity.html#2277" class="Bound">op</a> <a id="2280" class="Symbol">:</a> <a id="2282" href="Algebra.Core.html#527" class="Function">Op₂</a> <a id="2286" href="Algebra.Construct.Add.Identity.html#847" class="Generalizable">A</a><a id="2287" class="Symbol">}</a> <a id="2289" class="Keyword">where</a>
  <a id="2297" class="Keyword">private</a>
    <a id="2309" href="Algebra.Construct.Add.Identity.html#2309" class="Function Operator">_≈∙_</a> <a id="2314" class="Symbol">=</a> <a id="2316" href="Algebra.Construct.Add.Identity.html#560" class="Datatype">lift≈</a> <a id="2322" href="Algebra.Construct.Add.Identity.html#2261" class="Bound Operator">_≈_</a>
    <a id="2330" href="Algebra.Construct.Add.Identity.html#2330" class="Function">op∙</a> <a id="2334" class="Symbol">=</a> <a id="2336" href="Algebra.Construct.Add.Identity.html#858" class="Function">liftOp</a> <a id="2343" href="Algebra.Construct.Add.Identity.html#2277" class="Bound">op</a>

  <a id="2349" href="Algebra.Construct.Add.Identity.html#2349" class="Function">isMagma</a> <a id="2357" class="Symbol">:</a> <a id="2359" href="Algebra.Structures.html#1708" class="Record">IsMagma</a> <a id="2367" href="Algebra.Construct.Add.Identity.html#2261" class="Bound Operator">_≈_</a> <a id="2371" href="Algebra.Construct.Add.Identity.html#2277" class="Bound">op</a> <a id="2374" class="Symbol">→</a> <a id="2376" href="Algebra.Structures.html#1708" class="Record">IsMagma</a> <a id="2384" href="Algebra.Construct.Add.Identity.html#2309" class="Function Operator">_≈∙_</a> <a id="2389" href="Algebra.Construct.Add.Identity.html#2330" class="Function">op∙</a>
  <a id="2395" href="Algebra.Construct.Add.Identity.html#2349" class="Function">isMagma</a> <a id="2403" href="Algebra.Construct.Add.Identity.html#2403" class="Bound">M</a> <a id="2405" class="Symbol">=</a>
    <a id="2411" class="Keyword">record</a>
    <a id="2422" class="Symbol">{</a> <a id="2424" href="Algebra.Structures.html#1760" class="Field">isEquivalence</a> <a id="2438" class="Symbol">=</a> <a id="2440" href="Relation.Binary.Construct.Add.Point.Equality.html#2328" class="Function">≈∙-isEquivalence</a> <a id="2457" href="Algebra.Construct.Add.Identity.html#2261" class="Bound Operator">_≈_</a> <a id="2461" href="Algebra.Structures.html#1760" class="Field">M.isEquivalence</a>
    <a id="2481" class="Symbol">;</a> <a id="2483" href="Algebra.Structures.html#1798" class="Field">∙-cong</a>        <a id="2497" class="Symbol">=</a> <a id="2499" href="Algebra.Construct.Add.Identity.html#1204" class="Function">cong₂</a> <a id="2505" href="Relation.Binary.Structures.html#1596" class="Function">M.refl</a> <a id="2512" href="Algebra.Structures.html#1798" class="Field">M.∙-cong</a>
    <a id="2525" class="Symbol">}</a> <a id="2527" class="Keyword">where</a> <a id="2533" class="Keyword">module</a> <a id="2540" href="Algebra.Construct.Add.Identity.html#2540" class="Module">M</a> <a id="2542" class="Symbol">=</a> <a id="2544" href="Algebra.Structures.html#1708" class="Module">IsMagma</a> <a id="2552" href="Algebra.Construct.Add.Identity.html#2403" class="Bound">M</a>

  <a id="2557" href="Algebra.Construct.Add.Identity.html#2557" class="Function">isSemigroup</a> <a id="2569" class="Symbol">:</a> <a id="2571" href="Algebra.Structures.html#3380" class="Record">IsSemigroup</a> <a id="2583" href="Algebra.Construct.Add.Identity.html#2261" class="Bound Operator">_≈_</a> <a id="2587" href="Algebra.Construct.Add.Identity.html#2277" class="Bound">op</a> <a id="2590" class="Symbol">→</a> <a id="2592" href="Algebra.Structures.html#3380" class="Record">IsSemigroup</a> <a id="2604" href="Algebra.Construct.Add.Identity.html#2309" class="Function Operator">_≈∙_</a> <a id="2609" href="Algebra.Construct.Add.Identity.html#2330" class="Function">op∙</a>
  <a id="2615" href="Algebra.Construct.Add.Identity.html#2557" class="Function">isSemigroup</a> <a id="2627" href="Algebra.Construct.Add.Identity.html#2627" class="Bound">S</a> <a id="2629" class="Symbol">=</a> <a id="2631" class="Keyword">record</a>
    <a id="2642" class="Symbol">{</a> <a id="2644" href="Algebra.Structures.html#3436" class="Field">isMagma</a> <a id="2652" class="Symbol">=</a> <a id="2654" href="Algebra.Construct.Add.Identity.html#2349" class="Function">isMagma</a> <a id="2662" href="Algebra.Structures.html#3436" class="Field">S.isMagma</a>
    <a id="2676" class="Symbol">;</a> <a id="2678" href="Algebra.Structures.html#3460" class="Field">assoc</a>   <a id="2686" class="Symbol">=</a> <a id="2688" href="Algebra.Construct.Add.Identity.html#1462" class="Function">assoc</a> <a id="2694" href="Relation.Binary.Structures.html#1596" class="Function">S.refl</a> <a id="2701" href="Algebra.Structures.html#3460" class="Field">S.assoc</a>
    <a id="2713" class="Symbol">}</a> <a id="2715" class="Keyword">where</a> <a id="2721" class="Keyword">module</a> <a id="2728" href="Algebra.Construct.Add.Identity.html#2728" class="Module">S</a> <a id="2730" class="Symbol">=</a> <a id="2732" href="Algebra.Structures.html#3380" class="Module">IsSemigroup</a> <a id="2744" href="Algebra.Construct.Add.Identity.html#2627" class="Bound">S</a>

  <a id="2749" href="Algebra.Construct.Add.Identity.html#2749" class="Function">isMonoid</a> <a id="2758" class="Symbol">:</a> <a id="2760" href="Algebra.Structures.html#3380" class="Record">IsSemigroup</a> <a id="2772" href="Algebra.Construct.Add.Identity.html#2261" class="Bound Operator">_≈_</a> <a id="2776" href="Algebra.Construct.Add.Identity.html#2277" class="Bound">op</a> <a id="2779" class="Symbol">→</a> <a id="2781" href="Algebra.Structures.html#4457" class="Record">IsMonoid</a> <a id="2790" href="Algebra.Construct.Add.Identity.html#2309" class="Function Operator">_≈∙_</a> <a id="2795" href="Algebra.Construct.Add.Identity.html#2330" class="Function">op∙</a> <a id="2799" href="Agda.Builtin.Maybe.html#194" class="InductiveConstructor">∙</a>
  <a id="2803" href="Algebra.Construct.Add.Identity.html#2749" class="Function">isMonoid</a> <a id="2812" href="Algebra.Construct.Add.Identity.html#2812" class="Bound">S</a> <a id="2814" class="Symbol">=</a> <a id="2816" class="Keyword">record</a>
    <a id="2827" class="Symbol">{</a> <a id="2829" href="Algebra.Structures.html#4518" class="Field">isSemigroup</a> <a id="2841" class="Symbol">=</a> <a id="2843" href="Algebra.Construct.Add.Identity.html#2557" class="Function">isSemigroup</a> <a id="2855" href="Algebra.Construct.Add.Identity.html#2812" class="Bound">S</a>
    <a id="2861" class="Symbol">;</a> <a id="2863" href="Algebra.Structures.html#4550" class="Field">identity</a>    <a id="2875" class="Symbol">=</a> <a id="2877" href="Algebra.Construct.Add.Identity.html#2182" class="Function">identity</a> <a id="2886" href="Relation.Binary.Structures.html#1596" class="Function">S.refl</a>
    <a id="2897" class="Symbol">}</a> <a id="2899" class="Keyword">where</a> <a id="2905" class="Keyword">module</a> <a id="2912" href="Algebra.Construct.Add.Identity.html#2912" class="Module">S</a> <a id="2914" class="Symbol">=</a> <a id="2916" href="Algebra.Structures.html#3380" class="Module">IsSemigroup</a> <a id="2928" href="Algebra.Construct.Add.Identity.html#2812" class="Bound">S</a>

<a id="semigroup"></a><a id="2931" href="Algebra.Construct.Add.Identity.html#2931" class="Function">semigroup</a> <a id="2941" class="Symbol">:</a> <a id="2943" href="Algebra.Bundles.html#4756" class="Record">Semigroup</a> <a id="2953" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="2955" class="Symbol">(</a><a id="2956" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="2958" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2960" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a><a id="2961" class="Symbol">)</a> <a id="2963" class="Symbol">→</a> <a id="2965" href="Algebra.Bundles.html#4756" class="Record">Semigroup</a> <a id="2975" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="2977" class="Symbol">(</a><a id="2978" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="2980" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="2982" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a><a id="2983" class="Symbol">)</a>
<a id="2985" href="Algebra.Construct.Add.Identity.html#2931" class="Function">semigroup</a> <a id="2995" href="Algebra.Construct.Add.Identity.html#2995" class="Bound">S</a> <a id="2997" class="Symbol">=</a> <a id="2999" class="Keyword">record</a>
  <a id="3008" class="Symbol">{</a> <a id="3010" href="Algebra.Bundles.html#4838" class="Field">Carrier</a> <a id="3018" class="Symbol">=</a> <a id="3020" href="Agda.Builtin.Maybe.html#135" class="Datatype">Pointed</a> <a id="3028" href="Algebra.Bundles.html#4838" class="Field">S.Carrier</a>
  <a id="3040" class="Symbol">;</a> <a id="3042" href="Algebra.Bundles.html#4924" class="Field">isSemigroup</a> <a id="3054" class="Symbol">=</a> <a id="3056" href="Algebra.Construct.Add.Identity.html#2557" class="Function">isSemigroup</a> <a id="3068" href="Algebra.Bundles.html#4924" class="Field">S.isSemigroup</a>
  <a id="3084" class="Symbol">}</a> <a id="3086" class="Keyword">where</a> <a id="3092" class="Keyword">module</a> <a id="3099" href="Algebra.Construct.Add.Identity.html#3099" class="Module">S</a> <a id="3101" class="Symbol">=</a> <a id="3103" href="Algebra.Bundles.html#4756" class="Module">Semigroup</a> <a id="3113" href="Algebra.Construct.Add.Identity.html#2995" class="Bound">S</a>

<a id="monoid"></a><a id="3116" href="Algebra.Construct.Add.Identity.html#3116" class="Function">monoid</a> <a id="3123" class="Symbol">:</a> <a id="3125" href="Algebra.Bundles.html#4756" class="Record">Semigroup</a> <a id="3135" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="3137" class="Symbol">(</a><a id="3138" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="3140" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="3142" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a><a id="3143" class="Symbol">)</a> <a id="3145" class="Symbol">→</a> <a id="3147" href="Algebra.Bundles.html#6668" class="Record">Monoid</a> <a id="3154" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="3156" class="Symbol">(</a><a id="3157" href="Algebra.Construct.Add.Identity.html#831" class="Generalizable">a</a> <a id="3159" href="Agda.Primitive.html#961" class="Primitive Operator">⊔</a> <a id="3161" href="Algebra.Construct.Add.Identity.html#833" class="Generalizable">ℓ</a><a id="3162" class="Symbol">)</a>
<a id="3164" href="Algebra.Construct.Add.Identity.html#3116" class="Function">monoid</a> <a id="3171" href="Algebra.Construct.Add.Identity.html#3171" class="Bound">S</a> <a id="3173" class="Symbol">=</a> <a id="3175" class="Keyword">record</a>
  <a id="3184" class="Symbol">{</a> <a id="3186" href="Algebra.Bundles.html#6847" class="Field">isMonoid</a> <a id="3195" class="Symbol">=</a> <a id="3197" href="Algebra.Construct.Add.Identity.html#2749" class="Function">isMonoid</a> <a id="3206" href="Algebra.Bundles.html#4924" class="Field">S.isSemigroup</a>
  <a id="3222" class="Symbol">}</a> <a id="3224" class="Keyword">where</a> <a id="3230" class="Keyword">module</a> <a id="3237" href="Algebra.Construct.Add.Identity.html#3237" class="Module">S</a> <a id="3239" class="Symbol">=</a> <a id="3241" href="Algebra.Bundles.html#4756" class="Module">Semigroup</a> <a id="3251" href="Algebra.Construct.Add.Identity.html#3171" class="Bound">S</a>
</pre></body></html>