<!DOCTYPE HTML>
<html><head><meta charset="utf-8"><title>Relation.Binary.Closure.Transitive</title><link rel="stylesheet" href="Agda.css"></head><body><pre><a id="1" class="Comment">------------------------------------------------------------------------</a>
<a id="74" class="Comment">-- The Agda standard library</a>
<a id="103" class="Comment">--</a>
<a id="106" class="Comment">-- Transitive closures</a>
<a id="129" class="Comment">------------------------------------------------------------------------</a>

<a id="203" class="Keyword">module</a> <a id="210" href="Relation.Binary.Closure.Transitive.html" class="Module">Relation.Binary.Closure.Transitive</a> <a id="245" class="Keyword">where</a>

<a id="252" class="Keyword">open</a> <a id="257" class="Keyword">import</a> <a id="264" href="Function.html" class="Module">Function</a>
<a id="273" class="Keyword">open</a> <a id="278" class="Keyword">import</a> <a id="285" href="Function.Equivalence.html" class="Module">Function.Equivalence</a> <a id="306" class="Symbol">as</a> <a id="309" class="Module">Equiv</a> <a id="315" class="Keyword">using</a> <a id="321" class="Symbol">(</a><a id="322" href="Function.Equivalence.html#711" class="Function Operator">_⇔_</a><a id="325" class="Symbol">)</a>
<a id="327" class="Keyword">open</a> <a id="332" class="Keyword">import</a> <a id="339" href="Level.html" class="Module">Level</a>
<a id="345" class="Keyword">open</a> <a id="350" class="Keyword">import</a> <a id="357" href="Relation.Binary.html" class="Module">Relation.Binary</a>
<a id="373" class="Keyword">open</a> <a id="378" class="Keyword">import</a> <a id="385" href="Relation.Binary.PropositionalEquality.html" class="Module">Relation.Binary.PropositionalEquality</a> <a id="423" class="Keyword">using</a> <a id="429" class="Symbol">(</a><a id="430" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">_≡_</a> <a id="434" class="Symbol">;</a> <a id="436" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a><a id="440" class="Symbol">)</a>

<a id="443" class="Comment">------------------------------------------------------------------------</a>
<a id="516" class="Comment">-- Transitive closure</a>

<a id="539" class="Keyword">infix</a> <a id="545" class="Number">4</a> <a id="547" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a>

<a id="553" class="Keyword">syntax</a> <a id="560" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> R x y <a id="571" class="Symbol">=</a> x [ R ]⁺ y

<a id="585" class="Keyword">data</a> <a id="Plus"></a><a id="590" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> <a id="595" class="Symbol">{</a><a id="596" href="Relation.Binary.Closure.Transitive.html#596" class="Bound">a</a> <a id="598" href="Relation.Binary.Closure.Transitive.html#598" class="Bound">ℓ</a><a id="599" class="Symbol">}</a> <a id="601" class="Symbol">{</a><a id="602" href="Relation.Binary.Closure.Transitive.html#602" class="Bound">A</a> <a id="604" class="Symbol">:</a> <a id="606" class="PrimitiveType">Set</a> <a id="610" href="Relation.Binary.Closure.Transitive.html#596" class="Bound">a</a><a id="611" class="Symbol">}</a> <a id="613" class="Symbol">(</a><a id="614" href="Relation.Binary.Closure.Transitive.html#614" class="Bound Operator">_∼_</a> <a id="618" class="Symbol">:</a> <a id="620" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="624" href="Relation.Binary.Closure.Transitive.html#602" class="Bound">A</a> <a id="626" href="Relation.Binary.Closure.Transitive.html#598" class="Bound">ℓ</a><a id="627" class="Symbol">)</a> <a id="629" class="Symbol">:</a> <a id="631" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="635" href="Relation.Binary.Closure.Transitive.html#602" class="Bound">A</a> <a id="637" class="Symbol">(</a><a id="638" href="Relation.Binary.Closure.Transitive.html#596" class="Bound">a</a> <a id="640" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="642" href="Relation.Binary.Closure.Transitive.html#598" class="Bound">ℓ</a><a id="643" class="Symbol">)</a> <a id="645" class="Keyword">where</a>
  <a id="Plus.[_]"></a><a id="653" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[_]</a>     <a id="661" class="Symbol">:</a> <a id="663" class="Symbol">∀</a> <a id="665" class="Symbol">{</a><a id="666" href="Relation.Binary.Closure.Transitive.html#666" class="Bound">x</a> <a id="668" href="Relation.Binary.Closure.Transitive.html#668" class="Bound">y</a><a id="669" class="Symbol">}</a> <a id="671" class="Symbol">(</a><a id="672" href="Relation.Binary.Closure.Transitive.html#672" class="Bound">x∼y</a> <a id="676" class="Symbol">:</a> <a id="678" href="Relation.Binary.Closure.Transitive.html#666" class="Bound">x</a> <a id="680" href="Relation.Binary.Closure.Transitive.html#614" class="Bound Operator">∼</a> <a id="682" href="Relation.Binary.Closure.Transitive.html#668" class="Bound">y</a><a id="683" class="Symbol">)</a> <a id="685" class="Symbol">→</a> <a id="687" href="Relation.Binary.Closure.Transitive.html#666" class="Bound">x</a> <a id="689" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="691" href="Relation.Binary.Closure.Transitive.html#614" class="Bound Operator">_∼_</a> <a id="695" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="698" href="Relation.Binary.Closure.Transitive.html#668" class="Bound">y</a>
  <a id="Plus._∼⁺⟨_⟩_"></a><a id="702" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">_∼⁺⟨_⟩_</a> <a id="710" class="Symbol">:</a> <a id="712" class="Symbol">∀</a> <a id="714" href="Relation.Binary.Closure.Transitive.html#714" class="Bound">x</a> <a id="716" class="Symbol">{</a><a id="717" href="Relation.Binary.Closure.Transitive.html#717" class="Bound">y</a> <a id="719" href="Relation.Binary.Closure.Transitive.html#719" class="Bound">z</a><a id="720" class="Symbol">}</a> <a id="722" class="Symbol">(</a><a id="723" href="Relation.Binary.Closure.Transitive.html#723" class="Bound">x∼⁺y</a> <a id="728" class="Symbol">:</a> <a id="730" href="Relation.Binary.Closure.Transitive.html#714" class="Bound">x</a> <a id="732" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="734" href="Relation.Binary.Closure.Transitive.html#614" class="Bound Operator">_∼_</a> <a id="738" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="741" href="Relation.Binary.Closure.Transitive.html#717" class="Bound">y</a><a id="742" class="Symbol">)</a> <a id="744" class="Symbol">(</a><a id="745" href="Relation.Binary.Closure.Transitive.html#745" class="Bound">y∼⁺z</a> <a id="750" class="Symbol">:</a> <a id="752" href="Relation.Binary.Closure.Transitive.html#717" class="Bound">y</a> <a id="754" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="756" href="Relation.Binary.Closure.Transitive.html#614" class="Bound Operator">_∼_</a> <a id="760" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="763" href="Relation.Binary.Closure.Transitive.html#719" class="Bound">z</a><a id="764" class="Symbol">)</a> <a id="766" class="Symbol">→</a>
            <a id="780" href="Relation.Binary.Closure.Transitive.html#714" class="Bound">x</a> <a id="782" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="784" href="Relation.Binary.Closure.Transitive.html#614" class="Bound Operator">_∼_</a> <a id="788" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="791" href="Relation.Binary.Closure.Transitive.html#719" class="Bound">z</a>

<a id="794" class="Keyword">module</a> <a id="801" href="Relation.Binary.Closure.Transitive.html#801" class="Module">_</a> <a id="803" class="Symbol">{</a><a id="804" href="Relation.Binary.Closure.Transitive.html#804" class="Bound">a</a> <a id="806" href="Relation.Binary.Closure.Transitive.html#806" class="Bound">ℓ</a><a id="807" class="Symbol">}</a> <a id="809" class="Symbol">{</a><a id="810" href="Relation.Binary.Closure.Transitive.html#810" class="Bound">A</a> <a id="812" class="Symbol">:</a> <a id="814" class="PrimitiveType">Set</a> <a id="818" href="Relation.Binary.Closure.Transitive.html#804" class="Bound">a</a><a id="819" class="Symbol">}</a> <a id="821" class="Symbol">{</a><a id="822" href="Relation.Binary.Closure.Transitive.html#822" class="Bound Operator">_∼_</a> <a id="826" class="Symbol">:</a> <a id="828" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="832" href="Relation.Binary.Closure.Transitive.html#810" class="Bound">A</a> <a id="834" href="Relation.Binary.Closure.Transitive.html#806" class="Bound">ℓ</a><a id="835" class="Symbol">}</a> <a id="837" class="Keyword">where</a>

 <a id="845" href="Relation.Binary.Closure.Transitive.html#845" class="Function">[]-injective</a> <a id="858" class="Symbol">:</a> <a id="860" class="Symbol">∀</a> <a id="862" class="Symbol">{</a><a id="863" href="Relation.Binary.Closure.Transitive.html#863" class="Bound">x</a> <a id="865" href="Relation.Binary.Closure.Transitive.html#865" class="Bound">y</a> <a id="867" href="Relation.Binary.Closure.Transitive.html#867" class="Bound">p</a> <a id="869" href="Relation.Binary.Closure.Transitive.html#869" class="Bound">q</a><a id="870" class="Symbol">}</a> <a id="872" class="Symbol">→</a> <a id="874" class="Symbol">(</a><a id="875" href="Relation.Binary.Closure.Transitive.html#863" class="Bound">x</a> <a id="877" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="879" href="Relation.Binary.Closure.Transitive.html#822" class="Bound Operator">_∼_</a> <a id="883" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="886" href="Relation.Binary.Closure.Transitive.html#865" class="Bound">y</a> <a id="888" href="Function.html#3051" class="Function Operator">∋</a> <a id="890" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="892" href="Relation.Binary.Closure.Transitive.html#867" class="Bound">p</a> <a id="894" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a><a id="895" class="Symbol">)</a> <a id="897" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="899" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="901" href="Relation.Binary.Closure.Transitive.html#869" class="Bound">q</a> <a id="903" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a> <a id="905" class="Symbol">→</a> <a id="907" href="Relation.Binary.Closure.Transitive.html#867" class="Bound">p</a> <a id="909" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="911" href="Relation.Binary.Closure.Transitive.html#869" class="Bound">q</a>
 <a id="914" href="Relation.Binary.Closure.Transitive.html#845" class="Function">[]-injective</a> <a id="927" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="932" class="Symbol">=</a> <a id="934" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

 <a id="941" href="Relation.Binary.Closure.Transitive.html#941" class="Function">∼⁺⟨⟩-injectiveˡ</a> <a id="957" class="Symbol">:</a> <a id="959" class="Symbol">∀</a> <a id="961" class="Symbol">{</a><a id="962" href="Relation.Binary.Closure.Transitive.html#962" class="Bound">x</a> <a id="964" href="Relation.Binary.Closure.Transitive.html#964" class="Bound">y</a> <a id="966" href="Relation.Binary.Closure.Transitive.html#966" class="Bound">z</a><a id="967" class="Symbol">}</a> <a id="969" class="Symbol">{</a><a id="970" href="Relation.Binary.Closure.Transitive.html#970" class="Bound">p</a> <a id="972" href="Relation.Binary.Closure.Transitive.html#972" class="Bound">r</a> <a id="974" class="Symbol">:</a> <a id="976" href="Relation.Binary.Closure.Transitive.html#962" class="Bound">x</a> <a id="978" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="980" href="Relation.Binary.Closure.Transitive.html#822" class="Bound Operator">_∼_</a> <a id="984" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="987" href="Relation.Binary.Closure.Transitive.html#964" class="Bound">y</a><a id="988" class="Symbol">}</a> <a id="990" class="Symbol">{</a><a id="991" href="Relation.Binary.Closure.Transitive.html#991" class="Bound">q</a> <a id="993" href="Relation.Binary.Closure.Transitive.html#993" class="Bound">s</a><a id="994" class="Symbol">}</a> <a id="996" class="Symbol">→</a>
                   <a id="1017" class="Symbol">(</a><a id="1018" href="Relation.Binary.Closure.Transitive.html#962" class="Bound">x</a> <a id="1020" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="1022" href="Relation.Binary.Closure.Transitive.html#822" class="Bound Operator">_∼_</a> <a id="1026" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="1029" href="Relation.Binary.Closure.Transitive.html#966" class="Bound">z</a> <a id="1031" href="Function.html#3051" class="Function Operator">∋</a> <a id="1033" href="Relation.Binary.Closure.Transitive.html#962" class="Bound">x</a> <a id="1035" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="1039" href="Relation.Binary.Closure.Transitive.html#970" class="Bound">p</a> <a id="1041" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="1043" href="Relation.Binary.Closure.Transitive.html#991" class="Bound">q</a><a id="1044" class="Symbol">)</a> <a id="1046" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1048" class="Symbol">(</a><a id="1049" href="Relation.Binary.Closure.Transitive.html#962" class="Bound">x</a> <a id="1051" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="1055" href="Relation.Binary.Closure.Transitive.html#972" class="Bound">r</a> <a id="1057" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="1059" href="Relation.Binary.Closure.Transitive.html#993" class="Bound">s</a><a id="1060" class="Symbol">)</a> <a id="1062" class="Symbol">→</a> <a id="1064" href="Relation.Binary.Closure.Transitive.html#970" class="Bound">p</a> <a id="1066" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1068" href="Relation.Binary.Closure.Transitive.html#972" class="Bound">r</a>
 <a id="1071" href="Relation.Binary.Closure.Transitive.html#941" class="Function">∼⁺⟨⟩-injectiveˡ</a> <a id="1087" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="1092" class="Symbol">=</a> <a id="1094" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>

 <a id="1101" href="Relation.Binary.Closure.Transitive.html#1101" class="Function">∼⁺⟨⟩-injectiveʳ</a> <a id="1117" class="Symbol">:</a> <a id="1119" class="Symbol">∀</a> <a id="1121" class="Symbol">{</a><a id="1122" href="Relation.Binary.Closure.Transitive.html#1122" class="Bound">x</a> <a id="1124" href="Relation.Binary.Closure.Transitive.html#1124" class="Bound">y</a> <a id="1126" href="Relation.Binary.Closure.Transitive.html#1126" class="Bound">z</a><a id="1127" class="Symbol">}</a> <a id="1129" class="Symbol">{</a><a id="1130" href="Relation.Binary.Closure.Transitive.html#1130" class="Bound">p</a> <a id="1132" href="Relation.Binary.Closure.Transitive.html#1132" class="Bound">r</a> <a id="1134" class="Symbol">:</a> <a id="1136" href="Relation.Binary.Closure.Transitive.html#1122" class="Bound">x</a> <a id="1138" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="1140" href="Relation.Binary.Closure.Transitive.html#822" class="Bound Operator">_∼_</a> <a id="1144" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="1147" href="Relation.Binary.Closure.Transitive.html#1124" class="Bound">y</a><a id="1148" class="Symbol">}</a> <a id="1150" class="Symbol">{</a><a id="1151" href="Relation.Binary.Closure.Transitive.html#1151" class="Bound">q</a> <a id="1153" href="Relation.Binary.Closure.Transitive.html#1153" class="Bound">s</a><a id="1154" class="Symbol">}</a> <a id="1156" class="Symbol">→</a>
                   <a id="1177" class="Symbol">(</a><a id="1178" href="Relation.Binary.Closure.Transitive.html#1122" class="Bound">x</a> <a id="1180" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="1182" href="Relation.Binary.Closure.Transitive.html#822" class="Bound Operator">_∼_</a> <a id="1186" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="1189" href="Relation.Binary.Closure.Transitive.html#1126" class="Bound">z</a> <a id="1191" href="Function.html#3051" class="Function Operator">∋</a> <a id="1193" href="Relation.Binary.Closure.Transitive.html#1122" class="Bound">x</a> <a id="1195" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="1199" href="Relation.Binary.Closure.Transitive.html#1130" class="Bound">p</a> <a id="1201" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="1203" href="Relation.Binary.Closure.Transitive.html#1151" class="Bound">q</a><a id="1204" class="Symbol">)</a> <a id="1206" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1208" class="Symbol">(</a><a id="1209" href="Relation.Binary.Closure.Transitive.html#1122" class="Bound">x</a> <a id="1211" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="1215" href="Relation.Binary.Closure.Transitive.html#1132" class="Bound">r</a> <a id="1217" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="1219" href="Relation.Binary.Closure.Transitive.html#1153" class="Bound">s</a><a id="1220" class="Symbol">)</a> <a id="1222" class="Symbol">→</a> <a id="1224" href="Relation.Binary.Closure.Transitive.html#1151" class="Bound">q</a> <a id="1226" href="Agda.Builtin.Equality.html#83" class="Datatype Operator">≡</a> <a id="1228" href="Relation.Binary.Closure.Transitive.html#1153" class="Bound">s</a>
 <a id="1231" href="Relation.Binary.Closure.Transitive.html#1101" class="Function">∼⁺⟨⟩-injectiveʳ</a> <a id="1247" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a> <a id="1252" class="Symbol">=</a> <a id="1254" href="Agda.Builtin.Equality.html#140" class="InductiveConstructor">refl</a>


<a id="1261" class="Comment">-- &quot;Equational&quot; reasoning notation. Example:</a>
<a id="1306" class="Comment">--</a>
<a id="1309" class="Comment">--   lemma =</a>
<a id="1322" class="Comment">--     x  ∼⁺⟨ [ lemma₁ ] ⟩</a>
<a id="1349" class="Comment">--     y  ∼⁺⟨ lemma₂ ⟩∎</a>
<a id="1373" class="Comment">--     z  ∎</a>

<a id="finally"></a><a id="1386" href="Relation.Binary.Closure.Transitive.html#1386" class="Function">finally</a> <a id="1394" class="Symbol">:</a> <a id="1396" class="Symbol">∀</a> <a id="1398" class="Symbol">{</a><a id="1399" href="Relation.Binary.Closure.Transitive.html#1399" class="Bound">a</a> <a id="1401" href="Relation.Binary.Closure.Transitive.html#1401" class="Bound">ℓ</a><a id="1402" class="Symbol">}</a> <a id="1404" class="Symbol">{</a><a id="1405" href="Relation.Binary.Closure.Transitive.html#1405" class="Bound">A</a> <a id="1407" class="Symbol">:</a> <a id="1409" class="PrimitiveType">Set</a> <a id="1413" href="Relation.Binary.Closure.Transitive.html#1399" class="Bound">a</a><a id="1414" class="Symbol">}</a> <a id="1416" class="Symbol">{</a><a id="1417" href="Relation.Binary.Closure.Transitive.html#1417" class="Bound Operator">_∼_</a> <a id="1421" class="Symbol">:</a> <a id="1423" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="1427" href="Relation.Binary.Closure.Transitive.html#1405" class="Bound">A</a> <a id="1429" href="Relation.Binary.Closure.Transitive.html#1401" class="Bound">ℓ</a><a id="1430" class="Symbol">}</a> <a id="1432" href="Relation.Binary.Closure.Transitive.html#1432" class="Bound">x</a> <a id="1434" href="Relation.Binary.Closure.Transitive.html#1434" class="Bound">y</a> <a id="1436" class="Symbol">→</a>
          <a id="1448" href="Relation.Binary.Closure.Transitive.html#1432" class="Bound">x</a> <a id="1450" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="1452" href="Relation.Binary.Closure.Transitive.html#1417" class="Bound Operator">_∼_</a> <a id="1456" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="1459" href="Relation.Binary.Closure.Transitive.html#1434" class="Bound">y</a> <a id="1461" class="Symbol">→</a> <a id="1463" href="Relation.Binary.Closure.Transitive.html#1432" class="Bound">x</a> <a id="1465" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">[</a> <a id="1467" href="Relation.Binary.Closure.Transitive.html#1417" class="Bound Operator">_∼_</a> <a id="1471" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">]⁺</a> <a id="1474" href="Relation.Binary.Closure.Transitive.html#1434" class="Bound">y</a>
<a id="1476" href="Relation.Binary.Closure.Transitive.html#1386" class="Function">finally</a> <a id="1484" class="Symbol">_</a> <a id="1486" class="Symbol">_</a> <a id="1488" class="Symbol">=</a> <a id="1490" href="Function.html#1058" class="Function">id</a>

<a id="1494" class="Keyword">syntax</a> <a id="1501" href="Relation.Binary.Closure.Transitive.html#1386" class="Function">finally</a> x y x∼⁺y <a id="1518" class="Symbol">=</a> x ∼⁺⟨ x∼⁺y ⟩∎ y ∎

<a id="1539" class="Keyword">infixr</a> <a id="1546" class="Number">2</a> <a id="1548" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">_∼⁺⟨_⟩_</a>
<a id="1556" class="Keyword">infix</a>  <a id="1563" class="Number">3</a> <a id="1565" href="Relation.Binary.Closure.Transitive.html#1386" class="Function">finally</a>

<a id="1574" class="Comment">-- Map.</a>

<a id="map"></a><a id="1583" href="Relation.Binary.Closure.Transitive.html#1583" class="Function">map</a> <a id="1587" class="Symbol">:</a> <a id="1589" class="Symbol">∀</a> <a id="1591" class="Symbol">{</a><a id="1592" href="Relation.Binary.Closure.Transitive.html#1592" class="Bound">a</a> <a id="1594" href="Relation.Binary.Closure.Transitive.html#1594" class="Bound">a′</a> <a id="1597" href="Relation.Binary.Closure.Transitive.html#1597" class="Bound">ℓ</a> <a id="1599" href="Relation.Binary.Closure.Transitive.html#1599" class="Bound">ℓ′</a><a id="1601" class="Symbol">}</a> <a id="1603" class="Symbol">{</a><a id="1604" href="Relation.Binary.Closure.Transitive.html#1604" class="Bound">A</a> <a id="1606" class="Symbol">:</a> <a id="1608" class="PrimitiveType">Set</a> <a id="1612" href="Relation.Binary.Closure.Transitive.html#1592" class="Bound">a</a><a id="1613" class="Symbol">}</a> <a id="1615" class="Symbol">{</a><a id="1616" href="Relation.Binary.Closure.Transitive.html#1616" class="Bound">A′</a> <a id="1619" class="Symbol">:</a> <a id="1621" class="PrimitiveType">Set</a> <a id="1625" href="Relation.Binary.Closure.Transitive.html#1594" class="Bound">a′</a><a id="1627" class="Symbol">}</a>
        <a id="1637" class="Symbol">{</a><a id="1638" href="Relation.Binary.Closure.Transitive.html#1638" class="Bound Operator">_R_</a> <a id="1642" class="Symbol">:</a> <a id="1644" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="1648" href="Relation.Binary.Closure.Transitive.html#1604" class="Bound">A</a> <a id="1650" href="Relation.Binary.Closure.Transitive.html#1597" class="Bound">ℓ</a><a id="1651" class="Symbol">}</a> <a id="1653" class="Symbol">{</a><a id="1654" href="Relation.Binary.Closure.Transitive.html#1654" class="Bound Operator">_R′_</a> <a id="1659" class="Symbol">:</a> <a id="1661" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="1665" href="Relation.Binary.Closure.Transitive.html#1616" class="Bound">A′</a> <a id="1668" href="Relation.Binary.Closure.Transitive.html#1599" class="Bound">ℓ′</a><a id="1670" class="Symbol">}</a> <a id="1672" class="Symbol">{</a><a id="1673" href="Relation.Binary.Closure.Transitive.html#1673" class="Bound">f</a> <a id="1675" class="Symbol">:</a> <a id="1677" href="Relation.Binary.Closure.Transitive.html#1604" class="Bound">A</a> <a id="1679" class="Symbol">→</a> <a id="1681" href="Relation.Binary.Closure.Transitive.html#1616" class="Bound">A′</a><a id="1683" class="Symbol">}</a> <a id="1685" class="Symbol">→</a>
      <a id="1693" href="Relation.Binary.Closure.Transitive.html#1638" class="Bound Operator">_R_</a> <a id="1697" href="Relation.Binary.Core.html#1352" class="Function Operator">=[</a> <a id="1700" href="Relation.Binary.Closure.Transitive.html#1673" class="Bound">f</a> <a id="1702" href="Relation.Binary.Core.html#1352" class="Function Operator">]⇒</a> <a id="1705" href="Relation.Binary.Closure.Transitive.html#1654" class="Bound Operator">_R′_</a> <a id="1710" class="Symbol">→</a> <a id="1712" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> <a id="1717" href="Relation.Binary.Closure.Transitive.html#1638" class="Bound Operator">_R_</a> <a id="1721" href="Relation.Binary.Core.html#1352" class="Function Operator">=[</a> <a id="1724" href="Relation.Binary.Closure.Transitive.html#1673" class="Bound">f</a> <a id="1726" href="Relation.Binary.Core.html#1352" class="Function Operator">]⇒</a> <a id="1729" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> <a id="1734" href="Relation.Binary.Closure.Transitive.html#1654" class="Bound Operator">_R′_</a>
<a id="1739" href="Relation.Binary.Closure.Transitive.html#1583" class="Function">map</a> <a id="1743" href="Relation.Binary.Closure.Transitive.html#1743" class="Bound">R⇒R′</a> <a id="1748" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="1750" href="Relation.Binary.Closure.Transitive.html#1750" class="Bound">xRy</a> <a id="1754" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a>             <a id="1768" class="Symbol">=</a> <a id="1770" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="1772" href="Relation.Binary.Closure.Transitive.html#1743" class="Bound">R⇒R′</a> <a id="1777" href="Relation.Binary.Closure.Transitive.html#1750" class="Bound">xRy</a> <a id="1781" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a>
<a id="1783" href="Relation.Binary.Closure.Transitive.html#1583" class="Function">map</a> <a id="1787" href="Relation.Binary.Closure.Transitive.html#1787" class="Bound">R⇒R′</a> <a id="1792" class="Symbol">(</a><a id="1793" href="Relation.Binary.Closure.Transitive.html#1793" class="Bound">x</a> <a id="1795" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="1799" href="Relation.Binary.Closure.Transitive.html#1799" class="Bound">xR⁺z</a> <a id="1804" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="1806" href="Relation.Binary.Closure.Transitive.html#1806" class="Bound">zR⁺y</a><a id="1810" class="Symbol">)</a> <a id="1812" class="Symbol">=</a>
  <a id="1816" class="Symbol">_</a> <a id="1818" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="1822" href="Relation.Binary.Closure.Transitive.html#1583" class="Function">map</a> <a id="1826" href="Relation.Binary.Closure.Transitive.html#1787" class="Bound">R⇒R′</a> <a id="1831" href="Relation.Binary.Closure.Transitive.html#1799" class="Bound">xR⁺z</a> <a id="1836" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="1838" href="Relation.Binary.Closure.Transitive.html#1583" class="Function">map</a> <a id="1842" href="Relation.Binary.Closure.Transitive.html#1787" class="Bound">R⇒R′</a> <a id="1847" href="Relation.Binary.Closure.Transitive.html#1806" class="Bound">zR⁺y</a>

<a id="1853" class="Comment">------------------------------------------------------------------------</a>
<a id="1926" class="Comment">-- Alternative definition of transitive closure</a>

<a id="1975" class="Comment">-- A generalisation of Data.List.Nonempty.List⁺.</a>

<a id="2025" class="Keyword">infixr</a> <a id="2032" class="Number">5</a> <a id="2034" href="Relation.Binary.Closure.Transitive.html#2206" class="InductiveConstructor Operator">_∷_</a> <a id="2038" href="Relation.Binary.Closure.Transitive.html#2292" class="Function Operator">_++_</a>
<a id="2043" class="Keyword">infix</a>  <a id="2050" class="Number">4</a> <a id="2052" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">Plus′</a>

<a id="2059" class="Keyword">syntax</a> <a id="2066" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">Plus′</a> R x y <a id="2078" class="Symbol">=</a> x ⟨ R ⟩⁺ y

<a id="2092" class="Keyword">data</a> <a id="Plus′"></a><a id="2097" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">Plus′</a> <a id="2103" class="Symbol">{</a><a id="2104" href="Relation.Binary.Closure.Transitive.html#2104" class="Bound">a</a> <a id="2106" href="Relation.Binary.Closure.Transitive.html#2106" class="Bound">ℓ</a><a id="2107" class="Symbol">}</a> <a id="2109" class="Symbol">{</a><a id="2110" href="Relation.Binary.Closure.Transitive.html#2110" class="Bound">A</a> <a id="2112" class="Symbol">:</a> <a id="2114" class="PrimitiveType">Set</a> <a id="2118" href="Relation.Binary.Closure.Transitive.html#2104" class="Bound">a</a><a id="2119" class="Symbol">}</a> <a id="2121" class="Symbol">(</a><a id="2122" href="Relation.Binary.Closure.Transitive.html#2122" class="Bound Operator">_∼_</a> <a id="2126" class="Symbol">:</a> <a id="2128" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="2132" href="Relation.Binary.Closure.Transitive.html#2110" class="Bound">A</a> <a id="2134" href="Relation.Binary.Closure.Transitive.html#2106" class="Bound">ℓ</a><a id="2135" class="Symbol">)</a> <a id="2137" class="Symbol">:</a> <a id="2139" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="2143" href="Relation.Binary.Closure.Transitive.html#2110" class="Bound">A</a> <a id="2145" class="Symbol">(</a><a id="2146" href="Relation.Binary.Closure.Transitive.html#2104" class="Bound">a</a> <a id="2148" href="Agda.Primitive.html#657" class="Primitive Operator">⊔</a> <a id="2150" href="Relation.Binary.Closure.Transitive.html#2106" class="Bound">ℓ</a><a id="2151" class="Symbol">)</a> <a id="2153" class="Keyword">where</a>
  <a id="Plus′.[_]"></a><a id="2161" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">[_]</a> <a id="2165" class="Symbol">:</a> <a id="2167" class="Symbol">∀</a> <a id="2169" class="Symbol">{</a><a id="2170" href="Relation.Binary.Closure.Transitive.html#2170" class="Bound">x</a> <a id="2172" href="Relation.Binary.Closure.Transitive.html#2172" class="Bound">y</a><a id="2173" class="Symbol">}</a> <a id="2175" class="Symbol">(</a><a id="2176" href="Relation.Binary.Closure.Transitive.html#2176" class="Bound">x∼y</a> <a id="2180" class="Symbol">:</a> <a id="2182" href="Relation.Binary.Closure.Transitive.html#2170" class="Bound">x</a> <a id="2184" href="Relation.Binary.Closure.Transitive.html#2122" class="Bound Operator">∼</a> <a id="2186" href="Relation.Binary.Closure.Transitive.html#2172" class="Bound">y</a><a id="2187" class="Symbol">)</a> <a id="2189" class="Symbol">→</a> <a id="2191" href="Relation.Binary.Closure.Transitive.html#2170" class="Bound">x</a> <a id="2193" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟨</a> <a id="2195" href="Relation.Binary.Closure.Transitive.html#2122" class="Bound Operator">_∼_</a> <a id="2199" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟩⁺</a> <a id="2202" href="Relation.Binary.Closure.Transitive.html#2172" class="Bound">y</a>
  <a id="Plus′._∷_"></a><a id="2206" href="Relation.Binary.Closure.Transitive.html#2206" class="InductiveConstructor Operator">_∷_</a> <a id="2210" class="Symbol">:</a> <a id="2212" class="Symbol">∀</a> <a id="2214" class="Symbol">{</a><a id="2215" href="Relation.Binary.Closure.Transitive.html#2215" class="Bound">x</a> <a id="2217" href="Relation.Binary.Closure.Transitive.html#2217" class="Bound">y</a> <a id="2219" href="Relation.Binary.Closure.Transitive.html#2219" class="Bound">z</a><a id="2220" class="Symbol">}</a> <a id="2222" class="Symbol">(</a><a id="2223" href="Relation.Binary.Closure.Transitive.html#2223" class="Bound">x∼y</a> <a id="2227" class="Symbol">:</a> <a id="2229" href="Relation.Binary.Closure.Transitive.html#2215" class="Bound">x</a> <a id="2231" href="Relation.Binary.Closure.Transitive.html#2122" class="Bound Operator">∼</a> <a id="2233" href="Relation.Binary.Closure.Transitive.html#2217" class="Bound">y</a><a id="2234" class="Symbol">)</a> <a id="2236" class="Symbol">(</a><a id="2237" href="Relation.Binary.Closure.Transitive.html#2237" class="Bound">y∼⁺z</a> <a id="2242" class="Symbol">:</a> <a id="2244" href="Relation.Binary.Closure.Transitive.html#2217" class="Bound">y</a> <a id="2246" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟨</a> <a id="2248" href="Relation.Binary.Closure.Transitive.html#2122" class="Bound Operator">_∼_</a> <a id="2252" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟩⁺</a> <a id="2255" href="Relation.Binary.Closure.Transitive.html#2219" class="Bound">z</a><a id="2256" class="Symbol">)</a> <a id="2258" class="Symbol">→</a> <a id="2260" href="Relation.Binary.Closure.Transitive.html#2215" class="Bound">x</a> <a id="2262" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟨</a> <a id="2264" href="Relation.Binary.Closure.Transitive.html#2122" class="Bound Operator">_∼_</a> <a id="2268" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟩⁺</a> <a id="2271" href="Relation.Binary.Closure.Transitive.html#2219" class="Bound">z</a>

<a id="2274" class="Comment">-- Transitivity.</a>

<a id="_++_"></a><a id="2292" href="Relation.Binary.Closure.Transitive.html#2292" class="Function Operator">_++_</a> <a id="2297" class="Symbol">:</a> <a id="2299" class="Symbol">∀</a> <a id="2301" class="Symbol">{</a><a id="2302" href="Relation.Binary.Closure.Transitive.html#2302" class="Bound">a</a> <a id="2304" href="Relation.Binary.Closure.Transitive.html#2304" class="Bound">ℓ</a><a id="2305" class="Symbol">}</a> <a id="2307" class="Symbol">{</a><a id="2308" href="Relation.Binary.Closure.Transitive.html#2308" class="Bound">A</a> <a id="2310" class="Symbol">:</a> <a id="2312" class="PrimitiveType">Set</a> <a id="2316" href="Relation.Binary.Closure.Transitive.html#2302" class="Bound">a</a><a id="2317" class="Symbol">}</a> <a id="2319" class="Symbol">{</a><a id="2320" href="Relation.Binary.Closure.Transitive.html#2320" class="Bound Operator">_∼_</a> <a id="2324" class="Symbol">:</a> <a id="2326" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="2330" href="Relation.Binary.Closure.Transitive.html#2308" class="Bound">A</a> <a id="2332" href="Relation.Binary.Closure.Transitive.html#2304" class="Bound">ℓ</a><a id="2333" class="Symbol">}</a> <a id="2335" class="Symbol">{</a><a id="2336" href="Relation.Binary.Closure.Transitive.html#2336" class="Bound">x</a> <a id="2338" href="Relation.Binary.Closure.Transitive.html#2338" class="Bound">y</a> <a id="2340" href="Relation.Binary.Closure.Transitive.html#2340" class="Bound">z</a><a id="2341" class="Symbol">}</a> <a id="2343" class="Symbol">→</a>
       <a id="2352" href="Relation.Binary.Closure.Transitive.html#2336" class="Bound">x</a> <a id="2354" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟨</a> <a id="2356" href="Relation.Binary.Closure.Transitive.html#2320" class="Bound Operator">_∼_</a> <a id="2360" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟩⁺</a> <a id="2363" href="Relation.Binary.Closure.Transitive.html#2338" class="Bound">y</a> <a id="2365" class="Symbol">→</a> <a id="2367" href="Relation.Binary.Closure.Transitive.html#2338" class="Bound">y</a> <a id="2369" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟨</a> <a id="2371" href="Relation.Binary.Closure.Transitive.html#2320" class="Bound Operator">_∼_</a> <a id="2375" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟩⁺</a> <a id="2378" href="Relation.Binary.Closure.Transitive.html#2340" class="Bound">z</a> <a id="2380" class="Symbol">→</a> <a id="2382" href="Relation.Binary.Closure.Transitive.html#2336" class="Bound">x</a> <a id="2384" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟨</a> <a id="2386" href="Relation.Binary.Closure.Transitive.html#2320" class="Bound Operator">_∼_</a> <a id="2390" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">⟩⁺</a> <a id="2393" href="Relation.Binary.Closure.Transitive.html#2340" class="Bound">z</a>
<a id="2395" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">[</a> <a id="2397" href="Relation.Binary.Closure.Transitive.html#2397" class="Bound">x∼y</a> <a id="2401" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">]</a>      <a id="2408" href="Relation.Binary.Closure.Transitive.html#2292" class="Function Operator">++</a> <a id="2411" href="Relation.Binary.Closure.Transitive.html#2411" class="Bound">y∼⁺z</a> <a id="2416" class="Symbol">=</a> <a id="2418" href="Relation.Binary.Closure.Transitive.html#2397" class="Bound">x∼y</a> <a id="2422" href="Relation.Binary.Closure.Transitive.html#2206" class="InductiveConstructor Operator">∷</a> <a id="2424" href="Relation.Binary.Closure.Transitive.html#2411" class="Bound">y∼⁺z</a>
<a id="2429" class="Symbol">(</a><a id="2430" href="Relation.Binary.Closure.Transitive.html#2430" class="Bound">x∼y</a> <a id="2434" href="Relation.Binary.Closure.Transitive.html#2206" class="InductiveConstructor Operator">∷</a> <a id="2436" href="Relation.Binary.Closure.Transitive.html#2436" class="Bound">y∼⁺z</a><a id="2440" class="Symbol">)</a> <a id="2442" href="Relation.Binary.Closure.Transitive.html#2292" class="Function Operator">++</a> <a id="2445" href="Relation.Binary.Closure.Transitive.html#2445" class="Bound">z∼⁺u</a> <a id="2450" class="Symbol">=</a> <a id="2452" href="Relation.Binary.Closure.Transitive.html#2430" class="Bound">x∼y</a> <a id="2456" href="Relation.Binary.Closure.Transitive.html#2206" class="InductiveConstructor Operator">∷</a> <a id="2458" class="Symbol">(</a><a id="2459" href="Relation.Binary.Closure.Transitive.html#2436" class="Bound">y∼⁺z</a> <a id="2464" href="Relation.Binary.Closure.Transitive.html#2292" class="Function Operator">++</a> <a id="2467" href="Relation.Binary.Closure.Transitive.html#2445" class="Bound">z∼⁺u</a><a id="2471" class="Symbol">)</a>

<a id="2474" class="Comment">-- Plus and Plus′ are equivalent.</a>

<a id="equivalent"></a><a id="2509" href="Relation.Binary.Closure.Transitive.html#2509" class="Function">equivalent</a> <a id="2520" class="Symbol">:</a> <a id="2522" class="Symbol">∀</a> <a id="2524" class="Symbol">{</a><a id="2525" href="Relation.Binary.Closure.Transitive.html#2525" class="Bound">a</a> <a id="2527" href="Relation.Binary.Closure.Transitive.html#2527" class="Bound">ℓ</a><a id="2528" class="Symbol">}</a> <a id="2530" class="Symbol">{</a><a id="2531" href="Relation.Binary.Closure.Transitive.html#2531" class="Bound">A</a> <a id="2533" class="Symbol">:</a> <a id="2535" class="PrimitiveType">Set</a> <a id="2539" href="Relation.Binary.Closure.Transitive.html#2525" class="Bound">a</a><a id="2540" class="Symbol">}</a> <a id="2542" class="Symbol">{</a><a id="2543" href="Relation.Binary.Closure.Transitive.html#2543" class="Bound Operator">_∼_</a> <a id="2547" class="Symbol">:</a> <a id="2549" href="Relation.Binary.Core.html#898" class="Function">Rel</a> <a id="2553" href="Relation.Binary.Closure.Transitive.html#2531" class="Bound">A</a> <a id="2555" href="Relation.Binary.Closure.Transitive.html#2527" class="Bound">ℓ</a><a id="2556" class="Symbol">}</a> <a id="2558" class="Symbol">{</a><a id="2559" href="Relation.Binary.Closure.Transitive.html#2559" class="Bound">x</a> <a id="2561" href="Relation.Binary.Closure.Transitive.html#2561" class="Bound">y</a><a id="2562" class="Symbol">}</a> <a id="2564" class="Symbol">→</a>
             <a id="2579" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> <a id="2584" href="Relation.Binary.Closure.Transitive.html#2543" class="Bound Operator">_∼_</a> <a id="2588" href="Relation.Binary.Closure.Transitive.html#2559" class="Bound">x</a> <a id="2590" href="Relation.Binary.Closure.Transitive.html#2561" class="Bound">y</a> <a id="2592" href="Function.Equivalence.html#711" class="Function Operator">⇔</a> <a id="2594" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">Plus′</a> <a id="2600" href="Relation.Binary.Closure.Transitive.html#2543" class="Bound Operator">_∼_</a> <a id="2604" href="Relation.Binary.Closure.Transitive.html#2559" class="Bound">x</a> <a id="2606" href="Relation.Binary.Closure.Transitive.html#2561" class="Bound">y</a>
<a id="2608" href="Relation.Binary.Closure.Transitive.html#2509" class="Function">equivalent</a> <a id="2619" class="Symbol">{</a><a id="2620" class="Argument">_∼_</a> <a id="2624" class="Symbol">=</a> <a id="2626" href="Relation.Binary.Closure.Transitive.html#2626" class="Bound Operator">_∼_</a><a id="2629" class="Symbol">}</a> <a id="2631" class="Symbol">=</a> <a id="2633" href="Function.Equivalence.html#804" class="Function">Equiv.equivalence</a> <a id="2651" href="Relation.Binary.Closure.Transitive.html#2676" class="Function">complete</a> <a id="2660" href="Relation.Binary.Closure.Transitive.html#2816" class="Function">sound</a>
  <a id="2668" class="Keyword">where</a>
  <a id="2676" href="Relation.Binary.Closure.Transitive.html#2676" class="Function">complete</a> <a id="2685" class="Symbol">:</a> <a id="2687" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> <a id="2692" href="Relation.Binary.Closure.Transitive.html#2626" class="Bound Operator">_∼_</a> <a id="2696" href="Relation.Binary.Core.html#1162" class="Function Operator">⇒</a> <a id="2698" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">Plus′</a> <a id="2704" href="Relation.Binary.Closure.Transitive.html#2626" class="Bound Operator">_∼_</a>
  <a id="2710" href="Relation.Binary.Closure.Transitive.html#2676" class="Function">complete</a> <a id="2719" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="2721" href="Relation.Binary.Closure.Transitive.html#2721" class="Bound">x∼y</a> <a id="2725" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a>             <a id="2739" class="Symbol">=</a> <a id="2741" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">[</a> <a id="2743" href="Relation.Binary.Closure.Transitive.html#2721" class="Bound">x∼y</a> <a id="2747" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">]</a>
  <a id="2751" href="Relation.Binary.Closure.Transitive.html#2676" class="Function">complete</a> <a id="2760" class="Symbol">(</a><a id="2761" href="Relation.Binary.Closure.Transitive.html#2761" class="Bound">x</a> <a id="2763" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="2767" href="Relation.Binary.Closure.Transitive.html#2767" class="Bound">x∼⁺y</a> <a id="2772" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="2774" href="Relation.Binary.Closure.Transitive.html#2774" class="Bound">y∼⁺z</a><a id="2778" class="Symbol">)</a> <a id="2780" class="Symbol">=</a> <a id="2782" href="Relation.Binary.Closure.Transitive.html#2676" class="Function">complete</a> <a id="2791" href="Relation.Binary.Closure.Transitive.html#2767" class="Bound">x∼⁺y</a> <a id="2796" href="Relation.Binary.Closure.Transitive.html#2292" class="Function Operator">++</a> <a id="2799" href="Relation.Binary.Closure.Transitive.html#2676" class="Function">complete</a> <a id="2808" href="Relation.Binary.Closure.Transitive.html#2774" class="Bound">y∼⁺z</a>

  <a id="2816" href="Relation.Binary.Closure.Transitive.html#2816" class="Function">sound</a> <a id="2822" class="Symbol">:</a> <a id="2824" href="Relation.Binary.Closure.Transitive.html#2097" class="Datatype">Plus′</a> <a id="2830" href="Relation.Binary.Closure.Transitive.html#2626" class="Bound Operator">_∼_</a> <a id="2834" href="Relation.Binary.Core.html#1162" class="Function Operator">⇒</a> <a id="2836" href="Relation.Binary.Closure.Transitive.html#590" class="Datatype">Plus</a> <a id="2841" href="Relation.Binary.Closure.Transitive.html#2626" class="Bound Operator">_∼_</a>
  <a id="2847" href="Relation.Binary.Closure.Transitive.html#2816" class="Function">sound</a> <a id="2853" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">[</a> <a id="2855" href="Relation.Binary.Closure.Transitive.html#2855" class="Bound">x∼y</a> <a id="2859" href="Relation.Binary.Closure.Transitive.html#2161" class="InductiveConstructor Operator">]</a>      <a id="2866" class="Symbol">=</a> <a id="2868" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="2870" href="Relation.Binary.Closure.Transitive.html#2855" class="Bound">x∼y</a> <a id="2874" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a>
  <a id="2878" href="Relation.Binary.Closure.Transitive.html#2816" class="Function">sound</a> <a id="2884" class="Symbol">(</a><a id="2885" href="Relation.Binary.Closure.Transitive.html#2885" class="Bound">x∼y</a> <a id="2889" href="Relation.Binary.Closure.Transitive.html#2206" class="InductiveConstructor Operator">∷</a> <a id="2891" href="Relation.Binary.Closure.Transitive.html#2891" class="Bound">y∼⁺z</a><a id="2895" class="Symbol">)</a> <a id="2897" class="Symbol">=</a> <a id="2899" class="Symbol">_</a> <a id="2901" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">∼⁺⟨</a> <a id="2905" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">[</a> <a id="2907" href="Relation.Binary.Closure.Transitive.html#2885" class="Bound">x∼y</a> <a id="2911" href="Relation.Binary.Closure.Transitive.html#653" class="InductiveConstructor Operator">]</a> <a id="2913" href="Relation.Binary.Closure.Transitive.html#702" class="InductiveConstructor Operator">⟩</a> <a id="2915" href="Relation.Binary.Closure.Transitive.html#2816" class="Function">sound</a> <a id="2921" href="Relation.Binary.Closure.Transitive.html#2891" class="Bound">y∼⁺z</a>
</pre></body></html>